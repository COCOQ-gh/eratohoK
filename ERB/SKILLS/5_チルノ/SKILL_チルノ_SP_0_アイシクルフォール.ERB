;-----------------------------------
;氷符「アイシクルフォール」
;チルノの三能力合計で威力・発動率が変動する
;LUNATICは強力だが、HARDが解禁されるくらいチルノが育つ頃にはもうゲームが終わっている。
;ロマン技である。
;-----------------------------------
@SKILL_5_SP_0_EXIST
RETURN 1

@SKILL_5_SP_0_NAME
SELECTCASE SKILL_5_SP_0_CHECK_POWER()
	CASE 1
		RESULTS = アイシクルフォール -EASY-
	CASE 2
		RESULTS = アイシクルフォール -NORMAL-
	CASE 3
		RESULTS = アイシクルフォール -HARD-
	CASE 4
		RESULTS = アイシクルフォール -LUNATIC-
ENDSELECT

;能力次第でレベルを変動させる
;2-5
@SKILL_5_SP_0_LEVEL
RETURN SKILL_5_SP_0_CHECK_POWER() + 1

;対象選択
@SKILL_5_SP_0_SETTARGET(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊
;敵部隊対象
RETURN 1

;発動判定
@SKILL_5_SP_0_CAN_INVOKE(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊
対象側 = !発動側
RETURN RAND:100 < 15 + SKILL_5_SP_0_CHECK_POWER() * 2

;効果をここに記述
@SKILL_5_SP_0_INVOKE(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊
対象側 = !発動側
SELECTCASE SKILL_5_SP_0_CHECK_POWER()
	CASE 0, 1
		LOCAL:3 = 0
		LOCAL:4 = 0
	CASE 2
		LOCAL:3 = 6
		LOCAL:4 = 1000
	CASE 3
		LOCAL:3 = 12
		LOCAL:4 = 1500
	CASE 4
		LOCAL:3 = 20
		LOCAL:4 = 2500
ENDSELECT

IF 対象部隊 >= 0
	LOCAL:5 = 対象勢力
	LOCAL:6 = MIN(UNIT_SOLDIER:対象勢力:対象部隊 * LOCAL:3 / 100, 1500, LOCAL:4)
	LOCAL:7 = MAX(UNIT_SOLDIER:対象勢力:対象部隊 - LOCAL:6, 1)
	PRINTFORML %ANAME(GET_COUNTRY_BOSS(LOCAL:5)), 10, RIGHT%軍:{UNIT_SOLDIER:対象勢力:対象部隊, 6, RIGHT} → {LOCAL:7}
	UNIT_SOLDIER:対象勢力:対象部隊 = LOCAL:7
ELSE
	LOCAL:5 = CITY_OWNER:対象勢力
	LOCAL:6 = MIN(CITY_SOLDIER:対象勢力 * LOCAL:3 / 100, 1500, LOCAL:4)
	LOCAL:7 = MAX(CITY_SOLDIER:対象勢力 - LOCAL:6, 1)
	PRINTFORML %ANAME(GET_COUNTRY_BOSS(LOCAL:5)), 10, RIGHT%軍:{CITY_SOLDIER:対象勢力, 6, RIGHT} → {LOCAL:7}
	CITY_SOLDIER:対象勢力 = LOCAL:7
ENDIF

@SKILL_5_SP_0_EXPLANATION
RESULTS = 敵部隊に直接ダメージ。三能力の合計に応じて強化。


@SKILL_5_SP_0_CHECK_POWER
#FUNCTION
#DIM チルノ
チルノ = NAME_TO_CHARA("チルノ")
SIF チルノ == -1
	RETURNF 0
SELECTCASE ABL:チルノ:武闘 + ABL:チルノ:知略 + ABL:チルノ:政治
	CASE IS < 120
		RETURNF 1
	CASE IS < 200
		RETURNF 2
	CASE IS < 250
		RETURNF 3
	CASEELSE
		RETURNF 4
ENDSELECT