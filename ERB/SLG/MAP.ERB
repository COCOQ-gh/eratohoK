;マップ関係

;-------------------------------------------------
;全都市の描画色をリセットする関数
;-------------------------------------------------
@RESET_CITY_COLOR
VARSET CITY_COLOR, GETDEFCOLOR()

;-------------------------------------------------
;全都市の描画色を勢力の色にセットする関数
;-------------------------------------------------
@SET_CITY_COLOR_COUNTRY
FOR LOCAL:0, 0, MAX_CITY
	CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(CITY_OWNER:(LOCAL:0))
NEXT

;-------------------------------------------------
;兵の存在する中継地点に色を付ける関数
;-------------------------------------------------
@SET_CITY_COLOR_UNIT
CALL TMP_CREATE_UNIT_MAP
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_TYPE:(LOCAL:0) == 1
		IF IS_STAY_MYUNIT(LOCAL:0) == 1
			CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(CFLAG:MASTER:所属)
		ELSEIF IS_STAY_UNIT(LOCAL:0) == 2
			CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_野盗)))
		ELSEIF IS_COUNTRY(TMP_UNIT_ONCITY_CNT:(LOCAL:0):0)
			CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(TMP_UNIT_ONCITY_CNT:(LOCAL:0):0)
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;全都市の描画色を自勢力:青、敵勢力:赤にセットする関数
;-------------------------------------------------
@SET_CITY_COLOR_RELATION
FOR LOCAL:0, 0, MAX_CITY
	IF COUNTRY_BOSS:(CITY_OWNER:(LOCAL:0)) <= 0
		CITY_COLOR:(LOCAL:0) = GETDEFCOLOR()
	ELSEIF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属
		CITY_COLOR:(LOCAL:0) = COLOR("薄青")
	ELSEIF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属
		CITY_COLOR:(LOCAL:0) = COLOR("赤")
	ENDIF
NEXT

;-------------------------------------------------
;都市名をボタンとして描画する関数 ARG:0=都市番号
;-------------------------------------------------
@DRAWCITY_BUTTON(ARG:0)
SETCOLOR CITY_COLOR:(ARG:0)
FOR LOCAL:0,0,10
	SIF CITY_TYPE:(ARG:0) == 0
		FONTSTYLE IS_STAY_MYUNIT(ARG:0) * 2 + IS_STAY_ENEMY_UNIT(ARG:0) * 8
NEXT
PRINTBUTTON GET_CITYNAME_BUTTON(ARG:0), ARG:0 + 1000
RESETCOLOR
FONTSTYLE 0
;-------------------------------------------------
;戦略フェイズのメニューコマンドを表示する関数 ARG:0は行数
;-------------------------------------------------
@PRINT_SLG_MENU(ARG:0)
PRINTFORM %TOSTR_SPACE(4)%

SELECTCASE ARG:0
	;固定行・休憩
	CASE 33
		CALL COLUMN_LEFT_PRINTBUTTON( 81, "進行")
		GOTO SHOWN
	;固定行・設定
	CASE 35
		CALL COLUMN_LEFT_PRINTBUTTON( 91, "設定", FLAG:拠点フェイズ選択コマンド == 91)
		GOTO SHOWN
	;固定行・セーブ
	CASE 36
		CALL COLUMN_LEFT_PRINTBUTTON( 92, "セーブ", FLAG:拠点フェイズ選択コマンド == 92)
		GOTO SHOWN
	;固定行・ロード
	CASE 37
		CALL COLUMN_LEFT_PRINTBUTTON( 93, "ロード", FLAG:拠点フェイズ選択コマンド == 93)
		GOTO SHOWN
ENDSELECT


;観戦モードなら特殊メニュー
IF FLAG:観戦モード
	SELECTCASE ARG:0
		CASE 0
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(0, "勢力情報")
		CASE 1
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(1, "能力一覧")
		CASEELSE
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(0, "")
	ENDSELECT
;放浪中なら特殊メニュー
ELSEIF CFLAG:MASTER:所属 == 0
	SELECTCASE ARG:0
		CASE 0
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(0,  "仕官する")
		CASE 1
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(1,  "旗揚げ")
		CASE 4
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(5,  "勢力情報")
		CASE 5
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(6,  "能力一覧")
		CASE 8
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(8,  "訓練")
		CASE 9
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(9,  "謁見")
		CASEELSE
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(0, "")
	ENDSELECT

ELSE
	SELECTCASE ARG:0
		CASE 0
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(0,  "部隊編成")
		CASE 1
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(1,  "防衛解除")
		CASE 3
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(10, "外交")
		CASE 4
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(11, "勢力情報")
		CASE 5
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(12,  "能力一覧")
		CASE 7
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(20, "政策変更")
		CASE 8
			SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:所属)
				CASE 政策_経済
					SETCOLOR COLOR("パ緑")
					PRINTFORM %"現在:経済" , 12%
				CASE 政策_徴兵
					SETCOLOR COLOR("CREAM")
					PRINTFORM %"現在:徴兵", 12%
				CASE 政策_防衛
					SETCOLOR COLOR("パ青")
					PRINTFORM %"現在:防衛", 12%
				CASEELSE
					SETCOLOR COLOR("明灰色")
					PRINT %"-------", 12%
			ENDSELECT
			RESETCOLOR
		CASE 9
			SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:所属)
				CASE 政策_経済
					PRINTFORM %"経済成長x1.5" , 12%
				CASE 政策_徴兵
					PRINTFORM %"徴兵効率x1.2" , 12%
				CASE 政策_防衛
					PRINTFORM %"防衛倍率x1.5" , 12%
			ENDSELECT
		CASE 10
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(21, "都市管理")
		CASE 12
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(30, "特別訓練")
		CASE 13
			IF SPECIAL_TRAIN_IS_BARGAIN()
				SETCOLOR COLOR("注意")
				PRINTFORM %"現在割引中！", 12%
				RESETCOLOR
			ELSE
				SIF MONEY < SPECIAL_TRAIN_PRICE
					SETCOLOR COLOR("選択不可")
				PRINTFORM %"料金:" + TOSTR(SPECIAL_TRAIN_PRICE), 12%
				RESETCOLOR
			ENDIF
		CASE 14
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(31, "徴兵/解雇")
		CASE 15
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(32, "奴隷市場")
		CASE 16
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(33, "領地探索")
		CASE 17
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(34, "スカウト")
		CASE 18
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(35, "国庫入金")
		CASE 20
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(40, "独立")
		CASE 21
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(41, @"\@ GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER ? 国譲り # クーデター\@")
		CASE 22
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(42, "野に下る") 
		CASE 24
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(50, "オート設定")
		CASE 25
			IF CONFIG:302 == 0
				PRINTFORM %"現在:手動", 12%
			ELSE
				SETCOLOR COLOR("オレンジ")
				PRINTFORM %"現在:自動", 12%
				RESETCOLOR
			ENDIF
		CASEELSE
			CALL COLUMN_LEFT_PRINTBUTTON_SLG(0, "")
	ENDSELECT
ENDIF
$SHOWN
PRINT    


;-------------------------------------------------
;拠点フェイズの形式に合わせるよう間に合わせで作ったモノ
;-------------------------------------------------
@COLUMN_LEFT_PRINTBUTTON_SLG(ARG:0, ARGS:0)
#DIM EXTERNAL_LINK

SIF ARG:0 < 0 || 99 < ARG:0
	THROW COLUMN_LEFT_PRINTBUTTONの項番が0～99の範囲にありません

;空指定なら同幅のスペース
IF ARGS:0 == ""
	PRINTPLAINFORM %TOSTR_SPACE(12)%
;選択不可（表示が制限されていない場合）
ELSEIF !SHOP_AVAIL:(ARG:0)
	SETCOLOR COLOR("選択不可")
	PRINTPLAINFORM [%ARGS:0, 10%]
	RESETCOLOR
;現在選択中（選択式ではない行動も着色するように変更したためボタンに）
ELSEIF ARG:1
	SETCOLOR COLOR("選択中")
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
	RESETCOLOR
;通常のボタン
ELSE
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
ENDIF

;-------------------------------------------------
;全都市数を返す関数
;-------------------------------------------------
@GET_CITY_NUM()
#FUNCTION
RETURNF CITY_NUM

;-------------------------------------------------
;都市名を取得する関数 ボタン用 ARG:0=都市番号
;-------------------------------------------------
@GET_CITYNAME_BUTTON(ARG:0)
#FUNCTIONS
SIF ARG:0 < 0 || ARG:0 >= MAX_CITY
	RETURNF "無名"
RETURNF CITY_NAME_SHORT:(ARG:0)

;-------------------------------------------------
;都市名を取得する関数 ARG:0=都市番号
;-------------------------------------------------
@GET_CITYNAME(ARG:0)
#FUNCTIONS
SIF ARG:0 < 0 || ARG:0 >= MAX_CITY
	RETURNF "無名"	
SIF CITY_TYPE:(ARG:0) == 1
	RETURNF "●"
RETURNF CITY_NAME:(ARG:0)

;-------------------------------------------------
;都市名から番号を取得する関数 REGISTER_ROOTやCITY_OWNERでの糖衣化用。ARGS:0=都市名
;-------------------------------------------------
@GET_CITYNUMBER(ARGS:0)
#FUNCTION
RETURNF FINDELEMENT(CITY_NAME, ARGS:0)

;-------------------------------------------------
;中継地点の名称を取得する関数 ARG:0=都市番号
;-------------------------------------------------
@GET_RELAYNAME(ARG:0)
#FUNCTIONS
SIF ARG:0 < 0 || ARG:0 >= MAX_CITY
	RETURNF "無名"	
RETURNF CITY_NAME:(ARG:0)

;-------------------------------------------------
;ルートの登録を行う関数
;ARG:0　登録元　ARG:1~10 登録先
;なお既に登録されていたら無視する
;-------------------------------------------------
@REGISTER_ROOT(ARG:0, ARG:1 = -99, ARG:2 = -99, ARG:3 = -99, ARG:4 = -99, ARG:5 = -99, ARG:6 = -99, ARG:7 = -99, ARG:8 = -99, ARG:9 = -99, ARG:10 = -99)
;接続先についてカウントアップを行う
FOR LOCAL:0, 1, 10
	;-99だった場合無視
	IF ARG:(LOCAL:0) == -99
		BREAK
	ENDIF
	;接続元のCITY_ROOTの0~10についてカウントアップを行う
	FOR LOCAL:1, 0, 10
		;フェイルセーフ。既に該当地域が登録されているならBREAK。
		IF CITY_ROOT:(ARG:0):(LOCAL:1) == ARG:(LOCAL:0)
			BREAK
		;ARG:0のCITY_ROOTのうち、LOCAL:1番目が空であれば登録する
		ELSEIF CITY_ROOT:(ARG:0):(LOCAL:1) == 0
			CITY_ROOT:(ARG:0):(LOCAL:1) = ARG:(LOCAL:0)
			;接続先にも接続元を登録しておく
			FOR LOCAL:3, 0, 10
				;フェイルセーフ。REGISTER_ROOTを通して操作する限りありえないとは思うが。
				IF CITY_ROOT:(ARG:(LOCAL:0)):(LOCAL:3) == ARG:0
					;DEBUGPRINTFORML @REGISTER_ROOT:{ARG:(LOCAL:0)}→{ARG:0}が登録済み
					BREAK
				ELSEIF CITY_ROOT:(ARG:(LOCAL:0)):(LOCAL:3) == 0
					CITY_ROOT:(ARG:(LOCAL:0)):(LOCAL:3) = ARG:0
					BREAK
				ENDIF
			NEXT
			BREAK
		ENDIF
	NEXT
NEXT

;----------------------------------------------------------------------------------------------
;以下ラッパ
;----------------------------------------------------------------------------------------------

;-------------------------------------------------
;都市タイプの設定
;-------------------------------------------------
@SET_CITY_TYPE
	CALLFORM SET_CITY_TYPE_%MAPID%

;-------------------------------------------------
;ルートの設定
;-------------------------------------------------
@SET_MAP_ROOT
	CALLFORM SET_MAP_ROOT_%MAPID%
	
;-------------------------------------------------
;マップの準備
;-------------------------------------------------
@MAPSETUP(ARG:0 = 0)
SIF ARG:0 == 0 && MAPREADY == 1
	RETURN
CALL SET_CITY_TYPE
CALLFORM SET_CITYNAME_%MAPID%
CALLFORM SET_SHORTCITYNAME_%MAPID%
CALLFORM SET_CITY_NUM_%MAPID%
CALL SET_MAP_ROOT
MAPREADY = 1

;-------------------------------------------------
;経済や防衛などの初期化
;-------------------------------------------------
@MAP_INIT
CALLFORM MAP_INIT_%MAPID%

;-------------------------------------------------
;マップの描画 ARG:0に1を指定すると軍事画面用のボタンを生成
;-------------------------------------------------
@DRAWMAP(ARG:0 = 0, ARG:1 = 0)
CALLFORM DRAWMAP_%MAPID%, ARG:0, ARG:1
