;外交の種類に応じた個別の処理

;-------------------------------------------------
;停戦を申し込む
;-------------------------------------------------
@DIPLOMACY_CEASEFIRE(ARG:0)
#DIM REL_AI_TYPE, 2
#DIM REST_COUNTRY

VARSET REL_AI_TYPE, 0


LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)


REL_AI_TYPE:0 = COUNTRY_AI_TYPE:(CFLAG:MASTER:1), COUNTRY_AI_TYPE:(ARG:0)
;相手勢力の外交計画をチェック
CALL AI_DIPLOMACY_CHECK(ARG:0)

REST_COUNTRY = (MAX_COUNTRY - 1) - MATCH(COUNTRY_BOSS, 0)
LOCAL:11 = MAX(REST_COUNTRY * AI_ACTION:(COUNTRY_AI_TYPE:(ARG:0)):4 / 100, 1)
LOCAL:2 = COUNTRY_TREATY_NO:(ARG:0):1 <= LOCAL:11 ? COUNTRY_REQUEST_RATE_C:(ARG:0):(CFLAG:MASTER:1) # -1

;無条件で成立(友好)
IF LOCAL:2 == 5
	PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%は快く承諾した…
	LOCAL:10 = 1
;無条件で成立
ELSEIF LOCAL:2 == 1
	PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%は少し悩む素振りを見せたものの、すぐに承諾した
	PRINTFORMW どうやらお互いの利害が一致したようだ…
	LOCAL:10 = 1
;相手からの要求を飲めば成立
ELSEIF LOCAL:2 >= 2 && LOCAL:2 <= 4
	;要求の処理
	CALL DIPLOMACY_REQUESTED(ARG:0, LOCAL:2 - 2)
	IF RESULT == -1
		IF LOCAL:2 == 2
			PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%はしばらく悩んだ末に承諾した
			PRINTFORMW どうやらお互いの利害が一致したようだ…
			LOCAL:10 = 1
		ELSE
			PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%はしばらく悩んだ末に断った
			PRINTFORMW 何か対価を用意できれば受け入れてもらえそうだが…
			LOCAL:10 = 0
		ENDIF
	ELSE
		LOCAL:10 = RESULT
	ENDIF
;不成立(非隣接)
ELSEIF LOCAL:2 == 6
	PRINTFORMW %ANAME(MASTER)%は停戦を提案したが、%NAME:(LOCAL:5)%に軽くあしらわれてしまった
	PRINTFORMW 国境を接していない状態での停戦は、よほど良好な関係でなければ無理だろう…
	LOCAL:10 = 0
;不成立
ELSE
	PRINTFORMW %ANAME(MASTER)%は停戦を提案したが、%NAME:(LOCAL:5)%に軽くあしらわれてしまった
	PRINTFORMW 現在の情勢では停戦の成立は難しいだろう…
	LOCAL:10 = 0
ENDIF

;停戦が成立した場合
IF LOCAL:10
	IF LOCAL:10 == 2
		;性的な要求を飲んだ場合、停戦期間は5固定
		LOCAL:22 = 5
	ELSE
		;停戦期間の設定(好感度・敵対値に応じて延長)
		LOCAL:20 = REL_LIKE:(LOCAL:5):(LOCAL:4) * AI_RELATION:(REL_AI_TYPE:1):(REL_AI_TYPE:0) / 100
		LOCAL:21 = REL_HATE:(LOCAL:5):(LOCAL:4) * 100 / AI_RELATION:(REL_AI_TYPE:1):(REL_AI_TYPE:0)

		LOCAL:22 = 5 + RAND:5
		SIF LOCAL:20 >= AI_RELATION_BORDER:(REL_AI_TYPE:1):2 && LOCAL:21 < AI_RELATION_BORDER:(REL_AI_TYPE:1):3
			LOCAL:22 += LIMIT(LOCAL:20 / 100 - LOCAL:21 / 200, 1, 10) + RAND:5
	ENDIF

	;変数の処理
	FOR LOCAL:0, 0, MAX_TREATY_C
		IF TREATY_C_TERM:(LOCAL:0) <= 0
			TREATY_C_TERM:(LOCAL:0) = LOCAL:22
			TREATY_C_COUNTRY:(LOCAL:0):0 = CFLAG:MASTER:1
			TREATY_C_COUNTRY:(LOCAL:0):1 = ARG:0
			BREAK
		ENDIF
	NEXT

	;参加国間の好感度を上昇させ敵対値を低下させる
	CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:1, ARG:0, 50, -50)
	CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:1, 50, -50)

	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORMW %NAME:(LOCAL:5)%と{LOCAL:22}期間の停戦条約が成立しました
	RESETCOLOR
ENDIF

;-------------------------------------------------
;同盟を申し込む
;-------------------------------------------------
@DIPLOMACY_ALLIANCE(ARG:0)
#DIM REL_AI_TYPE, 2
#DIM REST_COUNTRY
VARSET REL_AI_TYPE, 0

REL_AI_TYPE:0 = COUNTRY_AI_TYPE:(CFLAG:MASTER:1), COUNTRY_AI_TYPE:(ARG:0)

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;相手勢力の外交計画をチェック
CALL AI_DIPLOMACY_CHECK(ARG:0)

REST_COUNTRY = (MAX_COUNTRY - 1) - MATCH(COUNTRY_BOSS, 0)
LOCAL:11 = MAX(REST_COUNTRY * AI_ACTION:(COUNTRY_AI_TYPE:(ARG:0)):3 / 100, 1)
LOCAL:2 = COUNTRY_TREATY_NO:(ARG:0):0 <= LOCAL:11 ? COUNTRY_REQUEST_RATE_A:(ARG:0):(CFLAG:MASTER:1) # -1


;無条件で成立(友好)
IF LOCAL:2 == 5
	PRINTFORMW %ANAME(MASTER)%が同盟を持ちかけると、%NAME:(LOCAL:5)%は快く承諾した…
	LOCAL:10 = 1
;無条件で成立
ELSEIF LOCAL:2 == 1
	PRINTFORMW %ANAME(MASTER)%が同盟を持ちかけると、%NAME:(LOCAL:5)%は少し悩む素振りを見せたものの、すぐに承諾した
	PRINTFORMW どうやらお互いの利害が一致したようだ…
	LOCAL:10 = 1
;相手からの要求を飲めば成立
ELSEIF LOCAL:2 >= 2 && LOCAL:2 <= 4
	;要求の処理
	CALL DIPLOMACY_REQUESTED(ARG:0, LOCAL:2 - 2)
	IF RESULT == -1
		IF LOCAL:2 == 2
			PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%はしばらく悩んだ末に承諾した
			PRINTFORMW どうやらお互いの利害が一致したようだ…
			LOCAL:10 = 1
		ELSE
			PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%はしばらく悩んだ末に断った
			PRINTFORMW 何か対価を用意できれば受け入れてもらえそうだが…
			LOCAL:10 = 0
		ENDIF
	ELSE
		LOCAL:10 = RESULT
	ENDIF
;不成立(非隣接)
ELSEIF LOCAL:2 == 6
	PRINTFORMW %ANAME(MASTER)%は同盟を提案したが、%NAME:(LOCAL:5)%に軽くあしらわれてしまった
	PRINTFORMW 国境を接していない状態での同盟は、よほど良好な関係でなければ無理だろう…
	LOCAL:10 = 0
;不成立
ELSE
	PRINTFORMW %ANAME(MASTER)%は同盟を提案したが、%NAME:(LOCAL:5)%に軽くあしらわれてしまった
	PRINTFORMW 現在の情勢では同盟の成立は難しいだろう…
	LOCAL:10 = 0
ENDIF

;同盟が成立した場合
IF LOCAL:10
	LOCAL:2 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
	LOCALS:0 = 

	;停戦が組まれていれば解消
	FOR LOCAL:0, 0, MAX_TREATY_C
		IF TREATY_C_TERM:(LOCAL:0) > 0
			LOCAL:31 = 0
			FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
				IF TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) == CFLAG:MASTER:1
					LOCAL:31 |= 0b001
				ELSEIF TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) == LOCAL:32
					LOCAL:31 |= 0b010
				ELSEIF TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) >= 1
					LOCAL:31 |= 0b100
				ENDIF
			NEXT
			;対象の２勢力間の停戦を削除し、残り期間を記録
			IF LOCAL:31 == 0b011
				LOCAL:11 = MAX(LOCAL:11, TREATY_C_TERM:(LOCAL:0))
				TREATY_C_TERM:(LOCAL:0) = 0
				FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
					TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
				NEXT
			ENDIF
		ENDIF
	NEXT

	IF LOCAL:10 == 2
		;性的な要求を飲んだ場合、同盟期間は5固定
		LOCAL:22 = 5
	ELSE
		;同盟期間の設定(好感度・敵対値に応じて延長)
		LOCAL:20 = REL_LIKE:(LOCAL:5):(LOCAL:4) * AI_RELATION:(REL_AI_TYPE:1):(REL_AI_TYPE:0) / 100
		LOCAL:21 = REL_HATE:(LOCAL:5):(LOCAL:4) * 100 / AI_RELATION:(REL_AI_TYPE:1):(REL_AI_TYPE:0)

		LOCAL:22 = 5 + RAND:5
		SIF LOCAL:20 >= AI_RELATION_BORDER:(REL_AI_TYPE:1):0 && LOCAL:21 < AI_RELATION_BORDER:(REL_AI_TYPE:1):1
			LOCAL:22 += LIMIT(LOCAL:20 / 100 - LOCAL:21 / 200, 1, 10) + RAND:5
	ENDIF

	;変数の処理
	FOR LOCAL:0, 0, MAX_TREATY_A
		IF TREATY_A_TERM:(LOCAL:0) <= 0
			TREATY_A_TERM:(LOCAL:0) = LOCAL:22
			TREATY_A_COUNTRY:(LOCAL:0):0 = CFLAG:MASTER:1
			TREATY_A_COUNTRY:(LOCAL:0):1 = ARG:0
			BREAK
		ENDIF
	NEXT

	;参加国間の好感度を上昇させ敵対値を低下させる
	CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:1, ARG:0, 100, -100)
	CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:1, 100, -100)

	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORMW %NAME:(LOCAL:5)%と{LOCAL:22}期間の同盟が成立しました
	RESETCOLOR
ENDIF

;-------------------------------------------------
;永久同盟を申し込む
;-------------------------------------------------
@DIPLOMACY_ALLIANCE_INDIFINITE(ARG:0)
CALL CHECK_COUNTRY_RELATION(CFLAG:MASTER:1, ARG:0)
LOCAL:3 = RESULT

IF LOCAL:3 != 3
	PRINTFORMW 永久同盟は、現在同盟中の勢力にしか提案することができません
	RETURN 0
ENDIF

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

LOCAL:6 = REL_LIKE:(LOCAL:5):(LOCAL:4)
LOCAL:7 = REL_HATE:(LOCAL:5):(LOCAL:4)

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に無期限の同盟を提案した

;好感度900以上、敵対値0
IF LOCAL:6 >= 900 && LOCAL:7 <= 0
	PRINTFORMW %NAME:(LOCAL:5)%は%NAME:(LOCAL:4)%と共に天下を統べる決意を固めたようだ…

	;変数の処理
	FOR LOCAL:0, 0, MAX_TREATY_A
		LOCAL:2 = 0
		FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
			IF TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1) == CFLAG:MASTER:1
				LOCAL:2 |= 0b01
			ENDIF
			IF TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1) == ARG:0
				LOCAL:2 |= 0b10
			ENDIF
		NEXT
		IF LOCAL:2 == 0b11
			TREATY_A_TERM:(LOCAL:0) = 9999
			BREAK
		ENDIF
	NEXT

	;参加国間の好感度を上昇させ敵対値を低下させる
	CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:1, ARG:0, 100, -100)
	CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:1, 100, -100)

	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORMW %NAME:(LOCAL:5)%と無期限の同盟が成立しました
	RESETCOLOR

;不成立
ELSE
	PRINTFORML だが、%NAME:(LOCAL:5)%は%ANAME(MASTER)%の提案を断った
	PRINTFORMW まだ%NAME:(LOCAL:4)%のことをそこまで信用できないようだ…
ENDIF

;-------------------------------------------------
;外交併合を行う
;-------------------------------------------------
@DIPLOMACY_PERSONAL_UNION(ARG:0)
CALL CHECK_COUNTRY_RELATION(CFLAG:MASTER:1, ARG:0)
LOCAL:3 = RESULT
IF LOCAL:3 != 3
	PRINTFORMW 外交併合は、現在同盟中の勢力にしか提案することができません
	RETURN 0
ENDIF

IF COUNTRY_EVENT_ID:(ARG:0) == COUNTRY_BANDIT
	PRINTFORMW 野盗などと併合するわけにはいきません
	RETURN 0
ENDIF

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

IF !TMP_COUNTRY_IS_NEIBORING:(ARG:0):(CFLAG:MASTER:1)
	PRINTW 隣接していない勢力と外交併合を行うことはできません
	RETURN 0
ENDIF

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に対して、同君連合を組む提案をした

LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:1)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)


LOCAL:9 = REL_LIKE:(LOCAL:5):(LOCAL:4)
LOCAL:10 = REL_HATE:(LOCAL:5):(LOCAL:4)

LOCAL:8 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:1):(LOCAL:0)
		LOCAL:8 += GET_SUM_ECONOMY(LOCAL:0)
	ENDIF
NEXT

;好感度が１０００以上で、我の君主の政治値が相手より上、勧告側の勢力に隣接する他勢力の経済力を合わせてもなお経済規模が1/2未満。もしくは国家好感度が3000以上
;もしくは敵国君主が隷属で我が国の経済地が対象国の4倍以上のときのみ、併合可能です
IF (LOCAL:8 * 2 < LOCAL:6 && LOCAL:9 >= 1000 && LOCAL:10 <= 0 && ABL:(LOCAL:4):政治 >= ABL:(LOCAL:5):政治) || (TALENT:(LOCAL:5):隷属 && LOCAL:7 * 4 < LOCAL:6) || (LOCAL:9 >= 1500)
	SETCOLOR COLOR("注意")
	PRINTFORMW 素晴らしい！%NAME:(LOCAL:5)%は我が国と１つになりました
	RESETCOLOR

	;兵数の移動
	CALL CLEAR_ALL_UNIT(ARG:0)
	COUNTRY_SOLDIER:(CFLAG:MASTER:1) += COUNTRY_SOLDIER:(ARG:0)
	COUNTRY_SOLDIER:(ARG:0) = 0

	;領地の接収
	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORML %NAME:(LOCAL:5)%の所領だった都市は我が国の領土となりました
	RESETCOLOR
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			CITY_OWNER:(LOCAL:0) = CFLAG:MASTER:1
			PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
		ENDIF
	NEXT
	WAIT

	PRINTL 
	PRINTFORMW %NAME:(LOCAL:5)%軍の兵士が我が国の兵力として加わりました

	;自動で登用
	IF CONFIG:303 == 0
		;士官の組み込み
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORML %NAME:(LOCAL:5)%に仕えていた士官/囚われていた捕虜は我が国の士官として組み込まれました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF (CFLAG:(LOCAL:0):1 == ARG:0 && !CFLAG:(LOCAL:0):9)  || CFLAG:(LOCAL:0):9 == ARG:0
				CALL FORCE_FREE(LOCAL:0)
				CFLAG:(LOCAL:0):1 = CFLAG:MASTER:1
				PRINTFORML %NAME:(LOCAL:0)%を登用しました
			ENDIF
		NEXT
		WAIT

	;自動で投獄
	ELSEIF CONFIG:303 == 1
		;士官の組み込み
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORML %NAME:(LOCAL:5)%に仕えていた士官/囚われていた捕虜を投獄しました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF (CFLAG:(LOCAL:0):1 == ARG:0 && !CFLAG:(LOCAL:0):9)  || CFLAG:(LOCAL:0):9 == ARG:0
				CALL FORCE_FREE(LOCAL:0)
				CFLAG:(LOCAL:0):1 = 0
				CFLAG:(LOCAL:0):9 = CFLAG:MASTER:1
				PRINTFORML %NAME:(LOCAL:0)%を投獄しました
			ENDIF
		NEXT
		WAIT

	;手動で選択
	ELSE
		CALL DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
	ENDIF

	;旧勢力の後始末
	CALL DESTROY_COUNTRY(ARG:0)
ELSE
	PRINTFORMW 残念ながら、%NAME:(LOCAL:5)%は我が国の外交併合に応じませんでした…
ENDIF

RETURN 1

;-------------------------------------------------
;同盟を破棄
;-------------------------------------------------
@DIPLOMACY_ALLIANCE_RENOUNCE(ARG:0)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
CALL CHECK_COUNTRY_RELATION(CFLAG:MASTER:1, ARG:0)
LOCAL:3 = RESULT

IF LOCAL:3 != 3 && LOCAL:3 != 4
	PRINTFORMW 同盟破棄は、そもそも同盟している勢力にしか行えません
	RETURN 0
ENDIF

PRINTFORML %ANAME(LOCAL:5)%と結んでいる同盟を一方的に破棄します
PRINTFORML 相手からはものすごい反発を受けますし、他勢力からもいい顔はされないでしょう
PRINTFORML それでも同盟を破棄しますか？
CALL ASK_YN
IF RESULT == 0
	;削除対象となる同盟の検索
	FOR LOCAL, 0, MAX_TREATY_A
		IF TREATY_A_TERM:LOCAL > 0
			FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
				IF TREATY_A_COUNTRY:LOCAL:(LOCAL:1) == CFLAG:MASTER:1
					FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
						IF TREATY_A_COUNTRY:LOCAL:(LOCAL:2) == ARG:0
							GOTO BREAK_LABEL
						ENDIF
					NEXT
				ENDIF
			NEXT
		ENDIF
	NEXT
	$BREAK_LABEL
	IF LOCAL != MAX_TREATY_A
		PRINTFORML %ANAME(LOCAL:5)%との同盟を破棄しました
		PRINTFORML この行いに、%ANAME(LOCAL:5)%は強い反発を覚えています
		PRINTFORMW また、他の勢力も、不信感を抱いたようです
		FOR LOCAL:1, 0, MAX_COUNTRY
			IF IS_COUNTRY(LOCAL:1) && LOCAL:1 != LOCAL:2
				CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:1, -300, 300)
				IF TREATY_A_TERM:LOCAL == 9999
					CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:1, -500, 500)
				ENDIF
			ELSEIF IS_COUNTRY(LOCAL:1) && LOCAL:1 == LOCAL:2
				CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:1, -800, 800)
				IF TREATY_A_TERM:LOCAL == 9999
					CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:1, -1000, 1000)
				ENDIF
			ENDIF
		NEXT
		CALL GO_TO_MASTERS_COUNTRY(TREATY_A_COUNTRY:LOCAL:(LOCAL:2))
		FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
			TREATY_A_COUNTRY:LOCAL:(LOCAL:1) = 0
		NEXT
		TREATY_A_TERM:LOCAL = 0
	ELSE
		PRINTFORMW 同盟破棄を選択した途中でエラーだよ　ここにはこないはずだよ
		PRINTFORMW もしこのメッセージを見かけたら教えてね 喚くより
		RETURN 0
	ENDIF
ELSE
	RETURN 0
ENDIF

;-------------------------------------------------
;会談する
;-------------------------------------------------
@DIPLOMACY_TALK(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

$SHOW_LOOP
PRINTFORML %ANAME(MASTER)%は%NAME:(LOCAL:5)%と%NAME:(LOCAL:4)%の会談の場を設けた
PRINTFORML ………………
PRINTFORML 昔から接待といえば、金か「楽しみ」が常道だ
PRINTFORML %NAME:(LOCAL:5)%を満足させるには、どちらかを用意すべきだろう……
DRAWLINE
LOCAL:1 = MIN(3000 + (GET_OWN_CITY(CFLAG:MASTER:所属) * 300), 10000)
IF MONEY >= LOCAL:1
	PRINTFORML [1] 金{LOCAL:1}を用意する 現在:{MONEY}
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAINFORM [1] 金{LOCAL:1}が必要だが、そんなに持っていない 現在:{MONEY}
	PRINTL
	RESETCOLOR
ENDIF
IF ABL:(LOCAL:5):性知識 >= 3
	PRINTFORML [2] 楽しみを用意する
ELSEIF ABL:(LOCAL:5):性知識 <= 2
	SETCOLOR COLOR("選択不可")
	PRINTPLAINFORM [2] 相手方に性の知識が浅く、「楽しみ」を提供しても効果を見込めない
	PRINTL
	RESETCOLOR
ENDIF
PRINTFORML [0] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 1 && MONEY >= LOCAL:1
	PRINTFORML %ANAME(MASTER)%は%NAME:(LOCAL:5)%に金を送った……
	MONEY -= LOCAL:1
	PRINTL
	RESULT = 30 + MIN(LOCAL:1 / 300, 30)
ELSEIF RESULT == 2 && ABL:(LOCAL:5):性知識 >= 3
	CALL DIPLOMACY_TALK_SEX(LOCAL:5)
	IF RESULT == -1
		GOTO SHOW_LOOP
	ELSE
		PRINTL 
	ENDIF
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF


;好感度上昇・敵対値下降基準値
LOCAL:2 = RESULT

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = 5140 + POWER_075(TMP_GET_POLITICS_POWER(CFLAG:MASTER:1))
LOCAL:1 = 5140 + POWER_075(TMP_GET_COOKING_POWER(CFLAG:MASTER:1))
LOCAL:2 = LOCAL:2 * (LOCAL:0 + LOCAL:1) / 10280
FOR LOCAL, 0, CHARANUM
	IF TALENT:LOCAL:風説流布 && RAND:100 < 20 && CFLAG:MASTER:所属 == CFLAG:LOCAL:所属 && IS_FREE(LOCAL)
		SETCOLOR COLOR("注意")
		PRINTFORMW %ANAME(LOCAL)%は、ついでに%ANAME(LOCAL:4)%の良い噂をながしたようだ……
		TIMES LOCAL:2, 1.2
	ENDIF
NEXT
;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:2)
RETURN 1

;-------------------------------------------------
;兵を譲り渡す
;-------------------------------------------------
@DIPLOMACY_GIVE_SOLDIER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = COUNTRY_SOLDIER:(CFLAG:MASTER:1)

PRINTFORML 総兵数:  {GET_SUM_SOLDIER(CFLAG:MASTER:1)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(CFLAG:MASTER:1)}
PRINTFORML 残存兵数:{LOCAL:6}
DRAWLINE

IF LOCAL:6 < 500
	PRINTFORMW 最低でも500以上の残存兵数が必要です
	RETURN 0
ENDIF

PRINTFORML 譲渡する兵数を入力して下さい(0以下でキャンセル)

CALL DRAW_SOLDIER_INPUT(LOCAL:6)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 > 0 && LOCAL:2 < 500
	CLEARLINE 1
	PRINTL 最低でも500以上の兵を譲渡する必要があります
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6
	CLEARLINE 1
	PRINTL 兵数が不足しています
	GOTO INPUT_LOOP
ENDIF

COUNTRY_SOLDIER:(CFLAG:MASTER:1) -= LOCAL:2
COUNTRY_SOLDIER:(ARG:0) += LOCAL:2

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に兵{LOCAL:2}を譲り渡した

;好感度上昇値・敵対値下降値の計算
;同じ兵数でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:10 = GET_SUM_ECONOMY(CFLAG:MASTER:1)
LOCAL:11 = GET_SUM_ECONOMY(ARG:0)
LOCAL:12 = POWER_075(LOCAL:2 * 13000 / LOCAL:10) + POWER_075(LOCAL:2 * 13000 / LOCAL:11)
LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = 5140 + POWER_075(TMP_GET_POLITICS_POWER(CFLAG:MASTER:1))
LOCAL:1 = 5140 + POWER_075(TMP_GET_COOKING_POWER(CFLAG:MASTER:1))
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1) / 10280

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1

;-------------------------------------------------
;経済力を譲り渡す
;-------------------------------------------------
@DIPLOMACY_GIVE_ECONOMY(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:1)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)

LOCAL:20 = MAX(STRLENS(NAME:(LOCAL:5)), STRLENS(NAME:(LOCAL:4)))
LOCAL:21 = MAX(GET_DIGIT(LOCAL:6 / 100), GET_DIGIT(LOCAL:7 / 100))

PRINTFORML %NAME:(LOCAL:4), LOCAL:20%の経済力:{LOCAL:6 / 100, LOCAL:21}
PRINTFORML %NAME:(LOCAL:5), LOCAL:20%の経済力:{LOCAL:7 / 100, LOCAL:21}
DRAWLINE

IF LOCAL:6 < 250
	PRINTFORMW 最低でも250以上の経済力が必要です
	RETURN 0
ENDIF

PRINTFORML 譲渡する経済力を入力して下さい(0以下でキャンセル、最大で経済力の20\%まで)

CALL DRAW_ECONOMY_INPUT(LOCAL:6 / 500)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 < 50
	CLEARLINE 1
	PRINTL 最低でも50以上の経済力を譲渡する必要があります
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6 / 500
	CLEARLINE 1
	PRINTFORML 上限を超えています({LOCAL:6 / 500}まで)
	GOTO INPUT_LOOP
ENDIF

;経済力の移動処理
CALL SHIFT_ECONOMY(CFLAG:MASTER:1, ARG:0, LOCAL:2 * 100)

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に経済力{LOCAL:2}を譲り渡した

;好感度上昇値・敵対値下降値の計算
;同じ経済力でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:12 = POWER_075(LOCAL:2 * 115000 / LOCAL:6) + POWER_075(LOCAL:2 * 115000 / LOCAL:7)

LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = 5140 + POWER_075(TMP_GET_POLITICS_POWER(CFLAG:MASTER:1))
LOCAL:1 = 5140 + POWER_075(TMP_GET_COOKING_POWER(CFLAG:MASTER:1))
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1) / 10280

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1

;-------------------------------------------------
;士官を移籍させる
;-------------------------------------------------
@DIPLOMACY_GIVE_CHARA(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;自国の士官の数を数える(君主・主人公・借り物除く)
LOCAL:6 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):9 && !TALENT:(LOCAL:0):借り物 && CFLAG:(LOCAL:0):4 >= 100
		LOCAL:6 ++
	ENDIF
NEXT

IF LOCAL:6 <= 0
	PRINTFORMW 移籍させられる士官がいません
	RETURN 0
ENDIF

LOCAL:20 = 1

;1ページに表示する最大キャラ数の設定
LOCAL:7 = 44

;ページ数を計算
LOCAL:8 = (LOCAL:6 - 1) / LOCAL:7 + 1

$SHOW_LOOP1

DRAWLINE
PRINTFORML 移籍させる士官を選択して下さい
DRAWLINE

$SHOW_LOOP2

LOCAL:10 = LOCAL:7 * (LOCAL:20 - 1)
LOCAL:11 = LOCAL:7 * LOCAL:20
LOCAL:12 = 0
LOCAL:13 = 0

FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):9 && !TALENT:(LOCAL:0):借り物 && CFLAG:(LOCAL:0):4 >= 100
		IF LOCAL:13 >= LOCAL:10 && LOCAL:13 < LOCAL:11
			IF LOCAL:12 % 2 != 0
				PRINTPLAIN   
			ELSE
				IF LOCAL:12 >= 1
					PRINTL 
				ENDIF
				PRINTPLAIN   
			ENDIF
			CALL PRINT_PARTNER_DATA(LOCAL:0)
			LOCAL:12 ++
		ENDIF
		LOCAL:13 ++
	ENDIF
NEXT
IF LOCAL:12 >= 1
	PRINTL 
ENDIF

IF LOCAL:8 >= 2
	DRAWLINE
	PRINTPLAIN   
	IF LOCAL:20 >= 2
		PRINTBUTTON "  54[前のページ    ]", 54
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   54[前のページ    ]
		RESETCOLOR
	ENDIF

	PRINTPLAINFORM         page{LOCAL:20, 2, RIGHT}/{LOCAL:8, 2, LEFT}     
	IF LOCAL:20 < LOCAL:8
		PRINTBUTTON "  56[次のページ    ]", 56
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   56[次のページ    ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAIN   
PRINTBUTTON "   0[戻る          ]", 0

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 54 && LOCAL:20 >= 2
	LOCAL:20 --
	CLEARLINE (LOCAL:12 + 1) / 2 + 5
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56 && LOCAL:20 < LOCAL:8
	LOCAL:20 ++
	CLEARLINE (LOCAL:12 + 1) / 2 + 5
	GOTO SHOW_LOOP2
ENDIF

LOCAL:15 = NO_TO_CHARA(RESULT - 100)

IF LOCAL:15 >= 0 && CFLAG:(LOCAL:15):1 == CFLAG:MASTER:1 && LOCAL:15 != MASTER && LOCAL:15 != LOCAL:4 && !CFLAG:(LOCAL:15):9 && !TALENT:(LOCAL:15):借り物 && CFLAG:(LOCAL:15):4 >= 100
	REDRAW 1

	;対象キャラの情報を表示
	DRAWLINE
	CALL SHOW_INFO(LOCAL:15)
	DRAWLINE
	PRINTFORML このキャラを移籍させますか？

	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 0
		PRINTFORML %ANAME(LOCAL:15)%は%NAME:(LOCAL:5)%勢力の士官になりました

		CALL FORCE_FREE(LOCAL:15)
		CFLAG:(LOCAL:15):1 = ARG:0

		;好感度上昇値・敵対値下降値の計算
		;主に能力により決定。相手君主がそのキャラに良い印象を抱いていればボーナス
		LOCAL:25 = 0
		LOCAL:25 += ABL_POWER(ABL:(LOCAL:15):武闘) + ABL_POWER(ABL:(LOCAL:15):知略) + ABL_POWER(ABL:(LOCAL:15):政治)
		LOCAL:25 += ABL_POWER(ABL:(LOCAL:15):歌唱) + ABL_POWER(ABL:(LOCAL:15):料理)
		LOCAL:25 /= 400

		IF REL_LIKE:(LOCAL:5):(LOCAL:15) >= 800
			LOCAL:25 += 50
		ELSEIF REL_LIKE:(LOCAL:5):(LOCAL:15) >= 300
			LOCAL:25 += 20
		ENDIF

		LOCAL:25 = LIMIT(LOCAL:25, 25, 400)

		;政治・料理の補正
		CALL TMP_CREATE_IS_FREE_MAP
		LOCAL:0 = 5140 + POWER_075(TMP_GET_POLITICS_POWER(CFLAG:MASTER:1))
		LOCAL:1 = 5140 + POWER_075(TMP_GET_COOKING_POWER(CFLAG:MASTER:1))
		LOCAL:25 = LOCAL:25 * (LOCAL:0 + LOCAL:1) / 10280

		;互いの国同士の好感度を上昇させ敵対値を低下させる
		CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
		RETURN 1
	ENDIF
	GOTO SHOW_LOOP1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;降伏勧告を行う
;-------------------------------------------------
@DIPLOMACY_SURRENDER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

IF !TMP_COUNTRY_IS_NEIBORING:(ARG:0):(CFLAG:MASTER:1)
	PRINTW 隣接していない勢力に降伏勧告を行うことはできません
	RETURN 0
ENDIF

IF COUNTRY_EVENT_ID:(ARG:0) == COUNTRY_BANDIT
	PRINTFORMW 野盗どもは勧告を突っぱねてきました
	RETURN 0
ENDIF

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に対して降伏するように諭した
PRINTFORMW ………………
PRINTFORMW ……


;要求受け入れ判定
IF DIPLOMACY_REQUEST_CHECK(CFLAG:MASTER:1, ARG:0, 40 + FLAG:99)
	SETCOLOR COLOR("注意")
	PRINTFORMW %NAME:(LOCAL:5)%は我が国の勧告を受け入れ降伏しました
	RESETCOLOR

	;兵数の移動
	CALL CLEAR_ALL_UNIT(ARG:0)
	COUNTRY_SOLDIER:(CFLAG:MASTER:1) += COUNTRY_SOLDIER:(ARG:0)
	COUNTRY_SOLDIER:(ARG:0) = 0

	;領地の接収
	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORML %NAME:(LOCAL:5)%の所領だった都市は我が国の領土となりました
	RESETCOLOR
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			CITY_OWNER:(LOCAL:0) = CFLAG:MASTER:1
			PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
		ENDIF
	NEXT
	WAIT

	PRINTL 
	PRINTFORMW %NAME:(LOCAL:5)%軍の兵士が我が国の兵力として加わりました

	;自動で登用
	IF CONFIG:303 == 0
		;士官の組み込み
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORML %NAME:(LOCAL:5)%に仕えていた士官/囚われていた捕虜は我が国の士官として組み込まれました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF (CFLAG:(LOCAL:0):1 == ARG:0 && (!CFLAG:(LOCAL:0):9 || CFLAG:(LOCAL:0):9 == CFLAG:MASTER:1))  || CFLAG:(LOCAL:0):9 == ARG:0
				CALL FORCE_FREE(LOCAL:0)
				CFLAG:(LOCAL:0):1 = CFLAG:MASTER:1
				CFLAG:(LOCAL:0):9 = 0
				PRINTFORML %NAME:(LOCAL:0)%を登用しました
			ENDIF
		NEXT
		WAIT

	;自動で投獄
	ELSEIF CONFIG:303 == 1
		;士官の組み込み
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORML %NAME:(LOCAL:5)%に仕えていた士官/囚われていた捕虜を投獄しました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF (CFLAG:(LOCAL:0):1 == ARG:0 && (!CFLAG:(LOCAL:0):9 || CFLAG:(LOCAL:0):9 == CFLAG:MASTER:1))  || CFLAG:(LOCAL:0):9 == ARG:0
				CALL FORCE_FREE(LOCAL:0)
				CFLAG:(LOCAL:0):1 = 0
				CFLAG:(LOCAL:0):9 = CFLAG:MASTER:1
				PRINTFORML %NAME:(LOCAL:0)%を投獄しました
			ENDIF
		NEXT
		WAIT

	;手動で選択
	ELSE
		CALL DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
	ENDIF

	;旧勢力の後始末
	CALL DESTROY_COUNTRY(ARG:0)
	
	;外交補正の加算処理
	FLAG:99 += 5
ELSE
	PRINTFORMW 残念ながら、%NAME:(LOCAL:5)%は我が国の降伏勧告に応じませんでした…
	;外交補正の加算処理（他国の警戒が上がったと考えていただければ）
	FLAG:99 += 8
ENDIF

RETURN 1

;-------------------------------------------------
;降伏した勢力の士官の処遇を個別に設定 ARG:0=降伏した勢力
;-------------------------------------------------
@DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
;キャラの扱い設定用変数
#DIM CHARA_TREATMENT, 1000
#DIM CONST IN_PAGE_CHARA_NUM = 22
#DIM COMMANDER_NUM
#DIM LCOUNT
#DIM MAX_PAGE
#DIM PAGE
#DIM CHARA_COUNT_FOR_PRINT
#DIM FIRST_CHARA_COUNT_IN_THIS_PAGE
#DIM FIRST_CHARA_COUNT_IN_NEXT_PAGE
#DIM SELECTED_CHARA
#DIM SELECTED_TREATMENT
#DIM INPUT_LINECOUNT
#DIM SHOW_LINECOUNT

VARSET CHARA_TREATMENT, 0

;●士官の人数をカウント
COMMANDER_NUM = GET_COMMANDER_NUM(ARG:0)

;士官が一人もいなければ戻る
IF COMMANDER_NUM == 0
	DEBUGPRINTFORML {ARG:0}番号の%NAME:(GET_COUNTRY_BOSS(ARG:0))%勢力に士官が誰もいませんでした
	RETURN
ENDIF

;●表示部分
MAX_PAGE = (COMMANDER_NUM - 1) / IN_PAGE_CHARA_NUM + 1
PAGE = 1

;SHOW_LOOP
SHOW_LINECOUNT = LINECOUNT
WHILE 1

	DRAWLINE
	PRINTFORML %NAME:(GET_COUNTRY_BOSS(ARG:0))%に仕えていた士官/囚われていた捕虜の処遇を決めて下さい
	DRAWLINE

	CHARA_COUNT_FOR_PRINT = 0
	FIRST_CHARA_COUNT_IN_THIS_PAGE = (PAGE - 1) * IN_PAGE_CHARA_NUM
	LOCAL:13 = PAGE * IN_PAGE_CHARA_NUM
	FOR LCOUNT, 0, CHARANUM
		IF CFLAG:LCOUNT:9 == ARG:0
			CFLAG:LCOUNT:1 = ARG:0
			CFLAG:LCOUNT:9 = 0
		ENDIF
		IF CFLAG:LCOUNT:1 == ARG:0 && (!CFLAG:LCOUNT:9 || CFLAG:LCOUNT:9 == CFLAG:MASTER:1)
			IF CHARA_COUNT_FOR_PRINT >= FIRST_CHARA_COUNT_IN_THIS_PAGE && CHARA_COUNT_FOR_PRINT < LOCAL:13
				PRINTFORM %ANAME(LCOUNT), MAX_CHARANAME_LENGTH, RIGHT% …… 
				IF CHARA_TREATMENT:LCOUNT == 0
					SETCOLOR COLOR("選択中")
					PRINTPLAIN [登用]
					RESETCOLOR
					PRINTBUTTON "[投獄]", LCOUNT * 2 + 101
				ELSE
					PRINTBUTTON "[登用]", LCOUNT * 2 + 100
					SETCOLOR COLOR("選択中")
					PRINTPLAIN [投獄]
					RESETCOLOR
				ENDIF
				PRINTL 
			ENDIF
			CHARA_COUNT_FOR_PRINT ++
		ENDIF
	NEXT
	DRAWLINE

	IF MAX_PAGE >= 2
		LOCALS:0 = 
		IF PAGE >= 2
			LOCALS:0 = [8] 前のページ
		ENDIF
		PRINTFORM %LOCALS:0, 25, LEFT%

		LOCALS:0 = page{PAGE}/{MAX_PAGE}
		PRINTPLAINFORM %LOCALS:0, 25, LEFT%

		LOCALS:0 = 
		IF PAGE < MAX_PAGE
			LOCALS:0 = [9] 次のページ
		ENDIF
		PRINTFORM %LOCALS:0, 25, LEFT%
		PRINTL 
		DRAWLINE
	ENDIF
	PRINT [1] 全員を登用に設定
	PRINTPLAIN     
	PRINT [2] 全員を投獄に設定
	PRINTL 
	DRAWLINE
	PRINTL [0] 決定

	REDRAW 2

	;INPUT_LOOP
	INPUT_LINECOUNT = LINECOUNT
	WHILE 1
		INPUT

		IF RESULT == 0
			FOR LCOUNT, 0, CHARANUM
				IF CFLAG:LCOUNT:1 == ARG:0 && (!CFLAG:LCOUNT:9 || CFLAG:LCOUNT:9 == CFLAG:MASTER:1)
					CALL FORCE_FREE(LCOUNT)
					IF CHARA_TREATMENT:LCOUNT == 0
						CFLAG:LCOUNT:1 = CFLAG:MASTER:1
						CFLAG:LCOUNT:9 = 0
					ELSE
						CFLAG:LCOUNT:1 = 0
						CFLAG:LCOUNT:9 = CFLAG:MASTER:1
					ENDIF
				ENDIF
			NEXT
			REDRAW 1
			RETURN
		ELSEIF RESULT == 1
			VARSET CHARA_TREATMENT, 0
			BREAK
		ELSEIF RESULT == 2
			VARSET CHARA_TREATMENT, 1
			BREAK
		ELSEIF RESULT == 8 && PAGE >= 2
			PAGE --
			BREAK
		ELSEIF RESULT == 9 && PAGE < MAX_PAGE
			PAGE ++
			BREAK
		ELSEIF RESULT >= 100
			SELECTED_CHARA = (RESULT - 100) / 2
			SELECTED_TREATMENT = (RESULT - 100) % 2
			IF SELECTED_CHARA < CHARANUM && CFLAG:SELECTED_CHARA:1 == ARG:0 && !CFLAG:SELECTED_CHARA:9
				CHARA_TREATMENT:(SELECTED_CHARA) = SELECTED_TREATMENT
				BREAK
			ENDIF
			PRINTW 不適切なキャラ番号です
		ENDIF
		CLEARLINE LINECOUNT - INPUT_LINECOUNT
	WEND
	CLEARLINE LINECOUNT - SHOW_LINECOUNT
WEND

;-------------------------------------------------
;醜聞を流す
;-------------------------------------------------
@DIPLOMACY_CREATE_SCANDAL(ARG:0)
VARSET LOCAL,0
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
DRAWLINE
PRINTFORML %ANAME(LOCAL:5)%に関する醜聞を他国に流し、関係を悪化させます
PRINTFORML その代わり、%ANAME(LOCAL:5)%と自国との関係がこじれます
PRINTFORML 政治力の高い相手なら、出所がバレてさらに関係が悪化するでしょう
PRINTFORM また、金
SETCOLOR COLOR("注意")
PRINTFORM 5000
RESETCOLOR
PRINTFORML が必要です
PRINTFORML 本当に醜聞を流して大丈夫ですか？ 現在の資金:{MONEY}
CALL ASK_YN
IF RESULT == 0
	IF MONEY < 5000
		PRINTFORMW 金が足りなかった……
		RETURN 0
	ELSE
		PRINTFORM %ANAME(LOCAL:5)%が、
		SELECTCASE RAND:13
			CASE 0
				PRINTFORM とんでもないオナニー中毒だ
			CASE 1
				PRINTFORM 無実の人を捕らえては処刑している
			CASE 2
				IF IS_MALE(LOCAL:5)
					PRINTFORM 少女をさらっては手込めにしている
				ELSE
					PRINTFORM 少年をさらっては食い散らかしている
				ENDIF
			CASE 3
				IF IS_MALE(LOCAL:5)
					PRINTFORM 女を囲ってハーレムを作り上げている
				ELSE
					PRINTFORM 男をはべらして毎夜乱交している
				ENDIF
			CASE 4
				PRINTFORM むやみに高い税をかけて民を苦しめている
			CASE 5
				PRINTFORM 逆らう家臣は皆処刑している
			CASE 6
				PRINTFORM 民が飢えているにもかかわらず、自らは豪遊している
			CASE 7
				PRINTFORM 色事にうつつをぬかし、政治をおろそかにしている
			CASE 8
				PRINTFORM 実はとても尻穴が敏感だ
			CASE 9
				PRINTFORM 正気を失っており、まともに政治ができない状態だ
			CASE 10
				IF IS_MALE(LOCAL:5)
					PRINTFORM 女とみれば誰でも犯すヤリチンだ
				ELSE
					PRINTFORM 男とみれば誰にでも股をひらくヤリマンだ
				ENDIF
			CASE 11
				IF IS_MALE(LOCAL:5)
					PRINTFORM 男娼として働いている
				ELSE
					PRINTFORM 娼婦として働いている
				ENDIF
			CASE 12
				PRINTFORM 未だにたまにおねしょする
			CASE 13
				PRINTFORM 怪しげな薬物に依存している
		ENDSELECT
		PRINTFORML という噂を流しました……
		PRINTFORML 各国は少なからず動揺したようです
		;先に政治と料理による補正値を計算
		CALL TMP_CREATE_IS_FREE_MAP
		LOCAL:1 = 5140 + POWER_075(TMP_GET_POLITICS_POWER(CFLAG:MASTER:1))
		LOCAL:2 = 5140 + POWER_075(TMP_GET_COOKING_POWER(CFLAG:MASTER:1))
		FOR LOCAL, 0, MAX_COUNTRY
			;基礎値取得
			LOCAL:3 = RAND:10 + 20
			IF IS_COUNTRY(LOCAL)
				;プレイヤー国家の場合
				IF CFLAG:MASTER:1 == LOCAL
					;政治と料理が高いほど割る数が大きい = 悪感情を得ずに済む
					LOCAL:3 = LOCAL:3 * 10280 / (LOCAL:1 + LOCAL:2)
					LOCAL:6 = 5140 + POWER_075(TMP_GET_POLITICS_POWER(CFLAG:(LOCAL:5):1))
					;ただし相手の方が政治力が高いとバレる
					IF LOCAL:6 > LOCAL:1
						PRINTFORML 噂の出所が%ANAME(LOCAL:5)%にバレたようだ……
						LOCAL:3 *= 2
					ENDIF
					CALL CHANGE_RELATION_C_TO_C(ARG:0, LOCAL, 0, LOCAL:3)
				ELSE
					;政治と料理が高いほどかける数が大きい = 悪感情を得させやすい
					LOCAL:3 = LOCAL:3 * (LOCAL:1 + LOCAL:2) / 10280
					IF COUNTRY_AI_TYPE:(ARG:0) == AI_外交
						TIMES LOCAL:3, 0.80
					ELSEIF COUNTRY_AI_TYPE:(ARG:0) == AI_好戦
						TIMES LOCAL:3, 1.20
					ENDIF
					SIF LOCAL == ARG:0
						CONTINUE
					FOR LOCAL:6, 0, CHARANUM
						IF TALENT:(LOCAL:6):風説流布 && RAND:100 < 5 && CFLAG:MASTER:所属 == CFLAG:(LOCAL:6):所属 && IS_FREE(LOCAL:6)
							SETCOLOR COLOR("注意")
							PRINTFORMW %ANAME(LOCAL:6)%の流した噂が、じわじわと聞いている……
							RESETCOLOR
							TIMES LOCAL:3, 1.2
						ENDIF
					NEXT
					PRINTFORM %ANAME(GET_COUNTRY_BOSS(LOCAL)) + "との関係", 20, LEFT%:
					CALL TMP_PRINT_COUNTRY_RELATION2(GET_COUNTRY_BOSS(LOCAL), GET_COUNTRY_BOSS(ARG:0))
					CALL CHANGE_RELATION_C_TO_C(LOCAL, ARG:0, -1 * LOCAL:3, LOCAL:3)
					PRINT →
					CALL TMP_PRINT_COUNTRY_RELATION2(GET_COUNTRY_BOSS(LOCAL), GET_COUNTRY_BOSS(ARG:0))
					PRINTL
				ENDIF
			ENDIF
		NEXT
		PRINTW
		PRINTFORML 資金:{MONEY} → {MONEY - 5000}
		MONEY -= 5000
		PRINTFORMW 
		RETURN 1
	ENDIF
ELSE
	RETURN 0
ENDIF


;-------------------------------------------------
;人質交渉
;-------------------------------------------------
@DIPLOMACY_PRISONER_TRADE(ARG:0)
VARSET LOCAL, 0
CVARSET CFLAG, 58, 0

LOCAL:12 = 1
LOCAL:22 = 1

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;士官の部隊所属マップを作成
CALL TMP_CREATE_IS_FREE_MAP

;政治能力の差による補正点数の計算
LOCAL:32 = TMP_GET_POLITICS_POWER(CFLAG:MASTER:1)
LOCAL:33 = TMP_GET_POLITICS_POWER(ARG:0)
LOCAL:34 = SQRT(ABS(LOCAL:32 - LOCAL:33) * 4)
IF LOCAL:32 >= LOCAL:33
	LOCAL:35 = 1000 + LOCAL:34
	LOCAL:36 = 1000000 / (1000 + LOCAL:34)
ELSE
	LOCAL:36 = 1000 + LOCAL:34
	LOCAL:35 = 1000000 / (1000 + LOCAL:34)
ENDIF

;両国の捕虜の数をそれぞれカウント
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):1 == ARG:0 && CFLAG:(LOCAL:0):9 == CFLAG:MASTER:1
		LOCAL:10 ++
	ELSEIF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && CFLAG:(LOCAL:0):9 == ARG:0
		LOCAL:20 ++
	ENDIF
NEXT

;各ページ数を計算
LOCAL:11 = MAX(LOCAL:10 - 1, 0) / 20 + 1
LOCAL:21 = MAX(LOCAL:20 - 1, 0) / 20 + 1

$SHOW_LOOP_INIT

DRAWLINE
PRINTL ★★お互いの解放する捕虜を選択して下さい★★
DRAWLINE
PRINTFORML 【囚えている捕虜】%TOSTR_SPACE(11)%価値   【囚えられている捕虜】%TOSTR_SPACE(7)%価値

$SHOW_LOOP

;互いの捕虜をリストに表示
LOCAL:13 = 0
LOCAL:14 = (LOCAL:12 - 1) * 20
LOCAL:15 = LOCAL:12 * 20
LOCAL:16 = 0
LOCAL:23 = 0
LOCAL:24 = (LOCAL:22 - 1) * 20
LOCAL:25 = LOCAL:22 * 20
LOCAL:26 = 0
FOR LOCAL:1, 0, 20
	LOCAL:17 = 1
	FOR LOCAL:0, LOCAL:13, CHARANUM
		IF CFLAG:(LOCAL:0):1 == ARG:0 && CFLAG:(LOCAL:0):9 == CFLAG:MASTER:1
			IF LOCAL:16 >= LOCAL:14 && LOCAL:16 < LOCAL:15
				PRINTPLAIN   
				IF CFLAG:(LOCAL:0):58
					SETCOLOR COLOR("選択中")
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				PRINT   ┃
				LOCAL:13 = LOCAL:0 + 1
				LOCAL:16 ++
				LOCAL:17 = 0
				BREAK
			ELSE
				LOCAL:16 ++
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:17
		PRINTFORM %TOSTR_SPACE(35)%┃
	ENDIF
	LOCAL:27 = 1
	FOR LOCAL:0, LOCAL:23, CHARANUM
		IF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && CFLAG:(LOCAL:0):9 == ARG:0
			IF LOCAL:26 >= LOCAL:24 && LOCAL:26 < LOCAL:25
				PRINTPLAIN  
				IF CFLAG:(LOCAL:0):58
					SETCOLOR COLOR("選択中")
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				LOCAL:23 = LOCAL:0 + 1
				LOCAL:26 ++
				LOCAL:27 = 0
				BREAK
			ELSE
				LOCAL:26 ++
			ENDIF
		ENDIF
	NEXT
	PRINTL 
NEXT

PRINTPLAIN    
IF LOCAL:12 <= 1
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 10[←前]
	RESETCOLOR
ELSE
	PRINTBUTTON "10[←前]", 10
ENDIF
PRINTPLAINFORM     {LOCAL:12, 2, RIGHT}/{LOCAL:11, 2, LEFT}    
IF LOCAL:12 >= LOCAL:11
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 11[次→]
	RESETCOLOR
ELSE
	PRINTBUTTON "11[次→]", 11
ENDIF
PRINTPLAIN    ┃  
IF LOCAL:22 <= 1
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 20[←前]
	RESETCOLOR
ELSE
	PRINTBUTTON "20[←前]", 20
ENDIF
PRINTPLAINFORM     {LOCAL:22, 2, RIGHT}/{LOCAL:21, 2, LEFT}    
IF LOCAL:22 >= LOCAL:21
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 21[次→]
	RESETCOLOR
ELSE
	PRINTBUTTON "21[次→]", 21
ENDIF
PRINTL 

;●交渉点数の計算
;選択した士官の点数を合計
LOCAL:30 = 0
LOCAL:31 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):58
		IF CFLAG:(LOCAL:0):1 == ARG:0 && CFLAG:(LOCAL:0):9 == CFLAG:MASTER:1
			LOCAL:30 += GET_PRISONER_VALUE(LOCAL:0)
		ELSEIF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && CFLAG:(LOCAL:0):9 == ARG:0
			LOCAL:31 += GET_PRISONER_VALUE(LOCAL:0)
		ENDIF
	ENDIF
NEXT

LOCAL:37 = LOCAL:30 * LOCAL:35 / 1000
LOCAL:38 = LOCAL:31 * LOCAL:36 / 1000
LOCAL:39 = LOCAL:37 - LOCAL:38
LOCAL:45 = MAX(GET_DIGIT(LOCAL:30), GET_DIGIT(LOCAL:31), 4)

DRAWLINE
PRINTFORM   解放点数: {ABS(LOCAL:30), LOCAL:45}(価値合計) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR COLOR("シアン")
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR COLOR("オレンジ")
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:35, 3)%
RESETCOLOR
PRINTFORML (政治能力差) = {LOCAL:37}

PRINTFORM   要求点数: {ABS(LOCAL:31), LOCAL:45}(価値合計) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR COLOR("シアン")
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR COLOR("オレンジ")
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:36, 3)%
RESETCOLOR
PRINTFORML (政治能力差) = {LOCAL:38}

PRINTFORM       合計:
IF LOCAL:39 == 0
	LOCALS:0 =  {ABS(LOCAL:39)}
ELSEIF LOCAL:39 > 0
	LOCALS:0 = +{ABS(LOCAL:39)}
	SETCOLOR COLOR("シアン")
ELSE
	LOCALS:0 = -{ABS(LOCAL:39)}
	SETCOLOR COLOR("オレンジ")
ENDIF
PRINTFORM %LOCALS:0, LOCAL:45 + 1%
RESETCOLOR
PRINTL 

LOCAL:40 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):58
		IF CFLAG:(LOCAL:0):1 == ARG:0 && CFLAG:(LOCAL:0):9 == CFLAG:MASTER:1
			LOCAL:40 = 1
			BREAK
		ELSEIF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && CFLAG:(LOCAL:0):9 == ARG:0
			LOCAL:40 = 1
			BREAK
		ENDIF
	ENDIF
NEXT

IF LOCAL:40
	PRINTBUTTON "  0[差分を兵で補填して精算]", 0
	PRINT     
	PRINTBUTTON "  1[差分を経済力で補填して精算]", 1
	PRINT     
	IF LOCAL:39 >= 0
	PRINTBUTTON "  2[かまわず返還]", 2
	ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN   2[かまわず返還]
	RESETCOLOR
	ENDIF
	
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN   0[差分を兵で補填して精算]      1[差分を経済力で補填して精算]      2[かまわず返還]
	RESETCOLOR

ENDIF
PRINTL 
DRAWLINE
PRINTBUTTON " 99[戻る]", 99
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 99
	REDRAW 1
	RETURN
ELSEIF RESULT == 0 && LOCAL:40
	;要求兵数は、両国の規模の平均値に比例
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:1) + GET_SUM_ECONOMY(ARG:0)
	LOCAL:52 = GET_SUM_SOLDIER(CFLAG:MASTER:1)
	LOCAL:53 = GET_SUM_ARMY_DF(CFLAG:MASTER:1)
	LOCAL:54 = MAX(GET_SUM_SOLDIER(ARG:0) - GET_OWN_CITY(ARG:0) * 500, 0)
	LOCAL:55 = MAX(GET_SUM_SOLDIER(CFLAG:MASTER:1) - GET_OWN_CITY(CFLAG:MASTER:1) * 500, 0)

	LOCAL:51 = ABS(LOCAL:39) * LOCAL:50 / 360000
	
	;ただし、自国が補填してもらう場合（解放点数が要求点数より大きい場合）、最大でも補填国の40%が限界
	;そして、自国が補填する場合（要求点数が解放点数以上の場合）、算出した要求兵数と合計点数のうち、大きい方を採用
	IF LOCAL:39 > 0
		LOCAL:51 = MIN(LOCAL:51, LOCAL:54 * 40 / 100)
	ELSE
		LOCAL:51 = MAX(LOCAL:51, ABS(LOCAL:39))
	ENDIF
	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %NAME:(LOCAL:5)%に対して兵{LOCAL:51}を要求することができます
		PRINTFORML 現在の総兵数:{LOCAL:52}(うち遊撃兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:1)})
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:51
				PRINTFORML %NAME:(LOCAL:5)%は要求を満たすだけの兵を用意できないようだ…(準備可能:{LOCAL:54})
				CALL ASK_YN("あるだけよこせ！", "それなら交渉は決裂だ")
				IF RESULT == 0
					LOCAL:51 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;相手勢力から自国に兵を移動
				CALL SHIFT_SOLDIER(ARG:0, CFLAG:MASTER:1, LOCAL:51)
				PRINTFORMW %NAME:(LOCAL:5)%勢力から{LOCAL:51}の兵を受け取りました
			ENDIF
		ENDIF

	ELSE
		PRINTFORML %NAME:(LOCAL:5)%に兵{LOCAL:51}を渡す必要があります
		PRINTFORML 現在の総兵数:{LOCAL:52}(うち遊撃兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:1)})
		IF LOCAL:51 > LOCAL:52
			CALL ASK_MULTI_JUDGE("決定", 0, "キャンセル", 1)
		ELSE
			IF LOCAL:51 > COUNTRY_SOLDIER:(CFLAG:MASTER:1)
				IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:1) + LOCAL:13
					PRINTFORML ※要求兵数が現在の遊撃兵数より多いため、全部隊を解散し、防衛に着いている兵も渡す必要があります
				ELSE
					PRINTFORML ※要求兵数が現在の遊撃兵数より多いため、防衛に着いている兵も渡す必要があります
				ENDIF
			ENDIF
			CALL ASK_YN("決定", "キャンセル")
		ENDIF
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;自国から相手勢力に兵を移動
			CALL SHIFT_SOLDIER(CFLAG:MASTER:1, ARG:0, LOCAL:51)
			PRINTFORMW %NAME:(LOCAL:5)%勢力に{LOCAL:51}の兵を引き渡しました
		ENDIF
	ENDIF

	REDRAW 1
	RESTART

ELSEIF RESULT == 1 && LOCAL:40
	;要求経済力は、両国の規模の平均値に比例
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:1) / 100
	LOCAL:51 = GET_SUM_ECONOMY(ARG:0) / 100
	LOCAL:52 = ABS(LOCAL:39) * (LOCAL:50 + LOCAL:51) / 70000
	LOCAL:53 = MAX(LOCAL:50 - GET_OWN_CITY(CFLAG:MASTER:1) * 50, 0)
	LOCAL:54 = MAX(LOCAL:51 - GET_OWN_CITY(ARG:0) * 50, 0)


	;ただし、自国が補填してもらう場合（解放点数が要求点数より大きい場合）、最大でも補填国の20%が限界
	;そして、自国が補填する場合（要求点数が解放点数以上の場合）、算出した要求経済力と合計点数の1/3のうち、大きい方を採用
	IF LOCAL:39 > 0
		LOCAL:52 = MIN(LOCAL:52, LOCAL:54 * 20 / 100)
	ELSE
		LOCAL:52 = MAX(LOCAL:52, ABS(LOCAL:39) / 3)
	ENDIF

	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %NAME:(LOCAL:5)%に対して経済力{LOCAL:52}を要求することができます
		PRINTFORML 現在の経済力……%NAME:(LOCAL:4)%:{LOCAL:50}    %NAME:(LOCAL:5)%:{LOCAL:51}
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:52
				PRINTFORML %NAME:(LOCAL:5)%は要求を満たすだけの経済力を持っていないようだ…(準備可能:{LOCAL:54})
				CALL ASK_YN("あるだけよこせ！", "それなら交渉は決裂だ")
				IF RESULT == 0
					LOCAL:52 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;相手勢力から自国に経済力を移動
				CALL SHIFT_ECONOMY(ARG:0, CFLAG:MASTER:1, LOCAL:52 * 100)
				PRINTFORMW %NAME:(LOCAL:5)%勢力から経済力{LOCAL:52}を受け取り所有都市に配分しました
			ENDIF
		ENDIF
	ELSE
		PRINTFORML %NAME:(LOCAL:5)%に経済力{LOCAL:52}を渡す必要があります
		PRINTFORML 現在の経済力……%NAME:(LOCAL:4)%:{LOCAL:50}    %NAME:(LOCAL:5)%:{LOCAL:51}
		IF LOCAL:52 > LOCAL:53
			PRINTFORML ※受け渡せる経済力の限界は{LOCAL:53}まで
			CALL ASK_MULTI_JUDGE("決定", 0, "キャンセル", 1)
		ELSE
			CALL ASK_YN("決定", "キャンセル")
		ENDIF
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;自国から相手勢力に兵を移動
			CALL SHIFT_ECONOMY(CFLAG:MASTER:1, ARG:0, LOCAL:52 * 100)
			PRINTFORMW %NAME:(LOCAL:5)%勢力に{LOCAL:52}の経済力を受け渡しました
		ENDIF
	ENDIF

	REDRAW 1
	RESTART
	
;;;追加ここから　人質交渉　2返還
ELSEIF RESULT == 2 && LOCAL:40
	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %NAME:(LOCAL:5)%に捕虜を返還します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1  ;解放の実施
			LOCAL:25 = LOCAL:39 / 30		;好感度・敵対度の操作値は要調整

			IF LOCAL:60	;解放を実施==1
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;互いの国同士の好感度を上昇させ敵対値を低下させる
				CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 自軍が優位の場合のみ選べます	;出るはずのないメッセージ
	ENDIF
	REDRAW 1
	RESTART
;;追加ここまで　人質交渉　2返還

ELSEIF RESULT == 10
	LOCAL:12 = MAX(LOCAL:12 - 1, 1)
ELSEIF RESULT == 11
	LOCAL:12 = MIN(LOCAL:12 + 1, LOCAL:11)
ELSEIF RESULT == 20
	LOCAL:22 = MAX(LOCAL:22 - 1, 1)
ELSEIF RESULT == 21
	LOCAL:22 = MIN(LOCAL:12 - 1, LOCAL:11)
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:2 < 0
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	IF (CFLAG:(LOCAL:2):1 == ARG:0 && CFLAG:(LOCAL:2):9 == CFLAG:MASTER:1) || (CFLAG:(LOCAL:2):1 == CFLAG:MASTER:1 && CFLAG:(LOCAL:2):9 == ARG:0)
		CFLAG:(LOCAL:2):58 = !CFLAG:(LOCAL:2):58
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
ENDIF
CLEARLINE 29
GOTO SHOW_LOOP

;-------------------------------------------------
;選択した解放し、解放メッセージを表示する
;-------------------------------------------------
@PRISONER_TRADE_RELEASE(ARG:0)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):58 && CFLAG:(LOCAL:0):1 == ARG:0
		CFLAG:(LOCAL:0):9 = 0
		PRINTFORML %NAME:(LOCAL:0)%を解放しました
	ENDIF
NEXT
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):58 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1
		CFLAG:(LOCAL:0):9 = 0
		PRINTFORML %NAME:(LOCAL:0)%は解放されました
	ENDIF
NEXT

;-------------------------------------------------
;勢力ARG:0から勢力ARG:1にARG:2だけの兵力を移動させる処理
;-------------------------------------------------
@SHIFT_SOLDIER(ARG:0, ARG:1, ARG:2)
LOCAL:12 = GET_SUM_SOLDIER(ARG:0)
LOCAL:13 = GET_SUM_ARMY_DF(ARG:0)

IF ARG:2 > COUNTRY_SOLDIER:(ARG:0)
	IF ARG:2 > COUNTRY_SOLDIER:(ARG:0) + LOCAL:13
		CALL CLEAR_ALL_UNIT(ARG:0)
	ENDIF

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			COUNTRY_SOLDIER:(ARG:0) += CITY_SOLDIER:(LOCAL:0)
			CITY_SOLDIER:(LOCAL:0) = 0
		ENDIF
	NEXT

	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			LOCAL:15 = MIN(COUNTRY_SOLDIER:(ARG:0), 500)
			CITY_SOLDIER:(LOCAL:0) += LOCAL:15
			COUNTRY_SOLDIER:(ARG:0) -= LOCAL:15
		ENDIF
	NEXT

ELSE
	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)
ENDIF

COUNTRY_SOLDIER:(ARG:1) += ARG:2

;-------------------------------------------------
;人質交渉におけるキャラARG:0の価値  能力が高いほど価値が高くなる
;-------------------------------------------------
@GET_PRISONER_VALUE(ARG:0)
#FUNCTION
LOCAL:0 = 0
LOCAL:0 += ABL_POWER(ABL:(ARG:0):武闘)
LOCAL:0 += ABL_POWER(ABL:(ARG:0):知略)
LOCAL:0 += ABL_POWER(ABL:(ARG:0):政治)
LOCAL:0 += ABL_POWER(ABL:(ARG:0):歌唱)
LOCAL:0 += ABL_POWER(ABL:(ARG:0):料理)
;対象が特殊な勢力に捕らえられており、それが自国の捕らえている捕虜でない場合、価値は3倍に
SIF CFLAG:MASTER:所属 != CFLAG:(ARG:0):捕虜先 && GROUPMATCH(CFLAG:(ARG:0):捕虜先, GET_COUNTRY_FROM_ID(COUNTRY_BANDIT), GET_COUNTRY_FROM_ID(COUNTRY_GAIRAI), GET_COUNTRY_FROM_ID(COUNTRY_GOBLIN))
	LOCAL:0 *= 5
RETURNF POWER(MAX(REV_ABL_POWER(LOCAL:0) - 40, 10), 2)

;-------------------------------------------------
;ARG:1の値分だけ勢力ARG:0との好感度を上昇させ敵対値を低下させる処理
;-------------------------------------------------
@DIPLOMACY_IMPROVE_RELATION(ARG:0, ARG:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:1, ARG:0, ARG:1, -ARG:1)
CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:1, ARG:1, -ARG:1)

PRINTL 
SETCOLOR COLOR("注意")
IF ARG:1 < 25
	PRINTFORMW %NAME:(LOCAL:5)%との関係がわずかながら改善されたようです
ELSEIF ARG:1 < 50
	PRINTFORMW %NAME:(LOCAL:5)%との関係が少しだけ改善されたようです
ELSEIF ARG:1 < 100
	PRINTFORMW %NAME:(LOCAL:5)%との関係がそれなりに改善されたようです
ELSEIF ARG:1 < 200
	PRINTFORMW %NAME:(LOCAL:5)%との関係が大きく改善されたようです
ELSE
	PRINTFORMW %NAME:(LOCAL:5)%との関係が非常に大きく改善されたようです
ENDIF
RESETCOLOR
