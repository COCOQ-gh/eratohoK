;外交の種類に応じた個別の処理

;-------------------------------------------------
;停戦を申し込む
;-------------------------------------------------
@DIPLOMACY_CEASEFIRE(相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手君主
#DIM 相手外交計画
#DIM 判定値
#DIM 期間
#DIM 勢力経済
#DIM 相手勢力経済

IF IS_SP_COUNTRY(相手勢力)
	PRINTFORMW 停戦をもちかけましたが、向こうは鼻で笑ってきました
	RETURN 0
ENDIF
勢力 = CFLAG:MASTER:所属
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(相手勢力)

相手外交計画 = CHECK_AI_DIPLOMACY_PLAN(相手勢力, CFLAG:MASTER:所属)

;相手の外交計画内ならば、無条件で成立
IF GROUPMATCH(相手外交計画, AI_外交計画_停戦, AI_外交計画_同盟)
	PRINTFORMW %ANAME(MASTER)%が停戦を持ちかけると、%NAME:(LOCAL:5)%は快く承諾した…
	GOTO SUCCESS
ENDIF

勢力経済 = GET_SUM_ECONOMY(勢力)
相手勢力経済 = GET_SUM_ECONOMY(相手勢力)


判定値 = (REL_LIKE:相手君主:君主 - REL_HATE:相手君主:君主) * 100 / 1500
判定値 = 判定値 * MAX(勢力経済 * 100 / 相手勢力経済, 100) / 100
判定値 += MAX((勢力経済 - 相手勢力経済) / 5000, 0)

DEBUGPRINTFORML 停戦のベース判定値:{判定値}

;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

SIF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
	判定値 /= 2

SELECTCASE 判定値
	CASE IS >= 30
		PRINTFORMW %ANAME(君主)%が停戦をもちかけると、%ANAME(相手君主)%は快く承諾した…
	CASE 15 TO 29
		CALL DIPLOMACY_REQUESTED(相手勢力)
		;要求できるものなし
		IF RESULT == -1
			IF INRANGE(判定値, 25, 30)
				PRINTFORMW %ANAME(君主)%が停戦をもちかけると、%ANAME(相手君主)%は悩みつつも承諾した…
			ELSE
				PRINTFORMW %ANAME(君主)%が停戦をもちかけると、%ANAME(相手君主)%は悩みつつも断った…
				RETURN
			ENDIF
		ELSEIF RESULT == 0
				RETURN 
		ENDIF
	CASEELSE
		PRINTFORMW %ANAME(君主)%は停戦を提案したが、%ANAME(相手君主)%は断った…
		SIF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
			PRINTFORMW 領地が隣接していない状態での停戦は、通常よりも難しいものとなるようだ…
		RETURN
ENDSELECT

$SUCCESS
期間 = MAX((REL_LIKE:相手君主:君主 * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100 -  REL_HATE:相手君主:君主 * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100) / 100, 3)

CALL INIT_CEASEFIRE(勢力, 相手勢力, 期間)

PRINTL 
SETCOLOR COLOR("注意")
PRINTFORMW %ANAME(相手君主)%と{期間}期間の停戦が成立しました
RESETCOLOR


;-------------------------------------------------
;同盟を申し込む
;-------------------------------------------------
@DIPLOMACY_ALLIANCE(相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手君主
#DIM 相手外交計画
#DIM 判定値
#DIM 期間
#DIM 勢力経済
#DIM 相手勢力経済

IF IS_SP_COUNTRY(相手勢力)
	PRINTFORMW 同盟をもちかけましたが、向こうは鼻で笑ってきました
	RETURN 0
ENDIF
勢力 = CFLAG:MASTER:所属
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(相手勢力)

相手外交計画 = CHECK_AI_DIPLOMACY_PLAN(相手勢力, CFLAG:MASTER:所属)

;相手の外交計画内ならば、無条件で成立
IF 相手外交計画 == AI_外交計画_同盟
	PRINTFORMW %ANAME(MASTER)%が同盟を持ちかけると、%NAME:(LOCAL:5)%は快く承諾した…
	GOTO SUCCESS
ENDIF

勢力経済 = GET_SUM_ECONOMY(勢力)
相手勢力経済 = GET_SUM_ECONOMY(相手勢力)


判定値 = (REL_LIKE:相手君主:君主 - REL_HATE:相手君主:君主) * 100 / 1500
判定値 = 判定値 * MAX(勢力経済 * 100 / 相手勢力経済, 100) / 100
判定値 += MAX((勢力経済 - 相手勢力経済) / 5000, 0)

DEBUGPRINTFORML 同盟のベース判定値:{判定値}

;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

SIF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
	判定値 /= 2

SELECTCASE 判定値
	CASE IS >= 50
		PRINTFORMW %ANAME(君主)%が同盟をもちかけると、%ANAME(相手君主)%は快く承諾した…
	CASE 35 TO 49
		CALL DIPLOMACY_REQUESTED(相手勢力)
		;要求できるものなし
		IF RESULT == -1
			IF INRANGE(判定値, 40, 49)
				PRINTFORMW %ANAME(君主)%が同盟をもちかけると、%ANAME(相手君主)%は悩みつつも承諾した…
			ELSE
				PRINTFORMW %ANAME(君主)%が同盟をもちかけると、%ANAME(相手君主)%は悩みつつも断った…
				RETURN
			ENDIF
		ELSEIF RESULT == 0
			RETURN
		ENDIF
	CASEELSE
		PRINTFORMW %ANAME(君主)%は同盟を提案したが、%ANAME(相手君主)%は断った…
		SIF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
			PRINTFORMW 領地が隣接していない状態での同盟は、通常よりも難しいものとなるようだ…
		RETURN
ENDSELECT

$SUCCESS
期間 = MAX((REL_LIKE:相手君主:君主 * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100 -  REL_HATE:相手君主:君主 * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100) / 100, 3)

CALL INIT_ALLIANCE(勢力, 相手勢力, 期間)

PRINTL 
SETCOLOR COLOR("注意")
PRINTFORMW %ANAME(相手君主)%と{期間}期間の同盟が成立しました
RESETCOLOR



;-------------------------------------------------
;永久同盟を申し込む
;-------------------------------------------------
@DIPLOMACY_ALLIANCE_INDIFINITE(相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手君主
#DIM 相手外交計画
#DIM 判定値
#DIM 期間
勢力 = CFLAG:MASTER:所属

IF CHECK_COUNTRY_RELATION_F(勢力, 相手勢力) != 3
	PRINTFORMW 永久同盟は、現在同盟中の勢力にしか提案することができません
	RETURN 0
ENDIF

IF IS_SP_COUNTRY(相手勢力)
	PRINTFORMW こんな連中と手を組むわけにはいきません
	RETURN 0
ENDIF

君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(相手勢力)

PRINTFORMW %ANAME(君主)%は%ANAME(相手君主)%に無期限の同盟を提案した

;好感度1200以上、敵対値0
IF REL_LIKE:相手君主:君主 >= 1200 && REL_HATE:相手君主:君主 <= 0
	PRINTFORMW %ANAME(相手君主)%は%ANAME(君主)%と共に天下を統べる決意を固めたようだ…
	CALL INIT_ALLIANCE(勢力, 相手勢力, 9999)

	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORMW %ANAME(相手君主)%と無期限の同盟が成立しました
	RESETCOLOR
;不成立
ELSE
	PRINTFORML だが、%ANAME(相手君主)%は%ANAME(君主)%の提案を断った
	PRINTFORMW まだ%ANAME(君主)%のことをそこまで信用できないようだ…
ENDIF

;-------------------------------------------------
;外交併合を行う
;-------------------------------------------------
@DIPLOMACY_PERSONAL_UNION(相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手君主
#DIM 相手外交計画
#DIM 判定値
#DIM 成功フラグ
#DIM 期間
勢力 = CFLAG:MASTER:所属

IF CHECK_COUNTRY_RELATION_F(勢力, 相手勢力) != 3
	PRINTFORMW 外交併合は、現在同盟中の勢力にしか提案することができません
	RETURN 0
ENDIF

IF IS_SP_COUNTRY(相手勢力)
	PRINTFORMW こんな連中と併合するわけにはいきません
	RETURN 0
ENDIF

君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(相手勢力)

;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

IF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
	PRINTW 隣接していない勢力と外交併合を行うことはできません
	RETURN 0
ENDIF

PRINTFORMW %ANAME(君主)%は%ANAME(相手君主)%に対して、外交併合する提案をした

成功フラグ = 0

;条件1　自勢力に対する好意-敵意が1350以上。
IF REL_LIKE:相手君主:君主 - REL_HATE:相手君主:君主 > 1350
	成功フラグ = 1
;条件2　相手が隷属しており、自勢力の経済力が相手勢力の8倍
ELSEIF TALENT:相手君主:隷属 && GET_SUM_ECONOMY(勢力) > GET_SUM_ECONOMY(相手勢力) * 8
	成功フラグ = 1
;条件3　自勢力の経済力ならびに政治力が、それぞれ相手勢力の4倍
ELSEIF GET_SUM_ECONOMY(勢力) > GET_SUM_ECONOMY(相手勢力) * 4 && TMP_GET_POLITICS_POWER(勢力) > TMP_GET_POLITICS_POWER(相手勢力) * 3
	成功フラグ = 1
ENDIF

IF !成功フラグ
	PRINTFORMW 残念ながら、%ANAME(相手君主)%は我が国の外交併合に応じませんでした…
	;併合提案による外交印象の悪化
	DIPLOMACY_HATE:勢力 += 2
	RETURN 1
ENDIF
PRINTFORMW 素晴らしい！%ANAME(相手君主)%は我が国と１つになりました
RESETCOLOR

;兵数の移動
CALL CLEAR_ALL_UNIT(相手勢力)
COUNTRY_SOLDIER:勢力 += COUNTRY_SOLDIER:相手勢力
COUNTRY_SOLDIER:相手勢力 = 0

;領地の接収
PRINTL 
SETCOLOR COLOR("注意")
PRINTFORML %ANAME(相手君主)%の所領だった都市は我が国の領土となりました
RESETCOLOR
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == 相手勢力
		CITY_OWNER:(LOCAL:0) = 勢力
		PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
	ENDIF
NEXT
WAIT

PRINTL 
PRINTFORMW %ANAME(相手君主)%軍の兵士が我が国の兵力として加わりました

;自動で登用
IF CONFIG:303 == 0
	;士官の組み込み
	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORML %ANAME(相手君主)%に仕えていた士官は我が国の士官として組み込まれました
	RESETCOLOR
	FOR LOCAL:0, 0, CHARANUM
		;SIF CFLAG:(LOCAL:0):捕虜先 == 相手勢力:0
		;	CALL CHANGE_COUNTRY(LOCAL:0, 相手勢力, 1)
		IF CFLAG:(LOCAL:0):所属 == 相手勢力
			IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
				CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属, 1)
				PRINTFORML %NAME:(LOCAL:0)%を登用しました
			ELSE
				CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
				SETCOLOR COLOR("シアン")
				PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
				RESETCOLOR
			ENDIF
		ENDIF
	NEXT
	WAIT

;自動で投獄
ELSEIF CONFIG:303 == 1
	;士官の組み込み
	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORML %ANAME(相手君主)%に仕えていた士官を投獄しました
	RESETCOLOR
	FOR LOCAL:0, 0, CHARANUM
		;SIF CFLAG:(LOCAL:0):捕虜先 == 相手勢力
		;	CALL CHANGE_COUNTRY(LOCAL:0, 相手勢力, 1)
		IF CFLAG:(LOCAL:0):所属 == 相手勢力
			IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
				CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
				CALL CAPTURE(LOCAL:0, CFLAG:MASTER:所属)
				PRINTFORML %ANAME(LOCAL:0)%を投獄しました
			ELSE
				CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
				SETCOLOR COLOR("シアン")
				PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
				RESETCOLOR
			ENDIF
		ENDIF
	NEXT
	WAIT

;手動で選択
ELSE
	CALL DIPLOMACY_SURRENDER_SET_TREATMENT(相手勢力)
ENDIF

;相手勢力勢力の捕虜となっていたキャラの処理
;相手勢力勢力が捕虜持ってるか
IF CMATCH(CFLAG:捕虜先, 相手勢力)
	PRINTL
	PRINTFORMW %ANAME(相手君主)%に囚われていた捕虜を見つけました
	LOCAL:2 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):捕虜先 == 相手勢力
			;自陣営の士官なら、そのまま元に戻る
			IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
				CALL CAPTURE(LOCAL:0, 0)
				SETCOLOR COLOR("注意")
				PRINTFORML %ANAME(相手君主)%勢力に囚えられていた%ANAME(LOCAL:0)%は、我が国に復帰した
				RESETCOLOR
				LOCAL:2 = 1
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:2
		WAIT
	ENDIF
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):捕虜先 == 相手勢力
			;第三勢力の士官なら、TREAT_PRISONERで扱いを決定
			CALL TREAT_PRISONER(LOCAL:0, 相手勢力, 勢力)
		ENDIF
	NEXT
ENDIF

;旧勢力の後始末
CALL DESTROY_COUNTRY(相手勢力)
;外交併合による外交印象の悪化
DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 3

RETURN 1

;-------------------------------------------------
;同盟を破棄
;-------------------------------------------------
@DIPLOMACY_ALLIANCE_RENOUNCE(ARG:0)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
CALL CHECK_COUNTRY_RELATION(CFLAG:MASTER:所属, ARG:0)
LOCAL:3 = RESULT

IF LOCAL:3 != 3 && LOCAL:3 != 4
	PRINTFORMW 同盟破棄は、そもそも同盟している勢力にしか行えません
	RETURN 0
ENDIF

PRINTFORML %ANAME(LOCAL:5)%と結んでいる同盟を一方的に破棄します
PRINTFORML 相手からはものすごい反発を受けますし、他勢力からもいい顔はされないでしょう
PRINTFORML それでも同盟を破棄しますか？
CALL ASK_YN
IF RESULT == 0
	;削除対象となる同盟の検索
	FOR LOCAL, 0, MAX_TREATY_A
		IF TREATY_A_TERM:LOCAL > 0
			FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
				IF TREATY_A_COUNTRY:LOCAL:(LOCAL:1) == CFLAG:MASTER:所属
					FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
						IF TREATY_A_COUNTRY:LOCAL:(LOCAL:2) == ARG:0
							GOTO BREAK_LABEL
						ENDIF
					NEXT
				ENDIF
			NEXT
		ENDIF
	NEXT
	$BREAK_LABEL
	IF LOCAL != MAX_TREATY_A
		PRINTFORML %ANAME(LOCAL:5)%との同盟を破棄しました
		PRINTFORML この行いに、%ANAME(LOCAL:5)%は強い反発を覚えています
		PRINTFORMW また、他の勢力も、不信感を抱いたようです
		FOR LOCAL:1, 0, MAX_COUNTRY
			IF IS_COUNTRY(LOCAL:1) && LOCAL:1 != LOCAL:2
				CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:所属, -300, 300)
				IF TREATY_A_TERM:LOCAL == 9999
					CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:所属, -500, 500)
				ENDIF
			ELSEIF IS_COUNTRY(LOCAL:1) && LOCAL:1 == LOCAL:2
				CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:所属, -800, 800)
				IF TREATY_A_TERM:LOCAL == 9999
					CALL CHANGE_RELATION_C_TO_C(LOCAL:1, CFLAG:MASTER:所属, -1000, 1000)
				ENDIF
			ENDIF
		NEXT
		CALL GO_TO_MASTERS_COUNTRY(TREATY_A_COUNTRY:LOCAL:(LOCAL:2))
		FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
			TREATY_A_COUNTRY:LOCAL:(LOCAL:1) = 0
		NEXT
		TREATY_A_TERM:LOCAL = 0
	ELSE
		PRINTFORMW 同盟破棄を選択した途中でエラーだよ　ここにはこないはずだよ
		PRINTFORMW もしこのメッセージを見かけたら教えてね 喚くより
		RETURN 0
	ENDIF
ELSE
	;同盟破棄による外交印象の悪化
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 3

	RETURN 0
ENDIF

;-------------------------------------------------
;会談する
;-------------------------------------------------
@DIPLOMACY_TALK(ARG:0)
#DIM 君主
#DIM FIRST_LINE, 2
#DIM CONST 支払額 = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
FIRST_LINE:0 = LINECOUNT
君主 = GET_COUNTRY_BOSS(ARG:0)

PRINTFORML %ANAME(君主)%と会談することにした……
PRINTFORML 昔から接待といえば、金か「楽しみ」が常道だ
PRINTFORML %ANAME(君主)%を満足させるには、どちらかを用意すべきだろう……
DRAWLINE
CALL ASK_MULTI_JUDGE(@"国庫から資金援助 現在:{MONEY:(CFLAG:MASTER:所属)}", 1, "「楽しみ」を用意(相手に性知識3が必要)", ABL:君主:性知識 >= 3, "戻る", 1)
SIF RESULT == 2
	RETURN 0
IF RESULT == 1
	CALL DIPLOMACY_TALK_SEX(君主)
	IF RESULT == -1
		CLEARLINE LINECOUNT - FIRST_LINE:0
		RESTART
	ELSE
		PRINTL 
	ENDIF
	;好感度上昇・敵対値下降基準値
	LOCAL:2 = RESULT
	LOCAL = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
	LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10

	LOCAL:2 = MAX(LOCAL:2 * (LOCAL + LOCAL:1 + 100) / 100, 5)

	;互いの国同士の好感度を上昇させ敵対値を低下させる
	CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:2)
ELSEIF RESULT == 0
	FIRST_LINE:1 = LINECOUNT
	$SEND_MONEY_LOOP
	PRINTFORML いくら送ろうか……(手入力・ボタンから入力、0以下でキャンセル, 最大50000)
	CALL ICPRINT(@"現在の国庫:<{MONEY:(CFLAG:MASTER:所属)}>", "L", COLOR("注意"))
	FOR LOCAL, 0, VARSIZE("支払額")
		SIF MONEY:(CFLAG:MASTER:所属) < 支払額:LOCAL
			BREAK
		PRINTFORM [{支払額:LOCAL}払う] 
	NEXT
	PRINTFORML
	PRINTFORML [{MONEY:(CFLAG:MASTER:所属)}払う]
	PRINTFORM [0] 戻る
	INPUT
	IF RESULT <= 0
		CLEARLINE LINECOUNT - FIRST_LINE:0
		RESTART
	ELSEIF MONEY:(CFLAG:MASTER:所属) < RESULT || RESULT > 50000
		CLEARLINE LINECOUNT - FIRST_LINE:1
		GOTO SEND_MONEY_LOOP
	ENDIF
	CALL SEND_MONEY(ARG:0, RESULT)
ENDIF

RETURN 1

;-------------------------------------------------
;送金処理用関数
;-------------------------------------------------
@CALC_SEND_MONEY_EFFECT(ARG:0)
#FUNCTION
RETURNF LIMIT(SQRT(ARG:0) * RAND(900, 1100) / 1000, 0, 300)

;-------------------------------------------------
;送金処理
;-------------------------------------------------
@SEND_MONEY(ARG:0, ARG:1)
CALL ICPRINT(@"%ANAME(MASTER)%は%ANAME(GET_COUNTRY_BOSS(ARG:0))%に国庫から金<{ARG:1}>を送った……", "L", COLOR("注意"))
MONEY:(CFLAG:MASTER:所属) -= ARG:1
LOCAL = CALC_SEND_MONEY_EFFECT(ARG:1)
;好感度上昇・敵対値下降基準値
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:2 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10


LOCAL:3 = MAX(LOCAL * (LOCAL:1 + LOCAL:2 + 100) / 100, 5)

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:3)

;-------------------------------------------------
;兵を譲り渡す
;-------------------------------------------------
@DIPLOMACY_GIVE_SOLDIER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属)

PRINTFORML 総兵数:  {GET_SUM_SOLDIER(CFLAG:MASTER:所属)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(CFLAG:MASTER:所属)}
PRINTFORML 残存兵数:{LOCAL:6}
DRAWLINE

IF LOCAL:6 < 500
	PRINTFORMW 最低でも500以上の残存兵数が必要です
	RETURN 0
ENDIF

PRINTFORML 譲渡する兵数を入力して下さい(0以下でキャンセル)

CALL DRAW_SOLDIER_INPUT(LOCAL:6)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 > 0 && LOCAL:2 < 500
	CLEARLINE 1
	PRINTL 最低でも500以上の兵を譲渡する必要があります
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6
	CLEARLINE 1
	PRINTL 兵数が不足しています
	GOTO INPUT_LOOP
ENDIF

COUNTRY_SOLDIER:(CFLAG:MASTER:所属) -= LOCAL:2
COUNTRY_SOLDIER:(ARG:0) += LOCAL:2

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に兵{LOCAL:2}を譲り渡した

;好感度上昇値・敵対値下降値の計算
;同じ兵数でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:10 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
LOCAL:11 = GET_SUM_ECONOMY(ARG:0)
LOCAL:12 = SQRT(LOCAL:2 * 26000 / LOCAL:10) + SQRT(LOCAL:2 * 26000 / LOCAL:11)
LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP

LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1

;-------------------------------------------------
;経済力を譲り渡す
;-------------------------------------------------
@DIPLOMACY_GIVE_ECONOMY(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)

LOCAL:20 = MAX(STRLENS(NAME:(LOCAL:5)), STRLENS(NAME:(LOCAL:4)))
LOCAL:21 = MAX(GET_DIGIT(LOCAL:6 / 100), GET_DIGIT(LOCAL:7 / 100))

PRINTFORML %NAME:(LOCAL:4), LOCAL:20%の経済力:{LOCAL:6 / 100, LOCAL:21}
PRINTFORML %NAME:(LOCAL:5), LOCAL:20%の経済力:{LOCAL:7 / 100, LOCAL:21}
DRAWLINE

IF LOCAL:6 < 250
	PRINTFORMW 最低でも250以上の経済力が必要です
	RETURN 0
ENDIF

PRINTFORML 譲渡する経済力を入力して下さい（0以下でキャンセル、最大で経済力の20％まで）

CALL DRAW_ECONOMY_INPUT(LOCAL:6 / 500)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 < 50
	CLEARLINE 1
	PRINTL 最低でも50以上の経済力を譲渡する必要があります
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6 / 500
	CLEARLINE 1
	PRINTFORML 上限を超えています({LOCAL:6 / 500}まで)
	GOTO INPUT_LOOP
ENDIF

;経済力の移動処理
CALL SHIFT_ECONOMY(CFLAG:MASTER:所属, ARG:0, LOCAL:2 * 100)

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に経済力{LOCAL:2}を譲り渡した

;好感度上昇値・敵対値下降値の計算
;同じ経済力でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:12 = SQRT(LOCAL:2 * 230000 / LOCAL:6) + SQRT(LOCAL:2 * 230000 / LOCAL:7)

LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1

;-------------------------------------------------
;士官を移籍させる
;-------------------------------------------------
@DIPLOMACY_GIVE_CHARA(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;自国の士官の数を数える(君主・主人公・借り物除く)
LOCAL:6 = 0
FOR LOCAL:0, 0, CHARANUM
{
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):捕虜先 && !GETBIT(TALENT:(LOCAL:0):デイリー系, 素質_デイリー_借り物) 
	&& CFLAG:(LOCAL:0):4 >= 100 && !IS_SP_CHARA(LOCAL:0)
}
		LOCAL:6 ++
	ENDIF
NEXT

IF LOCAL:6 <= 0
	PRINTFORMW 移籍させられる士官がいません
	RETURN 0
ENDIF

LOCAL:20 = 1

;1ページに表示する最大キャラ数の設定
LOCAL:7 = 44

;ページ数を計算
LOCAL:8 = (LOCAL:6 - 1) / LOCAL:7 + 1

$SHOW_LOOP1

DRAWLINE
PRINTFORML 移籍させる士官を選択して下さい
DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

LOCAL:10 = LOCAL:7 * (LOCAL:20 - 1)
LOCAL:11 = LOCAL:7 * LOCAL:20
LOCAL:12 = 0
LOCAL:13 = 0

FOR LOCAL:0, 0, CHARANUM
{
		IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):捕虜先 &&
		 !GETBIT(TALENT:(LOCAL:0):デイリー系, 素質_デイリー_借り物) && CFLAG:(LOCAL:0):4 >= 100 && !IS_SP_CHARA(LOCAL:0)
}
		IF LOCAL:13 >= LOCAL:10 && LOCAL:13 < LOCAL:11
			IF LOCAL:12 % 2 != 0
				PRINTPLAIN   
			ELSE
				IF LOCAL:12 >= 1
					PRINTL 
				ENDIF
				PRINTPLAIN   
			ENDIF
			CALL PRINT_PARTNER_DATA(LOCAL:0)
			LOCAL:12 ++
		ENDIF
		LOCAL:13 ++
	ENDIF
NEXT
IF LOCAL:12 >= 1
	PRINTL 
ENDIF

IF LOCAL:8 >= 2
	DRAWLINE
	PRINTPLAIN   
	IF LOCAL:20 >= 2
		PRINTBUTTON "  54[前のページ    ]", 54
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   54[前のページ    ]
		RESETCOLOR
	ENDIF

	PRINTPLAINFORM         page{LOCAL:20, 2, RIGHT}/{LOCAL:8, 2, LEFT}     
	IF LOCAL:20 < LOCAL:8
		PRINTBUTTON "  56[次のページ    ]", 56
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   56[次のページ    ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAIN   
PRINTBUTTON "   0[戻る          ]", 0

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 54 && LOCAL:20 >= 2
	LOCAL:20 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56 && LOCAL:20 < LOCAL:8
	LOCAL:20 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ENDIF

LOCAL:15 = NO_TO_CHARA(RESULT - 100)
{
IF LOCAL:15 >= 0 && CFLAG:(LOCAL:15):所属 == CFLAG:MASTER:所属 && LOCAL:15 != MASTER && LOCAL:15 != LOCAL:4 && !CFLAG:(LOCAL:15):捕虜先 &&
 !GETBIT(TALENT:(LOCAL:15):デイリー系, 素質_デイリー_借り物) && CFLAG:(LOCAL:15):4 >= 100 && !IS_SP_CHARA(LOCAL:15)
}
	REDRAW 1

	;対象キャラの情報を表示
	DRAWLINE
	CALL SHOW_INFO(LOCAL:15)
	DRAWLINE
	PRINTFORML このキャラを移籍させますか？

	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 0
		PRINTFORML %ANAME(LOCAL:15)%は%NAME:(LOCAL:5)%勢力の士官になりました
		CALL CHANGE_COUNTRY(LOCAL:15, ARG:0, 1)
		;好感度上昇値・敵対値下降値の計算
		;主に能力により決定。相手君主がそのキャラに良い印象を抱いていればボーナス
		LOCAL:25 = 0
		LOCAL:25 += ABL_SQRT(ABL:(LOCAL:15):武闘, -1) + ABL_SQRT(ABL:(LOCAL:15):防衛, -1) + ABL_SQRT(ABL:(LOCAL:15):知略, -1) + ABL_SQRT(ABL:(LOCAL:15):政治, -1)
		LOCAL:25 += ABL_SQRT(ABL:(LOCAL:15):歌唱, -1) + ABL_SQRT(ABL:(LOCAL:15):料理, -1)
		LOCAL:25 /= 500

		IF REL_LIKE:(LOCAL:5):(LOCAL:15) >= 800
			LOCAL:25 += 50
		ELSEIF REL_LIKE:(LOCAL:5):(LOCAL:15) >= 300
			LOCAL:25 += 20
		ENDIF

		LOCAL:25 = LIMIT(LOCAL:25, 25, 400)

		;政治・料理の補正
		CALL TMP_CREATE_IS_FREE_MAP
		LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
		LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
		LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

		;互いの国同士の好感度を上昇させ敵対値を低下させる
		CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
		RETURN 1
	ENDIF
	GOTO SHOW_LOOP1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;領土を割譲する
;-------------------------------------------------
@DIPLOMACY_GIVE_CITY(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
CALL TMP_CREATE_UNIT_MAP
CALL TMP_CREATE_RELATION_MAP
;自国の都市の数を数える
LOCAL:6 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属 && GET_CITYNAME(LOCAL:0) != "無名" && !IS_STAY_ENEMY_UNIT(LOCAL:0)
		LOCAL:6 ++
	ENDIF
NEXT

IF LOCAL:6 <= 1
	PRINTFORMW 割譲できる領土がありません
	RETURN 0
ENDIF

LOCAL:20 = 1

;1ページに表示する最大キャラ数の設定
LOCAL:7 = 20

;ページ数を計算
LOCAL:8 = (LOCAL:6 - 1) / LOCAL:7 + 1

$SHOW_LOOP1

DRAWLINE
PRINTFORML 割譲する都市を選択して下さい
DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

LOCAL:10 = LOCAL:7 * (LOCAL:20 - 1)
LOCAL:11 = LOCAL:7 * LOCAL:20
LOCAL:12 = 0
LOCAL:13 = 0

FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属 && GET_CITYNAME(LOCAL:0) != "無名" && !IS_STAY_ENEMY_UNIT(LOCAL:0)
		IF LOCAL:13 >= LOCAL:10 && LOCAL:13 < LOCAL:11
			IF LOCAL:12 % 2 != 0
				PRINTPLAIN   
			ELSE
				IF LOCAL:12 >= 1
					PRINTL 
				ENDIF
				PRINTPLAIN   
			ENDIF
			CALL DRAWCITY_BUTTON(LOCAL:0)
			LOCAL:12 ++
		ENDIF
		LOCAL:13 ++
	ENDIF
NEXT
IF LOCAL:12 >= 1
	PRINTL 
ENDIF

IF LOCAL:8 >= 2
	DRAWLINE
	PRINTPLAIN   
	IF LOCAL:20 >= 2
		PRINTBUTTON "  54[前のページ    ]", 54
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   54[前のページ    ]
		RESETCOLOR
	ENDIF

	PRINTPLAINFORM         page{LOCAL:20, 2, RIGHT}/{LOCAL:8, 2, LEFT}     
	IF LOCAL:20 < LOCAL:8
		PRINTBUTTON "  56[次のページ    ]", 56
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   56[次のページ    ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAIN   
PRINTBUTTON "   0[戻る          ]", 0

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 54 && LOCAL:20 >= 2
	LOCAL:20 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56 && LOCAL:20 < LOCAL:8
	LOCAL:20 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ENDIF

LOCAL:15 = (RESULT - 1000)

IF LOCAL:15 >= 0
	REDRAW 1

	;対象キャラの情報を表示
	DRAWLINE
	CALL SHOW_CITY_INFO(LOCAL:15)
	DRAWLINE
	PRINTFORML この領土を割譲しますか？

	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 0
		PRINTFORML %GET_CITYNAME(LOCAL:15)%は%NAME:(LOCAL:5)%勢力の領土になりました

		CALL SINGLE_MINIMIZE(LOCAL:15)
		CITY_OWNER:(LOCAL:15) = ARG:0

		;好感度上昇値・敵対値下降値の計算
		;主に能力により決定。相手君主がそのキャラに良い印象を抱いていればボーナス
		LOCAL:25 = 0
		LOCAL:25 += ABL_SQRT(ABL:(LOCAL:15):武闘, -1) + ABL_SQRT(ABL:(LOCAL:15):防衛, -1) + ABL_SQRT(ABL:(LOCAL:15):知略, -1) + ABL_SQRT(ABL:(LOCAL:15):政治, -1)
		LOCAL:25 += ABL_SQRT(ABL:(LOCAL:15):歌唱, -1) + ABL_SQRT(ABL:(LOCAL:15):料理, -1)
		LOCAL:25 /= 400

		LOCAL:25 = LIMIT(LOCAL:25, 25, 400)

		;政治・料理の補正
		CALL TMP_CREATE_IS_FREE_MAP
		LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
		LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
		LOCAL:25 = LOCAL:25 * (LOCAL:0 / 10 + LOCAL:1 / 10) / 100

		;互いの国同士の好感度を上昇させ敵対値を低下させる
		CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
		RETURN 1
	ENDIF
	GOTO SHOW_LOOP1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP
;-------------------------------------------------
;降伏勧告を行う
;-------------------------------------------------
@DIPLOMACY_SURRENDER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
CALL TMP_CREATE_SUM_ECONOMY_MAP
CALL TMP_CREATE_SUM_SOLDIER_MAP
CALL TMP_CREATE_POLITICS_POWER_MAP

IF !TMP_COUNTRY_IS_NEIBORING:(ARG:0):(CFLAG:MASTER:所属)
	PRINTW 隣接していない勢力に降伏勧告を行うことはできません
	RETURN 0
ENDIF

IF IS_SP_COUNTRY(ARG:0)
	PRINTFORMW 勧告を突っぱねてきました
	RETURN 0
ENDIF

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に対して降伏するように諭した
PRINTFORMW ………………
PRINTFORMW ……


;要求受け入れ判定
IF DIPLOMACY_REQUEST_CHECK(CFLAG:MASTER:所属, ARG:0, 80)
	SETCOLOR COLOR("注意")
	PRINTFORMW %NAME:(LOCAL:5)%は我が国の勧告を受け入れ降伏しました
	RESETCOLOR

	;兵数の移動
	CALL CLEAR_ALL_UNIT(ARG:0)
	COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += COUNTRY_SOLDIER:(ARG:0)
	COUNTRY_SOLDIER:(ARG:0) = 0

	;領地の接収
	PRINTL 
	SETCOLOR COLOR("注意")
	PRINTFORML %NAME:(LOCAL:5)%の所領だった都市は我が国の領土となりました
	RESETCOLOR
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			CITY_OWNER:(LOCAL:0) = CFLAG:MASTER:所属
			PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
		ENDIF
	NEXT
	WAIT

	PRINTL 
	PRINTFORMW %NAME:(LOCAL:5)%軍の兵士が我が国の兵力として加わりました

	;自動で登用
	IF CONFIG:303 == 0
		;士官の組み込み
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORML %NAME:(LOCAL:5)%に仕えていた士官は我が国の士官として組み込まれました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == ARG:0
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属, 1)
					PRINTFORML %NAME:(LOCAL:0)%を登用しました
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
					SETCOLOR COLOR("シアン")
					PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
					RESETCOLOR
				ENDIF
			ENDIF
		NEXT
		WAIT

	;自動で投獄
	ELSEIF CONFIG:303 == 1
		;士官の組み込み
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORML %NAME:(LOCAL:5)%に仕えていた士官を投獄しました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == ARG:0
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
					CALL CAPTURE(LOCAL:0, CFLAG:MASTER:所属)
					PRINTFORML %ANAME(LOCAL:0)%を投獄しました
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
					SETCOLOR COLOR("シアン")
					PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
					RESETCOLOR
				ENDIF
			ENDIF
		NEXT
		WAIT

	;手動で選択
	ELSE
		CALL DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
	ENDIF
	
	;ARG:0勢力の捕虜となっていたキャラの処理
	;ARG:0勢力が捕虜持ってるか
	IF CMATCH(CFLAG:捕虜先, ARG:0)
		PRINTL
		PRINTFORMW %NAME:(LOCAL:5)%に囚われていた捕虜を見つけました
		LOCAL:2 = 0
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
				;自陣営の士官なら、そのまま元に戻る
				IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
					CALL CAPTURE(LOCAL:0, 0)
					SETCOLOR COLOR("注意")
					PRINTFORML %NAME:(LOCAL:5)%勢力に囚えられていた%ANAME(LOCAL:0)%は、我が国に復帰した
					RESETCOLOR
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
		IF LOCAL:2
			WAIT
		ENDIF
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
				;第三勢力の士官なら、TREAT_PRISONERで扱いを決定
				CALL TREAT_PRISONER(LOCAL:0, ARG:0, CFLAG:MASTER:所属)
			ENDIF
		NEXT
	ENDIF

	;旧勢力の後始末
	CALL DESTROY_COUNTRY(ARG:0)
	
	;外交補正の加算処理
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 5
ELSE
	PRINTFORMW 残念ながら、%NAME:(LOCAL:5)%は我が国の降伏勧告に応じませんでした…
	;外交補正の加算処理（他国の警戒が上がったと考えていただければ）
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 3
ENDIF

RETURN 1

;-------------------------------------------------
;降伏した勢力の士官の処遇を個別に設定 ARG:0=降伏した勢力
;-------------------------------------------------
@DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
;キャラの扱い設定用変数
#DIM CHARA_TREATMENT, 3000
#DIM CONST IN_PAGE_CHARA_NUM = 22
#DIM COMMANDER_NUM
#DIM LCOUNT
#DIM MAX_PAGE
#DIM PAGE
#DIM CHARA_COUNT_FOR_PRINT
#DIM FIRST_CHARA_COUNT_IN_THIS_PAGE
#DIM FIRST_CHARA_COUNT_IN_NEXT_PAGE
#DIM SELECT_CHARA
#DIM SELECTED_TREATMENT
#DIM INPUT_LINECOUNT
#DIM SHOW_LINECOUNT

VARSET CHARA_TREATMENT, 0

;●士官の人数+捕虜の人数をカウント
COMMANDER_NUM = GET_COMMANDER_NUM(ARG:0)

;士官が一人もいなければ戻る
IF COMMANDER_NUM == 0
	DEBUGPRINTFORML {ARG:0}番号の%NAME:(GET_COUNTRY_BOSS(ARG:0))%勢力に士官が誰もいませんでした
	RETURN
ENDIF

;●表示部分
MAX_PAGE = (COMMANDER_NUM - 1) / IN_PAGE_CHARA_NUM + 1
PAGE = 1

;SHOW_LOOP
SHOW_LINECOUNT = LINECOUNT
WHILE 1

	DRAWLINE
	PRINTFORML %NAME:(GET_COUNTRY_BOSS(ARG:0))%に仕えていた士官の処遇を決めて下さい
	DRAWLINE

	CHARA_COUNT_FOR_PRINT = 0
	FIRST_CHARA_COUNT_IN_THIS_PAGE = (PAGE - 1) * IN_PAGE_CHARA_NUM
	LOCAL:13 = PAGE * IN_PAGE_CHARA_NUM
	FOR LCOUNT, 0, CHARANUM
		IF CFLAG:LCOUNT:所属 == ARG:0
			IF CHARA_COUNT_FOR_PRINT >= FIRST_CHARA_COUNT_IN_THIS_PAGE && CHARA_COUNT_FOR_PRINT < LOCAL:13
				PRINTFORM %ANAME(LCOUNT), MAX_CHARANAME_LENGTH, RIGHT% …… 
				IF CFLAG:LCOUNT:捕虜先 && CFLAG:LCOUNT:捕虜先 != CFLAG:MASTER:所属
					SETCOLOR COLOR("シアン")
					PRINTPLAIN [他国で捕虜]
					RESETCOLOR
				ELSEIF CHARA_TREATMENT:LCOUNT == 0
					SETCOLOR COLOR("選択中")
					PRINTPLAIN [登用]
					RESETCOLOR
					PRINTBUTTON "[投獄]", LCOUNT * 2 + 101
				ELSE
					PRINTBUTTON "[登用]", LCOUNT * 2 + 100
					SETCOLOR COLOR("選択中")
					PRINTPLAIN [投獄]
					RESETCOLOR
				ENDIF
				PRINTL 
			ENDIF
			CHARA_COUNT_FOR_PRINT ++
		ENDIF
	NEXT
	DRAWLINE

	IF MAX_PAGE >= 2
		LOCALS:0 = 
		IF PAGE >= 2
			LOCALS:0 = [8] 前のページ
		ENDIF
		PRINTFORM %LOCALS:0, 25, LEFT%

		LOCALS:0 = page{PAGE}/{MAX_PAGE}
		PRINTPLAINFORM %LOCALS:0, 25, LEFT%

		LOCALS:0 = 
		IF PAGE < MAX_PAGE
			LOCALS:0 = [9] 次のページ
		ENDIF
		PRINTFORM %LOCALS:0, 25, LEFT%
		PRINTL 
		DRAWLINE
	ENDIF
	PRINT [1] 全員を登用に設定
	PRINTPLAIN     
	PRINT [2] 全員を投獄に設定
	PRINTL 
	DRAWLINE
	PRINTL [0] 決定

	REDRAW 2

	;INPUT_LOOP
	INPUT_LINECOUNT = LINECOUNT
	WHILE 1
		INPUT

		IF RESULT == 0
			FOR LCOUNT, 0, CHARANUM
				IF CFLAG:LCOUNT:所属 == ARG:0
					CALL FORCE_FREE(LCOUNT)
					IF CFLAG:LCOUNT:捕虜先 && CFLAG:LCOUNT:捕虜先 != CFLAG:MASTER:所属
						CALL CHANGE_COUNTRY(LCOUNT, CFLAG:MASTER:所属)
					ELSEIF CHARA_TREATMENT:LCOUNT == 0
						CALL CHANGE_COUNTRY(LCOUNT, CFLAG:MASTER:所属, 1)
					ELSE
						CALL CHANGE_COUNTRY(LCOUNT, 0, 1)
						CALL CAPTURE(LCOUNT, CFLAG:MASTER:所属)
					ENDIF
				ENDIF
			NEXT
			REDRAW 1
			RETURN
		ELSEIF RESULT == 1
			VARSET CHARA_TREATMENT, 0
			BREAK
		ELSEIF RESULT == 2
			VARSET CHARA_TREATMENT, 1
			BREAK
		ELSEIF RESULT == 8 && PAGE >= 2
			PAGE --
			BREAK
		ELSEIF RESULT == 9 && PAGE < MAX_PAGE
			PAGE ++
			BREAK
		ELSEIF RESULT >= 100
			SELECT_CHARA = (RESULT - 100) / 2
			SELECTED_TREATMENT = (RESULT - 100) % 2
			IF SELECT_CHARA < CHARANUM && CFLAG:SELECT_CHARA:所属 == ARG:0 && !CFLAG:SELECT_CHARA:捕虜先
				CHARA_TREATMENT:(SELECT_CHARA) = SELECTED_TREATMENT
				BREAK
			ENDIF
			PRINTW 不適切なキャラ番号です
		ENDIF
		CLEARLINE LINECOUNT - INPUT_LINECOUNT
	WEND
	CLEARLINE LINECOUNT - SHOW_LINECOUNT
WEND

;-------------------------------------------------
;醜聞を流す
;-------------------------------------------------
@DIPLOMACY_CREATE_SCANDAL(ARG:0)
#DIM 君主
#DIM 相手君主
#DIM FIRST_LINE
#DIM CONST 支払額リスト = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
#DIM 支払額
#DIM 効果
FIRST_LINE = LINECOUNT

君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(ARG:0)
DRAWLINE
PRINTFORML 国庫を消費し、%ANAME(相手君主)%に関する醜聞を他勢力に流します。
PRINTFORML 他勢力から%ANAME(相手君主)%への関係が悪化しますが、%ANAME(相手君主)%と自国の関係も悪化します
PRINTFORML いくら消費しますか？(手入力・ボタンから入力, 0以下でキャンセル, 最大50000)
FOR LOCAL, 0, VARSIZE("支払額リスト")
	SIF MONEY:(CFLAG:MASTER:所属) < 支払額リスト:LOCAL
		BREAK
	PRINTFORM [{支払額リスト:LOCAL}払う] 
NEXT
PRINTFORML
PRINTFORML [{MONEY:(CFLAG:MASTER:所属)}払う]
PRINTFORM [0] 戻る
INPUT

IF RESULT <= 0
	RETURN 0
ELSEIF MONEY:(CFLAG:MASTER:所属) < RESULT || RESULT > 50000
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF
支払額 = RESULT
CALL ICPRINT(@"%ANAME(MASTER)%は金<{支払額}>を支払い、%ANAME(相手君主)%の醜聞を流した……", "W", COLOR("注意"))
MONEY:(CFLAG:MASTER:所属) -= 支払額
効果 = CALC_SEND_MONEY_EFFECT(支払額)
;好感度上昇・敵対値下降基準値
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:2 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10


効果 = MAX(効果 * (LOCAL:1 + LOCAL:2 + 100) / 100, 5)

FOR LOCAL, 0 ,MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL)
		CONTINUE
	IF LOCAL == CFLAG:MASTER:所属
		CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:所属, -効果, 効果)
	ELSE
		CALL CHANGE_RELATION_C_TO_C(LOCAL, ARG:0, -効果, 効果)
	ENDIF
NEXT

RETURN 1


;-------------------------------------------------
;人質交渉
;-------------------------------------------------
@DIPLOMACY_PRISONER_TRADE(ARG:0)
#DIM FIRST_LINE
VARSET LOCAL, 0
CVARSET CFLAG, 58, 0

LOCAL:12 = 1
LOCAL:22 = 1

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;士官の部隊所属マップを作成
CALL TMP_CREATE_IS_FREE_MAP

;政治能力の差による補正点数の計算
LOCAL:32 = TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)
LOCAL:33 = TMP_GET_POLITICS_POWER(ARG:0)
LOCAL:34 = SQRT(ABS(LOCAL:32 - LOCAL:33) * 4)
IF LOCAL:32 >= LOCAL:33
	LOCAL:35 = 1000 + LOCAL:34
	LOCAL:36 = 1000000 / (1000 + LOCAL:34)
ELSE
	LOCAL:36 = 1000 + LOCAL:34
	LOCAL:35 = 1000000 / (1000 + LOCAL:34)
ENDIF

;両国の捕虜の数をそれぞれカウント
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
		LOCAL:10 ++
	ELSEIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
		LOCAL:20 ++
	ENDIF
NEXT

;各ページ数を計算
LOCAL:11 = MAX(LOCAL:10 - 1, 0) / 20 + 1
LOCAL:21 = MAX(LOCAL:20 - 1, 0) / 20 + 1

$SHOW_LOOP_INIT

DRAWLINE
PRINTL ★★お互いの解放する捕虜を選択して下さい★★
DRAWLINE
PRINTFORML 【囚えている捕虜】%TOSTR_SPACE(11)%価値   【囚えられている捕虜】%TOSTR_SPACE(7)%価値

FIRST_LINE = LINECOUNT
$SHOW_LOOP

;互いの捕虜をリストに表示
LOCAL:13 = 0
LOCAL:14 = (LOCAL:12 - 1) * 20
LOCAL:15 = LOCAL:12 * 20
LOCAL:16 = 0
LOCAL:23 = 0
LOCAL:24 = (LOCAL:22 - 1) * 20
LOCAL:25 = LOCAL:22 * 20
LOCAL:26 = 0
FOR LOCAL:1, 0, 20
	LOCAL:17 = 1
	FOR LOCAL:0, LOCAL:13, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			IF LOCAL:16 >= LOCAL:14 && LOCAL:16 < LOCAL:15
				PRINTPLAIN   
				IF CFLAG:(LOCAL:0):外交選択フラグ
					SETCOLOR COLOR("選択中")
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				PRINT   ┃
				LOCAL:13 = LOCAL:0 + 1
				LOCAL:16 ++
				LOCAL:17 = 0
				BREAK
			ELSE
				LOCAL:16 ++
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:17
		PRINTFORM %TOSTR_SPACE(35)%┃
	ENDIF
	LOCAL:27 = 1
	FOR LOCAL:0, LOCAL:23, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
			IF LOCAL:26 >= LOCAL:24 && LOCAL:26 < LOCAL:25
				PRINTPLAIN  
				IF CFLAG:(LOCAL:0):外交選択フラグ
					SETCOLOR COLOR("選択中")
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				LOCAL:23 = LOCAL:0 + 1
				LOCAL:26 ++
				LOCAL:27 = 0
				BREAK
			ELSE
				LOCAL:26 ++
			ENDIF
		ENDIF
	NEXT
	PRINTL 
NEXT

PRINTPLAIN    
IF LOCAL:12 <= 1
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 10[←前]
	RESETCOLOR
ELSE
	PRINTBUTTON "10[←前]", 10
ENDIF
PRINTPLAINFORM     {LOCAL:12, 2, RIGHT}/{LOCAL:11, 2, LEFT}    
IF LOCAL:12 >= LOCAL:11
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 11[次→]
	RESETCOLOR
ELSE
	PRINTBUTTON "11[次→]", 11
ENDIF
PRINTPLAIN    ┃  
IF LOCAL:22 <= 1
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 20[←前]
	RESETCOLOR
ELSE
	PRINTBUTTON "20[←前]", 20
ENDIF
PRINTPLAINFORM     {LOCAL:22, 2, RIGHT}/{LOCAL:21, 2, LEFT}    
IF LOCAL:22 >= LOCAL:21
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN 21[次→]
	RESETCOLOR
ELSE
	PRINTBUTTON "21[次→]", 21
ENDIF
PRINTL 

;●交渉点数の計算
;選択した士官の点数を合計
LOCAL:30 = 0
LOCAL:31 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			LOCAL:30 += GET_PRISONER_VALUE(LOCAL:0)
		ELSEIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
			LOCAL:31 += GET_PRISONER_VALUE(LOCAL:0)
		ENDIF
	ENDIF
NEXT

LOCAL:37 = LOCAL:30 * LOCAL:35 / 1000
LOCAL:38 = LOCAL:31 * LOCAL:36 / 1000
LOCAL:39 = LOCAL:37 - LOCAL:38
LOCAL:45 = MAX(GET_DIGIT(LOCAL:30), GET_DIGIT(LOCAL:31), 4)

DRAWLINE
PRINTFORM   解放点数: {ABS(LOCAL:30), LOCAL:45}(価値合計) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR COLOR("シアン")
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR COLOR("オレンジ")
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:35, 3)%
RESETCOLOR
PRINTFORML (政治能力差) = {LOCAL:37}

PRINTFORM   要求点数: {ABS(LOCAL:31), LOCAL:45}(価値合計) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR COLOR("シアン")
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR COLOR("オレンジ")
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:36, 3)%
RESETCOLOR
PRINTFORML (政治能力差) = {LOCAL:38}

PRINTFORM       合計:
IF LOCAL:39 == 0
	LOCALS:0 =  {ABS(LOCAL:39)}
ELSEIF LOCAL:39 > 0
	LOCALS:0 = +{ABS(LOCAL:39)}
	SETCOLOR COLOR("シアン")
ELSE
	LOCALS:0 = -{ABS(LOCAL:39)}
	SETCOLOR COLOR("オレンジ")
ENDIF
PRINTFORM %LOCALS:0, LOCAL:45 + 1%
RESETCOLOR
PRINTL 

LOCAL:40 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			LOCAL:40 = 1
			BREAK
		ELSEIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
			LOCAL:40 = 1
			BREAK
		ENDIF
	ENDIF
NEXT

IF LOCAL:40
	PRINTBUTTON "  0[差分を兵で補填して精算]", 0
	PRINT     
	PRINTBUTTON "  1[差分を経済力で補填して精算]", 1
	PRINT     
	IF LOCAL:39 >= 0
	PRINTBUTTON "  2[かまわず返還]", 2
	ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN   2[かまわず返還]
	RESETCOLOR
	ENDIF
	
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN   0[差分を兵で補填して精算]      1[差分を経済力で補填して精算]      2[かまわず返還]
	RESETCOLOR

ENDIF
PRINTL 
DRAWLINE
PRINTBUTTON " 99[戻る]", 99
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 99
	REDRAW 1
	RETURN
ELSEIF RESULT == 0 && LOCAL:40
	;要求兵数は、両国の規模の平均値に比例
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:所属) + GET_SUM_ECONOMY(ARG:0)
	LOCAL:52 = GET_SUM_SOLDIER(CFLAG:MASTER:所属)
	LOCAL:53 = GET_SUM_ARMY_DF(CFLAG:MASTER:所属)
	LOCAL:54 = MAX(GET_SUM_SOLDIER(ARG:0) - GET_OWN_CITY(ARG:0) * 500, 0)
	LOCAL:55 = MAX(GET_SUM_SOLDIER(CFLAG:MASTER:所属) - GET_OWN_CITY(CFLAG:MASTER:所属) * 500, 0)

	LOCAL:51 = ABS(LOCAL:39) * LOCAL:50 / 360000
	
	;ただし、自国が補填してもらう場合（解放点数が要求点数より大きい場合）、最大でも補填国の40%が限界
	;そして、自国が補填する場合（要求点数が解放点数以上の場合）、算出した要求兵数と合計点数のうち、大きい方を採用
	IF LOCAL:39 > 0
		LOCAL:51 = MIN(LOCAL:51, LOCAL:54 * 40 / 100)
	ELSE
		LOCAL:51 = MAX(LOCAL:51, ABS(LOCAL:39))
	ENDIF
	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %NAME:(LOCAL:5)%に対して兵{LOCAL:51}を要求することができます
		PRINTFORML 現在の総兵数:{LOCAL:52}(うち遊撃兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:所属)})
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:51
				PRINTFORML %NAME:(LOCAL:5)%は要求を満たすだけの兵を用意できないようだ…(準備可能:{LOCAL:54})
				CALL ASK_YN("あるだけよこせ！", "それなら交渉は決裂だ")
				IF RESULT == 0
					LOCAL:51 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;相手勢力から自国に兵を移動
				CALL SHIFT_SOLDIER(ARG:0, CFLAG:MASTER:所属, LOCAL:51)
				PRINTFORMW %NAME:(LOCAL:5)%勢力から{LOCAL:51}の兵を受け取りました
			ENDIF
		ENDIF

	ELSE
		PRINTFORML %NAME:(LOCAL:5)%に兵{LOCAL:51}を渡す必要があります
		PRINTFORML 現在の総兵数:{LOCAL:52}(うち遊撃兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:所属)})
		IF LOCAL:51 > LOCAL:52
			CALL ASK_MULTI_JUDGE("決定", 0, "キャンセル", 1)
		ELSE
			IF LOCAL:51 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
				IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + LOCAL:13
					PRINTFORML ※要求兵数が現在の遊撃兵数より多いため、全部隊を解散し、防衛に着いている兵も渡す必要があります
				ELSE
					PRINTFORML ※要求兵数が現在の遊撃兵数より多いため、防衛に着いている兵も渡す必要があります
				ENDIF
			ENDIF
			CALL ASK_YN("決定", "キャンセル")
		ENDIF
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;自国から相手勢力に兵を移動
			CALL SHIFT_SOLDIER(CFLAG:MASTER:所属, ARG:0, LOCAL:51)
			PRINTFORMW %NAME:(LOCAL:5)%勢力に{LOCAL:51}の兵を引き渡しました
		ENDIF
	ENDIF

	REDRAW 1
	RESTART

ELSEIF RESULT == 1 && LOCAL:40
	;要求経済力は、両国の規模の平均値に比例
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100
	LOCAL:51 = GET_SUM_ECONOMY(ARG:0) / 100
	LOCAL:52 = ABS(LOCAL:39) * (LOCAL:50 + LOCAL:51) / 70000
	LOCAL:53 = MAX(LOCAL:50 - GET_OWN_CITY(CFLAG:MASTER:所属) * 50, 0)
	LOCAL:54 = MAX(LOCAL:51 - GET_OWN_CITY(ARG:0) * 50, 0)


	;ただし、自国が補填してもらう場合（解放点数が要求点数より大きい場合）、最大でも補填国の20%が限界
	;そして、自国が補填する場合（要求点数が解放点数以上の場合）、算出した要求経済力と合計点数の1/3のうち、大きい方を採用
	IF LOCAL:39 > 0
		LOCAL:52 = MIN(LOCAL:52, LOCAL:54 * 20 / 100)
	ELSE
		LOCAL:52 = MAX(LOCAL:52, ABS(LOCAL:39) / 3)
	ENDIF

	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %NAME:(LOCAL:5)%に対して経済力{LOCAL:52}を要求することができます
		PRINTFORML 現在の経済力……%NAME:(LOCAL:4)%:{LOCAL:50}    %NAME:(LOCAL:5)%:{LOCAL:51}
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:52
				PRINTFORML %NAME:(LOCAL:5)%は要求を満たすだけの経済力を持っていないようだ…(準備可能:{LOCAL:54})
				CALL ASK_YN("あるだけよこせ！", "それなら交渉は決裂だ")
				IF RESULT == 0
					LOCAL:52 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;相手勢力から自国に経済力を移動
				CALL SHIFT_ECONOMY(ARG:0, CFLAG:MASTER:所属, LOCAL:52 * 100)
				PRINTFORMW %NAME:(LOCAL:5)%勢力から経済力{LOCAL:52}を受け取り所有都市に配分しました
			ENDIF
		ENDIF
	ELSE
		PRINTFORML %NAME:(LOCAL:5)%に経済力{LOCAL:52}を渡す必要があります
		PRINTFORML 現在の経済力……%NAME:(LOCAL:4)%:{LOCAL:50}    %NAME:(LOCAL:5)%:{LOCAL:51}
		IF LOCAL:52 > LOCAL:53
			PRINTFORML ※受け渡せる経済力の限界は{LOCAL:53}まで
			CALL ASK_MULTI_JUDGE("決定", 0, "キャンセル", 1)
		ELSE
			CALL ASK_YN("決定", "キャンセル")
		ENDIF
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;自国から相手勢力に兵を移動
			CALL SHIFT_ECONOMY(CFLAG:MASTER:所属, ARG:0, LOCAL:52 * 100)
			PRINTFORMW %NAME:(LOCAL:5)%勢力に{LOCAL:52}の経済力を受け渡しました
		ENDIF
	ENDIF

	REDRAW 1
	RESTART
	
;;;追加ここから　人質交渉　2返還
ELSEIF RESULT == 2 && LOCAL:40
	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %NAME:(LOCAL:5)%に捕虜を返還します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1  ;解放の実施
			LOCAL:25 = LOCAL:39 / 30		;好感度・敵対度の操作値は要調整

			IF LOCAL:60	;解放を実施==1
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;互いの国同士の好感度を上昇させ敵対値を低下させる
				CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 自軍が優位の場合のみ選べます	;出るはずのないメッセージ
	ENDIF
	REDRAW 1
	RESTART
;;追加ここまで　人質交渉　2返還

ELSEIF RESULT == 10
	LOCAL:12 = MAX(LOCAL:12 - 1, 1)
ELSEIF RESULT == 11
	LOCAL:12 = MIN(LOCAL:12 + 1, LOCAL:11)
ELSEIF RESULT == 20
	LOCAL:22 = MAX(LOCAL:22 - 1, 1)
ELSEIF RESULT == 21
	LOCAL:22 = MIN(LOCAL:12 - 1, LOCAL:11)
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:2 < 0
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	IF (CFLAG:(LOCAL:2):所属 == ARG:0 && CFLAG:(LOCAL:2):捕虜先 == CFLAG:MASTER:所属) || (CFLAG:(LOCAL:2):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:2):捕虜先 == ARG:0)
		CFLAG:(LOCAL:2):外交選択フラグ = !CFLAG:(LOCAL:2):外交選択フラグ
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------------------------------
;選択した解放し、解放メッセージを表示する
;-------------------------------------------------
@PRISONER_TRADE_RELEASE(ARG:0)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ && CFLAG:(LOCAL:0):所属 == ARG:0
		CALL CAPTURE(LOCAL:0, 0)
		PRINTFORML %NAME:(LOCAL:0)%を解放しました
		IF TALENT:(LOCAL:0):妊娠
			SELECTCASE RAND:4
				CASE 0
					PRINTFORML 捕虜になっていた%NAME:(LOCAL:0)%のお腹にはお土産が持たされている
				CASE 1
					PRINTFORML %NAME:(LOCAL:0)%のお腹の子について様々な憶測が流れている
				CASE 2
					PRINTFORML 辱められた上、こんな姿で味方のもとに返されることに%NAME:(LOCAL:0)%は涙している
				CASE 3
					PRINTFORML %NAME:(LOCAL:0)%の膨らんだ腹は返還式についてきた兵士たちの好奇の視線に晒されている
			ENDSELECT
		ELSEIF IS_LOVER(LOCAL:0)
			PRINTFORML %NAME:(LOCAL:0)%は解放を喜びつつも%ANAME(MASTER)%のことを想い、何度も振り返っている
		ELSEIF TALENT:(LOCAL:0):人気
			PRINTFORML 人気のある%NAME:(LOCAL:0)%の帰還を皆が喜んでいる
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		CALL CAPTURE(LOCAL:0, 0)
		PRINTFORML %NAME:(LOCAL:0)%は解放されました
		IF IS_LOVER(LOCAL:0)
			PRINTFORML %NAME:(LOCAL:0)%は%ANAME(MASTER)%が解放に尽力してくれたことを喜んでいる
		ELSEIF IS_SLAVE(LOCAL:0)
			PRINTFORML %NAME:(LOCAL:0)%は%ANAME(MASTER)%に捕虜になってしまったことを詫びている
		ELSEIF TALENT:(LOCAL:0):人気
			PRINTFORML 人気のある%NAME:(LOCAL:0)%の帰還を皆が喜んでいる
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;勢力ARG:0から勢力ARG:1にARG:2だけの兵力を移動させる処理
;-------------------------------------------------
@SHIFT_SOLDIER(ARG:0, ARG:1, ARG:2)
LOCAL:12 = GET_SUM_SOLDIER(ARG:0)
LOCAL:13 = GET_SUM_ARMY_DF(ARG:0)

IF ARG:2 > COUNTRY_SOLDIER:(ARG:0)
	IF ARG:2 > COUNTRY_SOLDIER:(ARG:0) + LOCAL:13
		CALL CLEAR_ALL_UNIT(ARG:0)
	ENDIF

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			COUNTRY_SOLDIER:(ARG:0) += CITY_SOLDIER:(LOCAL:0)
			CITY_SOLDIER:(LOCAL:0) = 0
		ENDIF
	NEXT

	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			LOCAL:15 = MIN(COUNTRY_SOLDIER:(ARG:0), 500)
			CITY_SOLDIER:(LOCAL:0) += LOCAL:15
			COUNTRY_SOLDIER:(ARG:0) -= LOCAL:15
		ENDIF
	NEXT

ELSE
	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)
ENDIF

COUNTRY_SOLDIER:(ARG:1) += ARG:2

;-------------------------------------------------
;人質交渉におけるキャラARG:0の価値  能力が高いほど価値が高くなる
;-------------------------------------------------
@GET_PRISONER_VALUE(ARG:0)
#FUNCTION
LOCAL:0 = 0
LOCAL:0 += (ABL:(ARG:0):武闘 + ABL:(ARG:0):防衛 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治) / 4
LOCAL:0 += (ABL:(ARG:0):歌唱 + ABL:(ARG:0):料理) / 2
;対象が特殊な勢力に捕らえられており、それが自国の捕らえている捕虜でない場合、価値は5倍に
SIF CFLAG:MASTER:所属 != CFLAG:(ARG:0):捕虜先 && IS_SP_COUNTRY(CFLAG:(ARG:0):捕虜先)
	LOCAL:0 *= 5
RETURNF POWER(MAX(LOCAL:0 - 40, 10), 2)

;-------------------------------------------------
;ARG:1の値分だけ勢力ARG:0との好感度を上昇させ敵対値を低下させる処理
;-------------------------------------------------
@DIPLOMACY_IMPROVE_RELATION(ARG:0, ARG:1)
#DIM 君主
#DIM 自勢力君主
#DIM 好感度
#DIM 敵対度
君主  = GET_COUNTRY_BOSS(ARG:0)
自勢力君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
好感度 = REL_LIKE:君主:自勢力君主
敵対度 = REL_HATE:君主:自勢力君主

CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:所属, ARG:0, ARG:1, -ARG:1)
CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:所属, ARG:1, -ARG:1)

SETCOLOR COLOR("注意")
PRINTFORML 好感度:{好感度, 4, RIGHT} → {REL_LIKE:君主:自勢力君主, 4, RIGHT}
PRINTFORML 敵対度:{敵対度, 4, RIGHT} → {REL_HATE:君主:自勢力君主, 4, RIGHT}
RESETCOLOR
