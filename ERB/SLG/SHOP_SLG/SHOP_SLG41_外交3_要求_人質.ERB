;-------------------------------------------------
;人質を要求する
;-------------------------------------------------
@DIPLOMACY_REQUEST_PRISONER(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%に対して、人質をよこせと要求した

CALL DIPLOMACY_REQUEST_COMMON(ARG:0, 70)
LOCAL:2 = RESULT
SIF LOCAL:2 <= 0
	RETURN 0

;ページ数を計算
LOCAL:12 = (24 - LOCAL:20) * 2
LOCAL:10 = 0
FOR LOCAL:0, 0, CHARANUM
	SIF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == 0 && LOCAL:0 != LOCAL:5　&& !IS_SP_CHARA(LOCAL:0)
		LOCAL:10 ++
NEXT
LOCAL:10 = MAX((LOCAL:10 - 1) / LOCAL:12 + 1, 1)
LOCAL:11 = 1

$SHOW_LOOP1

CALL SINGLE_DRAWLINE
PRINTL 捕虜にするキャラを選んで下さい
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

;キャラリストの表示
LOCAL:13 = 0
LOCAL:14 = 0
LOCAL:15 = (LOCAL:11 - 1) * LOCAL:12
LOCAL:16 = LOCAL:11 * LOCAL:12
LOCAL:17 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == 0 && LOCAL:0 != LOCAL:5　&& !IS_SP_CHARA(LOCAL:0)
		IF LOCAL:13 >= LOCAL:15 && LOCAL:13 < LOCAL:16
			IF LOCAL:14 % 2 != 0
				PRINTPLAIN 　
			ELSEIF LOCAL:14 >= 1
				PRINTL 
				LOCAL:17 ++
			ENDIF
			CALL PRINT_PARTNER_DATA(LOCAL:0)
			LOCAL:14 ++
		ENDIF
		LOCAL:13 ++
	ENDIF
NEXT
IF LOCAL:14 >= 1
	PRINTL 
	LOCAL:17 ++
ENDIF

IF LOCAL:10 >= 2
	LOCAL:17 += 2
	CALL SINGLE_DRAWLINE
	PRINTPLAIN   
	IF LOCAL:11 >= 2
		PRINTBUTTON "54[前のページ]", 54
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAIN 54[前のページ]
		RESETCOLOR
	ENDIF
	PRINTFORM           page{LOCAL:11, 2, RIGHT}/{LOCAL:10, 2, LEFT}         
	IF LOCAL:11 < LOCAL:10
		PRINTBUTTON "56[次のページ]", 56
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAIN 56[次のページ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

CALL SINGLE_DRAWLINE
PRINT    
PRINTBUTTON "0[キャンセル]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 54
	LOCAL:11 = MAX(LOCAL:11 - 1, 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56
	LOCAL:11 = MIN(LOCAL:11 + 1, LOCAL:10)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSE
	LOCAL:30 = NO_TO_CHARA(RESULT - 100)
	IF LOCAL:30 >= 0 && CFLAG:(LOCAL:30):所属 == ARG:0 && CFLAG:(LOCAL:30):捕虜先 == 0 && LOCAL:30 != LOCAL:5　&& !IS_SP_CHARA(LOCAL:30)
		CALL SINGLE_DRAWLINE
		CALL SHOW_INFO(LOCAL:30)
		CALL SINGLE_DRAWLINE
		PRINTL このキャラを我が国の捕虜として連れて行きますか？
		CALL ASK_YN()
		IF RESULT == 0
			CALL CAPTURE(LOCAL:30, CFLAG:MASTER:所属)
			;部隊に含まれているなら取り除く
			FOR LOCAL, 0, MAX_UNIT
				FOR LOCAL:1 , 0, 3
					SIF LOCAL:30 == GET_UNIT_COMMANDER(ARG:0, LOCAL, LOCAL:1)
						CALL CLEAR_UNIT_COMMANDER(ARG:0, LOCAL, LOCAL:1)
				NEXT
			NEXT
			;都市の守将でも取り除く
			FOR LOCAL, 0, MAX_CITY
				FOR LOCAL:1, 0, 2
					SIF LOCAL:30 == GET_CITY_COMMANDER(LOCAL, LOCAL:1)
						CALL CLEAR_CITY_COMMANDER(LOCAL, LOCAL:1)
				NEXT
			NEXT
			REDRAW 1
			;快楽で言うことを聞かせた場合、ご褒美の処理
			SIF LOCAL:2 == 2
				CALL DIPLOMACY_REQUEST_PRIZE(ARG:0)
			;薬物で言うことを聞かせた場合、ドラッグの処理
			SIF LOCAL:2 == 3
				CALL DIPLOMACY_REQUEST_DRUG(ARG:0)
			RETURN 1
		ELSE
			REDRAW 1
			GOTO SHOW_LOOP1
		ENDIF
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

