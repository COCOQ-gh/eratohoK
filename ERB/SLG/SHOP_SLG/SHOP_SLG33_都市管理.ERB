;-------------------------------------------------
;「都市管理」の名称
;-------------------------------------------------
@SHOP_SLG_NAME33
RESULTS:0 '= "都市管理"

;-------------------------------------------------
;「都市管理」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK33
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「都市管理」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY33
FLAG:戦略フェイズページ = 1
RETURN 0

;-------------------------------------------------
;「都市管理」の表示処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY_SHOW33
CALL SHOW_CITY_ARMY

;-------------------------------------------------
;「都市管理」の表示処理本体
;-------------------------------------------------
@SHOW_CITY_ARMY
#DIM LINE
#DIM WIDTH_CITYNAME = 30
#DIM ARR, MAX_CITY
#DIM PTR
#DIM CITY_PER_PAGE
#DIM WIDTH
#DIM FIRST_LINE
CITY_PER_PAGE = (MENU_HEIGHT-2-2)

VARSET ARR, -1
PTR = 0

FOR LOCAL, 0, CITY_NUM+1
	IF CITY_OWNER:(LOCAL) == CFLAG:MASTER:所属
		ARR:PTR = LOCAL
		PTR++
	ENDIF
NEXT

SHOP_SLG_LIST1_MAXPAGE = (PTR/CITY_PER_PAGE)

;剰余算に変更
SIF PTR%CITY_PER_PAGE != 0
	SHOP_SLG_LIST1_MAXPAGE ++

CALL COLUMN_LEFT_MENU_SHOW_SLG(0)

PRINTFORM %TOSTR_SPACE( (WIDTH_CITYNAME-6)/2 )%都市名%TOSTR_SPACE( (WIDTH_CITYNAME-6+1)/2 )%┃  経済  ┃防衛兵数┃  守将  ┃前期兵損┃ 危険度 ┃
CALL COLUMN_RIGHT_PRINTL_SLG
PRINT ━━━━━━━━━━━━━━━╋━━━━╋━━━━╋━━━━╋━━━━╋━━━━┫
CALL COLUMN_RIGHT_PRINTL_SLG

FOR LINE, 0, CITY_PER_PAGE
	LOCAL = ARR:(LINE + CITY_PER_PAGE * (FLAG:戦略フェイズページ - 1))
	IF LOCAL >= 0
		LOCALS =  %CITY_NAME:LOCAL%%TOSTR_SPACE(WIDTH_CITYNAME-STRLENS(CITY_NAME:LOCAL))%
		PRINTBUTTON LOCALS, 1000+LOCAL
		PRINT ┃
		PRINTPLAINFORM  {(CITY_ECONOMY:LOCAL)/100, 7, RIGHT}┃
		LOCALS = {CITY_SOLDIER:LOCAL, 8, RIGHT}
		IF IS_STAY_ENEMY_UNIT(LOCAL) == 1
			SETCOLOR カラー_警告
			PRINTFORM %LOCALS%
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS, 200+LOCAL
		ENDIF
		PRINTPLAINFORM ┃
		LOCALS = \@ GET_CITY_COMMANDER(LOCAL, 0) != -1 ? %"有", 8, RIGHT% # %"無", 8, RIGHT% \@
		IF IS_STAY_ENEMY_UNIT(LOCAL) == 1
			SETCOLOR カラー_警告
			PRINTFORM %LOCALS%
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS, 400 + LOCAL
		ENDIF
		PRINTPLAINFORM ┃
		PRINTPLAINFORM  {CITY_SOLDIER_PREV:LOCAL, 7, RIGHT}┃
		SELECTCASE GET_CITY_RISK(LOCAL)
			CASE 0
				LOCALS '= "  安全  "
			CASE 1
				LOCALS '= "  注意  "
			CASE 2
				LOCALS '= "  注意  "
			CASE 3
				LOCALS '= "  注意  "
			CASE 4
				LOCALS '= "  前線  "
				SETCOLOR カラー_オレンジ
			CASE 5
				LOCALS '= " 敵発見 "
				SETCOLOR カラー_警告
			CASE 6
				LOCALS '= " 交戦中 "
				SETCOLOR カラー_警告
		ENDSELECT
		
		PRINTPLAINFORM %LOCALS%
		RESETCOLOR
		PRINT ┃
	ELSE
	ENDIF
	CALL COLUMN_RIGHT_PRINTL_SLG
NEXT

CALL COLUMN_RIGHT_PRINTL_SLG
PRINTFORM                         

IF FLAG:戦略フェイズページ != 1
	PRINTBUTTON " <<< ", SHOP_SLG_LIST1_PAGE_RESET
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  <<< 
	RESETCOLOR
ENDIF

PRINT   
IF SHOP_SLG_LIST1_MAXPAGE > 1
	PRINTBUTTON " << ", SHOP_SLG_LIST1_PAGE_BACK
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  << 
	RESETCOLOR
ENDIF

PRINT    
PRINTFORM {FLAG:戦略フェイズページ, 2, RIGHT} / {SHOP_SLG_LIST1_MAXPAGE, 2, LEFT}

PRINT    
IF SHOP_SLG_LIST1_MAXPAGE > 1
	PRINTBUTTON " >> ", SHOP_SLG_LIST1_PAGE_NEXT
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  >> 
	RESETCOLOR
ENDIF

PRINT   
IF FLAG:戦略フェイズページ != SHOP_SLG_LIST1_MAXPAGE
	PRINTBUTTON " >>> ", SHOP_SLG_LIST1_PAGE_MAX
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  >>> 
	RESETCOLOR
ENDIF
PRINTL 
CALL SHOW_CITY_INFO(SHOWN_CITY)

;-------------------------------------------------
;「都市管理」防衛兵数の設定
;-------------------------------------------------
@SET_CITY_SOLDIER(ARG)
#DIM FIRST_LINE
#DIM PLUS = 100, 500, 1000, 5000, 10000, 50000, 100000
#DIM ERRMSG = 0

ERRMSG = 0

SIF !INRANGE(ARG, 1, CITY_NUM) || CITY_OWNER:ARG != CFLAG:MASTER:所属
	RETURN 0
SIF IS_STAY_ENEMY_UNIT(ARG)
	RETURN 0


REDRAW 0
;CLEARLINE LINES_SHOP_SLG-27-3-1

LOCAL:11 = CITY_SOLDIER:ARG
FIRST_LINE = LINECOUNT 
$INPUT_LOOP
LOCAL:21 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属) - (LOCAL:11 - CITY_SOLDIER:ARG)
CALL SINGLE_DRAWLINE
PRINTFORML %CITY_NAME:ARG%の防衛兵数を決定してください
PRINTFORML 直接入力も可能です
PRINTFORML 防衛兵数   {CITY_SOLDIER:ARG,8,RIGHT} → {LOCAL:11,8,RIGHT}
PRINTFORML 残存兵数   {COUNTRY_SOLDIER:(CFLAG:MASTER:所属),8,RIGHT} → {LOCAL:21,8,RIGHT}
SETCOLOR カラー_パ青
FOR LOCAL , 0, 7
	LOCALS = [+{PLUS:LOCAL}]
	PRINTBUTTON LOCALS, -1000-LOCAL
NEXT
PRINTL 
SETCOLOR カラー_パ赤
FOR LOCAL , 0, 7
	LOCALS = [-{PLUS:LOCAL}]
	PRINTBUTTON LOCALS, -10000-LOCAL
NEXT
RESETCOLOR
PRINTL 
PRINTL 
PRINTBUTTON "[確定]", -1
PRINTBUTTON "[キャンセル]", -2
PRINTL
CALL SINGLE_DRAWLINE
PRINTL 

SELECTCASE ERRMSG
	CASE 1
		PRINTFORM 最低でも500の兵数が必要です
	CASE 2
		PRINT 兵数が足りません
ENDSELECT
PRINTL 
REDRAW 1
INPUT

ERRMSG = 0
IF RESULT >= 0
	IF RESULT >= 500
		IF COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + CITY_SOLDIER:ARG < RESULT
			ERRMSG = 2
		ELSE
			GOTO OUT_OF_INPUT_LOOP
		ENDIF
	ELSE
		ERRMSG = 1
	ENDIF
ELSE
	LOCAL:0 = -1
	
	SIF RESULT == -1
		GOTO OUT_OF_INPUT_LOOP
		
	SIF RESULT == -2
		RETURN
	
	IF ABS(RESULT) >= 1000 && ABS(RESULT) <= 1001 + CITY_NUM
		LOCAL:0 = ABS(RESULT)-1000
		LOCAL:1 = 1
	ELSEIF ABS(RESULT) >= 10000 && ABS(RESULT) < 10100
		LOCAL:0 = ABS(RESULT)-10000
		LOCAL:1 = -1
	ENDIF
	
	IF (LOCAL:0 >= 0 && LOCAL:0 < 7)
		LOCAL:11 = LOCAL:11 + (PLUS:(LOCAL:0))*LOCAL:1
		LOCAL:11 = MIN(LOCAL:11, COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + CITY_SOLDIER:ARG)
		LOCAL:11 = MAX(LOCAL:11, 500)
	ENDIF
ENDIF

REDRAW 0

CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

REDRAW 1

$OUT_OF_INPUT_LOOP
LOCAL:21 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属) - (LOCAL:11 - CITY_SOLDIER:ARG)
CITY_SOLDIER:ARG = LOCAL:11
COUNTRY_SOLDIER:(CFLAG:MASTER:所属) = LOCAL:21

;-------------------------------------------------
;どこからも呼び出されていない
;-------------------------------------------------
;@MEMORY_PREVIOUS_ARMY
;
;FOR LOCAL:0, 0, MAX_COUNTRY
;	FOR LOCAL:1, 0, MAX_UNIT
;		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1)
;	NEXT
;NEXT
;
;FOR LOCAL, 0, MAX_CITY
;	CITY_SOLDIER_PREV = CITY_SOLDIER
;NEXT
