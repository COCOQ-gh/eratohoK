;-------------------------------------------------
;「領地探索」の名称
;-------------------------------------------------
@SHOP_SLG_NAME71
RESULTS:0 '= "領地探索"

;-------------------------------------------------
;「領地探索」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK71
SIF FLAG:観戦モード || CFLAG:MASTER:所属 == 0
	RETURN 0
SIF DONE_TERRITORYSEARCH == 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「領地探索」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY71
CALL TERRITORYSEARCH
RETURN 1

;-------------------------------------------------
;「領地探索」本体
;-------------------------------------------------
@TERRITORYSEARCH
#DIM 対象
#DIM 候補キャラ, 1000
#DIM 候補数
VARSET 候補キャラ
VARSET 候補数

CALL SINGLE_DRAWLINE
PRINTFORML 手の空いている者に領地を探索させることにした
PRINTFORML 誰に行かせようか？
CALL SELECT_CHARA_LIST_SLG()
IF RESULT < 0
	PRINTFORMW やはりやめておこう
	RETURN
ENDIF
DONE_TERRITORYSEARCH = 1
対象 = RESULT
CALL SET_COOLTIME(対象, 1)
PRINTFORMW %ANAME(対象)%は領地探索に出かけた
PRINTFORML ・
PRINTFORML ・
PRINTFORMW ・
DUMPRAND
TRYCCALLFORM TERRITORY_EVENT{RAND:222}(対象)
	IF RESULT
		CALL TRAIN_AUTO_ABLUP(対象)
	ELSE
		CALL TERRITORY_EVENT_NOT_EXIST
	ENDIF
CATCH
	CALL TERRITORY_EVENT_NOT_EXIST
ENDCATCH
INITRAND

RETURN 

