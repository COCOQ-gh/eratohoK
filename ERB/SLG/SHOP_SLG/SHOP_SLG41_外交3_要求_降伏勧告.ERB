;-------------------------------------------------
;降伏勧告を行う
;-------------------------------------------------
@DIPLOMACY_SURRENDER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
CALL TMP_CREATE_SUM_ECONOMY_MAP
CALL TMP_CREATE_SUM_SOLDIER_MAP
CALL TMP_CREATE_POLITICS_POWER_MAP

IF !TMP_COUNTRY_IS_NEIBORING:(ARG:0):(CFLAG:MASTER:所属)
	PRINTW 隣接していない勢力に降伏勧告を行うことはできません
	RETURN 0
ENDIF

IF IS_SP_COUNTRY(ARG:0)
	PRINTFORMW 勧告を突っぱねてきました
	RETURN 0
ENDIF

PRINTFORMW %ANAME(MASTER)%は%ANAME(LOCAL:5)%に対して降伏するように諭した
PRINTFORMW ………………
PRINTFORMW ……


;要求受け入れ判定
IF DIPLOMACY_REQUEST_CHECK(CFLAG:MASTER:所属, ARG:0, 80)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(LOCAL:5)%は我が国の勧告を受け入れ降伏しました
	RESETCOLOR

	;兵数の移動
	CALL CLEAR_ALL_UNIT(ARG:0)
	COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += COUNTRY_SOLDIER:(ARG:0)
	COUNTRY_SOLDIER:(ARG:0) = 0

	;領地の接収
	PRINTL 
	SETCOLOR カラー_注意
	PRINTFORML %ANAME(LOCAL:5)%の所領だった都市は我が国の領土となりました
	RESETCOLOR
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			CITY_OWNER:(LOCAL:0) = CFLAG:MASTER:所属
			PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
		ENDIF
	NEXT
	WAIT

	PRINTL 
	PRINTFORMW %ANAME(LOCAL:5)%軍の兵士が我が国の兵力として加わりました

	;自動で登用
	IF CONFIG:303 == 0
		;士官の組み込み
		PRINTL 
		SETCOLOR カラー_注意
		PRINTFORML %ANAME(LOCAL:5)%に仕えていた士官は我が国の士官として組み込まれました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == ARG:0
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属, 1)
					PRINTFORML %ANAME(LOCAL:0)%を登用しました
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
					SETCOLOR カラー_シアン
					PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
					RESETCOLOR
				ENDIF
			ENDIF
		NEXT
		WAIT

	;自動で投獄
	ELSEIF CONFIG:303 == 1
		;士官の組み込み
		PRINTL 
		SETCOLOR カラー_注意
		PRINTFORML %ANAME(LOCAL:5)%に仕えていた士官を投獄しました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == ARG:0
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
					CALL CAPTURE(LOCAL:0, CFLAG:MASTER:所属)
					PRINTFORML %ANAME(LOCAL:0)%を投獄しました
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
					SETCOLOR カラー_シアン
					PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
					RESETCOLOR
				ENDIF
			ENDIF
		NEXT
		WAIT

	;手動で選択
	ELSE
		CALL DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
	ENDIF
	
	;ARG:0勢力の捕虜となっていたキャラの処理
	;ARG:0勢力が捕虜持ってるか
	IF CMATCH(CFLAG:捕虜先, ARG:0)
		PRINTL
		PRINTFORMW %ANAME(LOCAL:5)%に囚われていた捕虜を見つけました
		LOCAL:2 = 0
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
				;自陣営の士官なら、そのまま元に戻る
				IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
					CALL CAPTURE(LOCAL:0, 0)
					SETCOLOR カラー_注意
					PRINTFORML %ANAME(LOCAL:5)%勢力に囚えられていた%ANAME(LOCAL:0)%は、我が国に復帰した
					RESETCOLOR
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
		IF LOCAL:2
			WAIT
		ENDIF
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
				;第三勢力の士官なら、TREAT_PRISONERで扱いを決定
				CALL TREAT_PRISONER(LOCAL:0, ARG:0, CFLAG:MASTER:所属)
			ENDIF
		NEXT
	ENDIF

	;旧勢力の後始末
	CALL DESTROY_COUNTRY(ARG:0)
	
	;外交補正の加算処理
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 5
ELSE
	PRINTFORMW 残念ながら、%ANAME(LOCAL:5)%は我が国の降伏勧告に応じませんでした…
	;外交補正の加算処理（他国の警戒が上がったと考えていただければ）
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 3
ENDIF

RETURN 1

;-------------------------------------------------
;降伏した勢力の士官の処遇を個別に設定 ARG:0=降伏した勢力
;-------------------------------------------------
@DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
;キャラの扱い設定用変数
#DIM CHARA_TREATMENT, 3000
#DIM CONST IN_PAGE_CHARA_NUM = 22
#DIM COMMANDER_NUM
#DIM LCOUNT
#DIM MAX_PAGE
#DIM PAGE
#DIM CHARA_COUNT_FOR_PRINT
#DIM FIRST_CHARA_COUNT_IN_THIS_PAGE
#DIM FIRST_CHARA_COUNT_IN_NEXT_PAGE
#DIM SELECT_CHARA
#DIM SELECTED_TREATMENT
#DIM INPUT_LINECOUNT
#DIM SHOW_LINECOUNT

VARSET CHARA_TREATMENT, 0

;●士官の人数+捕虜の人数をカウント
COMMANDER_NUM = GET_COMMANDER_NUM(ARG:0)

;士官が一人もいなければ戻る
IF COMMANDER_NUM == 0
	DEBUGPRINTFORML {ARG:0}番号の%ANAME(GET_COUNTRY_BOSS(ARG:0))%勢力に士官が誰もいませんでした
	RETURN
ENDIF

;●表示部分
MAX_PAGE = (COMMANDER_NUM - 1) / IN_PAGE_CHARA_NUM + 1
PAGE = 1

;SHOW_LOOP
SHOW_LINECOUNT = LINECOUNT
WHILE 1

	CALL SINGLE_DRAWLINE
	PRINTFORML %ANAME(GET_COUNTRY_BOSS(ARG:0))%に仕えていた士官の処遇を決めて下さい
	CALL SINGLE_DRAWLINE

	CHARA_COUNT_FOR_PRINT = 0
	FIRST_CHARA_COUNT_IN_THIS_PAGE = (PAGE - 1) * IN_PAGE_CHARA_NUM
	LOCAL:13 = PAGE * IN_PAGE_CHARA_NUM
	FOR LCOUNT, 0, CHARANUM
		IF CFLAG:LCOUNT:所属 == ARG:0
			IF CHARA_COUNT_FOR_PRINT >= FIRST_CHARA_COUNT_IN_THIS_PAGE && CHARA_COUNT_FOR_PRINT < LOCAL:13
				PRINTFORM %ANAME(LCOUNT), MAX_CHARANAME_LENGTH, RIGHT% …… 
				IF CFLAG:LCOUNT:捕虜先 && CFLAG:LCOUNT:捕虜先 != CFLAG:MASTER:所属
					SETCOLOR カラー_シアン
					PRINTPLAIN [他国で捕虜]
					RESETCOLOR
				ELSEIF CHARA_TREATMENT:LCOUNT == 0
					SETCOLOR カラー_選択中
					PRINTPLAIN [登用]
					RESETCOLOR
					PRINTBUTTON "[投獄]", LCOUNT * 2 + 101
				ELSE
					PRINTBUTTON "[登用]", LCOUNT * 2 + 100
					SETCOLOR カラー_選択中
					PRINTPLAIN [投獄]
					RESETCOLOR
				ENDIF
				PRINTL 
			ENDIF
			CHARA_COUNT_FOR_PRINT ++
		ENDIF
	NEXT
	CALL SINGLE_DRAWLINE

	IF MAX_PAGE >= 2
		LOCALS:0 = 
		IF PAGE >= 2
			LOCALS:0 = [8] 前のページ
		ENDIF
		PRINTFORM %LOCALS:0, 25, LEFT%

		LOCALS:0 = page{PAGE}/{MAX_PAGE}
		PRINTPLAINFORM %LOCALS:0, 25, LEFT%

		LOCALS:0 = 
		IF PAGE < MAX_PAGE
			LOCALS:0 = [9] 次のページ
		ENDIF
		PRINTFORM %LOCALS:0, 25, LEFT%
		PRINTL 
		CALL SINGLE_DRAWLINE
	ENDIF
	PRINT [1] 全員を登用に設定
	PRINTPLAIN     
	PRINT [2] 全員を投獄に設定
	PRINTL 
	CALL SINGLE_DRAWLINE
	PRINTL [0] 決定

	REDRAW 2

	;INPUT_LOOP
	INPUT_LINECOUNT = LINECOUNT
	WHILE 1
		INPUT

		IF RESULT == 0
			FOR LCOUNT, 0, CHARANUM
				IF CFLAG:LCOUNT:所属 == ARG:0
					CALL FORCE_FREE(LCOUNT)
					IF CFLAG:LCOUNT:捕虜先 && CFLAG:LCOUNT:捕虜先 != CFLAG:MASTER:所属
						CALL CHANGE_COUNTRY(LCOUNT, CFLAG:MASTER:所属)
					ELSEIF CHARA_TREATMENT:LCOUNT == 0
						CALL CHANGE_COUNTRY(LCOUNT, CFLAG:MASTER:所属, 1)
					ELSE
						CALL CHANGE_COUNTRY(LCOUNT, 0, 1)
						CALL CAPTURE(LCOUNT, CFLAG:MASTER:所属)
					ENDIF
				ENDIF
			NEXT
			REDRAW 1
			RETURN
		ELSEIF RESULT == 1
			VARSET CHARA_TREATMENT, 0
			BREAK
		ELSEIF RESULT == 2
			VARSET CHARA_TREATMENT, 1
			BREAK
		ELSEIF RESULT == 8 && PAGE >= 2
			PAGE --
			BREAK
		ELSEIF RESULT == 9 && PAGE < MAX_PAGE
			PAGE ++
			BREAK
		ELSEIF RESULT >= 100
			SELECT_CHARA = (RESULT - 100) / 2
			SELECTED_TREATMENT = (RESULT - 100) % 2
			IF SELECT_CHARA < CHARANUM && CFLAG:SELECT_CHARA:所属 == ARG:0 && !CFLAG:SELECT_CHARA:捕虜先
				CHARA_TREATMENT:(SELECT_CHARA) = SELECTED_TREATMENT
				BREAK
			ENDIF
			PRINTW 不適切なキャラ番号です
		ENDIF
		CLEARLINE LINECOUNT - INPUT_LINECOUNT
	WEND
	CLEARLINE LINECOUNT - SHOW_LINECOUNT
WEND

