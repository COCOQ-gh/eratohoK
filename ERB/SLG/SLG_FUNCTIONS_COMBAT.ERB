;-------------------------------------------------
;BATTLE系のミラー変数をセットする
;
;-------------------------------------------------
@SET_BATTLE_MIRROR_VAL(SIDE, CHARA:0 = -1, CHARA:1 = -1, CHARA:2 = -1)
#DIM SIDE
#DIM CHARA, 3
SIF !GROUPMATCH(SIDE, 0, 1)
	THROW SIDEには0か1を代入してください

FOR LOCAL, 0, VARSIZE("BATTLE_COMMANDER", 1)
	BATTLE_COMMANDER:SIDE:LOCAL = 0
	BATTLE_武闘:SIDE:LOCAL = 0
	BATTLE_知略:SIDE:LOCAL = 0
	BATTLE_政治:SIDE:LOCAL = 0
	BATTLE_歌唱:SIDE:LOCAL = 0
	BATTLE_料理:SIDE:LOCAL = 0
	BATTLE_妖術:SIDE:LOCAL = 0
	BATTLE_素質:SIDE:LOCAL = 0
	
	
NEXT
BATTLE_COMMANDER_NUM:SIDE = 0

FOR LOCAL:0, 0, VARSIZE("CHARA")
	SIF CHARA:LOCAL < 0 || CHARA:LOCAL > CHARANUM
		BREAK
	BATTLE_COMMANDER_NUM:SIDE ++
	BATTLE_COMMANDER:SIDE:LOCAL = CHARA:LOCAL
	BATTLE_武闘:SIDE:LOCAL = ABL:(CHARA:LOCAL):武闘
	BATTLE_知略:SIDE:LOCAL = ABL:(CHARA:LOCAL):知略
	BATTLE_政治:SIDE:LOCAL = ABL:(CHARA:LOCAL):政治
	BATTLE_歌唱:SIDE:LOCAL = ABL:(CHARA:LOCAL):歌唱
	BATTLE_料理:SIDE:LOCAL = ABL:(CHARA:LOCAL):料理
	BATTLE_妖術:SIDE:LOCAL = ABL:(CHARA:LOCAL):妖術
	BATTLE_素質:SIDE:LOCAL = TALENT:(CHARA:LOCAL):ＳＬＧ素質
NEXT

;-------------------------------------------------
;BATTLE:SIDE側の攻撃能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@ATTACK_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM ATTACK
ATTACK = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	ATTACK += ABL_POWER_X(BATTLE_武闘:SIDE:LOCAL, CURRENT_CHARA)
	SIF TALENT:CURRENT_CHARA:妖術知識
		ATTACK += ABL_POWER_X(BATTLE_妖術:SIDE:LOCAL, CURRENT_CHARA) / 2
NEXT
ATTACK = MAX(ATTACK, ABL_50_POWER) * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000
;防衛部隊用の計算式
IF CITY_NO && CITY_TYPE:CITY_NO == 0
	ATTACK = ATTACK / 16 + 9400
	RETURNF ATTACK * ((CITY_GUARD_RATE(CITY_NO) - 100) / 3 + 100) / 100
ENDIF
RETURNF ATTACK / 3 + 8800

;-------------------------------------------------
;BATTLE:SIDE側の防御能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@DEFENSE_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM DEFENSE
DEFENSE = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	DEFENSE += ABL_POWER_X(BATTLE_武闘:SIDE:LOCAL, CURRENT_CHARA)
	SIF TALENT:CURRENT_CHARA:妖術知識
		DEFENSE += ABL_POWER_X(BATTLE_妖術:SIDE:LOCAL, CURRENT_CHARA) / 2
NEXT
DEFENSE = MAX(DEFENSE, ABL_50_POWER) * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000
;防衛部隊用の計算式
IF CITY_NO && CITY_TYPE:CITY_NO == 0
	DEFENSE = DEFENSE / 8 + 9200
	RETURNF DEFENSE * CITY_GUARD_RATE(CITY_NO) * 8 / 100

ENDIF
RETURNF DEFENSE / 3 + 8800


;-------------------------------------------------
;BATTLE:SIDE側の知略能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@INTELLIGENCE_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM INTELLIGENCE
INTELLIGENCE = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	INTELLIGENCE += ABL_POWER_X(BATTLE_知略:SIDE:LOCAL, CURRENT_CHARA)
NEXT
INTELLIGENCE = MAX(INTELLIGENCE, ABL_50_POWER) * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000
RETURNF INTELLIGENCE


;-------------------------------------------------
;BATTLE:SIDE側の妖術能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@MAGIC_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM MAGIC
MAGIC = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	SIF BATTLE_妖術:SIDE:LOCAL > 0
		MAGIC += ABL_POWER_X(BATTLE_妖術:SIDE:LOCAL, CURRENT_CHARA)
NEXT
MAGIC = MAX(MAGIC, ABL_50_POWER) * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000
RETURNF MAGIC

;-------------------------------------------------
;BATTLE:SIDE側の妖術能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@SUPPORT_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM SONG_SUPPORT
#DIM FOOD_SUPPORT
#DIM SUPPORT
SUPPORT = 0
SONG_SUPPORT = 0
FOOD_SUPPORT = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	SONG_SUPPORT += MAX(ABL_POWER_X(BATTLE_歌唱:SIDE:LOCAL, CURRENT_CHARA) - ABL_50_POWER, 0)
	FOOD_SUPPORT += MAX(ABL_POWER_X(BATTLE_料理:SIDE:LOCAL, CURRENT_CHARA) - ABL_50_POWER, 0)
NEXT
SONG_SUPPORT = POWER_075(SONG_SUPPORT) * 100 / 514
FOOD_SUPPORT = POWER_075(FOOD_SUPPORT) * 100 / 514
SUPPORT = 1000 + (CITY_NO == 0 ? SONG_SUPPORT # 0) + FOOD_SUPPORT
RETURNF SUPPORT