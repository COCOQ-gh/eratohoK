@SLG_NEW_DIPLOMACY(勢力)
#DIM 勢力
#DIM 計画

SIF COUNTRY_IS_CLOSED:勢力
	RETURN

AI_DIPLOMACY_TERM:勢力 --

;期限切れなら外交計画更新
IF AI_DIPLOMACY_TERM:勢力 <= 0
	CALL CREATE_AI_DIPLOMACY_PLAN(勢力)
ENDIF

FOR 計画, 0, AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_行動回数
	SIF AI_DIPLOMACY_PLAN:勢力:計画 == -1
		CONTINUE
	SIF !IS_COUNTRY(AI_DIPLOMACY_TARGET:勢力:計画)
		CONTINUE
	SIF COUNTRY_IS_CLOSED:(AI_DIPLOMACY_TARGET:勢力:計画)
		CONTINUE
	SELECTCASE AI_DIPLOMACY_PLAN:勢力:計画
		CASE AI_外交計画_会談
			CALL SLG_DIPLOMACY_CONFERENCE(勢力, AI_DIPLOMACY_TARGET:勢力:計画)
		CASE AI_外交計画_同盟
			CALL SLG_DIPLOMACY_ALLIANCE(勢力, AI_DIPLOMACY_TARGET:勢力:計画)
		CASE AI_外交計画_停戦
			CALL SLG_DIPLOMACY_CEASE_FIRE(勢力, AI_DIPLOMACY_TARGET:勢力:計画)
		CASE AI_外交計画_悪評
			CALL SLG_DIPLOMACY_CREATE_SCANDAL(勢力, AI_DIPLOMACY_TARGET:勢力:計画)
	ENDSELECT
NEXT

;------------------------
;外交計画を作成する
;------------------------
@CREATE_AI_DIPLOMACY_PLAN(勢力)
#DIM 勢力
#DIM 君主
#DIM 相手勢力
#DIM 勢力インデックス
#DIM 勢力数
#DIM 相手勢力君主
#DIM 勢力リスト, MAX_COUNTRY
#DIM 関係性リスト, MAX_COUNTRY
#DIM 逆関係性リスト, MAX_COUNTRY
#DIM 経済力リスト, MAX_COUNTRY
#DIM 計画
#DIM 理由
#DIM CONST 理由_好き = 0
#DIM CONST 理由_嫌い = 1
#DIM CONST 理由_好かれ = 2
#DIM CONST 理由_嫌われ = 3
#DIM CONST 理由_経済 = 4
VARSET 勢力リスト
VARSET 関係性リスト, 1500
VARSET 逆関係性リスト, 1500
VARSET 経済力リスト, __INT_MIN__

君主 = GET_COUNTRY_BOSS(勢力)

AI_DIPLOMACY_TERM:勢力 = RAND(3, 6)

;外交計画をリセットする
FOR 計画, 0, VARSIZE("AI_DIPLOMACY_PLAN", 1)
	AI_DIPLOMACY_PLAN:勢力:計画 = -1
	AI_DIPLOMACY_TARGET:勢力:計画 = 0
NEXT

勢力数 = 0
FOR 相手勢力, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(相手勢力)
		CONTINUE
	SIF COUNTRY_IS_CLOSED:相手勢力
		CONTINUE
	SIF 勢力 == 相手勢力
		CONTINUE
	勢力リスト:勢力数 = 相手勢力
	相手勢力君主 = GET_COUNTRY_BOSS(相手勢力)
	関係性リスト:勢力数 = 1500 + REL_LIKE:君主:相手勢力君主 - REL_HATE:君主:相手勢力君主
	逆関係性リスト:勢力数 = 1500 + REL_LIKE:相手勢力君主:君主 - REL_HATE:相手勢力君主:君主
	経済力リスト:勢力数 = TMP_SUM_ECONOMY:相手勢力
	勢力数 ++
NEXT

SIF 勢力数 <= 0
	RETURN

FOR 計画, 0, MIN(AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_行動回数, AI_DIPLOMACY_MAX_PLAN)
	SIF 勢力数 <= 0
		BREAK
	SELECTCASE RAND:100
		CASE IS < 15
			勢力インデックス = FINDELEMENT(逆関係性リスト, MAXARRAY(逆関係性リスト, 0, 勢力数), 0, 勢力数 + 1)
			理由 = 理由_好かれ
		CASE IS < 40
			勢力インデックス = FINDELEMENT(逆関係性リスト, MINARRAY(逆関係性リスト, 0, 勢力数), 0, 勢力数 + 1)
			理由 = 理由_嫌われ
		CASE IS < 65
			勢力インデックス = FINDELEMENT(関係性リスト, MAXARRAY(関係性リスト, 0, 勢力数), 0, 勢力数 + 1)
			理由 = 理由_好き
		CASE IS < 80
			勢力インデックス = FINDELEMENT(関係性リスト, MINARRAY(関係性リスト, 0, 勢力数), 0, 勢力数 + 1)
			理由 = 理由_嫌い
		CASEELSE
			勢力インデックス = FINDELEMENT(経済力リスト, MINARRAY(経済力リスト, 0, 勢力数), 0, 勢力数 + 1)
			理由 = 理由_経済
	ENDSELECT

	相手勢力 = 勢力リスト:勢力インデックス
	AI_DIPLOMACY_TARGET:勢力:計画 = 相手勢力

	SELECTCASE 理由
		CASE 理由_好かれ
			SELECTCASE IFRAND("0TO74", 1, "75TO89", 逆関係性リスト:勢力インデックス > 1500 && TMP_COUNTRY_RELATION:勢力:相手勢力 == 0, "90TO99", 逆関係性リスト:勢力インデックス > 1800 && TMP_COUNTRY_RELATION:勢力:相手勢力 <= 2)
				CASE IS < 75
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_会談
				CASE IS < 90
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_停戦
				CASEELSE
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_同盟
			ENDSELECT
		CASE 理由_嫌われ
			SELECTCASE RAND:100
				CASE IS < 80
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_会談
				CASE IS < 99
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_悪評
			ENDSELECT
		CASE 理由_好き
			SELECTCASE IFRAND("0TO74", 1, "75TO89", 逆関係性リスト:勢力インデックス > 1500 && TMP_COUNTRY_RELATION:勢力:相手勢力 == 0, "90TO99", 逆関係性リスト:勢力インデックス > 1800 && TMP_COUNTRY_RELATION:勢力:相手勢力 <= 2)
				CASE IS < 75
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_会談
				CASE IS < 90
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_停戦
				CASEELSE
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_同盟
			ENDSELECT
		CASE 理由_嫌い
			SELECTCASE RAND:100
				CASE IS < 100
					AI_DIPLOMACY_PLAN:勢力:計画 = AI_外交計画_悪評
			ENDSELECT
		CASE 理由_経済
			AI_DIPLOMACY_PLAN:勢力:計画 = 関係性リスト:勢力インデックス > 1500 ? AI_外交計画_会談 # AI_外交計画_悪評
	ENDSELECT

	勢力数 --
	;重複して登録されることがないよう消しとく
	ARRAYSHIFT 勢力リスト,     -1, 0, 勢力インデックス
	ARRAYSHIFT 関係性リスト,   -1, 0, 勢力インデックス
	ARRAYSHIFT 逆関係性リスト, -1, 0, 勢力インデックス
	ARRAYSHIFT 経済力リスト,   -1, 0, 勢力インデックス

	;DEBUGPRINTFORML %ANAME(君主)%の外交計画{計画}番:相手%ANAME(GET_COUNTRY_BOSS(AI_DIPLOMACY_TARGET:勢力:計画))% 内容:{AI_DIPLOMACY_PLAN:勢力:計画} 理由:{理由}
NEXT



;------------------------
;会談を行う
;------------------------
@SLG_DIPLOMACY_CONFERENCE(勢力, 相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手勢力君主
#DIM 計画
#DIM 政治パワー
#DIM 料理パワー
#DIM 効果量
#DIM 拒否フラグ
#DIM 持ち出し額

拒否フラグ = 0

君主 = GET_COUNTRY_BOSS(勢力)
相手勢力君主 = GET_COUNTRY_BOSS(相手勢力)

IF 相手勢力 == CFLAG:MASTER:所属
	CALL COLORPRINTFORM(@"%ANAME(君主)%が%ANAME(相手勢力君主)%と会談しました", COLOR("注意"), "L")
ELSE
	PRINTFORM %ANAME(君主)%は%ANAME(相手勢力君主)%への会談を試みました
	;相手の外交対象に入っておらず、かつ乱数判定に失敗したら拒否フラグがたつ
	IF !GROUPMATCH(CHECK_AI_DIPLOMACY_PLAN(相手勢力, 勢力), AI_外交計画_会談, AI_外交計画_同盟, AI_外交計画_停戦) && RAND:MAX(1500 + REL_LIKE:相手勢力君主:君主 - REL_HATE:相手勢力君主:君主, 1000) < 500
		PRINTFORML が、%ANAME(相手勢力君主)%は歓迎しませんでした
		拒否フラグ = 1
	ELSE
		PRINTFORML 。%ANAME(相手勢力君主)%は歓迎したようです
	ENDIF
ENDIF

;ベース効果量計算　拒否なら半減
効果量 = (30 + RAND:21 + RAND:21) * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100 / (拒否フラグ + 1)

;DEBUGPRINTFORML 会談のベース効果量{効果量}

政治パワー = SQRT(TMP_SUM_POLITICS_POWER:勢力) / 10
料理パワー = SQRT(TMP_SUM_COOKING_POWER:勢力) / 10

効果量 = 効果量 * (政治パワー + 料理パワー + 100) / 100

持ち出し額 = MONEY:勢力 * AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_会談支払額 / 100

;金銭授受
MONEY:勢力 -= 持ち出し額
MONEY:相手勢力 += 持ち出し額

;DEBUGPRINTFORML 会談の政治料理パワー補正後効果量{効果量}

CALL CHANGE_RELATION_C_TO_C(勢力, 相手勢力, 効果量, 効果量 * -1)
CALL CHANGE_RELATION_C_TO_C(相手勢力, 勢力, 効果量, 効果量 * -1)

IF 相手勢力 != CFLAG:MASTER:所属 && REL_LIKE:相手勢力:勢力 - REL_HATE:相手勢力:勢力 > 600 && !RAND:4
	CALL CHANGE_AI_DIPLOMACY_PLAN(勢力, 相手勢力, AI_外交計画_停戦)
	PRINTFORML %ANAME(君主)%は%ANAME(相手勢力君主)%との停戦を試みるようです
ENDIF
;------------------------
;停戦をこころみる
;------------------------
@SLG_DIPLOMACY_CEASE_FIRE(勢力, 相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手勢力君主
#DIM 計画
#DIM 政治パワー
#DIM 料理パワー
#DIM 効果量
#DIM 持ち出し額
#DIM 判定値
;停戦以上の状態ならRETURN
SIF TMP_COUNTRY_RELATION:勢力:相手勢力 >= 1
	RETURN

君主 = GET_COUNTRY_BOSS(勢力)
相手勢力君主 = GET_COUNTRY_BOSS(相手勢力)

判定値 = MAX(1500 + REL_LIKE:相手勢力君主:君主 - REL_HATE:相手勢力君主:君主, 1000)
判定値 += LIMIT((TMP_SUM_ECONOMY:勢力 - TMP_SUM_ECONOMY:相手勢力) / 500, 0, 500)

;プレイヤーに対しての外交であった場合
IF 相手勢力 == CFLAG:MASTER:所属
	CALL SLG_DIPLOMACY_TO_PLAYER(勢力, AI_外交計画_停戦)
	SIF RESULT == 0
		RETURN 
ELSE
	PRINTFORM %ANAME(君主)%は%ANAME(相手勢力君主)%との停戦を試みました
	;相手の外交対象に入っておらず、かつ乱数判定に失敗したら拒否フラグがたつ
	IF !GROUPMATCH(CHECK_AI_DIPLOMACY_PLAN(相手勢力, 勢力), AI_外交計画_同盟, AI_外交計画_停戦) && RAND:MAX(判定値, 1000) < 1500
		PRINTFORML が、%ANAME(相手勢力君主)%は拒否しました
		IF !RAND:3
			CALL CHANGE_AI_DIPLOMACY_PLAN(勢力, 相手勢力, AI_外交計画_会談)
			PRINTFORML %ANAME(君主)%は会談を重ねる方針に切り替えたようです
		ENDIF
		RETURN
	ELSE
		PRINTFORML 。%ANAME(相手勢力君主)%は停戦に同意しました
	ENDIF
ENDIF

政治パワー = SQRT(TMP_SUM_POLITICS_POWER:勢力) / 10
料理パワー = SQRT(TMP_SUM_COOKING_POWER:勢力) / 10

効果量 = LIMIT((REL_LIKE:相手勢力君主:君主 - REL_HATE:相手勢力君主:君主) * (政治パワー + 料理パワー + 100) / 20000, 5, 15)

CALL COLORPRINTFORM(@"%ANAME(君主)%と%ANAME(相手勢力君主)%の停戦期間は{効果量}ターンです", COLOR("注意"), "L")
CALL INIT_CEASEFIRE(勢力, 相手勢力, 効果量)

TMP_COUNTRY_RELATION:勢力:相手勢力 = 1
TMP_COUNTRY_RELATION:相手勢力:勢力 = 1

;外交計画を更新
CALL CLEAR_AI_DIPLOMACY_PLAN(勢力, 相手勢力)
CALL CLEAR_AI_DIPLOMACY_PLAN(相手勢力, 勢力)

持ち出し額 = MONEY:勢力 * AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_停戦支払額 / 100

;金銭授受
MONEY:勢力 -= 持ち出し額
MONEY:相手勢力 += 持ち出し額

;------------------------
;同盟を試みる
;------------------------
@SLG_DIPLOMACY_ALLIANCE(勢力, 相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手勢力君主
#DIM 計画
#DIM 政治パワー
#DIM 料理パワー
#DIM 効果量
#DIM 判定値
#DIM 持ち出し額

;連合以上の状態ならRETURN
SIF TMP_COUNTRY_RELATION:勢力:相手勢力 >= 2
	RETURN

君主 = GET_COUNTRY_BOSS(勢力)
相手勢力君主 = GET_COUNTRY_BOSS(相手勢力)



判定値 = MAX(1500 + REL_LIKE:相手勢力君主:君主 - REL_HATE:相手勢力君主:君主, 1000)
判定値 += LIMIT((TMP_SUM_ECONOMY:勢力 - TMP_SUM_ECONOMY:相手勢力) / 500, 0, 500)

;プレイヤーに対しての外交であった場合
IF 相手勢力 == CFLAG:MASTER:所属
	CALL SLG_DIPLOMACY_TO_PLAYER(勢力, AI_外交計画_停戦)
	SIF RESULT == 0
		RETURN 
ELSE
	PRINTFORM %ANAME(君主)%は%ANAME(相手勢力君主)%との同盟を試みました
	;相手の外交対象に入っておらず、かつ乱数判定に失敗したら拒否フラグがたつ
	IF !CHECK_AI_DIPLOMACY_PLAN(相手勢力, 勢力) == AI_外交計画_同盟 && RAND:MAX(判定値, 1000) < 2000
		PRINTFORML が、%ANAME(相手勢力君主)%は拒否しました
		IF !RAND:3
			CALL CHANGE_AI_DIPLOMACY_PLAN(勢力, 相手勢力, AI_外交計画_会談)
			PRINTFORML %ANAME(君主)%は会談を重ねる方針に切り替えたようです
		ENDIF
		RETURN
	ELSE
		PRINTFORML 。%ANAME(相手勢力君主)%は同盟に同意しました
	ENDIF
ENDIF

政治パワー = SQRT(TMP_SUM_POLITICS_POWER:勢力) / 10
料理パワー = SQRT(TMP_SUM_COOKING_POWER:勢力) / 10

効果量 = LIMIT((REL_LIKE:相手勢力君主:君主 - REL_HATE:相手勢力君主:君主) * (政治パワー + 料理パワー + 100) / 20000, 5, 15)

CALL COLORPRINTFORM(@"%ANAME(君主)%と%ANAME(相手勢力君主)%の同盟期間は{効果量}ターンです", COLOR("注意"), "L")
CALL INIT_ALLIANCE(勢力, 相手勢力, 効果量)

TMP_COUNTRY_RELATION:勢力:相手勢力 = 3
TMP_COUNTRY_RELATION:相手勢力:勢力 = 3

;外交計画を更新
CALL CLEAR_AI_DIPLOMACY_PLAN(勢力, 相手勢力)
CALL CLEAR_AI_DIPLOMACY_PLAN(相手勢力, 勢力)


持ち出し額 = MONEY:勢力 * AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_同盟支払額 / 100

;金銭授受
MONEY:勢力 -= 持ち出し額
MONEY:相手勢力 += 持ち出し額


;------------------------
;悪評を流す
;------------------------
@SLG_DIPLOMACY_CREATE_SCANDAL(勢力, 相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手勢力君主
#DIM 計画
#DIM 政治パワー
#DIM 料理パワー
#DIM 効果量
#DIM 拒否フラグ

君主 = GET_COUNTRY_BOSS(勢力)
相手勢力君主 = GET_COUNTRY_BOSS(相手勢力)

IF 相手勢力 == CFLAG:MASTER:所属
	CALL COLORPRINTFORM(@"%ANAME(君主)%に%ANAME(相手勢力君主)%についての悪評を流されました", COLOR("注意"), "L")
ELSE
	PRINTFORML %ANAME(君主)%は%ANAME(相手勢力君主)%の悪評を流しました
ENDIF

;ベース効果量計算　拒否なら半減
効果量 = (5 + RAND:5 + RAND:5)

;DEBUGPRINTFORML 悪評のベース効果量{効果量}

政治パワー = SQRT(TMP_SUM_POLITICS_POWER:勢力) / 10
料理パワー = SQRT(TMP_SUM_COOKING_POWER:勢力) / 10

効果量 = 効果量 * (政治パワー + 料理パワー + 100) / 100

;DEBUGPRINTFORML 悪評の政治料理パワー補正後効果量{効果量}

FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL)
		CONTINUE
	SIF GROUPMATCH(LOCAL, 勢力, 相手勢力)
		CONTINUE
	CALL CHANGE_RELATION_C_TO_C(LOCAL, 相手勢力, 効果量 * -1, 効果量)
NEXT


;------------------------
;勢力から相手勢力への外交計画を返す
;------------------------
@CHECK_AI_DIPLOMACY_PLAN(勢力, 相手勢力)
#FUNCTION
#DIM 勢力
#DIM 相手勢力
#DIM 計画
FOR 計画, 0, AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_行動回数
	SIF AI_DIPLOMACY_TARGET:勢力:計画 == 相手勢力
		RETURNF AI_DIPLOMACY_PLAN:勢力:計画
NEXT
RETURNF -1

;------------------------
;勢力から相手勢力への外交計画を消去する
;------------------------
@CLEAR_AI_DIPLOMACY_PLAN(勢力, 相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 計画
FOR 計画, 0, AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_行動回数
	IF AI_DIPLOMACY_TARGET:勢力:計画 == 相手勢力
		AI_DIPLOMACY_PLAN:勢力:計画 = -1
		AI_DIPLOMACY_TARGET:勢力:計画 = 0
		RETURN 1
	ENDIF
NEXT
RETURN 0

;------------------------
;勢力から相手勢力への外交計画を変更する
;------------------------
@CHANGE_AI_DIPLOMACY_PLAN(勢力, 相手勢力, 新計画)
#DIM 勢力
#DIM 相手勢力
#DIM 計画
#DIM 新計画
FOR 計画, 0, AI_DIPLOMACY_PROPERTY:(COUNTRY_AI_TYPE:勢力):AI_外交_行動回数
	IF AI_DIPLOMACY_TARGET:勢力:計画 == 相手勢力
		AI_DIPLOMACY_PLAN:勢力:計画 = 新計画
		RETURN 1
	ENDIF
NEXT
RETURN 0

;------------------------
;ある勢力からプレイヤーへの外交打診(停戦・同盟)
;------------------------
@SLG_DIPLOMACY_TO_PLAYER(勢力, 計画)
#DIM 勢力
#DIM 君主
#DIM 計画
#DIMS CONST 内容 = "会談", "停戦", "同盟"
君主 = GET_COUNTRY_BOSS(勢力)
CALL COLORPRINTFORM(@"%ANAME(君主)%が%内容:計画%を申し出てきました", COLOR("注意"), "L")
PRINTFORML どうしますか？
CALL ASK_YN("受け入れる", "拒否")
IF RESULT == 1
	PRINTFORML 拒否しました
	;毎ターンきいてくるのはうざいのでクリア
	CALL CLEAR_AI_DIPLOMACY_PLAN(勢力, CFLAG:MASTER:所属)
	RETURN 0
ENDIF
PRINTFORML %内容:計画%に合意することにしました
RETURN 1

