;-------------------------------------------------
;シナリオ19
;幻想狂騒曲
;テーマは「完全シャッフル」
;勢力メンバーはおろか、リーダー及びその本拠地すらプレイごとにランダムで決めます
;
;-------------------------------------------------
@SCENARIO_NAME_19
RESULTS = 幻想狂騒曲
RETURN

@SCENARIO_INTRO_19
PRINTFORMW ※君主・メンバー・領地がランダムに選ばれるシナリオです
PRINTFORML
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW …くるり
CLEARLINE 1
PRINTFORMW クルリ、クルリト狂イテ舞エヨ
PRINTFORMW …縁も因果も、
CLEARLINE 1
PRINTFORMW 縁モ因果モ、今宵ハ意味ヲナシハセズ
PRINTFORMW ヒラリ
CLEARLINE 1
PRINTFORMW ヒラリ、ヒラリト踊リ明カセヤ
PRINTFORMW 思イモ恨ミモ、マゼコゼニ
PRINTFORMW ……
PRINTFORMW …
PRINTFORMW サァカナデヨウゾ
CLEARLINE 1
PRINTFORML サァハジメヨウゾ
PRINTFORMW ウタカタノ夢ノ如キ狂乱騒ギヲ……
PRINTFORMW ……
PRINTFORMW …
PRINTL

;ランダムキャラは選択に委ねる
FLAG:OPランダムキャラ使用 = 0


@SCENARIO_PLACEMENT_19
;初期の都市グループを記録するSTART_CITY
;能力値計算用のTEAM_ABL
;勢力数COUNTRY_NO
#DIM START_CITY, 3
#DIM COUNTRY_NO
#DIM CHARA_NO, 2
VARSET START_CITY, 0
VARSET COUNTRY_NO, 0

;勢力設定
;勢力数は8～14程度
COUNTRY_NO = 12 + RAND:3 + RAND:3 + RAND:3
DEBUGPRINTFORML 【勢力数】:{COUNTRY_NO}
CALL FISHER_YATES_SHAFFLE(MAX_CITY)
;勢力ごとの支配都市の決定
;ロジックとしては
;・配列を順に見ていき、最初に拾った中立都市を起点とする
;・起点都市と隣接する都市全てを中立であれば確保する
;・中間点は80%の確率でその先を取る
LOCAL:0 = 1
FOR LOCAL:1 , 0 , MAX_CITY
	;シャッフルした配列から都市番号を参照
	LOCAL:2 = SHAFFLE_ARRAY:(LOCAL:1)
	IF !CITY_OWNER:(LOCAL:2) && CITY_TYPE:(LOCAL:2) == 0 && LOCAL:2
		CITY_OWNER:(LOCAL:2) = LOCAL:0
		START_CITY:0 = LOCAL:2
		FOR LOCAL:3, 0, 10
			START_CITY:1 = CITY_ROOT:(LOCAL:2):(LOCAL:3)
			SIF START_CITY:1 == 0
				BREAK
			SELECTCASE CITY_TYPE:(START_CITY:1)
			CASE 0
				SIF !CITY_OWNER:(START_CITY:1)
					CITY_OWNER:(START_CITY:1) = LOCAL:0
			CASE 1
				SIF RAND:100 >80
					CONTINUE
				;旧式の方法のままだが調整がめんどくさいので放置
				FOR LOCAL:4, 0, 10
					START_CITY:2 = CITY_ROOT:(START_CITY:1):(LOCAL:4)
					SIF START_CITY:2 == 0
						BREAK
					IF START_CITY:2 != START_CITY:1 && !CITY_OWNER:(START_CITY:2)
						CITY_OWNER:(START_CITY:2) = LOCAL:0
						BREAK
					ENDIF
				NEXT
			ENDSELECT
		NEXT
	DEBUGPRINTFORML 【勢力{LOCAL:0}】拠点:%CITY_NAME:(LOCAL:2)% 都市数:{GET_OWN_CITY(LOCAL:0)}
	LOCAL:0 ++ 
	SIF COUNTRY_NO < LOCAL:0
		BREAK
	ENDIF
NEXT

COUNTRY_BOSS:0 = 0
COUNTRY_COLOR:0 = GETDEFCOLOR()
CITY_OWNER:0 = 0

;勢力ごとにキャラクターを分配
;各勢力3-6人（都市数が3以下であれば+2、1であれば+3）
LOCAL:0 = 1
CALL FISHER_YATES_SHAFFLE(CHARANUM)
FOR LOCAL:1 , 0 , CHARANUM
	IF !CHARA_NO
		CHARA_NO = 3 + RAND:3 + RAND:3 + MAX((7 - GET_OWN_CITY(LOCAL:0)) / 2, 0)
		COUNTRY_AI_TYPE = RAND:6
		DEBUGPRINTFORML 【{LOCAL:0}士官数】{CHARA_NO}
	ENDIF
	IF !CFLAG:(SHAFFLE_ARRAY:(LOCAL:1)):1 && SHAFFLE_ARRAY:(LOCAL:1) != NAME_TO_CHARA("あなた")
		CFLAG:(SHAFFLE_ARRAY:(LOCAL:1)):1 = LOCAL:0
		CHARA_NO --
		IF CMATCH(CFLAG:1, LOCAL:0) == 1
			COUNTRY_BOSS:(LOCAL:0) = GET_ID(SHAFFLE_ARRAY:(LOCAL:1))
			COUNTRY_COLOR:(LOCAL:0) = CHARA_DIPRO_COLOR_F(CSTR:(SHAFFLE_ARRAY:(LOCAL:1)):99)
			;真っ白ならランダム
			SIF CHARA_DIPRO_COLOR_F(CSTR:(SHAFFLE_ARRAY:(LOCAL:1)):99) == 0xFFFFFF
				COUNTRY_COLOR:(LOCAL:0) = RAND:16 * (16 * 256 * 256) + RAND:16 * (16 * 256) + RAND:16 * 16
			;黒すぎたらどれか一系統を明るくして、多少なりとも分かりやすくする
			IF (COUNTRY_COLOR:(LOCAL:0) >> 16 & 0xFF) <= 0x50 && (COUNTRY_COLOR:(LOCAL:0) >> 8 & 0xFF) <= 0x50 && (COUNTRY_COLOR:(LOCAL:0) & 0xFF) <= 0x50
				SELECTCASE RAND:3
					CASE 0
						LOCAL:2 = RAND(0x30, 0x60)
					CASE 1
						LOCAL:2 = RAND(0x30, 0x60) << 8
					CASE 2
						LOCAL:2 = RAND(0x30, 0x60) << 16
				ENDSELECT
				COUNTRY_COLOR:(LOCAL:0) += LOCAL:2
			ENDIF
			;白すぎたらどれか一系統を暗めに
			IF (COUNTRY_COLOR:(LOCAL:0) >> 16 & 0xFF) >= 0xC0 && (COUNTRY_COLOR:(LOCAL:0) >> 8 & 0xFF) >= 0xC0 && (COUNTRY_COLOR:(LOCAL:0) & 0xFF) >= 0xC0
				SELECTCASE RAND:3
					CASE 0
						LOCAL:2 = RAND(0x30, 0x50)
					CASE 1
						LOCAL:2 = RAND:0x50 << 8
					CASE 2
						LOCAL:2 = RAND(0x30, 0x50) << 16
				ENDSELECT
				COUNTRY_COLOR:(LOCAL:0) -= LOCAL:2
			ENDIF
		ENDIF
		IF !CHARA_NO
			LOCAL:0 ++
			SIF COUNTRY_NO < LOCAL:0
				BREAK
		ENDIF
	ENDIF
NEXT

;指定されなかったキャラは放浪 「あなた」は除外（MASTER入れ替えたときに放浪するのを防ぐ）。
FOR LOCAL:0, 1, CHARANUM
	SIF CFLAG:(LOCAL:0):1 == 0  && CSTR:(LOCAL:0):99 != "あなた"
		CFLAG:(LOCAL:0):特殊状態 = 1
NEXT

;兵力の初期配置
CALL INIT_ARMY


;無所属都市の兵数は経済の7.5%
FOR LOCAL:0, 0, MAX_CITY
	SIF CITY_OWNER:(LOCAL:0) == 0
		CITY_SOLDIER:(LOCAL:0) = CITY_ECONOMY:(LOCAL:0) * 75 / 1000
NEXT

;■関係設定
CALL SHARED_SETTING_RELATION

;勢力間の感情値
FOR LOCAL:0, 0, COUNTRY_NO
	FOR LOCAL:1, LOCAL:0 + 1, COUNTRY_NO
		CALL CHANGE_RELATION_C_TO_C(LOCAL:0, LOCAL:1, 500 + POWER(-1, RAND:2 + 1) * (RAND:101 + RAND:101), 500 + POWER(-1, RAND:2 + 1) * (RAND:101 + RAND:101))
	NEXT
NEXT

;個人間の感情値
CALL FISHER_YATES_SHAFFLE(CHARANUM)
FOR LOCAL:1, 0, CHARANUM - 1
	CALL CHANGE_RELATION_O_TO_O(SHAFFLE_ARRAY:(LOCAL:1), SHAFFLE_ARRAY:(LOCAL:1 + 1), RAND:400 - 200, RAND:400 - 200)
NEXT
;-------------------------------------------------
;定例イベント
;ターンエンド時に呼び出される
;-------------------------------------------------
@SCENARIO_EVENT_19
#DIM UNIFY

