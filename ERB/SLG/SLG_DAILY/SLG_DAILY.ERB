;複数シナリオにまたがるイベント処理
;SLG_DAILYは戦略フェイズ終了時に判定が呼び出される。
;ただし観戦モード時は一律で無効。
;主人公が放浪しているか勢力に所属しているかは、各イベントの実行判定の責任において判断すること。
;クリアフラグを一応確認するようにしているが、あれが立つと戦略フェイズ画面でターンを進められなくなるはずなので、問題ないっちゃない。

@SLG_DAILY()
#DIM EVENT_LIST, 100
#DIM EVENT_PICK, 1000
#DIM EVENT_ODDS
#DIM EVENT_COUNT, 2
VARSET LOCAL
VARSET EVENT_PICK

SIF FLAG:観戦モード
	RETURN 0

DAILY_CANCEL = 0

;派生イベント関係の処理
CALL SLG_DAILY_DERIVATION

;イベント発生数のセット
EVENT_COUNT:0 = SDCONFIG:951 + 1, SDCONFIG:952 + 1

;イベントリストの作成（起動後作成していなければ）
IF !EVENT_LIST:1
	VARSET EVENT_LIST, -1
	EVENT_LIST:0 = SDCON_肉便器, SDCON_雌犬, SDCON_苗床, SDCON_薬物中毒
	EVENT_LIST:10 = SDCON_仕官の誘い, SDCON_闇討ちの提案, SDCON_弾幕ごっこ
ENDIF

;デイリーイベント発生チェック
;例の関数で順番を決めてSELECTCASEにぶん投げる
;順番にイベントフラグ及び発生期間の確認と確率ロールを行い、大丈夫であればイベント本体に移動
;その後、終了フラグが立っていればループを脱出する


;イベント発生倍率のセット
;2^(コンフィグ値-1)+1、つまりDCONFIG:950が0/1/2/3/4の場合1/2/3/5/10倍になる
;なお1000を超えた場合、そのイベントが必ず発生するだけで何ら問題はない（ぉ
EVENT_ODDS = POWER(2, SDCONFIG:SDCON_通常発生頻度 - 1) + 1

;いつもの順番決定、拡張性を考えて枠は100個用意させていただきました
CALL FISHER_YATES_SHAFFLE(100)

;念のため順番を記録
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_PICK"

FOR LOCAL:0, 0, 100
	RESULT = -1
	SELECTCASE EVENT_LIST:(EVENT_PICK:(LOCAL:0))

	;肉便器イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE SDCON_肉便器
		SIF !SDCONFIG:SDCON_肉便器
			CALL PUBLIC_TOILET

	;薬物依存イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE SDCON_薬物中毒
		SIF !SDCONFIG:SDCON_薬物中毒
			CALL DRUG_ADDICT

	;雌犬用イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE SDCON_雌犬
		SIF !SDCONFIG:SDCON_雌犬
			CALL ZOOPHILIA

	;苗床用イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE SDCON_苗床
		SIF !SDCONFIG:SDCON_苗床
			CALL SEEDBED

	;士官しないかと誘われる
	;確率判定は本体で行う
	CASE SDCON_仕官の誘い
		IF !SDCONFIG:SDCON_仕官の誘い && !FLAG:クリアフラグ && !SDVAR:仕官の誘い_非勧誘残ターン
			DAILY_NAME=士官の誘い
			CALL INVITE_TO_COUNTRY
		ENDIF
	;闇討ち
	;15期以降、確率2.5%で発生
	CASE SDCON_闇討ちの提案
		IF !SDCONFIG:SDCON_闇討ちの提案 && DAY >= 15 && RAND:1000 < 25 * EVENT_ODDS && !FLAG:クリアフラグ
			CALL ASSASSIN_CHALLENGE
		ENDIF
	;5期以降、確率3%で発生
	CASE SDCON_弾幕ごっこ
		IF !SDCONFIG:SDCON_弾幕ごっこ && DAY >= 5 && RAND:1000 < 30 * EVENT_ODDS
			CALL DANMAKU
		ENDIF
	;12期以降、確率5%で発生
	CASE SDCON_触手強制出産
		IF !SDCONFIG:SDCON_触手強制出産 && DAY >= 12 && RAND:1000 < 50 * EVENT_ODDS
			CALL TENTACLE_BIRTH
		ENDIF
	;12期以降、確率3%で発生
	CASE SDCON_兵士慰安
		IF !SDCONFIG:SDCON_兵士慰安 && DAY >= 12 && RAND:1000 < 30 * EVENT_ODDS
			CALL COMFORT
		ENDIF
	;未定義
	CASEELSE
		CONTINUE
	ENDSELECT

	;正常終了していたらカウントを減らす
	;イベントに入る前に弾かれていた場合はカウントしない
	SIF RESULT != -1
		EVENT_COUNT:0 --
	;終了フラグが立っていれば口上デイリー処理へ
	SIF DAILY_CANCEL || !EVENT_COUNT:0
		BREAK

NEXT


