;-------------------------------------------------
;各国から勧誘される処理
;-------------------------------------------------
@INVITE_TO_COUNTRY
#DIM 雇用確率
#DIM 支度金
#DIM 首領
;すでにどこかに所属OR捕虜だと駄目
SIF CFLAG:MASTER:所属 || CFLAG:MASTER:捕虜先
	RETURN -1
	
;雇用確率を能力に応じて決定
雇用確率 = (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治) * 2 / 3

;確率判定に失敗したら駄目
SIF 雇用確率 <= RAND:300
	RETURN -1


;確率判定に成功したら、現存する国家からランダムでループ
CALL FISHER_YATES_SHAFFLE(MAX_COUNTRY)
FOR LOCAL, 0, MAX_COUNTRY
	IF IS_COUNTRY(SHAFFLE_ARRAY:(LOCAL)) && !IS_SP_COUNTRY(SHAFFLE_ARRAY:(LOCAL))
		;仕官してから野に下った5勢力からは誘われない
		IF FINDELEMENT(COUNTRY_PLAYER_BELONGED, SHAFFLE_ARRAY:(LOCAL)) != -1
			CONTINUE
		ENDIF
		LOCAL:1 = LOCAL
		BREAK
	ENDIF
NEXT

;BREAKせずループしきった、つまり上記条件に該当する国がなければ駄目
SIF LOCAL == MAX_COUNTRY
	RETURN -1

;支度金の計算
首領 = GET_COUNTRY_BOSS(SHAFFLE_ARRAY:(LOCAL:1))
支度金 = 雇用確率 * 20 / 1000 * 1000 + RAND:1000 + RAND:(MAX(1, CFLAG:首領:好感度))

DAILY_NAME = 仕官の誘い
CALL DAILY_START()
PRINTFORMW %ANAME(首領)%から、仕官しないかという誘いが来た
PRINTFORM 支度金として金
SETCOLOR COLOR("注意")
PRINTFORM {支度金}
RESETCOLOR
PRINTFORMW を用意しているようだ
PRINTFORML この乱世、どこかに仕えて力を蓄えておくのは一つの手だ
PRINTFORML ただし、わざわざ誘われた以上、すぐに下野することは認められない
PRINTFORM 最低でも
SETCOLOR COLOR("注意")
PRINTFORM 10
RESETCOLOR
PRINTFORMW 期程度は務めなくてはならない
PRINTFORML どうしようか……
PRINTFORML
CALL ASK_YN("誘いを受ける", "丁重に辞退する")
IF RESULT == 0
	PRINTFORML %ANAME(首領)%に仕えることにした
	PRINTFORMW 配下の武将たちは%ANAME(MASTER)%を歓迎しているようだ……
	PRINTFORMW 支度金{支度金}を受け取った
	CALL CHANGE_COUNTRY(MASTER, SHAFFLE_ARRAY:(LOCAL:1))
	MONEY += 支度金
	FOR LOCAL:2, 0, CHARANUM
		IF CFLAG:(LOCAL:2):所属 == CFLAG:MASTER:所属
			CFLAG:(LOCAL:2):好感度 += RAND(300, 600)
		ENDIF
	NEXT
	PLAYER_HIRED_COUNTER = 10
ELSE
	PRINTFORMW 誘いを丁重に断った
	SDVAR:仕官の誘い_非勧誘残ターン = RAND(1, 3)
ENDIF
