; ARG:0に侵攻する
@INVADE(ARG:0)
#DIM POINTER
#DIM ARR,8
#DIM FROM
CLEARLINE 1
PRINTL 
IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}期までは侵攻を行うことができません
ELSEIF IS_INVADABLE(ARG:0, CFLAG:MASTER:1)
	
	;連合に参加しているなら他の国はだめ
	LOCAL:6 = GET_UNION_TARGET(CFLAG:MASTER:1)
	IF LOCAL:6 > 0 && LOCAL:6 != CITY_OWNER:(ARG:0)
		LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
		SETCOLOR COLOR("注意")
		PRINTFORML ※反%NAME:(LOCAL:7)%連合に参加中であるため、%NAME:(LOCAL:7)%勢力以外への出撃ができません
		RESETCOLOR
		WAIT
		RETURN
	ENDIF
	
	$INPUT_LOOP
	DRAWLINE
	PRINTL ●侵攻する部隊を選択してください
	DRAWLINE
	CALL LISTUP_FORCE(CFLAG:MASTER:1, "CHECK_FORCE_SELECTABLE_INVADE")
	DRAWLINE
	PRINTBUTTON " 0[    戻る]", 0
	INPUT
	LOCAL:10 = RESULT
	IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
		IF CHECK_FORCE_SELECTABLE_INVADE_F(CFLAG:MASTER:1, LOCAL:10 - 1)
			;チェック（コード流用）
			ARG:1 = CFLAG:MASTER:1
			POINTER = 0
			FOR LOCAL:0, 0, MAX_CITY
				; 条件
				; LOCAL:0が中継地でない
				; LOCAL:0がARG:0と隣接
				; LOCAL:0が通行可能（LOCAL:0の支配国との関係が2以上）
				; ARG:1がARG:0の支配国と敵対
				
				LOCAL:1 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   ARG:1)
				LOCAL:2 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(LOCAL:0), ARG:1)
				IF CITY_TYPE:(LOCAL:0) == 0 && IS_ROOT(ARG:0, LOCAL:0) && (LOCAL:1 <= 0 || CITY_OWNER:(ARG:0) == 0) && LOCAL:2 >= 2
					ARR:POINTER = LOCAL:0
					POINTER++
					SIF POINTER == 8
					BREAK
				ENDIF
			NEXT
			
			IF POINTER == 0
				PRINT このメッセージが表示されたら報告をお願いします
			ELSEIF POINTER == 1
				FROM = ARR:0
			ELSE
				DRAWLINE
				PRINTL ●出発地点を選んでください
				DRAWLINE
				FOR LOCAL:0, 0, POINTER
					LOCALS:0 = [{LOCAL:0}]%GET_CITYNAME(ARR:(LOCAL:0))%
					PRINTBUTTON LOCALS:0, LOCAL:0 + 1
					PRINT  
				NEXT
				PRINTL 
				$INPUT_LOOP2
				INPUT
				LOCAL:11 = RESULT
				IF LOCAL:11 > 0 && LOCAL:11 <=POINTER
					FROM = ARR:(LOCAL:11 - 1)
				ELSE
					CLEARLINE 1
					GOTO INPUT_LOOP2
				ENDIF
			ENDIF
			UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1)   = ARG:0
			IF FROM != UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1)
				UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1) = FROM
				PRINTFORML 部隊{LOCAL:10}を%GET_CITYNAME(FROM)%へ移動しました
				CALL SET_COMMANDER_COOLTIME(CFLAG:MASTER:1, LOCAL:10 - 1)
			ENDIF
			PRINTFORML 部隊{LOCAL:10}の移動目標を%GET_RELAYNAME(ARG:0)%に設定します
			WAIT
			;DEBUGPRINTFORML Invade {FROM} -> {ARG:0}
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF LOCAL:10 == 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ENDIF

;-------------------------------------------------
; ARG:0に移動する
;-------------------------------------------------
@MOVETO(ARG:0)
#DIM FROM
CLEARLINE 1
PRINTL 
IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}期までは移動を行うことができません
ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   CFLAG:MASTER:1) >= 2
	
	$INPUT_LOOP
	DRAWLINE
	PRINTL ●移動する部隊を選択してください
	DRAWLINE
	CALL LISTUP_FORCE(CFLAG:MASTER:1, "CHECK_FORCE_SELECTABLE_MOVETO", ARG:0)
	DRAWLINE
	PRINTBUTTON " 0[    戻る]", 0
	INPUT
	LOCAL:10 = RESULT
	IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
		IF CHECK_FORCE_SELECTABLE_MOVETO_F(CFLAG:MASTER:1, LOCAL:10 - 1, ARG:0)
			FROM = UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1)
			
			IF CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:1, CITY_OWNER:(UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1))) >= 2
				UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1) = ARG:0
				PRINTFORML 部隊{LOCAL:10}が%GET_CITYNAME(FROM)%から%GET_RELAYNAME(ARG:0)%へ移動しました
				IF UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1) != 0
					UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1) = 0
					PRINTL 現在の移動目標は解除されます
				ENDIF
				CALL SET_COMMANDER_COOLTIME(CFLAG:MASTER:1, LOCAL:10 - 1)
			ELSE
				UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1) = ARG:0
				PRINTFORML 部隊{LOCAL:10}の移動目標を%GET_RELAYNAME(ARG:0)%に設定します
			ENDIF
			
			WAIT
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF LOCAL:10 == 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ENDIF

;-------------------------------------------------
; ARG :0 の勢力の部隊を選択可能リストアップ
; ARGS:1 条件関数
; ARG :2 任意の引数
;  0=選択不可（表示無し）1=選択可 -1=選択不可（COLOR("選択不可")）-2=選択不可（COLOR("警告")）
;-------------------------------------------------
@LISTUP_FORCE(ARG:0, ARGS:1, ARG:2=0)
LOCAL:20 = GETCOLOR()
RESETCOLOR
FOR LOCAL:0, 0, MAX_UNIT
	RESETCOLOR
	RESULT = 0
	TRYCALLFORM %ARGS:1%, ARG:0, LOCAL:0, ARG:2
	
	IF RESULT != 0
		SELECTCASE RESULT
			CASE -1
				SETCOLOR COLOR("選択不可")
			CASE -2
				SETCOLOR COLOR("警告")
		ENDSELECT
	
		LOCALS:0  = {LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]
		LOCALS:0 += " "
		IF RESULT > 0
			PRINTBUTTON LOCALS:0, LOCAL:0 +1
		ELSE
			PRINTFORM %LOCALS:0%
		ENDIF
		CALL SHOW_UNIT_INFO(CFLAG:MASTER:1, LOCAL:0, 13)
	ENDIF
NEXT
SETCOLOR LOCAL:20

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊が侵攻で選択可能かチェックする
; REUTNRN
;  0=選択不可（表示無し）1=選択可 -1=選択不可（COLOR("選択不可")）-2=選択不可（COLOR("警告")）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_INVADE(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT
@CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
#FUNCTION
IF UNIT_SOLDIER:(ARG:0):(ARG:1) > 0
	;強制解散は赤
	IF UNIT_BREAK_TERM:(ARG:0):(ARG:1) >= 1
		RETURNF -2
	;関係2以上の国にいなかったら灰
	ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)), ARG:0) < 2
		RETURNF -1
	;そうじゃなかったら選べる
	ELSE
		RETURNF 1
	ENDIF
	RETURNF 0
ENDIF

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊がARG:2への移動で選択可能かチェックする
; REUTNRN
;  0=選択不可（表示無し）1=選択可 -1=選択不可（COLOR("選択不可")）-2=選択不可（COLOR("警告")）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_MOVETO(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT
@CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
#FUNCTION
IF UNIT_SOLDIER:(ARG:0):(ARG:1) > 0
	;強制解散は赤
	IF UNIT_BREAK_TERM:(ARG:0):(ARG:1) >= 1
		RETURNF -2
	;既にそこにいるなら灰
	ELSEIF UNIT_POSITION:(ARG:0):(ARG:1) == ARG:2
		RETURNF -1
	;関係2以上の国にいれば白
	ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)), ARG:0) >= 2
		RETURNF 1
	;目的地が繋がっていて目的地が関係2以上の国か中継地点なら白
	ELSEIF IS_ROOT(UNIT_POSITION:(ARG:0):(ARG:1), ARG:2) && (CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:2), ARG:0) >= 2 || CITY_TYPE:(ARG:2) == 1)
		RETURNF 1
	;そうじゃなかったら灰
	ELSE
		RETURNF -1
	ENDIF
	RETURNF 0
ENDIF

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊がARG:2のクールタイム
;-------------------------------------------------
@SET_COMMANDER_COOLTIME(ARG:0, ARG:1, ARG:2=1)
FOR LOCAL:0, 0, 3
	SIF GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL:0) >= 0
		SLG_COOLTIME:(GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL:0)):0 = ARG:2
NEXT