; ARG:0に侵攻する
@INVADE(ARG:0)
#DIM POINTER
#DIM POINTER2
#DIM ARR,10
#DIM ARR2,10
#DIM FROM
#DIM 勢力
#DIM 部隊攻撃
#DIM 部隊知略
#DIM 都市攻撃
#DIM 都市知略
VARSET 部隊攻撃
VARSET 部隊知略
VARSET 都市攻撃
VARSET 都市知略
CLEARLINE 1
PRINTL 
IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}期までは侵攻を行うことができません
ELSEIF IS_INVADABLE(ARG:0, CFLAG:MASTER:1)
	
	;連合に参加しているなら他の国はだめ
	LOCAL:6 = GET_UNION_TARGET(CFLAG:MASTER:1)
	IF LOCAL:6 > 0 && LOCAL:6 != CITY_OWNER:(ARG:0)
		LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
		SETCOLOR COLOR("注意")
		PRINTFORML ※反%NAME:(LOCAL:7)%連合に参加中であるため、%NAME:(LOCAL:7)%勢力以外への出撃ができません
		RESETCOLOR
		WAIT
		RETURN
	ENDIF
	勢力 = CITY_OWNER:(ARG:0)
	IF IS_COUNTRY(CITY_OWNER:(ARG:0))
		;部隊能力を計算
		CALL TMP_CREATE_IS_FREE_MAP
		LOCALS:0 = 
		LOCAL:2 = 0
		CALL CHECK_UNIT_COMMANDER_BEST(勢力)
		FOR LOCAL, 0, 3
			LOCAL:1 = UNIT_COMMANDER_BEST:LOCAL
			IF LOCAL:1 >= 0
				SIF LOCAL:2 != 0
					LOCALS:0 = %LOCALS:0%・
				LOCALS:0 = %LOCALS:0%%ANAME(LOCAL:1)%
			ENDIF
			LOCAL:2 ++
		NEXT
		SIF LOCALS:0 != ""
			LOCALS:0 = <%LOCALS:0%>
		部隊攻撃 = UNIT_ATTACK_LEVEL_VIRTUAL(勢力, UNIT_COMMANDER_BEST:0, UNIT_COMMANDER_BEST:1, UNIT_COMMANDER_BEST:2)
		部隊知略 = UNIT_INTELLIGENCE_LEVEL_VIRTUAL(勢力, UNIT_COMMANDER_BEST:0, UNIT_COMMANDER_BEST:1, UNIT_COMMANDER_BEST:2)

		;都市能力を計算
		LOCALS:1 = 
		LOCAL:2 = 0
		CALL CHECK_CITY_COMMANDER_BEST(ARG:0, 1)
		FOR LOCAL, 0, 2
			LOCAL:1 = CITY_COMMANDER_BEST:LOCAL
			IF LOCAL:1 >= 0
				SIF LOCAL:2 != 0
					LOCALS:1 = %LOCALS:1%・
				LOCALS:1 = %LOCALS:1%%ANAME(LOCAL:1)%
			ENDIF
			LOCAL:2 ++
		NEXT
		SIF LOCALS:1 != ""
			LOCALS:1 = <%LOCALS:1%>
		都市攻撃 = CITY_ATTACK_LEVEL_VIRTUAL(ARG:0, CITY_COMMANDER_BEST:0, CITY_COMMANDER_BEST:1)
		都市知略 = CITY_INTELLIGENCE_LEVEL_VIRTUAL(ARG:0, CITY_COMMANDER_BEST:0, CITY_COMMANDER_BEST:1)
	ENDIF
	
	$INPUT_LOOP
	DRAWLINE
	PRINTL ●侵攻する部隊を選択してください
	IF IS_COUNTRY(CITY_OWNER:(ARG:0))
		PRINTFORML 現時点でありえる最強の部隊:%LOCALS:0, 32, LEFT% 武闘:%TOSTR_UNIT_POWER_SHORT(部隊攻撃), 6, LEFT%知略:%TOSTR_UNIT_POWER_SHORT(部隊知略)%
		PRINTFORML 現時点でありえる最強の守将:%LOCALS:1, 32, LEFT% 武闘:%TOSTR_UNIT_POWER_SHORT(都市攻撃), 6, LEFT%知略:%TOSTR_UNIT_POWER_SHORT(都市知略)%
	ENDIF
	DRAWLINE
	CALL LISTUP_FORCE(CFLAG:MASTER:1, "CHECK_FORCE_SELECTABLE_INVADE")
	DRAWLINE
	PRINTBUTTON " 0[    戻る]", 0
	INPUT
	LOCAL:10 = RESULT
	IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
		IF CHECK_FORCE_SELECTABLE_INVADE_F(CFLAG:MASTER:1, LOCAL:10 - 1)
			;チェック（コード流用）
			ARG:1 = CFLAG:MASTER:1
			POINTER = 0
			POINTER2 = 0
			FOR LOCAL:0, 0, MAX_CITY
				; 条件
				; LOCAL:0が中継地でない
				; LOCAL:0がARG:0と隣接
				; LOCAL:0が通行可能（LOCAL:0の支配国との関係が2以上）
				; ARG:1がARG:0の支配国と敵対
				
				LOCAL:1 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   ARG:1)
				LOCAL:2 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(LOCAL:0), ARG:1)
				IF CITY_TYPE:(LOCAL:0) == 0 && IS_ROOT(ARG:0, LOCAL:0) && (LOCAL:1 <= 0 || CITY_OWNER:(ARG:0) == 0) && LOCAL:2 >= 2
					IF IS_ROOT(ARG:0, LOCAL:0) == 1
						ARR:POINTER = LOCAL:0
						POINTER++
					ELSE
						ARR2:POINTER2 = LOCAL:0
						POINTER2++
					ENDIF
				ENDIF
			NEXT
			
			IF (POINTER + POINTER2) == 0
				PRINT このメッセージが表示されたら報告をお願いします
			ELSEIF (POINTER + POINTER2) == 1
				IF POINTER == 1
					FROM = ARR:0
				ELSE
					FROM = ARR2:0
				ENDIF
			ELSE
				DRAWLINE
				PRINTL ●出発地点を選んでください
				DRAWLINE
				IF POINTER
					PRINTL 1ターンで進軍可能
					PRINT   
					FOR LOCAL:0, 0, POINTER
						LOCAL:1 = ARR:(LOCAL:0)
						SIF LOCAL:1 == UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1)
							SETCOLOR COLOR("オレンジ")
						LOCALS:0 = [{LOCAL:0+1}] %GET_CITYNAME(ARR:(LOCAL:0))%
						PRINTBUTTON LOCALS:0, LOCAL:0 + 1
						RESETCOLOR
						PRINT   
					NEXT
					PRINTL 
					PRINTL 
				ENDIF
				
				IF POINTER2
					PRINTL 中継地を経由
					PRINT   
					FOR LOCAL:0, 0, POINTER2
						LOCAL:1 = ARR2:POINTER2
						SIF LOCAL:1 == UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1)
							SETCOLOR COLOR("オレンジ")
						LOCALS:0 = [{LOCAL:0+101}] %GET_CITYNAME(ARR2:(LOCAL:0))%
						PRINTBUTTON LOCALS:0, LOCAL:0 + 101
						RESETCOLOR
						PRINT   
					NEXT
					PRINTL 
					PRINTL 
				ENDIF
				PRINTBUTTON "[0] やめる", 0
				PRINTL 
				PRINTL 
				$INPUT_LOOP2
				INPUT
				LOCAL:11 = RESULT
				
				IF POINTER && INRANGE(LOCAL:11, 1, 0+POINTER)
					FROM = ARR:(LOCAL:11 - 1)
				ELSEIF POINTER2 && INRANGE(LOCAL:11, 101, 100+POINTER2)
					FROM = ARR2:(LOCAL:11 - 101)
				ELSEIF LOCAL:11 == 0
					RETURN
				ELSE
					CLEARLINE 1
					GOTO INPUT_LOOP2
				ENDIF
			ENDIF
			UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1)   = ARG:0
			IF FROM != UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1)
				UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1) = FROM
				PRINTFORML 部隊{LOCAL:10}を%GET_CITYNAME(FROM)%へ移動しました
				CALL SET_COMMANDER_COOLTIME(CFLAG:MASTER:1, LOCAL:10 - 1)
			ENDIF
			PRINTFORML 部隊{LOCAL:10}の移動目標を%GET_RELAYNAME(ARG:0)%に設定します
			IF IS_ROOT(ARG:0, FROM) ==2
				SETCOLOR COLOR("注意")
				PRINTFORML 中継地点を経由して進軍します
				RESETCOLOR
			ENDIF
			WAIT
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF LOCAL:10 == 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ENDIF

;-------------------------------------------------
; ARG:0に移動する
;-------------------------------------------------
@MOVETO(ARG:0)
#DIM FROM
CLEARLINE 1
PRINTL 
IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}期までは移動を行うことができません
ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   CFLAG:MASTER:1) >= 2
	
	$INPUT_LOOP
	DRAWLINE
	PRINTL ●移動する部隊を選択してください
	DRAWLINE
	CALL LISTUP_FORCE(CFLAG:MASTER:1, "CHECK_FORCE_SELECTABLE_MOVETO", ARG:0)
	DRAWLINE
	PRINTBUTTON " 0[    戻る]", 0
	INPUT
	LOCAL:10 = RESULT
	IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
		IF CHECK_FORCE_SELECTABLE_MOVETO_F(CFLAG:MASTER:1, LOCAL:10 - 1, ARG:0)
			FROM = UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1)
			
			IF CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:1, CITY_OWNER:(UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1))) >= 2
				UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:10 - 1) = ARG:0
				PRINTFORML 部隊{LOCAL:10}が%GET_CITYNAME(FROM)%から%GET_RELAYNAME(ARG:0)%へ移動しました
				IF UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1) != 0
					UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1) = 0
					PRINTL 現在の移動目標は解除されます
				ENDIF
			ELSE
				UNIT_TARGET:(CFLAG:MASTER:1):(LOCAL:10 - 1) = ARG:0
				PRINTFORML 部隊{LOCAL:10}の移動目標を%GET_RELAYNAME(ARG:0)%に設定します
			ENDIF
			
			WAIT
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF LOCAL:10 == 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ENDIF

;-------------------------------------------------
; ARG :0 の勢力の部隊を選択可能リストアップ
; ARGS:1 条件関数
; ARG :2 任意の引数
;  0=選択不可（表示無し）1=選択可 -1=選択不可（COLOR("選択不可")）-2=選択不可（COLOR("警告")）
;-------------------------------------------------
@LISTUP_FORCE(ARG:0, ARGS:1, ARG:2=0)
LOCAL:20 = GETCOLOR()
RESETCOLOR
FOR LOCAL:0, 0, MAX_UNIT
	RESETCOLOR
	RESULT = 0
	TRYCALLFORM %ARGS:1%, ARG:0, LOCAL:0, ARG:2
	
	IF RESULT != 0
		SELECTCASE RESULT
			CASE -1
				SETCOLOR COLOR("選択不可")
			CASE -2
				SETCOLOR COLOR("警告")
		ENDSELECT
	
		LOCALS:0  = {LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]
		LOCALS:0 += " "
		IF RESULT > 0
			PRINTBUTTON LOCALS:0, LOCAL:0 +1
		ELSE
			PRINTFORM %LOCALS:0%
		ENDIF
		CALL SHOW_UNIT_INFO(CFLAG:MASTER:1, LOCAL:0, 13)
	ENDIF
NEXT
SETCOLOR LOCAL:20

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊が侵攻で選択可能かチェックする
; REUTNRN
;  0=選択不可（表示無し）1=選択可 -1=選択不可（COLOR("選択不可")）-2=選択不可（COLOR("警告")）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_INVADE(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT
@CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
#FUNCTION
IF UNIT_SOLDIER:(ARG:0):(ARG:1) > 0
	;強制解散は赤
	IF UNIT_BREAK_TERM:(ARG:0):(ARG:1) >= 1
		RETURNF -2
	;関係2以上の国にいなかったら灰
	ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)), ARG:0) < 2
		RETURNF -1
	;そうじゃなかったら選べる
	ELSE
		RETURNF 1
	ENDIF
	RETURNF 0
ENDIF

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊がARG:2への移動で選択可能かチェックする
; REUTNRN
;  0=選択不可（表示無し）1=選択可 -1=選択不可（COLOR("選択不可")）-2=選択不可（COLOR("警告")）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_MOVETO(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT
@CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
#FUNCTION
IF UNIT_SOLDIER:(ARG:0):(ARG:1) > 0
	;強制解散は赤
	IF UNIT_BREAK_TERM:(ARG:0):(ARG:1) >= 1
		RETURNF -2
	;既にそこにいるなら灰
	ELSEIF UNIT_POSITION:(ARG:0):(ARG:1) == ARG:2
		RETURNF -1
	;関係2以上の国にいれば白
	ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)), ARG:0) >= 2
		RETURNF 1
	;目的地が繋がっていて目的地が関係2以上の国か中継地点なら白
	ELSEIF IS_ROOT(UNIT_POSITION:(ARG:0):(ARG:1), ARG:2) && (CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:2), ARG:0) >= 2 || CITY_TYPE:(ARG:2) == 1)
		RETURNF 1
	;そうじゃなかったら灰
	ELSE
		RETURNF -1
	ENDIF
	RETURNF 0
ENDIF

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊がARG:2のクールタイム
;-------------------------------------------------
@SET_COMMANDER_COOLTIME(ARG:0, ARG:1, ARG:2=1)
FOR LOCAL:0, 0, 3
	SIF GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL:0) >= 0
		COOLTIME:(GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL:0)):0 = ARG:2
NEXT



;-------------------------------------------------
;ARG:1, ARG:2, ARG:3のキャラからなるARG:0勢力の歌唱・料理補正値を返す関数(0.1%単位)
;-------------------------------------------------
@UNIT_SUPPORT_LEVEL_VIRTUAL(ARG:0, ARG:1, ARG:2, ARG:3)
#FUNCTION
LOCAL:3 = 0
LOCAL:4 = 0
FOR LOCAL:0, 1, 4
	LOCAL:2 = ARG:(LOCAL:0)
	IF LOCAL:2 >= 0
		;能力が50未満だと切り捨てられる
		;(コスプレパッチ追加分)メイド服や踊り子衣装の場合、能力+10分の恩恵を得られる
		;(補正値は服装嗜好Lv×2)
		
		;踊り子衣装or吟遊詩人服
		IF CFLAG:(LOCAL:2):92 == 2 || CFLAG:(LOCAL:2):92 == 5
			LOCAL:3 += MAX(ABL_POWER_X(ABL:(LOCAL:2):歌唱+ABL:(LOCAL:2):衣装嗜好, LOCAL:2) - ABL_50_POWER, 0)
		ELSE
			LOCAL:3 += MAX(ABL_POWER_X(ABL:(LOCAL:2):歌唱, LOCAL:2) - ABL_50_POWER, 0)
		ENDIF
		
		;メイド服or執事服
		IF CFLAG:(LOCAL:2):92 == 1 || CFLAG:(LOCAL:2):92 == 4
			LOCAL:4 += MAX(ABL_POWER_X(ABL:(LOCAL:2):料理+ABL:(LOCAL:2):衣装嗜好, LOCAL:2) - ABL_50_POWER, 0)
		ELSE
			LOCAL:4 += MAX(ABL_POWER_X(ABL:(LOCAL:2):料理, LOCAL:2) - ABL_50_POWER, 0)
		ENDIF
	ENDIF
NEXT
LOCAL:3 = 1000 + POWER_075(LOCAL:3) * 100 / 514 + POWER_075(LOCAL:4) * 100 / 514
RETURNF LOCAL:3


;-------------------------------------------------
;ARG:1, ARG:2, ARG:3のキャラからなるARG:0勢力の部隊の攻撃力を計算
;-------------------------------------------------
@UNIT_ATTACK_LEVEL_VIRTUAL(ARG:0, ARG:1, ARG:2, ARG:3)
#FUNCTION
;将の補正
LOCAL:3 = 0
FOR LOCAL:0, 1, 4
	LOCAL:2 = ARG:(LOCAL:0)
	IF LOCAL:2 >= 0
		LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):武闘, LOCAL:2)
		
		;コスプレ衣装を着ている場合、0.9倍される
		IF CFLAG:(LOCAL:2):92
			LOCAL:3 = LOCAL:3 * 9 /10
			;申し訳程度に衣装嗜好による補正がかかる(最大1.1倍)
			LOCAL:3 = LOCAL:3 * (100+ABL:(LOCAL:2):衣装嗜好) / 100
		ENDIF
		
		IF TALENT:(LOCAL:2):妖術知識
			;巫女装束、陰陽師装束を着ている場合、妖術の補正が1.0倍〜1.2倍される
			;(衣装嗜好Lv×0.05倍)
			IF CFLAG:(LOCAL:2):92 == 3 || CFLAG:(LOCAL:2):92 == 6
				LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):妖術 * (100 + ABL:(LOCAL:2):衣装嗜好 * 2) / 100, LOCAL:2) / 2
			ELSE
				LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):妖術, LOCAL:2) / 2
			ENDIF
		ENDIF
	ENDIF
NEXT
;歌唱による補正
LOCAL:3 = MAX(LOCAL:3, ABL_50_POWER) * UNIT_SUPPORT_LEVEL_VIRTUAL(ARG:0, ARG:1) / 1000
;防衛AIの場合85%
IF ARG:0 != CFLAG:MASTER:1 && COUNTRY_AI_TYPE:(ARG:0) == AI_防衛
	TIMES LOCAL:3, 0.85
ENDIF
RETURNF LOCAL:3 / 4 + 8800


;-------------------------------------------------
;ARG:0勢力ARG:1部隊の知略パワーを決定
;-------------------------------------------------
@UNIT_INTELLIGENCE_LEVEL_VIRTUAL(ARG:0, ARG:1, ARG:2, ARG:3)
#FUNCTION
;将の補正
LOCAL:3 = 0
FOR LOCAL:0, 1, 4
	LOCAL:2 = ARG:(LOCAL:0)
	IF LOCAL:2 >= 0
		LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):知略, LOCAL:2)
	ENDIF
NEXT
;歌唱による補正
LOCAL:3 = MAX(LOCAL:3, ABL_50_POWER) * UNIT_SUPPORT_LEVEL_VIRTUAL(ARG:0, ARG:1) / 1000
RETURNF LOCAL:3


;-------------------------------------------------
;都市ARG:0にARG:1, ARG:2が守将として設定されたときの防衛部隊の歌唱・料理補正値を返す関数(0.1%単位)
;-------------------------------------------------
@CITY_SUPPORT_LEVEL_VIRTUAL(ARG:0, ARG:1, ARG:2)
#FUNCTION
LOCAL:3 = 0
LOCAL:4 = 0
FOR LOCAL:0, 1, 3
	LOCAL:2 = ARG:(LOCAL:0)
	IF LOCAL:2 >= 0
		;能力が50未満だと切り捨てられる
		;(コスプレパッチ追加分)メイド服や踊り子衣装の場合、能力+10分の恩恵を得られる
		;(補正値は服装嗜好Lv×2)
		
		;踊り子衣装
		IF CFLAG:(LOCAL:2):92 == 2 || CFLAG:(LOCAL:2):92 == 5
			LOCAL:3 += MAX(ABL_POWER_X(ABL:(LOCAL:2):歌唱+ABL:(LOCAL:2):衣装嗜好, LOCAL:2) - ABL_50_POWER, 0)
		ELSE
			LOCAL:3 += MAX(ABL_POWER_X(ABL:(LOCAL:2):歌唱, LOCAL:2) - ABL_50_POWER, 0)
		ENDIF
		
		;メイド服
		IF CFLAG:(LOCAL:2):92 == 1 || CFLAG:(LOCAL:2):92 == 4
			LOCAL:4 += MAX(ABL_POWER_X(ABL:(LOCAL:2):料理+ABL:(LOCAL:2):衣装嗜好, LOCAL:2) - ABL_50_POWER, 0)
		ELSE
			LOCAL:4 += MAX(ABL_POWER_X(ABL:(LOCAL:2):料理, LOCAL:2) - ABL_50_POWER, 0)
		ENDIF
	ENDIF
NEXT
LOCAL:3 = 1000 + POWER_075(LOCAL:3) * 100 / 514 + POWER_075(LOCAL:4) * 100 / 514
RETURNF LOCAL:3

;-------------------------------------------------
;都市ARG:0にARG:1, ARG:2が守将として設定されたときの防衛部隊の攻撃能力を決定
;-------------------------------------------------
@CITY_ATTACK_LEVEL_VIRTUAL(ARG:0, ARG:1, ARG:2)
#FUNCTION
;将の補正
LOCAL:3 = 0
FOR LOCAL:0, 1, 3
	LOCAL:2 = ARG:(LOCAL:0)
	IF LOCAL:2 >= 0
		LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):武闘, LOCAL:2)
		
		;コスプレ衣装を着ている場合、0.9倍される
		IF CFLAG:(LOCAL:2):92
			LOCAL:3 = LOCAL:3 * 9 /10
			;申し訳程度に衣装嗜好による補正がかかる(最大1.1倍)
			LOCAL:3 = LOCAL:3 * (100+ABL:(LOCAL:2):衣装嗜好) / 100
		ENDIF
		
		IF TALENT:(LOCAL:2):妖術知識
			;巫女装束、陰陽師装束を着ている場合、妖術の補正が1.0倍〜1.2倍される
			;(衣装嗜好Lv×0.05倍)
			IF CFLAG:(LOCAL:2):92 == 3 || CFLAG:(LOCAL:2):92 == 6
				LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):妖術*(100+ABL:(LOCAL:2):衣装嗜好*2)/100, LOCAL:2) / 2
			ELSE
				LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):妖術, LOCAL:2) / 2
			ENDIF
		ENDIF
	ENDIF
NEXT
;歌唱による補正
LOCAL:3 = MAX(LOCAL:3, ABL_50_POWER) * CITY_SUPPORT_LEVEL_VIRTUAL(ARG:0) / 1000
;防衛部隊では士官による攻撃力補正が1/4
LOCAL:3 = LOCAL:3 / 16 + 9400
;都市の防衛倍率による補正を掛ける
RETURNF LOCAL:3 * ((CITY_GUARD_RATE(ARG:0) - 100) / 3 + 100) * 2 / 100

;-------------------------------------------------
;都市ARG:0にARG:1, ARG:2が守将として設定されたときの防衛部隊の知略パワーを決定
;-------------------------------------------------
@CITY_INTELLIGENCE_LEVEL_VIRTUAL(ARG:0, ARG:1, ARG:2)
#FUNCTION
;将の補正
LOCAL:3 = 0
FOR LOCAL:0, 1, 3
	LOCAL:2 = ARG:(LOCAL:0)
	IF LOCAL:2 >= 0
		LOCAL:3 += ABL_POWER_X(ABL:(LOCAL:2):知略, LOCAL:2)
	ENDIF
NEXT
;歌唱による補正
LOCAL:3 = MAX(LOCAL:3, ABL_50_POWER) * CITY_SUPPORT_LEVEL_VIRTUAL(ARG:0) / 1000
RETURNF LOCAL:3

;--------------------------