;軍事画面の処理

;-------------------------------------------------
;部隊編成
;-------------------------------------------------
@FORM_ARMY
LOCAL:5 = CFLAG:MASTER:1
LOCAL:6 = GET_SUM_ARMY_DF(LOCAL:5)

DRAWLINE
PRINTL ●部隊編成
DRAWLINE
PRINTFORML 総兵数:  {GET_SUM_SOLDIER(LOCAL:5)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(LOCAL:5)}
PRINTFORML 残存兵数:{COUNTRY_SOLDIER:(LOCAL:5)}
DRAWLINE

LOCAL:3 = 0
FOR LOCAL:0, 0, MAX_UNIT
	IF UNIT_SOLDIER:(CFLAG:MASTER:1):(LOCAL:0) > 0
		LOCAL:3 = 1
		;強制解散中は赤文字＆選択不可
		IF UNIT_BREAK_TERM:(CFLAG:MASTER:1):(LOCAL:0) >= 1
			SETCOLOR COLOR("DARKRED")
			PRINTPLAINFORM {LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]  
			RESETCOLOR
			CALL SHOW_UNIT_INFO(CFLAG:MASTER:1, LOCAL:0, 12)
			RESETCOLOR
		ELSE
			SIF UNIT_POSITION:(CFLAG:MASTER:1):(LOCAL:0) == SHOWN_CITY
				SETCOLOR COLOR("オレンジ")
				PRINTBUTTON @"{LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]", LOCAL:0 + 1
				PRINT   
			CALL SHOW_UNIT_INFO(CFLAG:MASTER:1, LOCAL:0, 12)
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
IF !LOCAL:3
	PRINTFORML 現在編成中の部隊はありません
ENDIF

DRAWLINE
PRINTBUTTON "20[新規部隊]", 20
PRINTL 
PRINTBUTTON "30[最強編成除外設定]", 30
PRINTL
PRINTBUTTON " 0[    戻る]", 0
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 20
	LOCAL:1 = 0
	FOR LOCAL:0, 0, MAX_UNIT
		IF UNIT_SOLDIER:(CFLAG:MASTER:1):(LOCAL:0) <= 0
			LOCAL:1 = 1
			CALL CREATE_UNIT(CFLAG:MASTER:1, LOCAL:0)
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		PRINTW これ以上部隊を編成することはできません
	ENDIF
	RESTART
ELSEIF RESULT == 30
	CALL EXCLUDE_BEST_COMMANDER
	RESTART
ELSEIF RESULT == 0
	RETURN
ELSEIF RESULT >= 1 && RESULT <= MAX_UNIT && UNIT_SOLDIER:(CFLAG:MASTER:1):(RESULT - 1) > 0 && UNIT_BREAK_TERM:(CFLAG:MASTER:1):(RESULT - 1) == 0
	CALL UNIT_SETTING(CFLAG:MASTER:1, RESULT - 1)
	RESTART
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ARG:0勢力、部隊ARG:1の情報表示  ARG:2=調整用スペースの数
;-------------------------------------------------
@SHOW_UNIT_INFO(ARG:0, ARG:1, ARG:2 = 0)
PRINTFORM 兵数:{UNIT_SOLDIER:(ARG:0):(ARG:1), 7, LEFT} 所在:
LOCAL:2 = UNIT_POSITION:(ARG:0):(ARG:1)
LOCALS:0 = %GET_RELAYNAME(LOCAL:2)%
IF LOCALS:0 == "無名"
	LOCALS:0 = ----
ENDIF
PRINTFORM %LOCALS:0, 14, LEFT%
PRINT 　目標:
LOCAL:2 = UNIT_TARGET:(ARG:0):(ARG:1)
LOCALS:0 = %GET_RELAYNAME(LOCAL:2)%
IF LOCALS:0 == "無名"
	LOCALS:0 = なし
ENDIF
PRINTFORM %LOCALS:0, 14, LEFT%
PRINTFORM 武闘:%TOSTR_UNIT_POWER_SHORT(UNIT_ATTACK_LEVEL(ARG:0, ARG:1)), 6, LEFT%
PRINTFORM 知略:%TOSTR_UNIT_POWER_SHORT(UNIT_INTELLIGENCE_LEVEL(ARG:0, ARG:1)), 6, LEFT%
IF UNIT_BREAK_TERM:(ARG:0):(ARG:1) >= 1
	PRINTFORM (残り{UNIT_BREAK_TERM:(ARG:0):(ARG:1)}期)
ENDIF
PRINTL 
PRINTFORML %TOSTR_SPACE(ARG:2)%将:%GET_UNIT_COMMANDER_CALLNAME(ARG:0, ARG:1)%

;-------------------------------------------------
;ARG:0勢力のARG:1番部隊に対する設定
;-------------------------------------------------
@UNIT_SETTING(ARG:0, ARG:1)
;部隊がいる都市の勢力との関係を取得
CALL CHECK_COUNTRY_RELATION(ARG:0, CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)))
LOCAL:5 = RESULT:0

DRAWLINE
PRINTFORML ●部隊{ARG:1 + 1}への指示
PRINT     
CALL SHOW_UNIT_INFO(ARG:0, ARG:1, 4)
DRAWLINE

PRINTBUTTON " 0[移動／目標設定]", 0
PRINTL 
IF LOCAL:5 >= 2
	PRINTBUTTON " 1[兵数の変更]", 1
	PRINTL 
	PRINTBUTTON " 2[  将の変更]", 2
	PRINTL 
	PRINTBUTTON " 3[      解散]", 3
	PRINTL 
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN  4[  強制解散]
	RESETCOLOR
	PRINTL 
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN  1[兵数の変更]
	PRINTL 
	PRINTPLAIN  2[  将の変更]
	PRINTL 
	PRINTPLAIN  3[      解散]
	PRINTL 
	RESETCOLOR
	PRINTBUTTON " 4[  強制解散]", 4
	PRINTL 
ENDIF
DRAWLINE
PRINTBUTTON " 9[      戻る]", 9
PRINTL 

$INPUT_LOOP1
INPUT

IF RESULT == 0
	DRAWLINE
	CALL SET_CITY_COLOR_COUNTRY
	CALL SET_CITY_COLOR_UNIT
	CALL DRAWMAP
	CALL RESET_CITY_COLOR
	DRAWLINE

	PRINTFORML 部隊{ARG:1 + 1}の移動先を決めて下さい
	IF LOCAL:5 >= 2
		PRINTFORML 自勢力の都市、同盟・連合勢力の都市、部隊の現在位置に隣接した都市のみが選択可能です
	ELSE
		PRINTFORML 部隊の現在位置に隣接した都市のみが選択可能です
	ENDIF
	;連合に参加している
	LOCAL:6 = GET_UNION_TARGET(ARG:0)
	IF LOCAL:6 > 0
		LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
		SETCOLOR COLOR("注意")
		PRINTFORML ※反%NAME:(LOCAL:7)%連合に参加中であるため、%NAME:(LOCAL:7)%勢力以外への出撃ができません
		RESETCOLOR
	ENDIF
	PRINTFORML 現在の位置:%GET_RELAYNAME(UNIT_POSITION:(ARG:0):(ARG:1))%
	PRINTBUTTON "[キャンセル]", 0
	PRINTL 

	$INPUT_LOOP2
	INPUT

	IF RESULT == 0
		RESTART
	ELSEIF RESULT >= 1001 && RESULT < 1100
		LOCAL:2 = RESULT - 1000

		;勢力間の関係を取得
		CALL CHECK_COUNTRY_RELATION(ARG:0, CITY_OWNER:(LOCAL:2))
		LOCAL:7 = RESULT:0

		;自勢力・友好勢力から自勢力・友好勢力への移動
		IF LOCAL:5 >= 2 && LOCAL:7 >= 2
			LOCAL:3 = UNIT_POSITION:(ARG:0):(ARG:1)
			LOCAL:4 = UNIT_TARGET:(ARG:0):(ARG:1)
			UNIT_POSITION:(ARG:0):(ARG:1) = LOCAL:2
			UNIT_TARGET:(ARG:0):(ARG:1) = 0
			PRINTFORML 部隊{ARG:1 + 1}を%GET_RELAYNAME(LOCAL:2)%へ移動しました
			IF LOCAL:3 >= 1 && LOCAL:3 != LOCAL:4
				PRINTFORML 現在の移動目標は解除されます
			ENDIF
			FOR LOCAL:0, 0, 3
				SIF GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL:0) >= 0
					COOLTIME:(GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL:0)):0 = 1
			NEXT
			WAIT
			RESTART

		;停戦中の勢力への移動
		ELSEIF LOCAL:7 == 1
			PRINTFORMW 停戦中の勢力の都市へ部隊を移動させることはできません
			RESTART

		;現在位置と同じ場所を選択
		ELSEIF LOCAL:2 == UNIT_POSITION:(ARG:0):(ARG:1)
			UNIT_TARGET:(ARG:0):(ARG:1) = 0
			PRINTFORMW 目標の設定を解除します
			RESTART

		;連合に参加している場合
		ELSEIF LOCAL:6 > 0
			;部隊の現在位置に隣接した都市を選択
			IF IS_ROOT(LOCAL:2, UNIT_POSITION:(ARG:0):(ARG:1))
				;現在いる都市が自勢力／有効勢力／連合の対象敵勢力／中継地点のいずれでもない場合
				IF LOCAL:5 <= 1 && CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)) != LOCAL:6
					;このときは自由に移動可能(ハマり防止)
					UNIT_TARGET:(ARG:0):(ARG:1) = LOCAL:2
					PRINTFORMW 目標を%GET_RELAYNAME(LOCAL:2)%に設定します
					RESTART
				;それ以外は、選択都市が自勢力／有効勢力／連合の対象敵勢力／中継地点のいずれかなら移動可能
				ELSEIF CITY_OWNER:(LOCAL:2) == LOCAL:6 || CITY_TYPE:(LOCAL:2) == 1 || LOCAL:7 >= 2
					UNIT_TARGET:(ARG:0):(ARG:1) = LOCAL:2
					PRINTFORMW 目標を%GET_RELAYNAME(LOCAL:2)%に設定します
					RESTART
				ENDIF
			ENDIF

		;部隊の現在位置に隣接した都市を選択
		ELSEIF IS_ROOT(LOCAL:2, UNIT_POSITION:(ARG:0):(ARG:1))
			UNIT_TARGET:(ARG:0):(ARG:1) = LOCAL:2
			PRINTFORMW 目標を%GET_RELAYNAME(LOCAL:2)%に設定します
			RESTART
		ENDIF
	ENDIF
	GOTO INPUT_LOOP2

;兵数の変更
ELSEIF RESULT == 1 && LOCAL:5 >= 2
	LOCAL:2 = UNIT_SOLDIER:(ARG:0):(ARG:1)
	LOCAL:3 = COUNTRY_SOLDIER:(ARG:0) + LOCAL:2
	DRAWLINE
	PRINTFORML 部隊の兵数を入力して下さい(残存兵数:{LOCAL:3})
	PRINTFORML 現在の兵数は{LOCAL:2}です
	PRINTFORML 0以下の数を入力すると変更せずに戻ります

	CALL DRAW_SOLDIER_INPUT(LOCAL:3)

	$INPUT_LOOP3
	INPUT

	IF RESULT <= 0
		RESTART
	ELSEIF RESULT < 500
		CLEARLINE 1
		PRINTL 最低でも500の兵数が必要です
		GOTO INPUT_LOOP3
	ELSEIF RESULT <= LOCAL:3
		COUNTRY_SOLDIER:(ARG:0) += LOCAL:2
		UNIT_SOLDIER:(ARG:0):(ARG:1) = RESULT
		COUNTRY_SOLDIER:(ARG:0) -= RESULT
		PRINTFORMW {ARG:1 + 1}番隊の兵数を{RESULT}に設定します
		RESTART
	ENDIF

	CLEARLINE 1
	PRINTL 兵数が不足しています
	GOTO INPUT_LOOP3

;将の変更
ELSEIF RESULT == 2 && LOCAL:5 >= 2
	CALL SELECT_UNIT_COMMANDER(ARG:0, ARG:1)
	RESTART

;解散
ELSEIF RESULT == 3 && LOCAL:5 >= 2
	PRINTFORML 部隊{ARG:1 + 1}を解散します。宜しいですか？
	CALL ASK_YN
	IF RESULT == 0
		FOR LOCAL, 0, 3
			LOCAL:(10 + LOCAL) = GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL)
		NEXT
		CALL CLEAR_UNIT(ARG:0, ARG:1)
		FOR LOCAL, 0, 3
			SIF LOCAL:(10 + LOCAL) >= 0 && ASSIGNED_THIS_TURN:(LOCAL:(10 + LOCAL)):0
				COOLTIME:(LOCAL:(10 + LOCAL)):0 = 0
		NEXT
		RETURN
	ENDIF
	RESTART

;強制解散
ELSEIF RESULT == 4 && LOCAL:5 < 2
	PRINTFORML 部隊{ARG:1 + 1}を戦線から離脱させ、解散させます
	PRINTFORML 解散までに3期掛かります。宜しいですか？
	CALL ASK_YN
	IF RESULT == 0
		UNIT_POSITION:(ARG:0):(ARG:1) = 0
		UNIT_TARGET:(ARG:0):(ARG:1) = 0
		UNIT_BREAK_TERM:(ARG:0):(ARG:1) = 3
		RETURN
	ENDIF
	RESTART

ELSEIF RESULT == 9
	RETURN
ENDIF
GOTO INPUT_LOOP1

;-------------------------------------------------
;都市ARG:0の防衛兵数を設定
;-------------------------------------------------
@SHOP_CITY_SOLDIER(ARG:0)
LOCAL:2 = CITY_SOLDIER:(ARG:0)
LOCAL:3 = COUNTRY_SOLDIER:(CFLAG:MASTER:1) + LOCAL:2
DRAWLINE
PRINTFORML %GET_CITYNAME(ARG:0)%の防衛兵数を入力して下さい(残存兵数:{LOCAL:3})
PRINTFORML 現在の防衛兵数は{LOCAL:2}です
PRINTFORML 0以下の数を入力すると現在の兵数を維持します
DRAWLINE

CALL DRAW_SOLDIER_INPUT(LOCAL:3)

$INPUT_LOOP1
INPUT

IF RESULT > 0 && RESULT < 500
	CLEARLINE 1
	PRINTL 最低でも500の兵数が必要です
	GOTO INPUT_LOOP1
ELSEIF RESULT >= 500 && RESULT <= LOCAL:3
	COUNTRY_SOLDIER:(CFLAG:MASTER:1) += CITY_SOLDIER:(ARG:0)
	CITY_SOLDIER:(ARG:0) = RESULT
	COUNTRY_SOLDIER:(CFLAG:MASTER:1) -= CITY_SOLDIER:(ARG:0)
ELSEIF RESULT > LOCAL:3
	CLEARLINE 1
	PRINTL 兵数が不足しています
	GOTO INPUT_LOOP1
ENDIF

;-------------------------------------------------
;都市ARG:0の守将を設定
;-------------------------------------------------
@SHOP_CITY_COMMANDER(ARG:0)
#DIM 元キャラ, MAX_CITY_COMMANDER
LOCAL:4 = GET_CITY_COMMANDER(ARG:0, 0)
LOCAL:5 = GET_CITY_COMMANDER(ARG:0, 1)
元キャラ:0 = LOCAL:4
元キャラ:1 = LOCAL:5
CALL SET_CITY_COMMANDER(ARG:0, 0, -1)
CALL SET_CITY_COMMANDER(ARG:0, 1, -1)

;士官が部隊に所属しているかどうかのマップを作成(高速化用)
CALL TMP_CREATE_IS_FREE_MAP

;守将設定のページ数の計算
LOCAL:12 = 0
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみ ※寺子屋通いは除外
	IF (CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1) && !CFLAG:(LOCAL:0):寺子屋
		LOCAL:12 ++
	ENDIF
NEXT
LOCAL:12 = (LOCAL:12 - 1) / 40 + 1
LOCAL:13 = 1

$SHOW_LOOP2

LOCAL:6 = 0
LOCAL:7 = 0
LOCAL:8 = 0
;武闘・知略・歌唱のパワーを計算
FOR LOCAL:0, 4, 6
	LOCAL:9 = LOCAL:(LOCAL:0)
	IF LOCAL:9 >= 0
		LOCAL:6 += ABL_POWER(ABL:(LOCAL:9):武闘)
		LOCAL:7 += ABL_POWER(ABL:(LOCAL:9):知略)
		;歌唱は50未満だと切り捨てられる
		LOCAL:1 = ABL_POWER(ABL:(LOCAL:9):歌唱)
		IF LOCAL:1 >= ABL_50_POWER
			LOCAL:8 += LOCAL:1
		ENDIF
	ENDIF
NEXT
LOCAL:8 = 1000 + POWER_075(LOCAL:8) * 100 / 514
LOCAL:6 = MAX(LOCAL:6, ABL_50_POWER) * LOCAL:8 / 1000
LOCAL:7 = MAX(LOCAL:7, ABL_50_POWER) * LOCAL:8 / 1000

LOCAL:1 = MAX(GET_DIGIT(LOCAL:6), GET_DIGIT(LOCAL:7), 6)

DRAWLINE
PRINTFORML %GET_CITYNAME(ARG:0)%の守将を設定して下さい(最大2人まで)
PRINTFORML 【現在の防衛能力】
PRINTFORML 　　武闘:{LOCAL:6, LOCAL:1}(%TOSTR_UNIT_POWER(LOCAL:6)%)
PRINTFORML 　　知略:{LOCAL:7, LOCAL:1}(%TOSTR_UNIT_POWER(LOCAL:7)%)
DRAWLINE
PRINTFORM %TOSTR_SPACE(23)%   武|  知|  政|  妖|  歌|  料 %TOSTR_SPACE(28)%   武|  知|  政|  妖|  歌|  料
PRINTL 
LOCAL:25 = 13
LOCAL:1 = 0
LOCAL:14 = 0
LOCAL:15 = (LOCAL:13 - 1) * 40
LOCAL:16 = LOCAL:13 * 40
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみ　※寺子屋通いは除外
	IF (CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1) && !CFLAG:(LOCAL:0):寺子屋
		IF LOCAL:14 >= LOCAL:15 && LOCAL:14 < LOCAL:16
			IF LOCAL:1 % 2 != 0
				PRINT 　　　
			ELSEIF LOCAL:1 >= 1
				PRINTL 
				LOCAL:25 ++
			ENDIF
			IF LOCAL:0 == LOCAL:4 || LOCAL:0 == LOCAL:5
				SETCOLOR COLOR("シアン")
			ENDIF
			IF TMP_IS_FREE:(LOCAL:0):0 == 0
				PRINTFORM [{NO:(LOCAL:0) + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%
				PRINTFORM   
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):武闘)
				PRINTFORM {ABL:(LOCAL:0):武闘, 3}|
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):知略)
				PRINTFORM {ABL:(LOCAL:0):知略, 3}|
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):政治)
				PRINTFORM {ABL:(LOCAL:0):政治, 3}|
				CALL PRINT_ALPHABET_RANK(1, ABL:(LOCAL:0):妖術)
				PRINTFORM {ABL:(LOCAL:0):妖術, 3}|
				CALL PRINT_ALPHABET_RANK(2, ABL:(LOCAL:0):歌唱)
				PRINTFORM {ABL:(LOCAL:0):歌唱, 3}|
				CALL PRINT_ALPHABET_RANK(2, ABL:(LOCAL:0):料理)
				PRINTFORM {ABL:(LOCAL:0):料理, 3}
			ELSE
				LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%  %ALPHABET_RANK(0, ABL:LOCAL:武闘)%{ABL:(LOCAL:0):武闘, 3}|%ALPHABET_RANK(0, ABL:LOCAL:知略)%{ABL:(LOCAL:0):知略, 3}|
				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:政治)%{ABL:(LOCAL:0):政治, 3}|%ALPHABET_RANK(1, ABL:LOCAL:妖術)%{ABL:(LOCAL:0):妖術, 3}|%ALPHABET_RANK(2, ABL:LOCAL:歌唱)%{ABL:(LOCAL:0):歌唱, 3}|%ALPHABET_RANK(2, ABL:LOCAL:料理)%{ABL:(LOCAL:0):料理, 3}"
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM %LOCALS:0%
			ENDIF
			RESETCOLOR
			LOCAL:1 ++
		ENDIF
		LOCAL:14 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE

IF LOCAL:12 >= 2
	LOCAl:25 ++
	IF LOCAL:13 > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = page{LOCAL:13}/{LOCAL:12}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:13 < LOCAL:12
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF

PRINTL [  0] 決定
PRINTL [ 10] 最強
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 8 && LOCAL:13 > 1
	LOCAL:13 --
	CLEARLINE LOCAL:25
	GOTO SHOW_LOOP2
ELSEIF RESULT == 9 && LOCAL:13 < LOCAL:12
	LOCAL:13 ++
	CLEARLINE LOCAL:25
	GOTO SHOW_LOOP2
ELSEIF RESULT == 0
	;将をNoの若い順にソート
	FOR LOCAL:0, 0, 2
		IF LOCAL:(LOCAL:0 + 4) >= 0
			LOCAL:(LOCAL:0 + 6) = NO:(LOCAL:(LOCAL:0 + 4))
		ELSE
			LOCAL:(LOCAL:0 + 6) = 99999
		ENDIF
	NEXT
	IF LOCAL:6 > LOCAL:7
		SWAP LOCAL:4, LOCAL:5
	ENDIF
	CALL SET_CITY_COMMANDER(ARG:0, 0, LOCAL:4)
	CALL SET_CITY_COMMANDER(ARG:0, 1, LOCAL:5)
	;元からいたキャラでなければ、このターンに割り当てましたフラグをたてる
	SIF LOCAL:4 >= 0 && LOCAL:4 != 元キャラ:0 && LOCAL:4 != 元キャラ:1
		ASSIGNED_THIS_TURN:(LOCAL:4):0 = 1
	SIF LOCAL:5 >= 0 && LOCAL:5 != 元キャラ:0 && LOCAL:5 != 元キャラ:1
		ASSIGNED_THIS_TURN:(LOCAL:5):0 = 1
	;このターンに割り当てられたキャラでなければ、クールタイムを付与
	SIF 元キャラ:0 != -1 && 元キャラ:0 != LOCAL:4 && 元キャラ:0 != LOCAL:5 && !ASSIGNED_THIS_TURN:(元キャラ:0):0
		COOLTIME:(元キャラ:0):0 = 1
	SIF 元キャラ:1 != -1 && 元キャラ:1 != LOCAL:4 && 元キャラ:1 != LOCAL:5 && !ASSIGNED_THIS_TURN:(元キャラ:1):0
		COOLTIME:(元キャラ:1):0 = 1
	REDRAW 1
	RETURN
ELSEIF RESULT == 10
	CALL CLEAR_CITY_COMMANDER(ARG:0, 0)
	CALL CLEAR_CITY_COMMANDER(ARG:0, 1)
	CALL SET_CITY_COMMANDER_BEST(ARG:0, 1)
	LOCAL:4 = GET_CITY_COMMANDER(ARG:0, 0)
	LOCAL:5 = GET_CITY_COMMANDER(ARG:0, 1)
	CLEARLINE LOCAL:25
	GOTO SHOW_LOOP2
ELSE
	LOCAL:10 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:10 < 0 || CFLAG:(LOCAL:10):1 != CFLAG:MASTER:1 || TMP_IS_FREE:(LOCAL:10):0 != 0
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF

	;既に選択済みのキャラなら選択解除
	FOR LOCAL:0, 4, 6
		IF LOCAL:(LOCAL:0) == LOCAL:10
			LOCAL:(LOCAL:0) = -1
			CLEARLINE LOCAL:25
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	;未選択のキャラは選択
	FOR LOCAL:0, 4, 6
		IF LOCAL:(LOCAL:0) < 0
			LOCAL:(LOCAL:0) = LOCAL:10
			CLEARLINE LOCAL:25
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	CLEARLINE 2
	PRINTFORML 将の数が最大です
ENDIF
GOTO INPUT_LOOP2

;-------------------------------------------------
;ARG:0勢力のARG:1番に部隊を新規作成
;-------------------------------------------------
@CREATE_UNIT(ARG:0, ARG:1)
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE
PRINTL 部隊を編成する都市を設定して下さい
PRINTBUTTON " 0[キャンセル]", 0
PRINTL 

$INPUT_LOOP0
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT >= 1001 && RESULT < 1100
	LOCAL:5 = RESULT - 1000
	IF CITY_OWNER:(LOCAL:5) == ARG:0
		UNIT_POSITION:(ARG:0):(ARG:1) = LOCAL:5
	ELSE
		CLEARLINE 1
		PRINTL 部隊が編成できるのは自国領内だけです
		GOTO INPUT_LOOP0
	ENDIF
ELSE
	GOTO INPUT_LOOP0
ENDIF

DRAWLINE
PRINTFORML 部隊の兵数を入力して下さい(残存兵数:{COUNTRY_SOLDIER:(ARG:0)})

CALL DRAW_SOLDIER_INPUT(COUNTRY_SOLDIER:(ARG:0))

$INPUT_LOOP1
INPUT

IF RESULT <= 0
	RETURN
ELSEIF RESULT < 500
	CLEARLINE 1
	PRINTL 最低でも500の兵数が必要です
	GOTO INPUT_LOOP1
ELSEIF RESULT <= COUNTRY_SOLDIER:(ARG:0)
	UNIT_SOLDIER:(ARG:0):(ARG:1) = RESULT
	COUNTRY_SOLDIER:(ARG:0) -= RESULT
	PRINTFORMW {ARG:1 + 1}番隊の兵数を{RESULT}に設定します
ELSE
	CLEARLINE 1
	PRINTL 国の兵数が不足しています
	GOTO INPUT_LOOP1
ENDIF

UNIT_TARGET:(ARG:0):(ARG:1) = 0
UNIT_COMMANDER:(ARG:0):(ARG:1) = 0

;部隊の将の設定
CALL SELECT_UNIT_COMMANDER(ARG:0, ARG:1)

;-------------------------------------------------
;ARG:0勢力のARG:1番に部隊について、将を設定する
;-------------------------------------------------
@SELECT_UNIT_COMMANDER(ARG:0, ARG:1)
#DIM 元キャラ, MAX_UNIT_COMMANDER
LOCAL:10 = GET_UNIT_COMMANDER(ARG:0, ARG:1, 0)
LOCAL:11 = GET_UNIT_COMMANDER(ARG:0, ARG:1, 1)
LOCAL:12 = GET_UNIT_COMMANDER(ARG:0, ARG:1, 2)
元キャラ:0 = LOCAL:10, LOCAL:11, LOCAL:12
UNIT_COMMANDER:(ARG:0):(ARG:1) = 0

;士官が部隊に所属しているかどうかのマップを作成(高速化用)
CALL TMP_CREATE_IS_FREE_MAP

;ページ数の計算
LOCAL:20 = 0
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみカウント　※寺子屋通いは除外
	IF (CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1) && !CFLAG:(LOCAL:0):寺子屋
		LOCAL:20 ++
	ENDIF
NEXT
LOCAL:20 = (LOCAL:20 - 1) / 40 + 1
LOCAL:21 = 1

$SHOW_LOOP2

LOCAL:4 = 0
LOCAL:6 = 0
LOCAL:7 = 0
LOCAL:8 = 0
LOCAL:9 = 0
;部隊の武闘・知略パワーを計算
FOR LOCAL:0, 10, 13
	LOCAL:5 = LOCAL:(LOCAL:0)
	IF LOCAL:5 >= 0
		LOCAL:6 += ABL_POWER(ABL:(LOCAL:5):武闘)
		LOCAL:7 += ABL_POWER(ABL:(LOCAL:5):知略)
		IF TALENT:(LOCAL:5):妖術知識
			LOCAL:4 += ABL_POWER(ABL:(LOCAL:5):妖術)
		ENDIF
		;歌唱・料理は50未満だと切り捨てられる
		LOCAL:8 += MAX(ABL_POWER_X(ABL:(LOCAL:5):歌唱, LOCAL:5) - ABL_50_POWER, 0)
		LOCAL:9 += MAX(ABL_POWER_X(ABL:(LOCAL:5):料理, LOCAL:5) - ABL_50_POWER, 0)
	ENDIF
NEXT
LOCAL:8 = 1000 + POWER_075(LOCAL:8) * 100 / 514 + POWER_075(LOCAL:9) * 100 / 514
LOCAL:6 = MAX(LOCAL:6, ABL_50_POWER) * LOCAL:8 / 1000
LOCAL:7 = MAX(LOCAL:7, ABL_50_POWER) * LOCAL:8 / 1000

LOCAL:6 = LOCAL:6 / 4 + 8800
LOCAL:1 = MAX(GET_DIGIT(LOCAL:6), GET_DIGIT(LOCAL:7), GET_DIGIT(LOCAL:4), 6)

DRAWLINE
PRINTFORML 部隊の将を設定して下さい(最大3人まで)
PRINTFORML 【現在の部隊能力】
PRINTFORML 　　武闘:{LOCAL:6, LOCAL:1}(%TOSTR_UNIT_POWER(LOCAL:6)%)
PRINTFORML 　　知略:{LOCAL:7, LOCAL:1}(%TOSTR_UNIT_POWER(LOCAL:7)%)
PRINTFORML 　　妖術:{LOCAL:4, LOCAL:1}
DRAWLINE
PRINTFORM %TOSTR_SPACE(23)%   武|  知|  政|  妖|  歌|  料 %TOSTR_SPACE(28)%   武|  知|  政|  妖|  歌|  料
PRINTL 

LOCAL:25 = 14
LOCAL:1 = 0
LOCAL:22 = 0
LOCAL:23 = (LOCAL:21 - 1) * 40
LOCAL:24 = LOCAL:21 * 40
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみ　※寺子屋通いは除外
	IF (CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1) && !CFLAG:(LOCAL:0):寺子屋
		IF LOCAL:22 >= LOCAL:23 && LOCAL:22 < LOCAL:24
			IF LOCAL:1 % 2 != 0
				PRINT 　　　
			ELSEIF LOCAL:1 >= 1
				PRINTL 
				LOCAL:25 ++
			ENDIF
			IF MATCH(LOCAL, LOCAL:0, 10, 13)
				SETCOLOR COLOR("シアン")
			ENDIF

			IF TMP_IS_FREE:(LOCAL:0):0 == 0
				PRINTFORM [{NO:(LOCAL:0) + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%
				PRINTFORM   
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):武闘)
				PRINTFORM {ABL:(LOCAL:0):武闘, 3}|
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):知略)
				PRINTFORM {ABL:(LOCAL:0):知略, 3}|
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):政治)
				PRINTFORM {ABL:(LOCAL:0):政治, 3}|
				CALL PRINT_ALPHABET_RANK(1, ABL:(LOCAL:0):妖術)
				PRINTFORM {ABL:(LOCAL:0):妖術, 3}|
				CALL PRINT_ALPHABET_RANK(2, ABL:(LOCAL:0):歌唱)
				PRINTFORM {ABL:(LOCAL:0):歌唱, 3}|
				CALL PRINT_ALPHABET_RANK(2, ABL:(LOCAL:0):料理)
				PRINTFORM {ABL:(LOCAL:0):料理, 3}
			ELSE
				LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%  %ALPHABET_RANK(0, ABL:LOCAL:武闘)%{ABL:(LOCAL:0):武闘, 3}|%ALPHABET_RANK(0, ABL:LOCAL:知略)%{ABL:(LOCAL:0):知略, 3}|
				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:政治)%{ABL:(LOCAL:0):政治, 3}|%ALPHABET_RANK(1, ABL:LOCAL:妖術)%{ABL:(LOCAL:0):妖術, 3}|%ALPHABET_RANK(2, ABL:LOCAL:歌唱)%{ABL:(LOCAL:0):歌唱, 3}|%ALPHABET_RANK(2, ABL:LOCAL:料理)%{ABL:(LOCAL:0):料理, 3}"
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM %LOCALS:0%
			ENDIF
			RESETCOLOR
			LOCAL:1 ++
		ENDIF
		LOCAL:22 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE

IF LOCAL:20 >= 2
	LOCAL:25 += 2
	IF LOCAL:21 > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = page{LOCAL:21}/{LOCAL:20}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:21 < LOCAL:20
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF

PRINTL [  0] 決定
PRINTL [ 10] 最強
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 8 && LOCAL:21 > 1
	LOCAL:21 --
	CLEARLINE LOCAL:25
	GOTO SHOW_LOOP2
ELSEIF RESULT == 9 && LOCAL:21 < LOCAL:20
	LOCAL:21 ++
	CLEARLINE LOCAL:25
	GOTO SHOW_LOOP2
ELSEIF RESULT == 0
	;将をNoの若い順にソート
	FOR LOCAL:0, 0, 3
		IF LOCAL:(LOCAL:0 + 10) >= 0
			LOCAL:(LOCAL:0 + 20) = NO:(LOCAL:(LOCAL:0 + 10))
		ELSE
			LOCAL:(LOCAL:0 + 20) = 99999
		ENDIF
	NEXT
	LOCAL:15 = 1
	WHILE LOCAL:15
		LOCAL:15 = 0
		FOR LOCAL:0, 0, 2
			IF LOCAL:(LOCAL:0 + 20) > LOCAL:(LOCAL:0 + 21)
				SWAP LOCAL:(LOCAL:0 + 10), LOCAL:(LOCAL:0 + 11)
				SWAP LOCAL:(LOCAL:0 + 20), LOCAL:(LOCAL:0 + 21)
				LOCAL:15 = 1
			ENDIF
		NEXT
	WEND

	;将の登録
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 0, LOCAL:10)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 1, LOCAL:11)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 2, LOCAL:12)
	
	;このターンに割り当てたフラグをたてる
	SIF LOCAL:10 >= 0 && !GROUPMATCH(LOCAL:10, 元キャラ:0, 元キャラ:1, 元キャラ:2)
		ASSIGNED_THIS_TURN:(LOCAL:10):0 = 1
	SIF LOCAL:11 >= 0 && !GROUPMATCH(LOCAL:11, 元キャラ:0, 元キャラ:1, 元キャラ:2)
		ASSIGNED_THIS_TURN:(LOCAL:11):0 = 1
	SIF LOCAL:12 >= 0 && !GROUPMATCH(LOCAL:12, 元キャラ:0, 元キャラ:1, 元キャラ:2)
		ASSIGNED_THIS_TURN:(LOCAL:12):0 = 1
	
	;このターンに割り当てたキャラでなければ、元将のクールタイム設定
	SIF 元キャラ:0 != -1 && !GROUPMATCH(元キャラ:0, LOCAL:10, LOCAL:11, LOCAL:12) && !ASSIGNED_THIS_TURN:(元キャラ:0):0
		COOLTIME:(元キャラ:0):0 = 1
	SIF 元キャラ:1 != -1 && !GROUPMATCH(元キャラ:1, LOCAL:10, LOCAL:11, LOCAL:12) && !ASSIGNED_THIS_TURN:(元キャラ:1):0
		COOLTIME:(元キャラ:1):0 = 1
	SIF 元キャラ:2 != -1 && !GROUPMATCH(元キャラ:2, LOCAL:10, LOCAL:11, LOCAL:12) && !ASSIGNED_THIS_TURN:(元キャラ:2):0
		COOLTIME:(元キャラ:2):0 = 1
	REDRAW 1
	RETURN
;最強の組み合わせの探索
ELSEIF RESULT == 10
	CALL CHECK_UNIT_COMMANDER_BEST(CFLAG:MASTER:所属)
	LOCAL:10 = UNIT_COMMANDER_BEST:0
	LOCAL:11 = UNIT_COMMANDER_BEST:1
	LOCAL:12 = UNIT_COMMANDER_BEST:2
	CLEARLINE LOCAL:25
	GOTO SHOW_LOOP2
ELSE
	LOCAL:5 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:5 < 0 || CFLAG:(LOCAL:5):1 != CFLAG:MASTER:1 || TMP_IS_FREE:(LOCAL:5):0 != 0
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF

	;既に選択済みのキャラなら選択解除
	FOR LOCAL:0, 10, 13
		IF LOCAL:(LOCAL:0) == LOCAL:5
			LOCAL:(LOCAL:0) = -1
			CLEARLINE LOCAL:25
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	;未選択のキャラは選択
	FOR LOCAL:0, 10, 13
		IF LOCAL:(LOCAL:0) < 0
			LOCAL:(LOCAL:0) = LOCAL:5
			CLEARLINE LOCAL:25
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	CLEARLINE 2
	PRINTFORML 将の数が最大です
ENDIF
GOTO INPUT_LOOP2

;-------------------------------------------------
;兵数入力ボタンの描画 ARG:0=設定値の最大
;-------------------------------------------------
@DRAW_SOLDIER_INPUT(ARG:0)
PRINTL [     0]
IF ARG:0 >= 500
	PRINT [   500]
ENDIF
LOCAL:1 = MIN(10, ARG:0 / 1000 + 1)
FOR LOCAL:0, 1, LOCAL:1
	PRINTFORM [{LOCAL:0 * 1000, 6}]
NEXT
PRINTL 
IF ARG:0 >= 10000
	LOCAL:1 = MIN(10, (ARG:0 - 10000) / 2000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 2000 + 10000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 30000
	LOCAL:1 = MIN(10, (ARG:0 - 30000) / 5000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 5000 + 30000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 80000
	LOCAL:1 = MIN(10, (ARG:0 - 80000) / 10000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 10000 + 80000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 200000
	LOCAL:1 = MIN(10, (ARG:0 - 200000) / 50000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 50000 + 200000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 700000
	LOCAL:1 = MIN(9, (ARG:0 - 700000) / 100000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 100000 + 700000}]
	NEXT
	PRINTL 
ENDIF
PRINTBUTTON "[残り全て]", ARG:0

;-------------------------------------------------
;経済力入力ボタンの描画 ARG:0=設定値の最大
;-------------------------------------------------
@DRAW_ECONOMY_INPUT(ARG:0)
PRINTL [     0]
IF ARG:0 >= 50
	PRINT [    50]
ENDIF
LOCAL:1 = MIN(10, ARG:0 / 100 + 1)
FOR LOCAL:0, 1, LOCAL:1
	PRINTFORM [{LOCAL:0 * 100, 6}]
NEXT
PRINTL 
IF ARG:0 >= 1000
	LOCAL:1 = MIN(10, (ARG:0 - 1000) / 200 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 200 + 1000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 3000
	LOCAL:1 = MIN(10, (ARG:0 - 3000) / 500 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 500 + 3000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 8000
	LOCAL:1 = MIN(10, (ARG:0 - 8000) / 1000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 1000 + 8000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 20000
	LOCAL:1 = MIN(10, (ARG:0 - 20000) / 5000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 5000 + 20000, 6}]
	NEXT
	PRINTL 
ENDIF
IF ARG:0 >= 70000
	LOCAL:1 = MIN(9, (ARG:0 - 70000) / 10000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 10000 + 70000}]
	NEXT
	PRINTL 
ENDIF
PRINTBUTTON "[経済力の20%]", ARG:0

;-------------------------------------------------
;最強設定から外すキャラを指定
;-------------------------------------------------
@EXCLUDE_BEST_COMMANDER
#DIM 消去行
#DIM 対象
#DIM 表示位置
#DIM 表示ページ
#DIM 総ページ数
#DIM キャラ数
#DIM キャラカウンタ
#DIM 表示開始位置
#DIM 表示終了位置
VARSET キャラ数
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		キャラ数 ++
NEXT

総ページ数 = (キャラ数 - 1) / 40 + 1
表示ページ = 1


DRAWLINE
PRINTFORML 部隊編成・守将設定における「最強編成」から除外するキャラを設定できます
PRINTFORML クリックでトグル・除外されたキャラは桃色表示されます
DRAWLINE
$SHOW_LOOP

VARSET 消去行
VARSET 表示位置
VARSET キャラカウンタ
表示開始位置 = (表示ページ - 1) * 40
表示終了位置 = 表示ページ * 40

FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		IF 表示開始位置 <= キャラカウンタ && キャラカウンタ <= 表示終了位置
			IF 表示位置 % 3 != 0
				PRINT 　　
			ELSEIF 表示位置 > 0
				PRINTL
				消去行 ++
			ENDIF
			IF CFLAG:(LOCAL:0):最強編成除外フラグ
				SETCOLOR COLOR("桃")
			ELSE
				RESETCOLOR
			ENDIF
			PRINTFORM [{LOCAL:0 + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%
			表示位置 ++
		ENDIF
		キャラカウンタ ++
	ENDIF
NEXT
PRINTL
DRAWLINE

IF 総ページ数 >= 2
	消去行 += 2
	IF 表示ページ > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = ページ{表示ページ}/{総ページ数}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF 表示ページ < 総ページ数
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF

PRINTL [ 0]これでよし
REDRAW 0
INPUT
消去行 += 4
IF RESULT == 8 && 表示ページ > 1
	表示ページ --
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ELSEIF RESULT == 9 && 表示ページ < 総ページ数
	表示ページ ++
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	RETURN -1
ENDIF
対象 = RESULT - 99
IF 対象 > CHARANUM || 対象 < 0 || CFLAG:対象:所属 != CFLAG:MASTER:所属
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ENDIF

CFLAG:対象:最強編成除外フラグ = !CFLAG:対象:最強編成除外フラグ
CLEARLINE 消去行
GOTO SHOW_LOOP