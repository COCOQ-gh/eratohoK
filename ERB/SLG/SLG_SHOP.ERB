;戦略フェイズのショップ画面

;-------------------------------------------------
;各コマンドが選択可能かどうかを調べる
;-------------------------------------------------
@CHECK_SHOP_COMABLE_SLG
LOCAL:3 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)

;独立が選択可能かどうかをFLAG:25の4bit目に記録
IF LOCAL:3 != MASTER && CFLAG:MASTER:16 == 0 && GET_OWN_CITY(CFLAG:MASTER:1) >= 2 && CFLAG:MASTER:1 != GET_COUNTRY_FROM_ID(300)
	;主人公の所属する勢力が2都市以上所持していて、主人公が君主でないなら選択可能
	FLAG:25 |= (1 << 3)
ENDIF

;クーデターが選択可能かどうかをFLAG:25の5bit目に記録
IF LOCAL:3 != MASTER && CFLAG:MASTER:16 == 0 && CFLAG:MASTER:1 != GET_COUNTRY_FROM_ID(300)
	LOCAL:2 = 0
	FOR LOCAL:0, 0, CHARANUM
		;[恋慕][親友][服従]のいずれかを持っている士官の数をカウント
		IF LOCAL:0 != MASTER && LOCAL:0 != LOCAL:3 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):親友 || TALENT:(LOCAL:0):服従)
			LOCAL:2 ++
		ENDIF
	NEXT
	;自国の半数以上の士官が陥落済みならクーデターが可能
	IF LOCAL:2 * 2 >= GET_COMMANDER_NUM(CFLAG:MASTER:1) - 1
		FLAG:25 |= (1 << 4)
	ENDIF
ENDIF

;-------------------------------------------------
;ショップ画面の表示処理(戦略フェイズ)
;-------------------------------------------------
@SHOW_SHOP_SLG
;無効な入力時に消去する時の行数
FLAG:24 = LINES_SHOP_SLG

;コマンドの選択可否判定
CALL CHECK_SHOP_COMABLE_SLG

;マップとコマンドボタンを表示
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP(1)
CALL RESET_CITY_COLOR
DRAWLINE

;都市の情報を表示
CALL SHOW_CITY_INFO(SHOWN_CITY)

;-------------------------------------------------
;ショップ画面の入力処理(戦略フェイズ 0～99)
;ARG:0に入力した値が入る
;処理を行った場合は1を、行わなかった場合は0を返すこと
;-------------------------------------------------
@EVENTBUY_SLG(ARG:0)
;観戦モード
IF FLAG:1
	;勢力情報
	IF ARG:0 == 0
		CALL SHOW_COUNTRY_DATA
	;行動終了
	ELSEIF ARG:0 == 99 && !FLAG:28
		RESULT = 1
		BEGIN TURNEND
	ELSE
		RETURN 0
	ENDIF

;放浪中
ELSEIF CFLAG:MASTER:1 == 0
	;仕官する
	IF ARG:0 == 0
		CALL SHOP_SERVE_COUNTRY
	;旗揚げ
	ELSEIF ARG:0 == 1
		CALL SHOP_CREATE_COUNTRY
	;勢力情報
	ELSEIF ARG:0 == 5
		CALL SHOW_COUNTRY_DATA
	;次の周回へ
	ELSEIF ARG:0 == 50 && FLAG:28
		CALL GO_NEXT_LOOP
	;行動終了
	ELSEIF ARG:0 == 99 && !FLAG:28
		RESULT = 1
		BEGIN TURNEND
	ELSE
		RETURN 0
	ENDIF

;通常
ELSE
	;部隊編成
	IF ARG:0 == 0 && CONFIG:302 == 0
		IF DAY < SLG_PP:1
			PRINTFORMW 開始直後の{SLG_PP:1}期は部隊を編成することができません
		ELSE
			CALL FORM_ARMY
		ENDIF
	;防衛解除
	ELSEIF ARG:0 == 1 && CONFIG:302 == 0
		CALL MINIMIZE_GUARD
	;外交
	ELSEIF ARG:0 == 2 && !COUNTRY_IS_CLOSED:(CFLAG:MASTER:1)
		CALL DIPLOMACY
	;勢力情報
	ELSEIF ARG:0 == 5
		CALL SHOW_COUNTRY_DATA
	;政策変更
	ELSEIF ARG:0 == 6 && CONFIG:302 == 0
		COUNTRY_POLICY:(CFLAG:MASTER:1) ++
		IF COUNTRY_POLICY:(CFLAG:MASTER:1) >= 3
			COUNTRY_POLICY:(CFLAG:MASTER:1) = 0
		ENDIF
		RETURN 0
	;特別訓練
	ELSEIF ARG:0 == 7 && MONEY > 10000
		CALL SLG_SPECIAL_TRAIN
	;臨時徴兵
	ELSEIF ARG:0 == 8
		CALL SLG_EXTRA_DRAFT
	;独立
	ELSEIF ARG:0 == 10 && (FLAG:25 & (1 << 3))
		CALL SHOP_INDEPENDENCE
	;クーデター
	ELSEIF ARG:0 == 11 && (FLAG:25 & (1 << 4))
		CALL SHOP_COAP
	;オート設定
	ELSEIF ARG:0 == 20
		CONFIG:302 ++
		IF CONFIG:302 >= 2
			CONFIG:302 = 0
		ENDIF
		RETURN 0
	;下野
	ELSEIF ARG:0 == 50 && CFLAG:MASTER:1 && GET_COUNTRY_BOSS(CFLAG:MASTER:1) != MASTER
		CALL SHOP_RESIGN
	;行動終了
	ELSEIF ARG:0 == 99
		RESULT = 1
		BEGIN TURNEND
	ELSE
		RETURN 0
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;ショップ画面の入力処理(戦略フェイズ 100～)
;ARG:0に入力した値が入る
;処理を行った場合は1を、行わなかった場合は0を返すこと
;-------------------------------------------------
@USERSHOP_SLG(ARG:0)
;都市情報
IF ARG:0 >= 1000 && ARG:0 < 1000 + MAX_CITY
	SHOWN_CITY = ARG:0 - 1000
	REDRAW 0
	CLEARLINE FLAG:24
;防衛兵数の設定
ELSEIF ARG:0 >= 200 && ARG:0 < 200 + MAX_CITY && CONFIG:302 == 0
	LOCAL:5 = ARG:0 - 200
	IF CFLAG:MASTER:1 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:1
		;敵部隊のいる都市は変更不可
		CALL GET_STAY_ENEMY_UNIT(LOCAL:5)
		IF RESULT:0 >= 0
			PRINTFORMW 敵部隊が存在する都市の兵数は変更できません
		ELSE
			CALL SHOP_CITY_SOLDIER(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
;守将の設定
ELSEIF ARG:0 >= 400 && ARG:0 < 400 + MAX_CITY && CONFIG:302 == 0
	LOCAL:5 = RESULT - 400
	IF CFLAG:MASTER:1 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:1
		;敵部隊のいる都市は変更不可
		CALL GET_STAY_ENEMY_UNIT(LOCAL:5)
		IF RESULT:0 >= 0
			PRINTFORMW 敵部隊が存在する都市の守将は変更できません
		ELSE
			CALL SHOP_CITY_COMMANDER(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
;勢力色変更
ELSEIF ARG:0 > 600 && ARG:0 < 600 + MAX_CITY
	LOCAL:5 = ARG:0 - 600
	IF IS_COUNTRY(LOCAL:5)
		CALL CHANGE_COUNTRY_COLOR(LOCAL:5)
		RETURN 1
	ENDIF
	RETURN 0
;一か所だけ防衛解除
ELSEIF ARG:0 > 800 && ARG:0 < 800 + MAX_CITY
	CALL SINGLE_MINIMIZE(ARG:0 - 800)
;侵攻・移動ボタン
ELSEIF ARG:0 > 2000 && ARG:0 < 2000 + MAX_CITY
	IF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0 - 2000), CFLAG:MASTER:1) >= 2
		CALL MOVETO(ARG:0 - 2000)
	ELSE
		CALL INVADE(ARG:0 - 2000)
	ENDIF
	
	RETURN 1
;部隊一覧表示ボタン
ELSEIF ARG:0 > 2500 && ARG:0 < 2500 + MAX_CITY
	IF (CITY_OWNER:(ARG:0 - 2500) == CFLAG:MASTER:1 || IS_INVADABLE(ARG:0 - 2500, CFLAG:MASTER:1) || FLAG:1) && IS_STAY_UNIT(ARG:0 - 2500)
		CALL SHOW_CITY_UNIT_LIST(ARG:0 - 2500)
		RETURN 1
	ENDIF
	RETURN 0
ELSE
	RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;防衛部隊の解除
;-------------------------------------------------
@MINIMIZE_GUARD
SETCOLOR 0xFF6000
PRINT 敵部隊が存在する都市を除く
RESETCOLOR
PRINTL 全ての都市の防衛兵数を500にし、守将を解除します
PRINTL よろしいですか？

CALL ASK_YN("はい", "キャンセル")
IF RESULT == 0
	;部隊マップを作成
	CALL TMP_CREATE_UNIT_MAP
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:1 && GET_CITYNAME(LOCAL:0) != "無名" && !TMP_IS_STAY_ENEMY_UNIT(LOCAL:0)
			IF CITY_SOLDIER:(LOCAL:0) > 500
				COUNTRY_SOLDIER:(CFLAG:MASTER:1) += CITY_SOLDIER:(LOCAL:0) - 500
				CITY_SOLDIER:(LOCAL:0) = 500
			ENDIF
			SIF GET_CITY_COMMANDER(LOCAL:0, 0) >= 0
				SLG_COOLTIME:GET_CITY_COMMANDER(LOCAL:0, 0):0 = 1
			SIF GET_CITY_COMMANDER(LOCAL:0, 1) >= 0
				SLG_COOLTIME:GET_CITY_COMMANDER(LOCAL:0, 1):0 = 1
			CITY_COMMANDER:(LOCAL:0) = 0
		ENDIF
	NEXT
ENDIF

@SINGLE_MINIMIZE(ARG)
IF TMP_IS_STAY_ENEMY_UNIT(ARG)
	PRINTW 敵部隊が存在する都市には実行できません
ELSEIF CITY_OWNER:(ARG) == CFLAG:MASTER:1 && GET_CITYNAME(ARG) != "無名"
	IF CITY_SOLDIER:(ARG) > 500
		COUNTRY_SOLDIER:(CFLAG:MASTER:1) += CITY_SOLDIER:(ARG) - 500
		CITY_SOLDIER:(ARG) = 500
	ENDIF
	SIF GET_CITY_COMMANDER(ARG, 0) >= 0
		SLG_COOLTIME:GET_CITY_COMMANDER(ARG, 0):0 = 1
	SIF GET_CITY_COMMANDER(ARG, 1) >= 0
		SLG_COOLTIME:GET_CITY_COMMANDER(ARG, 1):0 = 1
	CITY_COMMANDER:(ARG) = 0
	PRINTW この都市の防衛を解除しました
ENDIF

;-------------------------------------------------
;仕官する
;-------------------------------------------------
@SHOP_SERVE_COUNTRY
LOCAL:10 = 0

$SHOW_LOOP

DRAWLINE
PRINTL 仕官先の国家を選択して下さい
PRINT 一度仕官すると、
SETCOLOR COLOR("注意")
PRINT 5
RESETCOLOR
PRINTL 期の間、下野することができません
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:10))

IF LOCAL:2 < 0
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL 
	PRINTBUTTON " 0[戻る]", 0
	PRINTL 
ELSE
	PRINT この都市は 
	SETCOLOR COUNTRY_COLOR:(LOCAL:10)
	PRINTFORM %NAME_DOUBLE(LOCAL:2)%
	RESETCOLOR
	PRINTL  の勢力下にあります
	IF COUNTRY_IS_CLOSED:(LOCAL:10)
		SETCOLOR COLOR("選択不可")
		PRINTFORM  X[この勢力には仕官できません]
		RESETCOLOR
	ELSEIF FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL:10) != -1
		SETCOLOR COLOR("選択不可")
		PRINTFORM  X[勢力から離脱したばかりです]
		RESETCOLOR
	ELSE
		PRINTBUTTON " 1[この勢力に仕官する]", 1
	ENDIF
	PRINTL 
	PRINTBUTTON " 0[戻る]", 0
	PRINTL 
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 1 && !COUNTRY_IS_CLOSED:(LOCAL:10) && FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL:10) == -1
	IF LOCAL:2 >= 0
		CFLAG:MASTER:1 = LOCAL:10
		TIME = 0
		PRINTFORMW %NAME:(LOCAL:2)%勢力に仕官します
		PLAYER_HIRED_COUNTER  = 5
		RETURN
	ENDIF
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		LOCAL:10 = LOCAL:4
		CLEARLINE FLAG:24
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		LOCAL:10 = 0
		CLEARLINE FLAG:24
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;旗揚げ
;-------------------------------------------------
@SHOP_CREATE_COUNTRY
DRAWLINE
PRINTL 旗揚げする都市を選択して下さい。無所属の都市のみ選択可能です
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

PRINTBUTTON " 0[戻る]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF !IS_COUNTRY(LOCAL:4) && CITY_TYPE:(LOCAL:3) == 0 && GET_RELAYNAME(LOCAL:3) != "無名"
		PRINTFORML %GET_RELAYNAME(LOCAL:3)%で旗揚げしますか？
		PRINTBUTTON " 1[旗揚げする]", 1
		PRINTPLAIN     
		PRINTBUTTON " 2[キャンセル]", 2
		PRINTL 
		PRINTBUTTON " 0[戻る]", 0

		REDRAW 0

		$INPUT_LOOP2
		INPUT

		IF RESULT == 0
			RETURN
		ELSEIF RESULT == 1
			LOCAL:5 = GET_NEW_COUNTRY()
			CITY_OWNER:(LOCAL:3) = LOCAL:5
			COUNTRY_BOSS:(LOCAL:5) = GET_ID(MASTER)
			COUNTRY_SOLDIER:(LOCAL:5) = CITY_ECONOMY:(LOCAL:3) / 20
			CITY_SOLDIER:(LOCAL:3) = 2000
			CFLAG:MASTER:1 = LOCAL:5

			DRAWLINE
			PRINTL 新しい勢力の色を選んで下さい
			DRAWLINE
			CALL CHANGE_COUNTRY_COLOR(LOCAL:5, 1)

			TIME = 0
			RETURN
		ELSEIF RESULT == 2
			RESTART
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;臨時徴兵
;-------------------------------------------------
@SLG_EXTRA_DRAFT()
$SHOW_LOOP
LOCAL = 3 + (MAX(DAY - 5, 0) / 10)
DRAWLINE
PRINTFORML 資金を消費して臨時に徴兵することができます
PRINTFORML 何人雇いますか？(0以下を入力すると戻る)
PRINTFORML 現在の資金:{MONEY}  レート:{LOCAL}/兵
PRINTFORML 現在の兵数（防衛・部隊所属除く）:{COUNTRY_SOLDIER:(CFLAG:MASTER:1)}
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT <= 0
	RETURN
ELSEIF MONEY >= LOCAL * RESULT
	LOCAL:1 = RESULT
	PRINTFORM 金
	SETCOLOR COLOR("注意")
	PRINTFORM {LOCAL:1 * LOCAL}
	RESETCOLOR
	PRINTFORM を消費し、
	SETCOLOR COLOR("注意")
	PRINTFORM {LOCAL:1}
	RESETCOLOR
	PRINTFORML の兵を雇います。
	PRINTFORM 雇用後の資金は
	SETCOLOR COLOR("注意")
	PRINTFORM {MONEY - LOCAL:1 * LOCAL}
	RESETCOLOR
	PRINTFORML です。
	PRINTFORML よろしいですか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 兵を{LOCAL:1}人雇いました！
		PRINTFORML 兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:1)} → {COUNTRY_SOLDIER:(CFLAG:MASTER:1) + LOCAL:1}
		PRINTFORMW 資金:{MONEY} → {MONEY - LOCAL:1 * LOCAL}
		COUNTRY_SOLDIER:(CFLAG:MASTER:1) += LOCAL:1
		MONEY -= LOCAL:1 * LOCAL
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSE
	LOCAL:1 = RESULT
	PRINTFORML 必要資金は{LOCAL:1 * LOCAL}ですが、そんなに資金がありません。
	GOTO SHOW_LOOP
ENDIF


;-------------------------------------------------
;野に下る
;-------------------------------------------------
@SHOP_RESIGN()
DRAWLINE
PRINTFORML %NAME:(GET_COUNTRY_BOSS(CFLAG:MASTER:1))%の元を去り、野に下ります
PRINTFORML 本当によろしいですか？
CALL ASK_YN()
IF RESULT == 0
	ARRAYSHIFT COUNTRY_PLAYER_BELONGED, 1, 0
	COUNTRY_PLAYER_BELONGED:0 = CFLAG:MASTER:1
	PRINTFORMW 下野しました
	CFLAG:MASTER:1 = 0
ELSE
	PRINTFORMW 思いとどまりました
ENDIF
