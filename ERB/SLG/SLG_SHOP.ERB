;戦略フェイズのショップ画面

;-------------------------------------------------
;各コマンドが選択可能かどうかを調べる
;-------------------------------------------------
@CHECK_SHOP_COMABLE_SLG
LOCAL:3 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
;観戦モード
IF FLAG:観戦モード
	SIF FLAG:クリアフラグ
		SHOP_AVAIL:99 = 0
;放浪
ELSEIF CFLAG:MASTER:所属 == 0
	;周回とターン終了
	IF FLAG:クリアフラグ
		SHOP_AVAIL:99 = 0
	ELSE
		SHOP_AVAIL:50 = 0
	ENDIF
	;旗揚げは勢力に空きがなければ不可
	SIF GET_NEW_COUNTRY() == -1
		SHOP_AVAIL:1 = 0
;どっかに仕官しているとき
ELSE
	SIF DAY < SLG_PP:1 || CONFIG:302 != 0
		SHOP_AVAIL:0 = 0

	SIF CONFIG:302 != 0
		SHOP_AVAIL:1 = 0

	SIF DAY < SLG_PP:0 || COUNTRY_IS_CLOSED:(CFLAG:MASTER:所属)
		SHOP_AVAIL:10 = 0

	SIF CONFIG:302 != 0
		SHOP_AVAIL:20 = 0

	SIF MONEY < SPECIAL_TRAIN_PRICE
		SHOP_AVAIL:30 = 0

	SIF !IS_SLAVEMARKET_AVAILABLE()
		SHOP_AVAIL:32 = 0

	SIF DONE_TERRITORYSEARCH == 1
		SHOP_AVAIL:33 = 0

	SIF !IS_SCOUT_AVAILABLE()
	SHOP_AVAIL:34 = 0


	SIF GET_NEW_COUNTRY() == -1
		SHOP_AVAIL:40 = 0
		
	;独立が選択可能かどうか
	;「主人公の所属する勢力が2都市以上所持していて、主人公が君主でない」が選択可能条件
	SIF !(LOCAL:3 != MASTER && CFLAG:MASTER:行動不能状態 == 0 && GET_OWN_CITY(CFLAG:MASTER:所属) >= 2 && !IS_SP_COUNTRY(CFLAG:MASTER:所属))
		SHOP_AVAIL:40 = 0

	;クーデターが選択可能かどうか
	;条件がややこしいので、一旦フラグ折って、実行可能なら勃てる
	SHOP_AVAIL:41 = 0
	IF CFLAG:MASTER:行動不能状態 == 0 && !IS_SP_COUNTRY(CFLAG:MASTER:所属)
		LOCAL:2 = 0
		;主人公が君主の場合、自分以外に士官がいればオッケー
		IF GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
			IF GET_COMMANDER_NUM(CFLAG:MASTER:所属) >= 2
				SHOP_AVAIL:41 = 1
			ELSE
				SHOP_AVAIL:41 = 0
			ENDIF
		ELSE
			FOR LOCAL:0, 0, CHARANUM
				;[恋慕][親友][服従]のいずれかを持っている士官の数をカウント
				IF LOCAL:0 != MASTER && LOCAL:0 != LOCAL:3 && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):親友 || TALENT:(LOCAL:0):服従)
					LOCAL:2 ++
				ENDIF
			NEXT
			;自国の半数以上の士官が陥落済みならクーデターが可能
			SIF LOCAL:2 * 2 >= GET_COMMANDER_NUM(CFLAG:MASTER:所属) - 1
				SHOP_AVAIL:41 = 1
			;王様が親愛か隷属でもオッケー
			SIF TALENT:(GET_COUNTRY_BOSS(CFLAG:MASTER:所属)):親愛 || TALENT:(GET_COUNTRY_BOSS(CFLAG:MASTER:所属)):隷属
				SHOP_AVAIL:41 = 1
		ENDIF
	ENDIF

	;野に下る
	;君主じゃなくて雇用カウンタが0
	SIF !(CFLAG:MASTER:所属 && GET_COUNTRY_BOSS(CFLAG:MASTER:所属) != MASTER && PLAYER_HIRED_COUNTER == 0)
		SHOP_AVAIL:42 = 0


ENDIF

;-------------------------------------------------
;ショップ画面の表示処理(戦略フェイズ)
;-------------------------------------------------
@SHOW_SHOP_SLG
;コマンドの選択可否判定
CALL CHECK_SHOP_COMABLE_SLG

MAP_LINES = MENU_HEIGHT
IF FLAG:200 == 2
	CALL SHOW_CITY_ARMY()
ELSE
	;マップとコマンドボタンを表示
	CALL SET_CITY_COLOR_COUNTRY
	CALL SET_CITY_COLOR_UNIT
	CALL DRAWMAP(1, 1)
	CALL RESET_CITY_COLOR
	;都市の情報を表示
	CALL SHOW_CITY_INFO(SHOWN_CITY)
ENDIF
;-------------------------------------------------
;ショップ画面の入力処理(戦略フェイズ 0～99)
;ARG:0に入力した値が入る
;処理を行った場合は1を、行わなかった場合は0を返すこと
;-------------------------------------------------
@EVENTBUY_SLG(ARG:0)

IF FLAG:200 == 2
	SELECTCASE ARG:0
		CASE 54
			CALL SHOW_CITY_ARMY(0, -1)
			RETURN 0
		CASE 56
			CALL SHOW_CITY_ARMY(0, 1)
			RETURN 0
		CASE 44
			CALL SHOW_CITY_ARMY(0, -2)
			RETURN 0
		CASE 66
			CALL SHOW_CITY_ARMY(0, 2)
			RETURN 0
	ENDSELECT
ENDIF

IF SHOP_AVAIL:(ARG:0)
	;観戦モード
	IF FLAG:観戦モード
		SELECTCASE ARG:0
			;勢力情報
			CASE 0
				CALL SHOW_COUNTRY_DATA
			;能力一覧
			CASE 1
				FLAG:能力表示フィルタ = 3
				CALL CREATE_SELECTOR_SORT_LIST
				CALL SHOP_LIFE_CHARA_TABLE
			;行動終了
			CASE 81
				RESULT = 1
				BEGIN TURNEND
			CASEELSE
				RETURN 0
		ENDSELECT
	;放浪中
	ELSEIF CFLAG:MASTER:所属 == 0
		SELECTCASE ARG:0
			;仕官する
			CASE 0
				CALL SHOP_SERVE_COUNTRY
			;旗揚げ
			CASE 1
				CALL SHOP_CREATE_COUNTRY
			;勢力情報
			CASE 5
				CALL SHOW_COUNTRY_DATA
			;能力一覧
			CASE 6
				CALL CREATE_SELECTOR_SORT_LIST
				CALL SHOP_LIFE_CHARA_TABLE
			;訓練
			CASE 8
				CALL SLG_TRAIN
			;謁見
			CASE 9
				CALL SLG_AUDIENCE
			;行動終了
			CASE  81
				RESULT = 1
				BEGIN TURNEND
			CASEELSE
				RETURN 0
		ENDSELECT
	;通常
	ELSE
		SELECTCASE ARG:0
			;部隊編成
			CASE 0
				CALL FORM_ARMY(CFLAG:MASTER:所属)
			;防衛解除
			CASE 1
				CALL MINIMIZE_GUARD
			;外交
			CASE 10
				CALL DIPLOMACY
			;勢力情報
			CASE 11
				CALL SHOW_COUNTRY_DATA
			;能力一覧
			CASE 12
				FLAG:能力表示フィルタ = 3
				CALL CREATE_SELECTOR_SORT_LIST
				CALL SHOP_LIFE_CHARA_TABLE
			;政策変更
			CASE 20
				COUNTRY_POLICY:(CFLAG:MASTER:所属) ++
				IF COUNTRY_POLICY:(CFLAG:MASTER:所属) >= 3
					COUNTRY_POLICY:(CFLAG:MASTER:所属) = 0
				ENDIF
				RETURN 0
			;都市管理
			CASE 21
				IF FLAG:200 == 2
					FLAG:200 = -1
				ELSE
					CALL SHOW_CITY_ARMY(1)
					FLAG:201 = 0
					FLAG:200 = 2
				ENDIF
				RETURN 0
			;特別訓練
			CASE 30
				CALL SLG_SPECIAL_TRAIN
			;臨時徴兵
			CASE 31
				CALL SLG_EXTRA_DRAFT
			;奴隷市場
			CASE 32
				CALL SLAVEMARKET
			;領地探索
			CASE 33
				CALL TERRITORYSEARCH
			;スカウト
			CASE 34
				CALL SLG_SCOUT
			;国庫入金
			CASE 35
				CALL SLG_TREASURY_DEPOSIT
			;独立
			CASE 40
				CALL PLAYER_INDEPENDENCE
			;クーデター
			CASE 41
				IF GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
					CALL PLAYER_GIVE_COUNTRY
				ELSE
					CALL PLAYER_COUP
				ENDIF
			;オート設定
			CASE 50
				CONFIG:302 ++
				IF CONFIG:302 >= 2
					CONFIG:302 = 0
				ENDIF
				RETURN 0
			;下野
			CASE 42
				CALL SHOP_RESIGN
			;行動終了
			CASE 81
				RESULT = 1
				BEGIN TURNEND
			CASEELSE
				RETURN 0
		ENDSELECT
	ENDIF
ELSE
	SIF ARG:0 != 21
		FLAG:200 = -1
	RETURN 0
ENDIF
SIF ARG:0 != 21
	FLAG:200 = -1

RETURN 1

;-------------------------------------------------
;ショップ画面の入力処理(戦略フェイズ 100～)
;ARG:0に入力した値が入る
;処理を行った場合は1を、行わなかった場合は0を返すこと
;-------------------------------------------------
@USERSHOP_SLG(ARG:0)

;都市情報
IF ARG:0 >= 1000 && ARG:0 < 1000 + MAX_CITY
	SHOWN_CITY = ARG:0 - 1000
	REDRAW 0
	CLEARLINE LINECOUNT - LINES_SHOP
;防衛兵数の設定
ELSEIF ARG:0 >= 200 && ARG:0 < 200 + MAX_CITY && CONFIG:302 == 0
	LOCAL:5 = ARG:0 - 200
	;表示する都市をその都市に（都市管理画面や直打ちしたとき用）
	SHOWN_CITY = LOCAL:5
	IF CFLAG:MASTER:所属 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:所属
		;敵部隊のいる都市は変更不可
		CALL GET_STAY_ENEMY_UNIT(LOCAL:5)
		IF RESULT:0 >= 0
			PRINTFORMW 敵部隊が存在する都市の兵数は変更できません
		ELSE
			CALL SHOP_CITY_SOLDIER(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
;守将の設定
ELSEIF ARG:0 >= 400 && ARG:0 < 400 + MAX_CITY && CONFIG:302 == 0
	LOCAL:5 = RESULT - 400
	;表示する都市をその都市に（都市管理画面や直打ちしたとき用）
	SHOWN_CITY = LOCAL:5
	IF CFLAG:MASTER:所属 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:所属
		;敵部隊のいる都市は変更不可
		CALL GET_STAY_ENEMY_UNIT(LOCAL:5)
		IF RESULT:0 >= 0
			PRINTFORMW 敵部隊が存在する都市の守将は変更できません
		ELSE
			CALL SHOP_CITY_COMMANDER(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
;勢力色変更
ELSEIF ARG:0 >= 600 && ARG:0 < 600 + MAX_COUNTRY
	LOCAL:5 = ARG:0 - 600
	IF LOCAL:5 == 0
		CALL CHANGE_COUNTRY_COLOR(LOCAL:5)
		RETURN 1
	ENDIF
	IF IS_COUNTRY(LOCAL:5)
		CALL CHANGE_COUNTRY_COLOR(LOCAL:5)
		RETURN 1
	ENDIF
	RETURN 0
;一か所だけ防衛解除
ELSEIF ARG:0 > 800 && ARG:0 < 800 + MAX_CITY
	CALL SINGLE_MINIMIZE(ARG:0 - 800)
;侵攻・移動ボタン
ELSEIF ARG:0 > 2000 && ARG:0 < 2000 + MAX_CITY
	IF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0 - 2000), CFLAG:MASTER:所属) >= 2
		CALL MOVETO(ARG:0 - 2000)
	ELSE
		CALL INVADE(ARG:0 - 2000)
	ENDIF
	
	RETURN 1
;部隊編成ボタン
ELSEIF ARG:0 >= 2500 && ARG:0 < 2500 + MAX_UNIT && IS_COUNTRY(CFLAG:MASTER:所属) && UNIT_SOLDIER:(CFLAG:MASTER:所属):(ARG:0 - 2500) > 0 && !UNIT_BREAK_TERM:(CFLAG:MASTER:所属):(ARG:0 - 2500)
	CALL UNIT_SETTING(CFLAG:MASTER:所属, ARG:0 - 2500)
ELSE
	RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;防衛部隊の解除
;-------------------------------------------------
@MINIMIZE_GUARD
SETCOLOR 0xFF6000
PRINT 敵部隊が存在する都市を除く
RESETCOLOR
PRINTL 全ての都市の防衛兵数を500にし、守将を解除します
PRINTL よろしいですか？

CALL ASK_YN("はい", "キャンセル")
IF RESULT == 0
	;部隊マップを作成
	CALL TMP_CREATE_UNIT_MAP
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属 && GET_CITYNAME(LOCAL:0) != "無名" && !TMP_IS_STAY_ENEMY_UNIT(LOCAL:0, CFLAG:MASTER:所属)
			IF CITY_SOLDIER:(LOCAL:0) > 500
				COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += CITY_SOLDIER:(LOCAL:0) - 500
				CITY_SOLDIER:(LOCAL:0) = 500
			ENDIF
			;このターンに割り当てたわけでなければクールタイム付与
			SIF GET_CITY_COMMANDER(LOCAL:0, 0) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(LOCAL:0, 0)):0
				COOLTIME:GET_CITY_COMMANDER(LOCAL:0, 0):0 = 1
			SIF GET_CITY_COMMANDER(LOCAL:0, 1) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(LOCAL:0, 1)):0
				COOLTIME:GET_CITY_COMMANDER(LOCAL:0, 1):0 = 1
			CITY_COMMANDER:(LOCAL:0) = 0
		ENDIF
	NEXT
ENDIF

@SINGLE_MINIMIZE(ARG)
IF TMP_IS_STAY_ENEMY_UNIT(ARG, CFLAG:MASTER:所属)
	PRINTW 敵部隊が存在する都市には実行できません
ELSEIF CITY_OWNER:(ARG) == CFLAG:MASTER:所属 && GET_CITYNAME(ARG) != "無名"
	IF CITY_SOLDIER:(ARG) > 500
		COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += CITY_SOLDIER:(ARG) - 500
		CITY_SOLDIER:(ARG) = 500
	ENDIF
	;このターンに割り当てたわけでなければクールタイム付与
	SIF GET_CITY_COMMANDER(ARG, 0) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(ARG:0, 0)):0
		COOLTIME:GET_CITY_COMMANDER(ARG, 0):0 = 1
	SIF GET_CITY_COMMANDER(ARG, 1) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(ARG:0, 1)):0
		COOLTIME:GET_CITY_COMMANDER(ARG, 1):0 = 1
	CITY_COMMANDER:(ARG) = 0
	PRINTW この都市の防衛を解除しました
ENDIF

;-------------------------------------------------
;仕官する
;-------------------------------------------------
@SHOP_SERVE_COUNTRY
LOCAL:10 = 0

$SHOW_LOOP

DRAWLINE
PRINTL 仕官先の国家を選択して下さい
PRINT 一度仕官すると、
SETCOLOR COLOR("注意")
PRINT 5
RESETCOLOR
PRINTL 期の間、下野することができません
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:10))

IF LOCAL:2 < 0
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL 
	PRINTBUTTON " 0[戻る]", 0
	PRINTL 
ELSE
	PRINT この都市は 
	SETCOLOR COUNTRY_COLOR:(LOCAL:10)
	PRINTFORM %NAME_FORMAL(LOCAL:2)%
	RESETCOLOR
	PRINTL  の勢力下にあります
	IF COUNTRY_IS_CLOSED:(LOCAL:10)
		SETCOLOR COLOR("選択不可")
		PRINTFORM  X[この勢力には仕官できません]
		RESETCOLOR
	ELSEIF FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL:10) != -1
		SETCOLOR COLOR("選択不可")
		PRINTFORM  X[勢力から離脱したばかりです]
		RESETCOLOR
	ELSE
		PRINTBUTTON " 1[この勢力に仕官する]", 1
	ENDIF
	PRINTL 
	PRINTBUTTON " 0[戻る]", 0
	PRINTL 
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 1 && !COUNTRY_IS_CLOSED:(LOCAL:10) && FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL:10) == -1
	IF LOCAL:2 >= 0
		CALL CHANGE_COUNTRY(MASTER, LOCAL:10)
		TIME = 0
		PRINTFORMW %NAME:(LOCAL:2)%勢力に仕官します
		PLAYER_HIRED_COUNTER  = 5
		RETURN
	ENDIF
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		LOCAL:10 = LOCAL:4
		CLEARLINE LINECOUNT - LINES_SHOP
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		LOCAL:10 = 0
		CLEARLINE LINECOUNT - LINES_SHOP
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;旗揚げ
;-------------------------------------------------
@SHOP_CREATE_COUNTRY
DRAWLINE
PRINTL 旗揚げする都市を選択して下さい。無所属の都市のみ選択可能です
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

PRINTBUTTON " 0[戻る]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF !IS_COUNTRY(LOCAL:4) && CITY_TYPE:(LOCAL:3) == 0 && GET_RELAYNAME(LOCAL:3) != "無名"
		PRINTFORML %GET_RELAYNAME(LOCAL:3)%で旗揚げしますか？
		PRINTBUTTON " 1[旗揚げする]", 1
		PRINTPLAIN     
		PRINTBUTTON " 2[キャンセル]", 2
		PRINTL 
		PRINTBUTTON " 0[戻る]", 0

		REDRAW 0

		$INPUT_LOOP2
		INPUT

		IF RESULT == 0
			RETURN
		ELSEIF RESULT == 1
			LOCAL:5 = GET_NEW_COUNTRY()
			CITY_OWNER:(LOCAL:3) = LOCAL:5
			COUNTRY_BOSS:(LOCAL:5) = GET_ID(MASTER)
			COUNTRY_SOLDIER:(LOCAL:5) = CITY_ECONOMY:(LOCAL:3) / 20
			CITY_SOLDIER:(LOCAL:3) = 2000
			CALL CHANGE_COUNTRY(MASTER, LOCAL:5)

			DRAWLINE
			PRINTL 新しい勢力の色を選んで下さい
			DRAWLINE
			CALL CHANGE_COUNTRY_COLOR(LOCAL:5, 1)

			TIME = 0
			RETURN
		ELSEIF RESULT == 2
			RESTART
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;野に下る
;-------------------------------------------------
@SHOP_RESIGN()
DRAWLINE
PRINTFORML %NAME:(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%の元を去り、野に下ります
PRINTFORML 本当によろしいですか？
CALL ASK_YN()
IF RESULT == 0
	ARRAYSHIFT COUNTRY_PLAYER_BELONGED, 1, 0
	COUNTRY_PLAYER_BELONGED:0 = CFLAG:MASTER:所属
	PRINTFORMW 下野しました
	IF MATCH(COUNTRY_PLAYER_BELONGED, 0) < VARSIZE("COUNTRY_PLAYER_BELONGED") - 2
		CALL COLORPRINT(@"所属を転々とする%ANAME(MASTER)%は、諸国から警戒されています", COLOR("警告"), "W")
		FOR LOCAL, 0, MAX_COUNTRY
			SIF IS_COUNTRY(LOCAL)
				CALL CHANGE_RELATION_C_TO_O(LOCAL, MASTER, RAND(-100, -50), RAND(50, 100))
		NEXT
	ENDIF
	CALL CHANGE_COUNTRY(MASTER, 0, 1)
ELSE
	PRINTFORMW 思いとどまりました
ENDIF
