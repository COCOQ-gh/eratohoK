@SLG_SPECIAL_TRAIN()
#DIM 対象, 1
#DIM 対象項目, 1
#DIM 取得経験値, 1
$SHOW_LOOP
DRAWLINE
PRINTFORM 金
SETCOLOR COLOR("注意")
PRINTFORM {SPECIAL_TRAIN_PRICE}
RESETCOLOR
PRINTFORML を消費し、配下に訓練を受けさせることができます
SIF SPECIAL_TRAIN_IS_BARGAIN()
	CALL COLORPRINTFORM("ただいま大幅割引中！", COLOR("注意"), "L")
PRINTFORML 誰を訓練しますか？
PRINTFORML 現在の金:{MONEY}
DRAWLINE
CALL SELECT_CHARA_LIST_SLG()
IF RESULT == -1
	RETURN 0
ELSE
	対象 = RESULT
	DRAWLINE
	PRINTFORML %ANAME(対象)%のどの能力を訓練しますか？
	DRAWLINE
	PRINTFORM [{GETNUM(ABL, "武闘")}] 武闘 
	CALL PRINT_ALPHABET_RANK(ランク_三能力, ABL:(対象):武闘)
	PRINTFORML {ABL:(対象):武闘, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "知略")}] 知略 
	CALL PRINT_ALPHABET_RANK(ランク_三能力, ABL:(対象):知略)
	PRINTFORML {ABL:(対象):知略, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "政治")}] 政治 
	CALL PRINT_ALPHABET_RANK(ランク_三能力, ABL:(対象):政治)
	PRINTFORML {ABL:(対象):政治, 3, RIGHT}
	IF TALENT:対象:妖術知識
		PRINTFORM [{GETNUM(ABL, "妖術")}] 妖術 
		CALL PRINT_ALPHABET_RANK(ランク_妖術, ABL:(対象):妖術)
		PRINTFORML {ABL:(対象):妖術, 3, RIGHT}
	ENDIF
	PRINTFORM [{GETNUM(ABL, "歌唱")}] 歌唱 
	CALL PRINT_ALPHABET_RANK(ランク_料理歌唱, ABL:(対象):歌唱)
	PRINTFORML {ABL:(対象):歌唱, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "料理")}] 料理 
	CALL PRINT_ALPHABET_RANK(ランク_料理歌唱, ABL:(対象):料理)
	PRINTFORML {ABL:(対象):料理, 3, RIGHT}
	PRINTL 
	DRAWLINE
	PRINTFORML [0] 戻る
	PRINTL 
	$INPUT_LOOP2
	INPUT
	IF RESULT == 0
		GOTO SHOW_LOOP
	ELSEIF !GROUPMATCH(RESULT, GETNUM(ABL, "武闘"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "妖術"))
		GOTO INPUT_LOOP2
	ELSEIF RESULT == GETNUM(ABL, "妖術") && !TALENT:(対象):妖術知識
		GOTO INPUT_LOOP2
	ELSE
		対象項目 = RESULT
		;バーゲン時は安い
		取得経験値 = SPECIAL_TRAIN_CALC_EFFECT(対象, 対象項目)
		PRINTFORML 訓練を行った！
		CALL PRINT_ADD_EXP(対象, EXPNAME:GET_EXP(対象項目), 取得経験値, 1)
		CALL TRAIN_AUTO_ABLUP(対象)
		PRINTFORMW %ANAME(対象)%はいい汗をかいたようだ……
		MONEY -= SPECIAL_TRAIN_PRICE
		SPECIAL_TRAIN_COUNT ++
		SPECIAL_TRAIN_COUNT_THIS_TURN ++
		;再計算
		CALL CALC_SPECIAL_TRAIN_PRICE()
		IF MONEY >= SPECIAL_TRAIN_PRICE
			RESTART
		ELSE
			PRINTFORML 特別訓練を受けるだけのお金がなくなりました
			PRINTFORMW メニュー画面に戻ります
		ENDIF
	ENDIF
	PRINTFORML
	RETURN 0
ENDIF


@CALC_SPECIAL_TRAIN_PRICE()
;経済値の10倍（GET_SUM_ECONOMYで拾ってこれるのは表示の100倍）
LOCAL = GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100

;その周回の実施回数およびそのターンの実施回数で補正
SELECTCASE SPECIAL_TRAIN_COUNT
	CASE IS < 5
		LOCAL += SPECIAL_TRAIN_COUNT * 500
	CASE IS < 15
		LOCAL += SPECIAL_TRAIN_COUNT * 1000
	CASE IS < 50
		LOCAL += SPECIAL_TRAIN_COUNT * 1500
	CASEELSE
		LOCAL += SPECIAL_TRAIN_COUNT * 3000
ENDSELECT

;そのターンに行った回数を乗せる
LOCAL += SPECIAL_TRAIN_COUNT_THIS_TURN * 1000

;勢力の都市数を乗せる(6都市以上から)
LOCAL +=  MAX(GET_OWN_CITY(CFLAG:MASTER:所属) - 6 * 500, 0)

;勢力の★数で補正 ★3を補正0として±20%まで
LOCAL = LOCAL * (100 + (TMP_COUNTRY_STARS:(CFLAG:MASTER:所属) - 3) * 20) / 100

;乱数を乗せる
LOCAL = LOCAL * RAND(90, 110) / 100

;日数で補正
;LOCAL = LOCAL * (100 + MIN(DAY - 20, 100)) / 100

;俸給の方が高いなら、俸給の1/2をよこせ
SIF LOCAL < CALC_SALARY()
	LOCAL += CALC_SALARY() / 2

;その周回で最初の特別訓練、または5期に1回、1/10で特訓できる
SIF SPECIAL_TRAIN_IS_BARGAIN()
	LOCAL /= 10
;1ターン目の最初の1回は500固定
SIF DAY == 1 && SPECIAL_TRAIN_COUNT == 0
	LOCAL = 500

SPECIAL_TRAIN_PRICE = LOCAL


@SPECIAL_TRAIN_IS_BARGAIN()
#FUNCTION
RETURNF SPECIAL_TRAIN_COUNT == 0 || (DAY % 5 == 0 && SPECIAL_TRAIN_COUNT_THIS_TURN == 0)



@SPECIAL_TRAIN_CALC_EFFECT(対象, 対象項目)
#FUNCTION
#DIM 対象
#DIM 対象項目
#DIM 対象能力値
#DIM 能力合計
#DIM 取得経験基礎値
#DIM 取得経験値
#DIM 成長必要値
#DIM 返却値
対象能力値 = ABL:対象:対象項目
SELECTCASE 対象項目
	;三能力
	CASE GETNUM(ABL, "武闘"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治")
		成長必要値 = ABLUP_NEED_EXP_SLG_THREE(対象能力値)
		能力合計 = ABL:対象:武闘 + ABL:対象:知略 + ABL:対象:政治
		取得経験基礎値 = (300 - 能力合計) / 3
		;選択した項目が高いと取得経験値は下がる
		SELECTCASE 対象能力値
			CASE IS < ランク閾値:ランク_三能力:ランク_D
				取得経験値 = 取得経験基礎値 * 120 / 100
			CASE IS < ランク閾値:ランク_三能力:ランク_B
				取得経験値 = 取得経験基礎値 * 70 / 100
			CASE IS < ランク閾値:ランク_三能力:ランク_A
				取得経験値 = 取得経験基礎値 * 50 / 100
			CASEELSE
				取得経験値 = 取得経験基礎値 * 30 / 100
		ENDSELECT
	;歌唱と料理
	CASE GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱")
		成長必要値 = ABLUP_NEED_EXP_SLG_TWO(対象能力値)
		能力合計 = ABL:(対象):歌唱 + ABL:(対象):料理
		取得経験基礎値 = (200 - 能力合計) / 2
		SELECTCASE 対象能力値
			CASE IS < ランク閾値:ランク_料理歌唱:ランク_E
				取得経験値 = 取得経験基礎値 * 120 / 100
			CASE IS < ランク閾値:ランク_料理歌唱:ランク_C
				取得経験値 = 取得経験基礎値 * 50 / 100
			CASE IS < ランク閾値:ランク_料理歌唱:ランク_A
				取得経験値 = 取得経験基礎値 * 30 / 100
			CASEELSE
				取得経験値 = 取得経験基礎値 * 20 / 100
		ENDSELECT
	;妖術
	CASE GETNUM(ABL, "妖術")
		成長必要値 = ABLUP_NEED_EXP_SLG_THREE(対象能力値) * 40
		取得経験基礎値 = RAND(500, 1000)
		SELECTCASE 対象能力値
			CASE IS < ランク閾値:ランク_妖術:ランク_E
				取得経験値 = 取得経験基礎値 * 120 / 100
			CASE IS < ランク閾値:ランク_妖術:ランク_C
				取得経験値 = 取得経験基礎値 * 70 / 100
			CASE IS < ランク閾値:ランク_料理歌唱:ランク_A
				取得経験値 = 取得経験基礎値 * 50 / 100
			CASEELSE
				取得経験値 = 取得経験基礎値 * 30 / 100
		ENDSELECT
ENDSELECT
;一回につき10%ずつ減る
取得経験値 = 取得経験値 * MAX(100 - SPECIAL_TRAIN_COUNT_THIS_TURN * 10, 50) / 100
;バーゲン時は半減。1度に20まで成長
返却値 = LIMIT(取得経験値 / (SPECIAL_TRAIN_IS_BARGAIN() + 1), 5, 成長必要値 * 20)
RETURNF 返却値