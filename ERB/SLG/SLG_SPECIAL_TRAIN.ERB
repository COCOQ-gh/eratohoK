@SLG_SPECIAL_TRAIN()
#DIM 対象, 1
#DIM 対象項目, 1
#DIM 取得経験値, 1
$SHOW_LOOP
DRAWLINE
PRINTFORM 金
SETCOLOR COLOR("注意")
PRINTFORM {CALC_SPECIAL_TRAIN_MONEY()}
RESETCOLOR
PRINTFORML を消費し、配下に訓練を受けさせることができます
PRINTFORML 訓練を行うとターンが消費されます
PRINTFORML 誰を訓練しますか？
PRINTFORML 現在の金:{MONEY}
DRAWLINE
CALL SELECT_CHARA_LIST_SLG()
IF RESULT == -1
	RETURN 0
ELSE
	対象 = RESULT
	DRAWLINE
	PRINTFORML %ANAME(対象)%のどの能力を訓練しますか？
	DRAWLINE
	PRINTFORM [50] 武闘 
	CALL PRINT_ALPHABET_RANK(ランク_三能力, ABL:(対象):武闘)
	PRINTFORML {ABL:(対象):武闘, 3, RIGHT}
	PRINTFORM [51] 知略 
	CALL PRINT_ALPHABET_RANK(ランク_三能力, ABL:(対象):知略)
	PRINTFORML {ABL:(対象):知略, 3, RIGHT}
	PRINTFORM [52] 政治 
	CALL PRINT_ALPHABET_RANK(ランク_三能力, ABL:(対象):政治)
	PRINTFORML {ABL:(対象):政治, 3, RIGHT}
	IF TALENT:(対象):妖術知識
		PRINTFORM [53] 妖術 
		CALL PRINT_ALPHABET_RANK(ランク_妖術, ABL:(対象):妖術)
		PRINTFORML {ABL:(対象):妖術, 3, RIGHT}
	ENDIF
	PRINTFORM [60] 歌唱 
	CALL PRINT_ALPHABET_RANK(ランク_料理歌唱, ABL:(対象):歌唱)
	PRINTFORML {ABL:(対象):歌唱, 3, RIGHT}
	PRINTFORM [61] 料理 
	CALL PRINT_ALPHABET_RANK(ランク_料理歌唱, ABL:(対象):料理)
	PRINTFORML {ABL:(対象):料理, 3, RIGHT}
	PRINTL 
	DRAWLINE
	PRINTFORML [0] 戻る
	PRINTL 
	$INPUT_LOOP2
	INPUT
	IF RESULT == 0
		GOTO SHOW_LOOP
	ELSEIF RESULT != 50 && RESULT != 51 && RESULT != 52  && RESULT != 53 && RESULT != 60 && RESULT != 61
		GOTO INPUT_LOOP2
	ELSEIF RESULT == 53 && !TALENT:(対象):妖術知識
		GOTO INPUT_LOOP2
	ELSE
		対象項目 = RESULT
		LOCAL:1 = ABL:(対象):(対象項目)
		SELECTCASE 対象項目
			;三能力
			CASE 50 TO 52
				LOCAL:2 = ABL:(対象):武闘 + ABL:(対象):知略 + ABL:(対象):政治
				LOCAL:3 = LIMIT(CALC_SPECIAL_TRAIN_MONEY() / 200, 100, 200)
				取得経験値 = LOCAL:3 * (300 - LOCAL:2) / 300
				;選択した項目が高いと取得経験値は下がる
				SELECTCASE LOCAL:1
					CASE IS < 50
					CASE IS < 70
						取得経験値 = 取得経験値 * 70 / 100
					CASE IS < 80
						取得経験値 = 取得経験値 * 50 / 100
					CASEELSE
						取得経験値 = 取得経験値 * 30 / 100
				ENDSELECT
			;歌唱と料理
			CASE 60 TO 61
				LOCAL:2 = ABL:(対象):歌唱 + ABL:(対象):料理
				LOCAL:3 = LIMIT(CALC_SPECIAL_TRAIN_MONEY() / 200, 100, 200)
				取得経験値 = LOCAL:3 * (200 - LOCAL:2) / 200
				SELECTCASE LOCAL:1
					CASE IS < 30
					CASE IS < 50
						取得経験値 = 取得経験値 * 50 / 100
					CASE IS < 70
						取得経験値 = 取得経験値 * 30 / 100
					CASEELSE
						取得経験値 = 取得経験値 * 20 / 100
				ENDSELECT
			CASE 53
				取得経験値 = RAND(500, 1000)
				SELECTCASE LOCAL:1
					CASE IS < 20
					CASE IS < 40
						取得経験値 = 取得経験値 * 70 / 100
					CASE IS < 50
						取得経験値 = 取得経験値 * 50 / 100
					CASEELSE
						取得経験値 = 取得経験値 * 30 / 100
				ENDSELECT
		ENDSELECT
		取得経験値 = MAX(取得経験値, 5)
		PRINTFORML 訓練を行った！
		SELECTCASE 対象項目
			CASE 50
				CALL PRINT_ADD_EXP(対象, "武闘経験値", 取得経験値, 1)
			CASE 51
				CALL PRINT_ADD_EXP(対象, "知略経験値", 取得経験値, 1)
			CASE 52
				CALL PRINT_ADD_EXP(対象, "政治経験値", 取得経験値, 1)
			CASE 53
				CALL PRINT_ADD_EXP(対象, "妖術経験値", 取得経験値, 1)
			CASE 60
				CALL PRINT_ADD_EXP(対象, "歌唱経験値", 取得経験値, 1)
			CASE 61
				CALL PRINT_ADD_EXP(対象, "料理経験値", 取得経験値, 1)
		ENDSELECT
		CALL TRAIN_AUTO_ABLUP(対象)
		PRINTFORMW %ANAME(対象)%はいい汗をかいたようだ……
		MONEY -= CALC_SPECIAL_TRAIN_MONEY()
	ENDIF
	PRINTFORML
	DRAWLINE
	DONE_SPECIAL_TRAIN = 1
	RETURN 0
ENDIF

@CALC_SPECIAL_TRAIN_MONEY()
#FUNCTION
LOCAL = 5000
SIF IS_COUNTRY(CFLAG:MASTER:1)
	LOCAL += MIN(GET_OWN_CITY(CFLAG:MASTER:1) * 200, 20000)
LOCAL += MIN(DAY * 100, 15000)
RETURNF LOCAL