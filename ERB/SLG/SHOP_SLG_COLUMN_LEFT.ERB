;-------------------------------------------------
;左カラムカテゴリ名称設定
;-------------------------------------------------
@COLUMN_LEFT_CATEGORY_NAME_SLG
COLUMN_LEFT_CATEGORY_NAME_SLG:1 '= "自動"
COLUMN_LEFT_CATEGORY_NAME_SLG:2 '= "政策"
COLUMN_LEFT_CATEGORY_NAME_SLG:3 '= "戦略"
COLUMN_LEFT_CATEGORY_NAME_SLG:4 '= "交渉"
COLUMN_LEFT_CATEGORY_NAME_SLG:5 '= "情報"
COLUMN_LEFT_CATEGORY_NAME_SLG:6 '= "人材"
COLUMN_LEFT_CATEGORY_NAME_SLG:7 '= "特殊"
COLUMN_LEFT_CATEGORY_NAME_SLG:8 '= "進行"
COLUMN_LEFT_CATEGORY_NAME_SLG:9 '= "設定"

;-------------------------------------------------
;左カラムカテゴリ表示
;-------------------------------------------------
@COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG(ARGS:0)
;空指定なら同幅のスペース
IF ARGS:0 == ""
	LOCALS:0 '= TOSTR_SPACE(6)
	PRINTFORM %LOCALS:0%
;指定があれば色付けして表示
ELSE
	SETCOLOR COLOR("注釈")
	PRINTFORM %TOSTR_SPACE(1)%
	PRINTFORM %ARGS:0, 4, LEFT%
	PRINTFORM %TOSTR_SPACE(1)%
	RESETCOLOR
ENDIF
RETURN

;-------------------------------------------------
;拠点フェイズと違って選択中カラー表示のないボタン
;都市管理以外トグル式でないのでこのままにしたが
;操作結果として色を残したほうがいいだろうか？
;-------------------------------------------------
@COLUMN_LEFT_PRINTBUTTON_SLG(ARG:0, ARGS:0)
#DIM EXTERNAL_LINK

SIF ARG:0 < 0 || 99 < ARG:0
	THROW COLUMN_LEFT_PRINTBUTTONの項番が0～99の範囲にありません

;空指定なら同幅のスペース
IF ARGS:0 == ""
	PRINTPLAINFORM %TOSTR_SPACE(12)%
;選択不可（表示が制限されていない場合）
ELSEIF !SHOP_AVAIL:(ARG:0)
	SETCOLOR COLOR("選択不可")
	PRINTPLAINFORM [%ARGS:0, 10%]
	RESETCOLOR
;通常のボタン
ELSE
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
ENDIF

;-------------------------------------------------
;左カラムの配列：行番号にカテゴリとメニューを保存
;-------------------------------------------------
@COLUMN_LEFT_MENULINE_SAVE_SLG
#DIM COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG, 10

VARSET COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG, 0
VARSET COLUMN_LEFT_CATEGORY_NUM_SLG, 0
VARSET COLUMN_LEFT_MENU_NUM_SLG, 0

CALL COLUMN_LEFT_CATEGORY_NAME_SLG

LOCAL:0 = 1
LOCAL:4 = 0
FOR LOCAL:1, 1, 8
	IF COLUMN_LEFT_CATEGORY_NAME_SLG:(LOCAL:1) != ""
		FOR LOCAL:2, 1, 10
			LOCAL:3 = LOCAL:1 * 10 + LOCAL:2
			RESULTS:0 '= ""
			TRYCALLFORM SHOP_SLG_NAME{LOCAL:3}
			IF RESULTS:0 != "" && SHOP_AVAIL:(LOCAL:3)
				IF COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG:(LOCAL:1) != 1
					IF LOCAL:4 == 1
						LOCAL:0 ++
					ENDIF
					LOCAL:4 = 1
					COLUMN_LEFT_CATEGORY_NUM_SLG:(LOCAL:0) = LOCAL:1
					COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG:(LOCAL:1) = 1
				ENDIF
				COLUMN_LEFT_MENU_NUM_SLG:(LOCAL:0) = LOCAL:3
				IF COLUMN_LEFT_MENU_NUM_SLG:(LOCAL:0) == 65
					LOCAL:0 ++
				ENDIF
				LOCAL:0 ++
			ENDIF
		NEXT
	ENDIF
NEXT

;-------------------------------------------------
;左カラムメニュー表示ボタン呼び出し ARG:0は行数
;-------------------------------------------------
@COLUMN_LEFT_MENU_SHOW_SLG(ARG:0)

;固定行
SELECTCASE ARG:0
	;固定・案内
	CASE 0
		SETCOLOR COLOR("注釈")
		IF FLAG:観戦モード
			CALL COLORPRINT(" 観戦中           ", 0xB0BA90)
		ELSEIF CFLAG:MASTER:所属 == 0
			CALL COLORPRINT(" 放浪中           ", 0xB0BA90)
		ELSE
			CALL COLORPRINT(" 委任時は自動行動 ", 0xB0BA90)
		ENDIF
		RESETCOLOR
	;固定・開戦
	CASE 33
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG(COLUMN_LEFT_CATEGORY_NAME:8)
		TRYCALLFORM SHOP_SLG_NAME81
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 81, RESULTS:0)
	;固定・設定
	CASE 35
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG(COLUMN_LEFT_CATEGORY_NAME:9)
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 91, "設定")
	;固定・セーブ
	CASE 36
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG("")
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 92, "セーブ")
	;固定・ロード
	CASE 37
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG("")
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 93, "ロード")
	;可変・他カテゴリ名とメニューボタン
	CASEELSE
		;カテゴリ
		IF COLUMN_LEFT_CATEGORY_NUM_SLG:(ARG:0) != 0
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG(COLUMN_LEFT_CATEGORY_NAME_SLG:(COLUMN_LEFT_CATEGORY_NUM_SLG:(ARG:0)))
		ELSE
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW_SLG("")
		ENDIF

		;自動
		IF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0) == 11
			IF CONFIG:302 != 0
				SETCOLOR COLOR("オレンジ")
			ENDIF
		;政策
		ELSEIF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0) == 21
			SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:所属)
				CASE 政策_経済
					SETCOLOR COLOR("パ緑")
				CASE 政策_徴兵
					SETCOLOR COLOR("CREAM")
				CASE 政策_防衛
					SETCOLOR COLOR("パ青")
				CASEELSE
					SETCOLOR COLOR("明灰色")
			ENDSELECT
		ENDIF
		;メニューボタン
		IF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0) != 0
			TRYCALLFORM SHOP_SLG_NAME{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			CALL COLUMN_LEFT_PRINTBUTTON_SLG( COLUMN_LEFT_MENU_NUM_SLG:(ARG:0), RESULTS:0 )
		;前の行が特別訓練の場合以外は空行
		ELSEIF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0 - 1) != 65
			CALL COLUMN_LEFT_PRINTBUTTON_SLG( 0, "")
		ENDIF
		;前の行が特別訓練
		IF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0 - 1) == 65
			IF SPECIAL_TRAIN_IS_BARGAIN()
				SETCOLOR COLOR("注意")
				PRINTFORM %"現在割引料金", 12%
				RESETCOLOR
			ELSE
				SIF MONEY < SPECIAL_TRAIN_PRICE
					SETCOLOR COLOR("選択不可")
				PRINTFORM %"料金:" + TOSTR(SPECIAL_TRAIN_PRICE), 12%
				RESETCOLOR
			ENDIF
		ENDIF
ENDSELECT

RESETCOLOR
PRINT   

