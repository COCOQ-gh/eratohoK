;-------------------------------------------------
;汎用型謀反フラグチェッカー
;チェック項目は以下のとおり
;・(ARG:1)期以降（ARG:2）期までであること
;・対象(ARG:0)がプレイヤーでないこと
;・対象が捕虜／死亡／放浪状態ではないこと
;・所属勢力の支配都市数が(ARG:3)～(ARG:4)
;・％ロールの結果が(ARG:5)以下であること
;初期値 ARG:1 = 6, ARG:2 = 300, ARG:3 = 5, ARG:4 = 99, ARG:5 = 100
;RETURNF 0:謀反実行せず、機を待ちます, 1:機が熟したのでちょっと旗揚げしてくる
;-------------------------------------------------
@REBELLION_FLAG(ARGS:0 , ARG:1 = 6, ARG:2 = 300 , ARG:3 = 5, ARG:4 = 99 , ARG:5 = 100)
#FUNCTION
;キャラ番号を取得。
LOCAL:0 = FINDCHARA(CSTR:99, ARGS:0)
;指定期数が経過しているか
SIF !INRANGE(DAY , ARG:1 , ARG:2)
	RETURNF 0
;プレイヤー名が対象と一致していないか
SIF CSTR:MASTER:99 == ARGS:0
	RETURNF 0
;無所属でない／いずれかの勢力の捕虜ではない／放浪もしくは死亡状態ではないか
SIF CFLAG:(LOCAL:0):所属 == 0 || CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):特殊状態
	RETURNF 0
;さすがに君主が謀反起こしたらダメですよね。
SIF GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):所属) == LOCAL:0
	RETURNF 0
;所属勢力の支配都市数が指定範囲内か
SIF !INRANGE(GET_OWN_CITY(CFLAG:(LOCAL:0):所属), ARG:3 , ARG:4)
	RETURNF 0
;確実に謀反起こすのもどうかと思うので％ロール
SIF RAND:100 > ARG:5 
	RETURNF 0
;ここまでお膳立てされていれば謀反を起こさざるをえない
RETURNF 1





;-------------------------------------------------
;汎用型ランダム街強奪候補選択関数
;ARG:0勢力から独立したARG:1勢力がARG:2都市かっぱらう関数です。
;ARG:0のある一都市を奪い、続いてその都市に連続しているARG:0の都市のみを奪います。
;LOCAL 0:残り強奪予定都市数, 1:健気なカウンタ君, 2:判定する都市番号
;RETURN 0:強奪できました♪, 1:もしかして：全部奪っても足りないか０を指定, 2:もっとおかしな何かが起きました
;------------------------------------------------- 
@STEAL_CITIES(独立元, 独立先, 奪取数)
#DIM 独立元
#DIM 独立先
#DIM 奪取数
#DIM 奪取残数
#DIM 都市
#DIM ループ, 2
#DIM ルート
#DIM 中継点接続先
#DIM 奪取候補
;都市数のチェック
SIF MATCH(CITY_OWNER, 独立元) <= 1 || 奪取数 <= 0
	RETURN 1
奪取残数 = 奪取数
;都市のシャッフル
CALL FISHER_YATES_SHAFFLE(GET_CITY_NUM())
FOR ループ, 0, GET_CITY_NUM()
	都市 = SHAFFLE_ARRAY:ループ
	SIF CITY_OWNER:都市 != 独立元
		CONTINUE
	CALL TAKEOVER_CITY_FROM_NO(独立先, 都市)
	PRINTFORML %GET_CITYNAME(都市)%を奪いました
	奪取残数 --
	SIF 奪取残数 == 0
		RETURN 0
	FOR ルート, 0, VARSIZE("CITY_ROUTE", 1)
		SIF CITY_ROUTE:都市:ルート < 1
			BREAK
		;中継点
		IF CITY_TYPE:(CITY_ROUTE:都市:ルート) == 1
			FOR ループ:1, 0, VARSIZE("CITY_ROUTE", 1)
				中継点接続先 = CITY_ROUTE:(CITY_ROUTE:都市:ルート):(ループ:1)
				SIF 中継点接続先 == CITY_ROUTE:都市:ルート
					CONTINUE
				SIF 中継点接続先 < 1
					BREAK
				SIF CITY_OWNER:中継点接続先 != 独立元
					CONTINUE
				CALL TAKEOVER_CITY_FROM_NO(独立先, 中継点接続先)
				PRINTFORML %GET_CITYNAME(中継点接続先)%を奪いました
				奪取残数 --
				SIF 奪取残数 == 0
					RETURN 0
			NEXT
		ELSE
			奪取候補 = CITY_ROUTE:都市:ルート
			SIF IS_COUNTRY(CITY_OWNER:奪取候補) && GET_OWN_CITY(CITY_OWNER:奪取候補) > 1
				CONTINUE
			CALL TAKEOVER_CITY_FROM_NO(独立先, 奪取候補)
			PRINTFORML %GET_CITYNAME(奪取候補)%
			奪取残数 --
			SIF 奪取残数 == 0
				RETURN 0
		ENDIF
	NEXT
	RETURN 1
NEXT
;普通ここまできませんが念のため
RETURN 2
