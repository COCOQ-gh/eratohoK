;勢力が滅亡した場合や捕縛された場合の処理

;-------------------------------------------------
;ARG:1勢力がARG:0勢力を滅亡させた時の敗将の処理
;-------------------------------------------------
@LOSERS_ACTION(ARG:0, ARG:1)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = GET_COUNTRY_BOSS(ARG:1)

IF ARG:0 == CFLAG:MASTER:所属
	IF IS_SP_COUNTRY(ARG:1)
		TRYCCALLFORM MASTER_CAPTURED_%SP_COUNTRY_NAME_ENG:SP_COUNTRY_TO_CONST(ARG:1)%(ARG:1)
			;主人公の勢力に仕官していたか捕虜になっていたキャラは全員捕えらえる
			FOR LOCAL:0, 0 , CHARANUM
				IF (CFLAG:(LOCAL:0):所属 == ARG:0 ||CFLAG:(LOCAL:0):捕虜先 == ARG:0)
					CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
					CALL CAPTURE(LOCAL:0, ARG:1)
				ENDIF
			NEXT
			RETURN 1
		CATCH
		ENDCATCH
	ENDIF
ENDIF

;主人公勢力の手で滅亡させた場合
IF ARG:1 == CFLAG:MASTER:所属
	WAIT
	PRINTL 
	PRINTW ………………
	PRINTW ……
	PRINTL 
ENDIF

;滅亡した勢力の士官のうち、他のCPU勢力の捕虜となっていたキャラは、滅亡と同時に囚われていた勢力の士官になる
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 && CFLAG:(LOCAL:0):捕虜先 != CFLAG:MASTER:所属
		;滅亡する軍の捕虜となっていた（自国の捕虜となっていた）場合は、TREAT_PRISONERで判定
		IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
			CALL TREAT_PRISONER(ARG:0, ARG:1, LOCAL:0)
		ELSE
			;滅亡した勢力の士官のうち、囚われているのが特殊勢力の場合はなにもしない。
			IF IS_SP_COUNTRY(CFLAG:(LOCAL:0):捕虜先)
			;滅亡した勢力の士官のうち、他のCPU勢力の捕虜となっていたキャラは、登用判定を通す。
			ELSEIF GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先) != -1 && JUDGE_ASSIGN(LOCAL:0, CFLAG:(LOCAL:0):捕虜先, CFLAG:(LOCAL:0):所属) && !IS_SP_COUNTRY(CFLAG:(LOCAL:0):捕虜先)
				SETCOLOR COLOR("注意")
				PRINTFORML 囚われていた%ANAME(LOCAL:0)%は、%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に仕えることになったようだ
				RESETCOLOR
				CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:(LOCAL:0):捕虜先, 1)
			;登用判定にひっかからなければ放浪
			ELSE
				;フェイルセーフ
				SETCOLOR COLOR("注意")
				PRINTFORML 囚われていた%ANAME(LOCAL:0)%は、放浪したようだ
				RESETCOLOR
				CALL CHANGE_COUNTRY(LOCAL:0, 0)
			ENDIF
		ENDIF
	ENDIF
NEXT

;●まず君主について処理する
;逃走判定
CALL JUDGE_ESCAPE(ARG:0, ARG:1, LOCAL:5, 1)

;逃走に失敗した場合
IF CFLAG:(LOCAL:5):特殊状態 == 0
	;扱いを決定
	CALL TREAT_PRISONER(ARG:0, ARG:1, LOCAL:5)
	PRINTL 

	;登用した場合
	IF RESULT == 0
		LOCAL:7 = 0
	;解放した場合
	ELSEIF RESULT == 1
		LOCAL:7 = 1
	;投獄した場合
	ELSEIF RESULT == 2
		LOCAL:7 = 2
	;処刑した場合
	ELSE
		LOCAL:7 = 2
	ENDIF
;逃走に成功した場合
ELSEIF CFLAG:(ARG:2):特殊状態 == 1
	LOCAL:7 = 1
;死亡／削除待ちの場合
ELSE
	LOCAL:7 = 2
ENDIF

;君主と共にARG:1勢力に仕えるパターン(君主を登用)
IF LOCAL:7 == 0
	LOCAL:1 = 0
	LOCAL:2 = CFLAG:MASTER:所属

	IF ARG:0 == CFLAG:MASTER:所属
		IF MASTER == LOCAL:5
			PRINTFORMW 君主が登用に応じたため、%ANAME(MASTER)%に仕えていた全ての士官が%NAME:(LOCAL:6)%勢力に編入されます
			PRINTFORMW 以降は%NAME:(LOCAL:6)%勢力として行動します
		ELSE
			PRINTFORMW 君主が登用に応じたため、%ANAME(MASTER)%を含む全ての士官が%NAME:(LOCAL:6)%勢力に編入されます
			PRINTFORMW 以降は%NAME:(LOCAL:6)%勢力として行動します
		ENDIF
	ENDIF

	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == ARG:0
			;MASTER所属の国が吸収する
			IF ARG:1 == LOCAL:2
				IF !LOCAL:1
					LOCAL:1 = 1
					PRINTFORMW 君主が登用に応じたため、%NAME:(LOCAL:5)%に仕えていた士官は全員我が国に編入されます
				ENDIF
				;捕虜状態でない
				IF !CFLAG:(LOCAL:0):捕虜先
					PRINTFORML %NAME:(LOCAL:0)%が我が国の士官として加わりました
					CALL CHANGE_COUNTRY(LOCAL:0, ARG:1, 1)
				;すでに捕虜にしてた(ここで登用に応じるかチェックしても面白い？)
				ELSEIF CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					PRINTL 
					PRINTFORML %NAME:(LOCAL:0)%は現在我が国が投獄しています
					PRINTFORML 牢から出して士官として登用しますか？
					CALL ASK_YN("はい", "いいえ(現在の所属が無所属になります)")
					IF RESULT == 0
						PRINTFORML %NAME:(LOCAL:0)%が我が国の士官として復帰しました
						CALL CHANGE_COUNTRY(LOCAL:0, ARG:1, 1)
					ELSE
						PRINTFORML %NAME:(LOCAL:0)%は無所属の捕虜としてこのまま牢屋に閉じ込めておきます
						CALL CHANGE_COUNTRY(LOCAL:0, 0)
						CALL CAPTURE(LOCAL:0, CFLAG:MASTER:所属)
					ENDIF
					PRINTL 
				;他国が捕虜にしてる(通常AI国はすでに滅亡国の捕虜を取り込みしてるはず？なので特殊勢力に囚われてる可能性？)
				ELSE
					PRINTFORML %NAME:(LOCAL:0)%が我が国所属となりましたが、他国で捕虜となったままです
					CALL CHANGE_COUNTRY(LOCAL:0, ARG:1)
				ENDIF
			;AI国が吸収する
			ELSE
				;捕虜状態でないor吸収する国がすでに捕虜にしてた
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == ARG:1
					CALL CHANGE_COUNTRY(LOCAL:0, ARG:1, 1)
				;他国が捕虜にしてる(ここで捕虜解除してしまうと、第三勢力が持ってる捕虜ごと奪われてしまう)
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, ARG:1)
				ENDIF
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:1
		WAIT
	ENDIF

;君主と共に別の勢力へ移動するパターン(君主を解放・君主が逃亡)
;各々が独立して行動するパターン(君主を投獄・処刑)
ELSEIF LOCAL:7 == 1 || LOCAL:7 == 2
	;士官の逃走判定(主人公は後回し)
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && LOCAL:0 != LOCAL:5 && LOCAL:0 != MASTER
			IF CFLAG:(LOCAL:0):捕虜先 != 0
				;捕虜なら無所属に
				CALL CHANGE_COUNTRY(LOCAL:0, 0)
			ELSE
				CALL JUDGE_ESCAPE(ARG:0, ARG:1, LOCAL:0, 1)
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && LOCAL:0 != LOCAL:5 && LOCAL:0 != MASTER
			;逃走に失敗した場合
			IF CFLAG:(LOCAL:0):特殊状態 == 0
				CALL TREAT_PRISONER(ARG:0, ARG:1, LOCAL:0)
			ENDIF
		ENDIF
	NEXT

	IF CFLAG:MASTER:所属 == ARG:0 && MASTER != LOCAL:5
		IF CFLAG:MASTER:捕虜先 != 0
			;捕虜なら無所属に
			CALL CHANGE_COUNTRY(MASTER, 0)
		ELSE
			;主人公の逃走判定
			CALL JUDGE_ESCAPE(ARG:0, ARG:1, MASTER, 1)
			;逃走に失敗した場合
			IF CFLAG:MASTER:特殊状態 != 1
				CALL TREAT_PRISONER(ARG:0, ARG:1, MASTER)
			ENDIF
		ENDIF
	ENDIF
ENDIF

;ARG:0勢力の捕虜となっていたキャラの処理
LOCAL:2 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
		;滅亡させた側の勢力の士官なら、そのまま元の勢力に戻る
		IF CFLAG:(LOCAL:0):所属 == ARG:1
			CALL CAPTURE(LOCAL:0, 0)
			SETCOLOR COLOR("注意")
			PRINTFORML %NAME:(LOCAL:5)%勢力に囚えられていた%ANAME(LOCAL:0)%は、元の勢力に復帰した
			RESETCOLOR
			LOCAL:2 = 1
		ENDIF
	ENDIF
NEXT
IF LOCAL:2
	WAIT
ENDIF
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
		;第三勢力の士官なら、滅ぼした側が扱いを決定
		CALL TREAT_PRISONER(ARG:0, ARG:1, LOCAL:0)
	ENDIF
NEXT

;削除待ち状態のキャラを一斉削除
LOCAL:2 = CHARANUM
FOR LOCAL:0, 0, LOCAL:2
	LOCAL:1 = LOCAL:2 - LOCAL:0 - 1
	IF CFLAG:(LOCAL:1):特殊状態 == 3
		CALL DELETE_CHARA(LOCAL:1)
	ENDIF
NEXT

;ARG:0勢力に所属していたキャラを無所属にする
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == ARG:0
		CFLAG:(LOCAL:0):所属 = 0
	ENDIF
NEXT

;-------------------------------------------------
;ARG:2番のキャラの、勢力滅亡時の逃走判定(ARG:0=滅亡した勢力、ARG:1=滅亡させた勢力)
;ARG:3……0=戦闘中、1=滅亡時
;-------------------------------------------------
@JUDGE_ESCAPE(ARG:0, ARG:1, ARG:2, ARG:3)
IF ARG:3 == 0
	LOCAL:10 = 330
ELSE
	LOCAL:10 = 331
ENDIF

LOCAL:5 = GET_COUNTRY_BOSS(ARG:1)
LOCAL:6 = REL_LIKE:(ARG:2):(LOCAL:5)
LOCAL:7 = REL_HATE:(ARG:2):(LOCAL:5)

;主人公は逃走不可
IF ARG:2 == MASTER

;相手君主への好感度が300以上、敵対値が300未満なら大人しく捕まる
ELSEIF LOCAL:6 >= 300 && LOCAL:7 < 300
	IF ARG:1 == CFLAG:MASTER:所属
		CALL KOJO_EVENT(ARG:2, LOCAL:10)
		PRINTFORML %NAME:(ARG:2)%を捕らえました
	ENDIF

;滅亡させたのが主人公勢力で[恋慕][服従]がある
ELSEIF ARG:1 == CFLAG:MASTER:所属 && (TALENT:(ARG:2):恋慕 || TALENT:(ARG:2):服従)
	CALL KOJO_EVENT(ARG:2, LOCAL:10)
	PRINTFORML %NAME:(ARG:2)%を捕らえました

;上記の条件を満たさなければ逃走を試みる
ELSE
	;LOCAL:8は捕縛される確率
	;君主なら基礎確率がない
	IF ARG:2 == GET_COUNTRY_BOSS(ARG:0)
		LOCAL:8 = 0
	ELSE
		LOCAL:8 = 10
	ENDIF
	;三能力が高いほど逃げられやすくなる
	LOCAL:8 += (600 - (ABL:(ARG:2):武闘 * 2 + ABL:(ARG:2):防衛 * 2 + ABL:(ARG:2):知略 + ABL:(ARG:2):政治)) / 7

	;部隊に所属していなければ逃走確率上昇
	IF IS_FREE(ARG:2)
		LOCAL:8 -= 10
	ENDIF

	;イベントキャラは捕まらない
	IF IS_SP_CHARA(ARG:2)
		LOCAL:8 = 0
	ELSE
		LOCAL:8 = LIMIT(LOCAL:8, 30, 90)
	ENDIF

	IF RAND:100 < LOCAL:8
		IF ARG:1 == CFLAG:MASTER:所属
			CALL KOJO_EVENT(ARG:2, LOCAL:10)
			PRINTFORML %NAME:(ARG:2)%を捕らえました
		ENDIF
	ELSE
		PRINTFORML %NAME:(ARG:2)%は逃走したようです…
		;状態フラグを逃走状態に
		CFLAG:(ARG:2):特殊状態 = 1
		;勢力を滅亡させた君主のIDを保存
		CFLAG:(ARG:2):恨む君主 = COUNTRY_BOSS:(ARG:1)
		;旧君主のIDを保存
		CFLAG:(ARG:2):滅亡前の君主 = COUNTRY_BOSS:(ARG:0)
	ENDIF
ENDIF

;-------------------------------------------------
;ARG:0番のキャラの逃走先を決定する関数
;逃走先が見つからなければ0を返す
;-------------------------------------------------
@ESCAPE_TO(ARG:0)
#FUNCTION
#DIM 特殊勢力以外ない
;特殊勢力フラグ
特殊勢力以外ない = 1
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && !IS_SP_COUNTRY(LOCAL:0)
		特殊勢力以外ない = 0
		BREAK
	ENDIF
NEXT

LOCAL:8 = RAND:601 + RAND:601 + RAND:2 * 600 - 600
LOCAL:9 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	;通常勢力が存在する場合、ゴブリン、外来、野盗は除外
	SIF !特殊勢力以外ない && IS_SP_COUNTRY(LOCAL:0)
		CONTINUE
	LOCAL:5 = COUNTRY_BOSS:(LOCAL:0)
	LOCAL:6 = ID_TO_CHARA(LOCAL:5)
	IF LOCAL:6 >= 0 && LOCAL:5 != CFLAG:(ARG:0):恨む君主
		LOCAL:10 = REL_LIKE:(ARG:2):(LOCAL:6)
		LOCAL:11 = REL_HATE:(ARG:2):(LOCAL:6)
		LOCAL:12 = LOCAL:10 * 2 - LOCAL:11
		IF LOCAL:12 >= LOCAL:8
			LOCAL:(LOCAL:9 + 20) = LOCAL:0
			LOCAL:9 ++
		ENDIF
	ENDIF
NEXT
IF LOCAL:9 >= 1
	RETURNF LOCAL:(RAND:(LOCAL:9) + 20)
ENDIF
RETURNF 0

;-------------------------------------------------
;ARG:2番のキャラの、捕虜としての扱いを決める関数(ARG:0=捕獲されたキャラの勢力、ARG:1=捕獲した勢力)
;戦闘による捕獲の場合はARG:3に1を設定すること
;戻り値 0=登用 1=解放 2=投獄 3=処刑
;-------------------------------------------------
@TREAT_PRISONER(ARG:0, ARG:1, ARG:2, ARG:3 = 0)
;滅亡させたのが主人公の勢力
IF ARG:1 == CFLAG:MASTER:所属
	LOCAL:5 = 1
	;自動
	IF GROUPMATCH(CONFIG:301, 1, 2)
		CALL AUTO_PRISONER_TREATMENT(ARG:0, ARG:1, ARG:2)
	;手動
	ELSE
		CALL SELECT_PRISONER_TREATMENT(ARG:0, ARG:1, ARG:2)
	ENDIF

	RETURN RESULT
;滅亡させたのがCPU勢力
ELSE
	LOCAL:5 = GET_COUNTRY_BOSS(ARG:1)

	;外来勢力(ID:299)、ホフゴブリン、野盗なら必ず投獄
	IF IS_SP_COUNTRY(ARG:1)
		IF ARG:2 == MASTER
			CALL MASTER_CAPTURED(ARG:1)
			RETURN RESULT
		ENDIF
		CALL CAPTURE(ARG:2, ARG:1)
		;野盗勢力に囚われた場合はイベントに送る
		SIF COUNTRY_EVENT_ID:(ARG:1) == SP_COUNTRY_ID:(特殊勢力_野盗) && TRAIN_IS_SHOW_TEXT(ARG:2)
			CALL TREAT_PRISONER_BANDIT(ARG:2)
		RETURN 2

	;登用を再優先(主人公はここでは処理しない)
	ELSEIF ARG:2 != MASTER && JUDGE_ASSIGN(ARG:2, GET_COUNTRY_BOSS(ARG:1), GET_COUNTRY_BOSS(ARG:0))
		IF ARG:2 == GET_COUNTRY_BOSS(ARG:0)
			PRINTFORMW 君主である%NAME:(ARG:2)%は%NAME:(LOCAL:5)%の前に引きずり出された…
			PRINTFORMW %NAME:(ARG:2)%は頭を垂れて%NAME:(LOCAL:5)%に忠誠を誓いました
		;主人公以外で士官の場合
		ELSE
			IF CFLAG:(ARG:2):所属 == CFLAG:MASTER:所属
				SETCOLOR COLOR("警告")
				PRINTFORMW %NAME:(ARG:2)%が登用に応じたようです…
			ELSE
				SETCOLOR COLOR("注意")
				PRINTFORML %NAME:(ARG:2)%は登用に応じたようです
			ENDIF
			RESETCOLOR
		ENDIF
		CALL CHANGE_COUNTRY(ARG:2, ARG:1, 1)
		RETURN 0

	;登用できなかった場合
	ELSE
		LOCAL:6 = REL_LIKE:(LOCAL:5):(ARG:2)
		LOCAL:7 = REL_HATE:(LOCAL:5):(ARG:2)

		;主人公の場合はここで登用処理	
		IF ARG:2 == MASTER
			CALL MASTER_CAPTURED(ARG:1)
			;登用に応じた
			IF RESULT == 1
				CALL CHANGE_COUNTRY(MASTER, ARG:1, 1)
				RETURN 0
			;解放された
			ELSEIF RESULT == 2
				CALL CAPTURE(MASTER, 0, 1)
				RETURN 1
			;投獄された
			ELSEIF RESULT == 3
				CALL CAPTURE(MASTER, ARG:1)
				RETURN 2
			;処刑
			ELSE
				FLAG:強制エンドフラグ = 1
				RETURN 3
			ENDIF

		;好感度が低く敵対値が特に高い場合は処刑(処刑禁止の設定なら処刑しない)
		ELSEIF LOCAL:6 < 100 && LOCAL:7 >= 800 && CONFIG:304 == 0
			;主人公勢力が滅亡した場合
			IF ARG:0 == CFLAG:MASTER:所属
				;主人公以外で君主の場合
				IF ARG:2 == GET_COUNTRY_BOSS(ARG:0)
					PRINTFORMW 君主である%NAME:(ARG:2)%は%NAME:(LOCAL:5)%の前に引きずり出された…
					PRINTFORMW %NAME:(ARG:2)%にあまり良い感情を持たない%NAME:(LOCAL:5)%は、躊躇なく部下に%NAME:(ARG:2)%の処刑を命じた
					PRINTFORMW ………………
					CALL EXECUTION(ARG:2)
				;主人公以外で士官の場合
				ELSE
					IF CFLAG:(ARG:2):捕虜先 == ARG:0 && ARG:0 >= 0
						LOCALS:0 = %NAME:GET_COUNTRY_BOSS(CFLAG:(ARG:2):捕虜先)%
						PRINTFORMW 捕虜となっていた%LOCALS:0%勢力の%NAME:(ARG:2)%は処刑されたようです…
						CALL EXECUTION(ARG:2)
					ELSE
						PRINTFORMW %NAME:(ARG:2)%は処刑されたようです…
						CALL EXECUTION(ARG:2)
					ENDIF
				ENDIF
			ELSE
				SETCOLOR COLOR("注意")
				PRINTFORMW %NAME:(ARG:2)%が処刑されたという報告が入っています
				CALL EXECUTION(ARG:2)
				RESETCOLOR
			ENDIF
			;状態フラグを死亡状態に
			CFLAG:(ARG:2):特殊状態 = 2
			RETURN 3

		;戦闘による捕縛ならば投獄
		ELSEIF ARG:3
			CALL CAPTURE(ARG:2, ARG:1)

		;それ以外は解放
		ELSE
			;主人公勢力が滅亡した場合
			IF ARG:0 == CFLAG:MASTER:所属
				;主人公以外で君主の場合
				IF ARG:2 == GET_COUNTRY_BOSS(ARG:0)
					PRINTFORMW 君主である%NAME:(ARG:2)%は%NAME:(LOCAL:5)%の前に引きずり出された…
					PRINTFORMW だが%NAME:(LOCAL:5)%に%NAME:(ARG:2)%の首を獲る気はないようだ
					PRINTFORMW %NAME:(ARG:2)%は解放されました
				;主人公以外で士官の場合
				ELSE
					IF CFLAG:(ARG:2):捕虜先 == ARG:0 && ARG:0 >= 0
						LOCALS:0 = %NAME:GET_COUNTRY_BOSS(CFLAG:(ARG:2):捕虜先)%
						PRINTFORMW 捕虜となっていた%LOCALS:0%勢力の%NAME:(ARG:2)%は解放されたようです
					ELSE
						PRINTFORMW %NAME:(ARG:2)%は解放されたようです
					ENDIF
				ENDIF
			ENDIF

			IF CFLAG:(ARG:2):捕虜先 != 0
				;捕虜設定を解除
				CALL CAPTURE(ARG:2, 0)
			ELSE
				;状態フラグを逃走状態に
				CFLAG:(ARG:2):特殊状態 = 1
				;勢力を滅亡させた君主のIDを設定
				CFLAG:(ARG:2):恨む君主 = COUNTRY_BOSS:(ARG:1)
				;旧君主のIDを設定
				CFLAG:(ARG:2):滅亡前の君主 = COUNTRY_BOSS:(ARG:0)
				;好感度を上昇、敵対値を減少
				CALL CHANGE_RELATION_O_TO_C(ARG:2, ARG:1, 80, -80)
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;ARG:2番のキャラの、捕虜としての扱いを手動で決める関数(ARG:0=捕獲されたキャラの勢力、ARG:1=捕獲した勢力)
;戦闘による捕獲の場合はARG:3に1を設定すること
;戻り値 0=登用 1=解放 2=投獄 3=処刑
;-------------------------------------------------
@SELECT_PRISONER_TREATMENT(対象勢力, 捕獲勢力, 対象, 戦闘フラグ)
#DIM 対象勢力
#DIM 捕獲勢力
#DIM 対象
#DIM 戦闘フラグ
#DIM 拒否フラグ

拒否フラグ = 0


$RESELECT_LOOP
;捕虜の場合
IF IS_COUNTRY(対象勢力) && CFLAG:対象:捕虜先 == 対象勢力
	LOCAL:1 = GET_COUNTRY_BOSS(対象勢力)
	LOCAL:2 = GET_COUNTRY_BOSS(CFLAG:対象:所属)
	PRINTFORML %LOCAL:1 != -1 ? ANAME(LOCAL:1) + "の" # ""%捕虜となっていた%LOCAL:2 != -1 ? ANAME(LOCAL:2) + "所属の" # ""%%ANAME(対象)%を保護しました
	PRINTFORML %ANAME(対象)%の処遇を決めて下さい
ELSE
	PRINTFORML %ANAME(対象)%の処遇を決めて下さい
ENDIF
CALL SHOW_SIMPLE_INFO(対象)
IF !拒否フラグ
	PRINTL [0] 登用
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [0] 登用
	PRINTL 
	RESETCOLOR
ENDIF
PRINTL [1] 解放
PRINTL [2] 投獄
PRINTL [3] 処刑
PRINTL [4] 軟禁

$INPUT_LOOP
INPUT



RETURN RESULT

IF RESULT == 0 && !拒否フラグ
	拒否フラグ = 1
	;登用に応じるかどうかを判定
	IF JUDGE_ASSIGN(対象, GET_COUNTRY_BOSS(捕獲勢力), GET_COUNTRY_BOSS(対象勢力))
		CALL KOJO_EVENT(対象, 332)
		PRINTFORMW %ANAME(対象)%を登用しました
		CALL CHANGE_COUNTRY(対象, 捕獲勢力, 1)
		;好感度を上昇、敵対値を減少
		CALL CHANGE_RELATION_O_TO_C(対象, 捕獲勢力, 50, -50)
	ELSE
		CALL KOJO_EVENT(対象, 333)
		PRINTFORMW %ANAME(対象)%は応じる気はないようです…
		;好感度を上昇、敵対値を減少
		CALL CHANGE_RELATION_O_TO_C(対象, 捕獲勢力, 50, -50)
		GOTO RESELECT_LOOP
	ENDIF
	RETURN 0
ELSEIF RESULT == 1
	CALL KOJO_EVENT(対象, 334)
	PRINTFORMW %ANAME(対象)%を解放しました
	IF CFLAG:対象:捕虜先 != 0
		;捕虜設定を解除
		CALL CAPTURE(対象, 0)
	ELSE
		;状態フラグを逃走状態に
		CFLAG:(対象):特殊状態 = 1
		;勢力を滅亡させた君主のIDを保存
		CFLAG:(対象):恨む君主 = COUNTRY_BOSS:(捕獲勢力)
		;旧君主のIDを保存
		CFLAG:(対象):滅亡前の君主 = COUNTRY_BOSS:(対象勢力)
	ENDIF

	;好感度を上昇、敵対値を減少
	CALL CHANGE_RELATION_O_TO_C(対象, 捕獲勢力, 50, -50)
	RETURN 1

ELSEIF RESULT == 2
	CALL KOJO_EVENT(対象, 335)
	PRINTFORMW %ANAME(対象)%を投獄しました
	CALL CAPTURE(対象, 捕獲勢力)
	RETURN 2

ELSEIF RESULT == 4
	CALL KOJO_EVENT(対象, 335)
	PRINTFORMW %ANAME(対象)%を軟禁しました
	CALL CAPTURE(対象, 捕獲勢力)
	CFLAG:対象:監禁状態 = 1
	RETURN 4

ELSEIF RESULT == 3
	CALL KOJO_EVENT(対象, 336)
	PRINTFORMW ……………………
	PRINTFORMW ……
	PRINTFORMW %ANAME(対象)%を処刑しました
	CALL EXECUTION(対象)
	CALL CHANGE_COUNTRY(対象, 0, 1, 1)
	RETURN 3
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;CONFIG:301が立っているときの、プレイヤー勢力による捕虜の自動処遇
;ARG:2番のキャラの、捕虜としての扱いを手動で決める関数(ARG:0=捕獲されたキャラの勢力、ARG:1=捕獲した勢力)
;戦闘による捕獲の場合はARG:3に1を設定すること
;戻り値 0=登用 1=解放 2=投獄 3=処刑
;-------------------------------------------------
@AUTO_PRISONER_TREATMENT(対象勢力, 捕獲勢力, 対象)
#DIM 対象勢力
#DIM 捕獲勢力
#DIM 対象

;登用に応じるかどうかを判定
IF JUDGE_ASSIGN(対象, GET_COUNTRY_BOSS(捕獲勢力), GET_COUNTRY_BOSS(対象勢力))
	CALL KOJO_EVENT(対象, 332)
	PRINTFORMW %ANAME(対象)%を登用しました
	CALL CHANGE_COUNTRY(対象, 捕獲勢力, 1)
	;好感度を上昇、敵対値を減少
	CALL CHANGE_RELATION_O_TO_C(対象, 捕獲勢力, 50, -50)
	RETURN 0
ENDIF

CALL KOJO_EVENT(対象, 333)
PRINTFORMW %ANAME(対象)%は登用に応じる気はないようです…
;好感度を上昇、敵対値を減少
CALL CHANGE_RELATION_O_TO_C(対象, 捕獲勢力, 50, -50)

IF CONFIG:301 == 1
	CALL KOJO_EVENT(対象, 334)
	PRINTFORMW %ANAME(対象)%を解放しました
	IF CFLAG:対象:捕虜先 != 0
		;捕虜設定を解除
		CALL CAPTURE(対象, 0)
	ELSE
		;状態フラグを逃走状態に
		CFLAG:(対象):特殊状態 = 1
		;勢力を滅亡させた君主のIDを保存
		CFLAG:(対象):恨む君主 = COUNTRY_BOSS:(捕獲勢力)
		;旧君主のIDを保存
		CFLAG:(対象):滅亡前の君主 = COUNTRY_BOSS:(対象勢力)
	ENDIF

	;好感度を上昇、敵対値を減少
	CALL CHANGE_RELATION_O_TO_C(対象, 捕獲勢力, 50, -50)
	RETURN 1
ELSEIF CONFIG:301 == 2
	CALL KOJO_EVENT(対象, 335)
	PRINTFORMW %ANAME(対象)%を投獄しました
	CALL CAPTURE(対象, 捕獲勢力)
	RETURN 2
ENDIF

;-------------------------------------------------
;主人公がARG:0勢力に捕らえられた場合の処理
;戻り値 0=処刑 1=部下になる 2=解放される 3=投獄される
;-------------------------------------------------
@MASTER_CAPTURED(ARG:0)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = REL_LIKE:(LOCAL:5):MASTER
LOCAL:7 = REL_HATE:(LOCAL:5):MASTER

FLAG:逆調教メイン調教者 = 0

TRYCCALLFORM MASTER_CAPTURED_%SP_COUNTRY_NAME_ENG:SP_COUNTRY_TO_CONST(ARG:0)%(ARG:0)
	RETURN RESULT
CATCH
ENDCATCH


IF CFLAG:MASTER:捕虜先 != 0
	PRINTFORMW 捕虜として捕えられていた%ANAME(MASTER)%は、%NAME:(LOCAL:5)%の前に引き立てられた…
ELSE
	PRINTFORMW 捕らえられた%ANAME(MASTER)%は%NAME:(LOCAL:5)%の前に引きずり出された…
ENDIF

;王からの好感度が低く敵対値が特に高い場合で、王の好感度・依存度が共に500未満なら選択肢を表示せずに処刑
IF LOCAL:6 < 100 && LOCAL:7 >= 800 && CFLAG:(LOCAL:5):2 < 500 && CFLAG:(LOCAL:5):3 < 500
	PRINTFORMW %ANAME(MASTER)%にあまり良い感情を持たない%NAME:(LOCAL:5)%は、躊躇なく部下に%ANAME(MASTER)%の処刑を命じた
	LOCAL:8 = 1
	CALL EXECUTION(MASTER)
;捕虜の場合
ELSEIF CFLAG:MASTER:捕虜先 != 0
	PRINTFORML %NAME:(LOCAL:5)%は%ANAME(MASTER)%に対し、力を貸す気はないかと持ち掛けてきた
	PRINTL [0] 私で宜しければ喜んで
	PRINTL [1] 解放して欲しい

	$INPUT_LOOP2
	INPUT

	IF RESULT == 0
		PRINTFORML %CALLNAME:MASTER%は%NAME:(LOCAL:5)%の家臣となりました
		PRINTFORMW 以降は%NAME:(LOCAL:5)%勢力として行動します
		RETURN 1
	ELSEIF RESULT == 1
		PRINTFORMW %ANAME(MASTER)%が仕官を拒むと、%NAME:(LOCAL:5)%は少し残念そうにしながらも%ANAME(MASTER)%を解放した
		RETURN 2
	ENDIF
	GOTO INPUT_LOOP2

ELSE
	PRINTFORML 曰く、忠誠を誓い力を貸すならば命は助ける、とのことだが
	PRINTL [0] 寛大な処置に感謝します
	PRINTL [1] ……斬れ
	LOCAL:8 = 0

	$INPUT_LOOP
	INPUT

	IF RESULT == 0
		PRINTFORML %CALLNAME:MASTER%は%NAME:(LOCAL:5)%の家臣となりました
		PRINTFORMW 以降は%NAME:(LOCAL:5)%勢力として行動します
		RETURN 1
	ELSEIF RESULT != 1
		GOTO INPUT_LOOP
	ENDIF
ENDIF

;以下は処刑が仮決定したときの処理

;王の好感度が500以上かつ主導度Ｕ100以上または<サド>
IF !LOCAL:8 && ((CFLAG:(LOCAL:5):2 >= 500 && (ABL:(LOCAL:5):主導度Ｕ >= 100 || GETBIT(TALENT:(LOCAL:5):淫乱系, 素質_淫乱_サド))))
	PRINTFORMW %ANAME(MASTER)%が仕官を拒むと、%NAME:(LOCAL:5)%は不敵な笑みを浮かべ、%ANAME(MASTER)%を投獄するように命じた…
	SETCOLOR COLOR("注意")
	PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%の捕虜となりました
	RESETCOLOR
	FLAG:逆調教メイン調教者 = GET_ID(LOCAL:5)
	RETURN 3
ENDIF

LOCAL:6 = -1
LOCAL:7 = 0
FOR LOCAL:0, 0, CHARANUM
	;ARG:0勢力の中に主人公への好感度500以上かつ主導度Ｕ100以上または<サド>のキャラがいる場合
	IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == 0 && CFLAG:(LOCAL:0):2 >= 500 && (ABL:(LOCAL:0):主導度Ｕ >= 100 || GETBIT(TALENT:(LOCAL:0):淫乱系, 素質_淫乱_サド))
		IF CFLAG:(LOCAL:0):2 > LOCAL:7
			;条件を満たすキャラのうち、好感度が最大のキャラを探索
			LOCAL:6 = LOCAL:0
			LOCAL:7 = CFLAG:(LOCAL:0):2
		ENDIF
	ENDIF
NEXT

;条件を満たすキャラが見つかった場合
IF LOCAL:6 >= 0
	IF LOCAL:8
		PRINTFORML すると、それを見ていた%NAME:(LOCAL:6)%が%NAME:(LOCAL:5)%に対して、自分に%NAME:MASTER%の処置を任せてほしいと申し出た
	ELSE
		PRINTFORML %ANAME(MASTER)%が仕官を拒むと、それを見ていた%NAME:(LOCAL:6)%が%NAME:(LOCAL:5)%に対して、
		PRINTFORMW 自分に説得を任せてほしいと申し出た
	ENDIF
	PRINTFORMW %NAME:(LOCAL:5)%は%NAME:(LOCAL:6)%の言葉を聞き入れて、%ANAME(MASTER)%の処遇を%NAME:(LOCAL:6)%に任せた…
	SETCOLOR COLOR("注意")
	PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%の捕虜となりました
	RESETCOLOR
	FLAG:逆調教メイン調教者 = GET_ID(LOCAL:6)
	RETURN 3
ENDIF

;王の好感度 or 依存度が1500以上
IF !LOCAL:8 && CFLAG:(LOCAL:5):2 >= 500 || CFLAG:(LOCAL:5):3 >= 500
	PRINTFORMW %NAME:(LOCAL:5)%には%ANAME(MASTER)%を斬ることが出来ないようだ…
	PRINTFORMW %NAME:(LOCAL:5)%は部下に対し、%ANAME(MASTER)%を投獄するように命じた
	SETCOLOR COLOR("注意")
	PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%の捕虜となりました
	RESETCOLOR
	RETURN 3
ENDIF

LOCAL:6 = -1
LOCAL:7 = 0
FOR LOCAL:0, 0, CHARANUM
	;ARG:0勢力の中に主人公への好感度 or 依存度が1500以上のキャラがいる場合
	IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == 0 && (CFLAG:(LOCAL:0):2 >= 1500 || CFLAG:(LOCAL:0):3 >= 1500)
		IF CFLAG:(LOCAL:0):2 > LOCAL:7
			;条件を満たすキャラのうち、好感度が最大のキャラを探索
			LOCAL:6 = LOCAL:0
			LOCAL:7 = CFLAG:(LOCAL:0):2
		ENDIF
	ENDIF
NEXT

;条件を満たすキャラが見つかった場合
IF LOCAL:6 >= 0
	IF !LOCAL:8
		PRINTFORMW %ANAME(MASTER)%が仕官を拒むと、%NAME:(LOCAL:5)%は残念そうにしながらも部下に%ANAME(MASTER)%の処刑を命じた
	ENDIF
	PRINTFORMW すると、それを見ていた%ANAME(LOCAL:6)%が%NAME:(LOCAL:5)%に駆け寄り、必死になって%ANAME(MASTER)%の助命を嘆願した…
	PRINTFORML %NAME:(LOCAL:6)%の勢いに押された%NAME:(LOCAL:5)%は、
	PRINTFORMW どうやら%ANAME(MASTER)%の首を取らずに捕虜として投獄しておくことにしたようだ
	SETCOLOR COLOR("注意")
	PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%の捕虜となりました
	RESETCOLOR
	RETURN 3
ENDIF

;3能力に応じた確率で
IF !LOCAL:8 && ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 >= RAND:300
	PRINTFORMW %NAME:(LOCAL:5)%は、%ANAME(MASTER)%のような人材を斬るのを惜しんだようだ
	PRINTFORMW %NAME:(LOCAL:5)%は部下に対し、%ANAME(MASTER)%を投獄するように命じた
	SETCOLOR COLOR("注意")
	PRINTFORMW %ANAME(MASTER)%は%NAME:(LOCAL:5)%の捕虜となりました
	RESETCOLOR
	RETURN 3
ENDIF

;処刑が本決定
IF !LOCAL:8
	PRINTFORMW %ANAME(MASTER)%が仕官を拒むと、%NAME:(LOCAL:5)%は残念そうにしながらも部下に%ANAME(MASTER)%の処刑を命じた
	CALL EXECUTION(MASTER)
ENDIF
RETURN 0

;-------------------------------------------------
;ARG:0番のキャラが登用に応じるかどうかを判定(ARG:1=登用側の君主のキャラ番号、ARG:2=ARG:0の君主(or 旧君主)のキャラ番号)
;戻り値 0=拒否 1=応じる
;-------------------------------------------------
@JUDGE_ASSIGN(ARG:0, ARG:1, ARG:2)
#FUNCTION
LOCAL:5 = GET_COUNTRY_FROM_BOSS_ID(GET_ID(ARG:1))
LOCAL:6 = GET_COUNTRY_FROM_BOSS_ID(GET_ID(ARG:2))

LOCAL:10 = 200

;特殊勢力所属なら1/2で判定を無視して応じる
SIF RAND:2 && IS_SP_COUNTRY(LOCAL:6)
RETURNF 1

;登用側の君主への好感度と敵対度の補正
LOCAL:10 += REL_LIKE:(ARG:0):(ARG:1) * 2 - REL_HATE:(ARG:0):(ARG:1)

;主人公が登用側の勢力に所属している場合
IF CFLAG:MASTER:所属 == LOCAL:5
	;好感度・依存度・従属度による補正
	LOCAL:10 += (CFLAG:(ARG:0):2 + MAX(CFLAG:(ARG:0):3, 0) + MAX(CFLAG:(ARG:0):4, 0)) / 20
	LOCAL:10 += (TALENT:(ARG:0):恋慕 != 0) * 250
	LOCAL:10 += (TALENT:(ARG:0):親愛 != 0) * 500
	LOCAL:10 += (TALENT:(ARG:0):服従 != 0) * 250
	LOCAL:10 += (TALENT:(ARG:0):隷属 != 0) * 500
	LOCAL:10 += (TALENT:(ARG:0):親友 != 0) * 150
	LOCAL:10 += (TALENT:(ARG:0):恋人 != 0) * 80
	LOCAL:10 += (TALENT:(ARG:0):烙印 != 0) * 80
ENDIF

;自身が君主の場合
IF ARG:0 == ARG:2
	LOCAL:10 -= 200

;君主が不在／死亡した場合
ELSEIF ARG:2 < 0 || CFLAG:(ARG:2):特殊状態 == 2
	LOCAL:10 += 200

;君主が存命中で登用側の勢力に仕えている場合
ELSEIF CFLAG:(ARG:2):所属 == LOCAL:5
	;現在の君主への好感度と敵対度の補正(＋補正)
	LOCAL:10 += REL_LIKE:(ARG:0):(ARG:2) * 2 - REL_HATE:(ARG:0):(ARG:2)

	;主人公がARG:0の君主であるか、主人公がARG:0と同じ勢力に所属している場合
	IF ARG:2 == MASTER || (LOCAL:6 >= 1 && CFLAG:MASTER:所属 == LOCAL:6)
		;好感度・依存度・従属度による補正(＋補正)
		LOCAL:10 += (CFLAG:(ARG:0):2 + MAX(CFLAG:(ARG:0):3, 0) + MAX(CFLAG:(ARG:0):4, 0)) / 20
		LOCAL:10 += (TALENT:(ARG:0):恋慕 != 0) * 250
		LOCAL:10 += (TALENT:(ARG:0):親愛 != 0) * 500
		LOCAL:10 += (TALENT:(ARG:0):服従 != 0) * 250
		LOCAL:10 += (TALENT:(ARG:0):隷属 != 0) * 500
		LOCAL:10 += (TALENT:(ARG:0):親友 != 0) * 150
		LOCAL:10 += (TALENT:(ARG:0):恋人 != 0) * 80
		LOCAL:10 += (TALENT:(ARG:0):烙印 != 0) * 80
	ENDIF

	;君主の素質による補正
	LOCAL:10 += (TALENT:(ARG:2):謎の魅力 != 0) * 80
	LOCAL:10 += (TALENT:(ARG:2):魅惑 != 0) * 40
	LOCAL:10 += (TALENT:(ARG:2):人気 != 0) * 40

;君主が存命中で登用側の勢力に仕えていない場合
ELSE
	;現在の君主への好感度と敵対度の補正(－補正)
	LOCAL:10 -= REL_LIKE:(ARG:0):(ARG:2) * 2 - REL_HATE:(ARG:0):(ARG:2)

	;主人公がARG:0の君主であるか、主人公がARG:0と同じ勢力に所属している場合
	IF ARG:2 == MASTER || (LOCAL:6 >= 1 && CFLAG:MASTER:所属 == LOCAL:6)
		;好感度・依存度・従属度による補正(－補正)
		LOCAL:10 -= (CFLAG:(ARG:0):2 + MAX(CFLAG:(ARG:0):3, 0) + MAX(CFLAG:(ARG:0):4, 0)) / 20
		LOCAL:10 -= (TALENT:(ARG:0):恋慕 != 0) * 250
		LOCAL:10 -= (TALENT:(ARG:0):親愛 != 0) * 500
		LOCAL:10 -= (TALENT:(ARG:0):服従 != 0) * 250
		LOCAL:10 -= (TALENT:(ARG:0):隷属 != 0) * 500
		LOCAL:10 -= (TALENT:(ARG:0):親友 != 0) * 150
		LOCAL:10 -= (TALENT:(ARG:0):恋人 != 0) * 80
		LOCAL:10 -= (TALENT:(ARG:0):烙印 != 0) * 80
	ENDIF

	;君主の素質による補正
	LOCAL:10 -= (TALENT:(ARG:2):謎の魅力 != 0) * 80
	LOCAL:10 -= (TALENT:(ARG:2):魅惑 != 0) * 40
	LOCAL:10 -= (TALENT:(ARG:2):人気 != 0) * 40
ENDIF

;本人の素質による補正
LOCAL:10 += (TALENT:(ARG:0):臆病 != 0) * 50
LOCAL:10 -= (TALENT:(ARG:0):反抗的 != 0) * 70
LOCAL:10 -= (TALENT:(ARG:0):気丈 != 0) * 50
LOCAL:10 -= (TALENT:(ARG:0):プライド高い != 0) * 100
LOCAL:10 += (TALENT:(ARG:0):プライド低い != 0) * 100
LOCAL:10 -= (TALENT:(ARG:0):自制心 != 0) * 70
LOCAL:10 -= (TALENT:(ARG:0):献身的 != 0) * 50

;登用側の君主の素質による補正
LOCAL:10 += (TALENT:(ARG:1):謎の魅力 != 0) * 100
LOCAL:10 += (TALENT:(ARG:1):魅惑 != 0) * 60
LOCAL:10 += (TALENT:(ARG:1):人気 != 0) * 60

;判定値が0以上なら登用に応じる
IF LOCAL:10 >= 0
	RETURNF 1
ENDIF
RETURNF 0
