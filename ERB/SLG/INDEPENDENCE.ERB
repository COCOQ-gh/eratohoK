;独立・クーデターの処理

;-------------------------------------------------
;独立の処理
;-------------------------------------------------
@SHOP_INDEPENDENCE
;選択したキャラのリスト
#DIM SELECT_LIST, 1000
#DIM FIRST_LINE
VARSET LOCAL, 0
VARSET SELECT_LIST, 0

LOCAL:4 = CFLAG:MASTER:1

DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE
PRINTL 旗揚げする都市を選んで下さい
PRINTL 現在所属している勢力の都市のみ指定可能です
DRAWLINE
PRINTBUTTON " 0[キャンセル]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP_CITY
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN
ELSEIF RESULT >= 1001 && RESULT < 1100
	LOCAL:5 = RESULT - 1000
	;指定された都市が存在し、主人公勢力の支配下にある場合
	IF GET_RELAYNAME(LOCAL:5) != "無名" && CITY_TYPE:(LOCAL:5) == 0 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:1
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(LOCAL:5)%で旗揚げします
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF

DRAWLINE
PRINTFORML [恋慕][親友][服従]のいずれかを持つ士官を%ANAME(MASTER)%の勢力に誘うことができます
PRINTFORML 新たな勢力の士官として加えるキャラを選択して下さい
DRAWLINE

LOCAL:7 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)

;ページ数の計算
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 != MASTER && LOCAL:0 != LOCAL:7 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):親友 || TALENT:(LOCAL:0):服従)
		LOCAL:10 ++
	ENDIF
NEXT
LOCAL:11 = LOCAL:10 / 44 + 1
LOCAL:12 = 1
FIRST_LINE = LINECOUNT
$SHOW_LOOP_CHARA

LOCAL:13 = (LOCAL:12 - 1) * 44
LOCAL:14 = LOCAL:12 * 44
LOCAL:15 = 0
LOCAL:16 = 0
LOCAL:17 = 5
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 != MASTER && LOCAL:0 != LOCAL:7 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):親友 || TALENT:(LOCAL:0):服従)
		IF LOCAL:15 >= LOCAL:13 && LOCAL:15 < LOCAL:14
			IF LOCAL:16 % 2 != 0
				PRINT 　
			ELSEIF LOCAL:16 >= 1
				PRINTL 
				LOCAL:17 ++
			ENDIF
			IF SELECT_LIST:(LOCAL:0)
				SETCOLOR COLOR("選択中")
			ENDIF
			PRINTFORM [{NO:(LOCAL:0) + 99, 3}] %ANAME(LOCAL:0), 10, LEFT%
			PRINTFORM <{ABL:(LOCAL:0):武闘, 3}/{ABL:(LOCAL:0):知略, 3}/{ABL:(LOCAL:0):政治, 3}/{ABL:(LOCAL:0):歌唱, 3}/{ABL:(LOCAL:0):料理, 3}>
			RESETCOLOR
			LOCAL:16 ++
		ENDIF
		LOCAL:15 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE

IF LOCAL:11 >= 2
	LOCAL:17 += 2

	IF LOCAL:12 > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = page{LOCAL:12}/{LOCAL:11}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:12 < LOCAL:11
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF

PRINTL [  0] 決定
PRINTL [  1] キャンセル

REDRAW 0

$INPUT_LOOP_CHARA
INPUT

IF RESULT == 0
	REDRAW 1
ELSEIF RESULT == 1
	REDRAW 1
	RETURN
ELSEIF RESULT == 8 && LOCAL:12 > 1
	LOCAL:12 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP_CHARA
ELSEIF RESULT == 9 && LOCAL:12 < LOCAL:11
	LOCAL:12 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP_CHARA
ELSE
	LOCAL:10 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:10 >= 0 && LOCAL:10 != MASTER && LOCAL:10 != LOCAL:7 && CFLAG:(LOCAL:10):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:10):恋慕 || TALENT:(LOCAL:10):親友 || TALENT:(LOCAL:10):服従)
		SELECT_LIST:(LOCAL:10) = !SELECT_LIST:(LOCAL:10)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP_CHARA
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP_CHARA
ENDIF

;●独立に伴う各種処理
FOR LOCAL:0, 1, MAX_COUNTRY
	IF !IS_COUNTRY(LOCAL:0)
		;士官を新勢力に移す
		FOR LOCAL:1, 0, CHARANUM
			IF LOCAL:1 == MASTER || SELECT_LIST:(LOCAL:1)
				CFLAG:(LOCAL:1):1 = LOCAL:0
				;部隊に配属されている場合強制解除
				CALL FORCE_FREE(LOCAL:1)
			ENDIF
		NEXT

		CITY_OWNER:(LOCAL:5) = LOCAL:0
		COUNTRY_BOSS:(LOCAL:0) = GET_ID(MASTER)
		COUNTRY_COLOR:(LOCAL:0) = 0xD0D0D0

		;旧勢力の部隊を解散
		CALL CLEAR_ALL_UNIT(LOCAL:4, 1)

		;兵力の分配
		COUNTRY_SOLDIER:(LOCAL:4) += CITY_SOLDIER:(LOCAL:5)
		CITY_SOLDIER:(LOCAL:5) = 0
		COUNTRY_SOLDIER:(LOCAL:0) = COUNTRY_SOLDIER:(LOCAL:4) / (GET_OWN_CITY(LOCAL:4) + 1)
		COUNTRY_SOLDIER:(LOCAL:4) -= COUNTRY_SOLDIER:(LOCAL:0)

		;防衛兵力の初期設定(兵数の2/5弱、最低500)
		IF COUNTRY_SOLDIER:(LOCAL:0) < 500
			CITY_SOLDIER:(LOCAL:5) = 500
			COUNTRY_SOLDIER:(LOCAL:0) = 0
		ELSE
			CITY_SOLDIER:(LOCAL:5) = (COUNTRY_SOLDIER:(LOCAL:0) / 1250 + 1) * 500
			COUNTRY_SOLDIER:(LOCAL:0) -= CITY_SOLDIER:(LOCAL:5)
		ENDIF

		BREAK
	ENDIF
NEXT

DRAWLINE
PRINTL 新しい勢力の色を選んで下さい
DRAWLINE
CALL CHANGE_COUNTRY_COLOR(CFLAG:MASTER:1, 1)

SETCOLOR COLOR("注意")
PRINTL 
PRINTFORMW %ANAME(MASTER)%は%GET_RELAYNAME(LOCAL:5)%を占拠し独立しました！
PRINTL 
RESETCOLOR

;元の君主との関係を大幅に悪化させる
CALL CHANGE_RELATION_O_TO_C(LOCAL:7, CFLAG:MASTER:1, -200, 500)

;-------------------------------------------------
;クーデターの処理
;-------------------------------------------------
@SHOP_COAP
#DIM FIRST_LINE
VARSET LOCAL, 0

DRAWLINE
IF GET_COUNTRY_BOSS(CFLAG:MASTER:1) != MASTER
	PRINTFORML 現在の君主を捕らえて新たな君主を立てます
	PRINTFORML もし現在の君主が[親愛][隷属]を所持しているならば、平和裏に代替わりできるでしょう
	PRINTFORML 新君主は主人公、または[恋慕][親友][服従]のいずれかを持つキャラの中から選ぶことができます
	PRINTFORML 本当にクーデターを実行しますか？
ELSE
	PRINTFORML 君主の座を別の誰かに譲ります
	PRINTFORML 新君主は主人公、または[恋慕][親友][服従]のいずれかを持つキャラの中から選ぶことができます
	PRINTFORML 本当に国譲りを実行しますか？
ENDIF
CALL ASK_YN
IF RESULT == 1
	RETURN
ENDIF

;全部隊の解散
CALL CLEAR_ALL_UNIT(CFLAG:MASTER:1, 1)

LOCAL:5 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
LOCAL:6 = TALENT:(LOCAL:5):恋慕 || TALENT:(LOCAL:5):親友 || TALENT:(LOCAL:5):服従

;旧君主が部隊に配属されている場合強制解除
CALL FORCE_FREE(LOCAL:5)


IF LOCAL:5 == MASTER
	CALL COLORPRINTFORM(@"%ANAME(MASTER)%は、代替わりにすることにした……", COLOR("注意"), "W")
;君主が代替わりに同意
ELSEIF TALENT:(LOCAL:5):親愛 || TALENT:(LOCAL:5):隷属
	CALL COLORPRINTFORM(@"%ANAME(MASTER)%の説得で、%ANAME(LOCAL:5)%は、代替わりに同意した……", COLOR("注意"), "W")
;君主を確保
ELSEIF LOCAL:6 || GET_OWN_CITY(CFLAG:MASTER:1) <= 1 || RAND:250 < MAX(ABL:(LOCAL:5):武闘, ABL:(LOCAL:5):知略, ABL:(LOCAL:5):政治) + 30
	LOCAL:40 = 0

	SETCOLOR COLOR("注意")
	PRINTFORMW 現君主である%NAME:(LOCAL:5)%を確保することに成功しました！
	RESETCOLOR
	IF LOCAL:6
		PRINTFORML %ANAME(MASTER)%に裏切られるとは露にも思っていなかった%ANAME(LOCAL:5)%は
		PRINTFORML 何が起きたのかわからず呆然としている…
	ELSE
		PRINTFORML %ANAME(MASTER)%に裏切られた%ANAME(LOCAL:5)%は怒りと悔しさのあまり俯いて歯軋りをしている…
	ENDIF

	$RESELECT_LOOP

	PRINTFORML 捕えた%NAME:(LOCAL:5)%の処遇を決めて下さい
	IF LOCAL:7
		PRINTBUTTON " 0[登用]", 0
		PRINTL 
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTL  0[登用]
		RESETCOLOR
	ENDIF
	PRINTBUTTON " 1[解放]", 1
	PRINTL 
	PRINTBUTTON " 2[投獄]", 2
	PRINTL 
	PRINTBUTTON " 3[処刑]", 3
	PRINTL 
	PRINTBUTTON " 4[軟禁]", 4
	PRINTL 

	$INPUT_LOOP
	INPUT

	IF RESULT == 0 && LOCAL:7
		LOCAL:5 = 0
		;登用に応じるかどうかを判定
		IF JUDGE_ASSIGN(LOCAL:5, MASTER, LOCAL:5)
			;CALL KOJO_EVENT(ARG:2, 332)
			PRINTFORMW %NAME:(LOCAL:5)%を登用しました
		ELSE
			;CALL KOJO_EVENT(ARG:2, 333)
			PRINTFORMW %NAME:(LOCAL:5)%は応じる気はないようです…
			GOTO RESELECT_LOOP
		ENDIF

	ELSEIF RESULT == 1
		;CALL KOJO_EVENT(ARG:2, 334)
		PRINTFORMW %NAME:(LOCAL:5)%を解放しました
		;所属を無所属に
		CFLAG:(LOCAL:5):1 = 0
		;状態フラグを逃走状態に
		CFLAG:(LOCAL:5):特殊状態フラグ = 1
		;旧君主のIDを保存
		CFLAG:(LOCAL:5):滅亡前の君主 = GET_ID(LOCAL:5)
		;後始末が必要なのでフラグを立てておく
		LOCAL:30 = 1

	ELSEIF RESULT == 2
		;CALL KOJO_EVENT(ARG:2, 335)
		PRINTFORMW %NAME:(LOCAL:5)%を投獄しました
		CFLAG:(LOCAL:5):1 = 0
		CFLAG:(LOCAL:5):捕虜先 = CFLAG:MASTER:1

	ELSEIF RESULT == 4
		;CALL KOJO_EVENT(ARG:2, 335)
		PRINTFORMW %NAME:(LOCAL:5)%を軟禁しました
		CFLAG:(LOCAL:5):1 = 0
		CFLAG:(LOCAL:5):捕虜先 = CFLAG:MASTER:1
		CFLAG:(LOCAL:5):監禁状態 = 1

	ELSEIF RESULT == 3
		;CALL KOJO_EVENT(ARG:2, 336)
		PRINTFORMW ……………………
		PRINTFORMW ……
		PRINTFORMW %NAME:(LOCAL:5)%を処刑しました
		;状態フラグを死亡状態に
		CFLAG:(LOCAL:5):1 = 0
		CFLAG:(LOCAL:5):特殊状態フラグ = 2
	ELSE
		GOTO INPUT_LOOP
	ENDIF

;君主が逃亡
ELSE
	LOCAL:40 = 1
	;新たに勢力を生成できない状態であれば、放浪させる
	;勢力数最大の状態でクーデターした方がはるかに有利になっちゃうんだけど、しかたなし
	IF GET_NEW_COUNTRY() == -1
		SETCOLOR COLOR("注意")
		PRINTFORML 現君主である%NAME:(LOCAL:5)%の確保に失敗しました…
		PRINTFORMW %NAME:(LOCAL:5)%は放浪した模様です
		RESETCOLOR
		CFLAG:(LOCAL:5):所属 = 0
		CFLAG:(LOCAL:5):特殊状態フラグ = 1
		CALL CHANGE_RELATION_O_TO_C(LOCAL:5, CFLAG:MASTER:1, -500, 1000)
	ELSE
		;接する自国都市の数が最も少ない都市の中からランダムに逃亡先を選択する

		;自国の都市をリスト化
		LOCAL:30 = 0
		FOR LOCAL:0, 0, MAX_CITY
			IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:1 && GET_CITYNAME(LOCAL:0) != "無名"
				LOCAL:(LOCAL:30 + 100) = LOCAL:0
				LOCAL:30 ++
			ENDIF
		NEXT

		;それぞれの都市が他の自国都市と接している数をカウント
		FOR LOCAL:0, 0, LOCAL:30
			FOR LOCAL:1, LOCAL:0 + 1, LOCAL:30
				LOCAL:2 = LOCAL:(LOCAL:0 + 100)
				LOCAL:3 = LOCAL:(LOCAL:1 + 100)
				IF IS_ROOT(LOCAL:2, LOCAL:3)
					LOCAL:(LOCAL:0 + 200) ++
					LOCAL:(LOCAL:1 + 200) ++
				ENDIF
			NEXT
		NEXT

		;最も接する自国都市が少ない都市を抜き出してリスト化
		LOCAL:31 = 999
		FOR LOCAL:0, 0, LOCAL:30
			IF LOCAL:(LOCAL:0 + 200) < LOCAL:31
				LOCAL:300 = LOCAL:(LOCAL:0 + 100)
				LOCAL:32 = 1
			ELSEIF LOCAL:(LOCAL:0 + 200) == LOCAL:31
				LOCAL:(LOCAL:32 + 300) = LOCAL:(LOCAL:0 + 100)
				LOCAL:32 ++
			ENDIF
		NEXT

		;リスト化した都市の中から逃亡先の都市をランダムに選び出す
		LOCAL:33 = LOCAL:(RAND:(LOCAL:32) + 300)

		SETCOLOR COLOR("注意")
		PRINTFORML 現君主である%NAME:(LOCAL:5)%の確保に失敗しました…
		PRINTFORMW %NAME:(LOCAL:5)%は%GET_RELAYNAME(LOCAL:33)%へと逃走した模様です
		RESETCOLOR

		;この場合、クーデター後の勢力は新勢力として作成する
		FOR LOCAL:0, 1, MAX_COUNTRY
			IF !IS_COUNTRY(LOCAL:0)
				;元の国家番号を保存
				LOCAL:8 = CFLAG:MASTER:1

				;士官を新勢力に移す
				FOR LOCAL:1, 0, CHARANUM
					IF LOCAL:1 == MASTER || (LOCAL:1 != LOCAL:5 && CFLAG:(LOCAL:1):1 == LOCAL:8 && (TALENT:(LOCAL:1):恋慕 || TALENT:(LOCAL:1):親友 || TALENT:(LOCAL:1):服従))
						CFLAG:(LOCAL:1):1 = LOCAL:0
						;部隊に配属されている場合強制解除
						CALL FORCE_FREE(LOCAL:1)
					ENDIF
				NEXT
				;旧君主の逃亡先を除く都市の所属先を新勢力に変更
				FOR LOCAL:1, 0, LOCAL:30
					LOCAL:2 = LOCAL:(LOCAL:1 + 100)
					IF LOCAL:2 != LOCAL:33
						CITY_OWNER:(LOCAL:2) = LOCAL:0
					ENDIF
				NEXT
				COUNTRY_COLOR:(LOCAL:0) = 0xD0D0D0

				;兵力の分配
				COUNTRY_SOLDIER:(LOCAL:0) = COUNTRY_SOLDIER:(LOCAL:8) + CITY_SOLDIER:(LOCAL:33)
				CITY_SOLDIER:(LOCAL:33) = 0
				COUNTRY_SOLDIER:(LOCAL:8) = COUNTRY_SOLDIER:(LOCAL:0) / (GET_OWN_CITY(LOCAL:0) + 1)
				COUNTRY_SOLDIER:(LOCAL:0) -= COUNTRY_SOLDIER:(LOCAL:8)

				;COMは先に行動できないので兵数で優遇
				COUNTRY_SOLDIER:(LOCAL:8) = COUNTRY_SOLDIER:(LOCAL:8) * 2 + 500

				;防衛兵力の初期設定(兵数の2/3弱、最低500)
				CITY_SOLDIER:(LOCAL:33) = (COUNTRY_SOLDIER:(LOCAL:8) / 750 + 1) * 500
				COUNTRY_SOLDIER:(LOCAL:8) -= CITY_SOLDIER:(LOCAL:33)

				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF

;調教参加フラグをクリア
CVARSET CFLAG, 6, 0

;君主候補の数をカウント
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):親友 || TALENT:(LOCAL:0):服従)
		LOCAL:10 ++
	ENDIF
NEXT

;ページ数を計算
LOCAL:11 = LOCAL:10 / 44 + 1
LOCAL:12 = 1

DRAWLINE
PRINTFORML 新たな君主を決めて下さい
DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP_SELECTBOSS

PRINTFORML [ 99] %CALLNAME:MASTER%

LOCAL:13 = (LOCAL:12 - 1) * 44
LOCAL:14 = LOCAL:12 * 44
LOCAL:15 = 0
LOCAL:16 = 0
LOCAL:17 = 4
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):親友 || TALENT:(LOCAL:0):服従)
		IF LOCAL:15 >= LOCAL:13 && LOCAl:15 < LOCAL:14
			IF LOCAL:16 % 2 != 0
				PRINT 　
			ELSEIF LOCAL:16 >= 1
				PRINTL 
				LOCAL:17 ++
			ENDIF
			CALL PRINT_PARTNER_DATA(LOCAL:0)
			LOCAL:16 ++
		ENDIF
		LOCAL:15 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE

IF LOCAL:11 >= 2
	LOCAL:17 += 1

	IF LOCAL:12 > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = page{LOCAL:12}/{LOCAL:11}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:12 < LOCAL:11
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
ENDIF

REDRAW 0

$INPUT_LOOP_SELECTBOSS
INPUT

IF RESULT == 8 && LOCAL:12 > 1
	LOCAL:12 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP_SELECTBOSS
ELSEIF RESULT == 9 && LOCAL:12 < LOCAL:11
	LOCAL:12 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP_SELECTBOSS
ELSEIF RESULT == 99
	LOCAL:20 = MASTER
ELSE
	LOCAL:20 = NO_TO_CHARA(RESULT - 100)
	IF LOCAL:20 >= 0 && LOCAL:20 != MASTER && CFLAG:(LOCAL:20):1 == CFLAG:MASTER:1 && (TALENT:(LOCAL:20):恋慕 || TALENT:(LOCAL:20):親友 || TALENT:(LOCAL:20):服従)
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_SELECTBOSS
	ENDIF
ENDIF

REDRAW 1

COUNTRY_BOSS:(CFLAG:MASTER:1) = GET_ID(LOCAL:20)

SETCOLOR COLOR("注意")
PRINTFORMW %NAME:(LOCAL:20)%が新たな君主となりました！
RESETCOLOR
PRINTL 

DRAWLINE
PRINTL 新しい勢力の色を選んで下さい
DRAWLINE
CALL CHANGE_COUNTRY_COLOR(CFLAG:MASTER:1, LOCAL:40)
DRAWLINE

PRINTL 
SETCOLOR COLOR("注意")
PRINTFORMW 混乱の影響で我が軍の防衛兵力・遊撃兵力が減少しました
PRINTL 
PRINTFORMW 軍を再編するため、我が軍の全部隊を解散させました
RESETCOLOR

FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:1
		CITY_SOLDIER:(LOCAL:0) = MAX(CITY_SOLDIER:(LOCAL:0) * (85 + RAND:14) / 100, 500)
	ENDIF
NEXT
FOR LOCAL:0, 0, 10
	IF UNIT_SOLDIER:(CFLAG:MASTER:1):(LOCAL:0) > 0
		UNIT_SOLDIER:(CFLAG:MASTER:1):(LOCAL:0) = MAX(UNIT_SOLDIER:(CFLAG:MASTER:1):(LOCAL:0) * (85 + RAND:14) / 100, 1)
	ENDIF
NEXT
COUNTRY_SOLDIER:(CFLAG:MASTER:1) = COUNTRY_SOLDIER:(CFLAG:MASTER:1) * (90 + RAND:4) / 100

;旧君主を解放した場合の後始末
IF LOCAL:30
	;勢力を滅亡させた君主のIDを保存
	CFLAG:(LOCAL:5):恨む君主 = GET_ID(LOCAL:20)
ENDIF
