[SKIPSTART]
MAPDEBUG
========
概要
----
　地図をシナリオから切り出して正常に機能してるかテストするための機能。
MAP_LIBRARYを利用していないと使えないので注意。
[SKIPEND]

;地図のデバッグのエントリーポイント
;地図を選択したりやめたりすることができる
@MAPDEBUG()
#DIMS DEFAULT_MAPID
#DIM SELECTEDMENU
#DIM LCOUNT
#DIM CITY_ID
;ここはゲームの外なのでそれを都合よく用いて書き換え 抜け出すときにデフォルト値に戻す
DEFAULT_MAPID '= MAPID
MAPID '= ""
CITY_ID = 0
WHILE 1
	CALL SHOP_AVAIL_MAPDEBUG_SET
	CALL SINGLE_DRAWLINE
	PRINTFORML MAPDEBUG 現在選択されているマップ %(MAPID == "" ? "未選択" # MAPID)%
	PRINT -------------- 0 1□□□◇□□□□10□□□□◇□□□□20□□□□◇□□□□30□□□□◇□□□□□40□□□□◇□47
	IF MAPID != ""
		CALL DRAWMAP(DRAWMAP_MENUBARTYPE_DEBUGMENU)
		CALL SHOW_CITY_INFO_MAPDEBUG(CITY_ID)
	ELSE
		FOR LCOUNT, 0, LINES_SHOP_DRAW
			CALL PRINT_MAPDEBUG_MENU(LCOUNT)
			SELECTCASE LCOUNT
				CASE MENU_HEIGHT
					CALL REPEAT_DASH(0, 0)
			ENDSELECT
			PRINTL
		NEXT
	ENDIF
	INPUT
	SELECTEDMENU = RESULT
	SELECTCASE SELECTEDMENU
		CASE 0
			MAPID '= DEFAULT_MAPID
			CALL DELETE_MAP_STATUS_DEBUG()
			RETURN
		CASE 1
			CALL DEBUG_SELECT_MAPID()
			CITY_ID = 0
	ENDSELECT
	IF MAPID != ""
		SELECTCASE SELECTEDMENU
			CASE IS > 1000
				IF CITY_NAME:(SELECTEDMENU - 1000) != "無名"
					CITY_ID = SELECTEDMENU - 1000
				ENDIF
		ENDSELECT
	ENDIF
WEND

@DEBUG_SELECT_MAPID()
#DIMS hoge
#DIM DEFAULT_CITY_NUM
	DEFAULT_CITY_NUM = CITY_NUM
	CALL SINGLE_DRAWLINE
	PRINTL マップ選択
	PRINTFORML 現在選択されているマップ %(MAPID == "" ? "未選択" # MAPID)%
	CALL SINGLE_DRAWLINE
	PRINTL MAPIDを入力してください
	INPUTS
	hoge '= TOUPPER(RESULTS)
	TRYCCALLFORM SET_CITY_NUM_%hoge%
		CITY_NUM = DEFAULT_CITY_NUM
		MAPID '= hoge
		PRINTFORML %MAPID%を選択しました
		CALL RESET_MAPDEBUG_STATUS
		CALL MAPSETUP(1)
		CALL MAP_INIT
	CATCH
		PRINTFORML 入力されたMAPID %hoge% は存在しません
	ENDCATCH

@DELETE_MAP_STATUS_DEBUG()
VARSET SHOP_AVAIL
CALL RESET_MAPDEBUG_STATUS
MAPREADY = 0

@PRINT_MAPDEBUG_MENU(MENU_LINE)
#DIM MENU_LINE
SELECTCASE MENU_LINE
	CASE 0
		CALL PRINTBUTTON_SHOP_DEBUG(0, "終わる")
	CASE 1
		CALL PRINTBUTTON_SHOP_DEBUG(1, "マップ選択")
	CASEELSE
		CALL PRINT_MAPDEBUG_MENU_EMPTYLINE()
ENDSELECT
PRINTFORM {MENU_LINE + 1, 2, RIGHT}

@PRINTBUTTON_SHOP_DEBUG(ARG:0, ARGS:0)
CALL PRINTBUTTON_SHOP(ARG:0, @"[%ARGS:0, 10, RIGHT%]", 7)

@PRINT_MAPDEBUG_MENU_EMPTYLINE()
PRINT 　　　　　　　

@SHOP_AVAIL_MAPDEBUG_SET
SHOP_AVAIL:0 = 1
SHOP_AVAIL:1 = 1
IF MAPID != ""
	SHOP_AVAIL:2 = 0
ENDIF

@DRAW_CITY_BUTTON_MAPDEBUG(CITY_ID)
#DIM CITY_ID
PRINTBUTTON GET_CITYNAME_BUTTON(CITY_ID), CITY_ID + 1000

@RESET_MAPDEBUG_STATUS
VARSET CITY_NUM
VARSET CITY_ROOT, 0
VARSET CITY_TYPE, 0
VARSET CITY_ECONOMY, 0
VARSET CITY_ECONOMY_LIMIT, 0
VARSET CITY_GUARD, 0
VARSET CITY_NAME_SHORT, "無名"
VARSET CITY_NAME, "無名"

@SHOW_CITY_INFO_MAPDEBUG(CITY_ID)
#DIM CITY_ID
#DIM LCOUNT
FOR LCOUNT, MENU_HEIGHT, LINES_SHOP_DRAW
	CALL PRINT_MAPDEBUG_MENU(LCOUNT)
	SELECTCASE LCOUNT
		;MENU_HEIGHTが動くことも考えてこのように処理する
		CASE MENU_HEIGHT
			CALL REPEAT_DASH(0, 0)
		CASE MENU_HEIGHT + 1
			PRINTFORM %@"◆%(CITY_TYPE:CITY_ID == 1 ? @"中継地点(%CITY_NAME:CITY_ID%)" # CITY_NAME:CITY_ID)%◆", 20, LEFT%
		CASE MENU_HEIGHT + 2
			PRINTFORM %@"経済:{CITY_ECONOMY:CITY_ID, 4, RIGHT}/{CITY_ECONOMY_LIMIT:CITY_ID, 4, RIGHT}", 20, LEFT%防衛倍率:%DECIMAL_STRING(CITY_GUARD:CITY_ID, 2)%
		CASE MENU_HEIGHT + 3
			PRINTFORM 経路表示:%PRINT_MAPDEBUG_ROOT(CITY_ID, 1), 40, RIGHT%%PRINT_MAPDEBUG_ROOT(CITY_ID, 2), 40, RIGHT%
		CASE MENU_HEIGHT + 4
			PRINTFORM 　　　　 %PRINT_MAPDEBUG_ROOT(CITY_ID, 3), 40, RIGHT%%PRINT_MAPDEBUG_ROOT(CITY_ID, 4), 40, RIGHT%
		CASE MENU_HEIGHT + 5
			PRINTFORM 　　　　 %PRINT_MAPDEBUG_ROOT(CITY_ID, 5), 40, RIGHT%%PRINT_MAPDEBUG_ROOT(CITY_ID, 6), 40, RIGHT%
		CASE MENU_HEIGHT + 6
			PRINTFORM 　　　　 %PRINT_MAPDEBUG_ROOT(CITY_ID, 7), 40, RIGHT%%PRINT_MAPDEBUG_ROOT(CITY_ID, 8), 40, RIGHT%
		CASE MENU_HEIGHT + 7
			PRINTFORM 　　　　 %PRINT_MAPDEBUG_ROOT(CITY_ID, 9), 40, RIGHT%%PRINT_MAPDEBUG_ROOT(CITY_ID, 10), 40, RIGHT%
	ENDSELECT
	PRINTL
NEXT

@PRINT_MAPDEBUG_ROOT(CITY_ID, INDEX)
#FUNCTIONS
#DIM CITY_ID
#DIM INDEX
#DIM LCOUNT
#DIM ROOT_CITY_COUNT
ROOT_CITY_COUNT = 0
FOR LCOUNT, 0, CITY_NUM + 1
	IF IS_ROOT(CITY_ID, LCOUNT)
		ROOT_CITY_COUNT++
		IF ROOT_CITY_COUNT == INDEX
			SELECTCASE IS_ROOT(CITY_ID, LCOUNT)
				CASE 1
					RETURNF @"%CITY_NAME:LCOUNT%"
				CASE 2
					RETURNF @"%CITY_NAME:LCOUNT%(中継地点:%CITY_NAME:GET_RELAYPOINT(CITY_ID, LCOUNT)%)"
			ENDSELECT
		ENDIF
	ENDIF
NEXT
	


@GET_ROOTCOUNT(CITY_ID)
#FUNCTION
#DIM CITY_ID
#DIM ROOTCOUNT
#DIM LCOUNT
ROOTCOUNT = 0
FOR LCOUNT, 0, CITY_NUM + 1
	SIF IS_ROOT(CITY_ID, LCOUNT)
		ROOTCOUNT++
NEXT
RETURNF ROOTCOUNT
