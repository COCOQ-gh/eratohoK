;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K26_ASLEEP_1_RATE(対象)
#DIM 対象
RETURN 40


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K26_ASLEEP_1_DECISION(対象)
#DIM 対象

SIF CFLAG:対象:好感度 < 500
	RETURN 0

SIF KDVAR:対象:慧音_うたたね発見
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K26_ASLEEP_1_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K26_ASLEEP_1(対象)
#DIM 対象

PRINTFORMW 用があって%ANAME(対象)%の部屋を訪ねたが、反応がない
PRINTFORMW この時間の%PRONOUN(対象)%は自室で仕事をしているはずだが……
PRINTL
CALL ASK_YN("のぞいてみる", "外出しているのだろう")

IF RESULT == 1
	PRINTFORMW たまたま外出しているのだろう
	PRINTFORMW %ANAME(MASTER)%はその場を後にした……
	KDVAR:対象:慧音_うたたね発見 = 1
	RETURN
ENDIF

PRINTFORMW 怪訝に思ってのぞいてみると、%ANAME(対象)%が机に突っ伏しているのが見えた
PRINTFORMW どうやら仕事中にうたた寝してしまったようだ……
CALL ASK_MULTI("起こす", "毛布を掛けておく", "立ち去る")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW %ANAME(対象)%を優しく揺り起こした
		PRINTFORMW 「んぅ……ん……？　わっ、あ、ああ、%ANAME(MASTER)%か……」
		PRINTFORMW 「私は……居眠りしてしまっていたのか。恥ずかしいところを見られてしまったな」
		PRINTFORMW 「少し眠って、頭も冴えてきたようだ。残りの仕事を終えてしまうよ」
		PRINTFORMW %PRONOUN(対象)%ははにかみながら仕事に戻った……
		CFLAG:対象:好感度 += 50
	CASE 1
		PRINTFORMW 疲れているのだろう。起こすのも悪い
		PRINTFORMW 毛布だけかけて、その場を立ち去った……
		CFLAG:対象:好感度 += 100
	CASE 2
		PRINTFORMW 疲れているのだろう。起こすのも悪い
		PRINTFORMW そっと扉を閉め、その場を立ち去った……
ENDSELECT

KDVAR:対象:慧音_うたたね発見 = 1

RETURN 1
