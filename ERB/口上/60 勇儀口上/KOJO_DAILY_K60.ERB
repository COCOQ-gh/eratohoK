@KOJO_DAILY_EXIST_K60()

@KOJO_DAILY_K60()
#DIM 発生フラグ
発生フラグ = 0

;力比べ
DAILY_NAME = 力比べ
CALL KOJO_DAILY_K60_COMPETE
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ


;----------------------------------
;力比べ
;----------------------------------
@KOJO_DAILY_K60_COMPETE
#DIM 勇儀
#DIM 判定値
#DIM 拉致
勇儀 = NAME_TO_CHARA("勇儀")

;勇儀がいないと駄目
SIF 勇儀 == -1
	RETURN -1

;おわってたら出ない
SIF KDVAR:勇儀:勇儀_力比べ == -1
	RETURN -1

;マスターが男でないと出ない
SIF !IS_MALE(MASTER)
	RETURN -1

;「MASTERの武闘が60以上、確率は2%に武闘がちょっと補正、勇儀と面識がなく、所属がことなり、勇儀が死んでいない
;こういうちょっと変わった条件に対してはCHECK_KOJO_DAILY_HAPPENは通用しないんだなぁ
SIF !(60 <= ABL:MASTER:武闘 && (RAND:1000 < (20 + (ABL:MASTER:武闘 - 60) * 5)) && !CFLAG:勇儀:面識 && CFLAG:勇儀:所属 != CFLAG:MASTER:所属 && CFLAG:勇儀:特殊状態フラグ != 2)
	RETURN -1

IF CFLAG:勇儀:所属 && GET_COUNTRY_BOSS(CFLAG:MASTER:所属) != MASTER
	拉致 = 1
ELSE
	拉致 = 0
ENDIF

CALL KOJO_DAILY_START()

PRINTFORMW 「%ANAME(MASTER)%、だっけ？　ってのはいるかい！？」
PRINTFORMW %ANAME(MASTER)%が仕事に忙殺されているさなか、急に誰かが入ってきた
PRINTFORML しっかりとした体つきの、金髪の女性だ。額から生えた立派な角と、零れそうな乳がそれぞれ目を惹く
PRINTFORMW いかにも自分が%ANAME(MASTER)%だと応えると、女性はニカッと笑ってみせた
PRINTFORMW 「なるほどアンタが！　なかなかいい男じゃないか、会いに来た甲斐があったよ」
PRINTFORM 「私は%ANAME(勇儀)%、鬼の四天王、
CALL COLORPRINTFORM(@"%NAME_FORMAL(勇儀)%", COLOR("注意"))
PRINTFORMW さ。ひとつよろしく」
PRINTFORML 鬼。なるほど、それは立派な角も生えているはずだ
PRINTFORMW しかしその鬼が、一体何の用だというのだろう
PRINTFORMW 「いやなに、私は強い奴が好きでね、たまにこうして出歩いては、手合わせ願ってるわけさ」
PRINTFORML ……つまり、武者修行というわけか
PRINTFORMW 悪いが今は本当に仕事が忙しい、そう言って断ろうとするが、%ANAME(勇儀)%は食い下がった
PRINTFORMW 「そう言わずにさ、一つ頼むよ。実際、アンタにとっても悪い話じゃないからさ」
PRINTFORM 「もしアンタが勝てば、そうだね……
CALL COLORPRINTFORM(@"アンタの頼みを一つ聞いてやろう", COLOR("注意"))
PRINTFORMW 」
PRINTFORMW 「嘘じゃないよ？　鬼は嘘が嫌いなんだ。信用してくれていい」
PRINTFORMW 「そうだ、ハンデもつけてあげるよ。私は片手に杯をもって、酒を一滴もこぼさないよう立ち回る」
PRINTFORM 「その代わり……私が勝ったら、
IF 拉致
	CALL COLORPRINTFORM(@"配下になってもらおうか", COLOR("注意"))
	PRINTFORMW 」
ELSE
	CALL COLORPRINTFORM(@"有り金全て", COLOR("注意"))
	PRINTFORMW いただこうか」
ENDIF
PRINTFORMW 何を言っているんだと思ったが、どうやら勇儀は本気であるようだった
PRINTFORML ……鬼といえば、昔話でおなじみの、力自慢の妖怪だ。ハンデをつけても、勝つのは容易ではあるまい
PRINTFORML しかし――もし勝てば、頼みを何でも一つ。
PRINTFORMW ……勇儀の顔つき、身体つきは、女性として実に魅力的なものだ。有り金全てを賭けてでも、受けて立つ価値はある……
PRINTL
CALL ASK_YN("のぞむところだ！", "いや今忙しいつってんだろ")
IF RESULT == 1
	PRINTFORMW いやいや、仕事があるのだ。この大量の書類を見ろ。手合わせどころではない
	PRINTFORMW 「むっ……。そうかい、そりゃ残念だ」
	PRINTFORMW 「あんたは中々腕が立つと聞いてたから、是非やりあってみたかったんだけど」
	PRINTFORMW 「ま、そういうことならしょうがない。邪魔したね」
	PRINTFORMW %ANAME(勇儀)%は立ち去っていった……
ELSE
	PRINTFORMW 「そうこなくっちゃぁ！　さぁ、さっそくやろうじゃないか！」
	PRINTFORMW 勇儀に腕を引かれるようにして、中庭に出た……
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ・
	判定値 = ABL:MASTER:武闘 / 2
	IF RAND:100 <= 判定値
		PRINTFORMW 「ははは、負けだ！　私の！」
		PRINTFORML どうにかなった……ぎりぎりだったが
		PRINTFORMW 鬼の拳は予想以上だった。ぎりぎりのところでかわしながら、酒一滴をこぼさせて勝つのがやっとだった
		PRINTFORMW 「それでもいいさ、アンタの勝ちってことに変わりはない」
		PRINTFORMW 「さて……まさか正直、負けるとは思ってなかったけど。頼みを一つ、なんでも聞いてやろう」
		PRINTL
		CALL ASK_MULTI_JUDGE("部下になって", GET_COUNTRY_BOSS(CFLAG:勇儀:所属) != 勇儀, "カキタレになって", GET_COUNTRY_BOSS(CFLAG:勇儀:所属) != 勇儀, "力が欲しい", 1, "金くれ", 1)
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「あぁ、そういうことならこっちも大歓迎だよ」
				PRINTFORMW 「私に勝てる奴だなんて、久々だからね。さぁて、それじゃあ一つ、やってやろうじゃないか！」
				CALL COLORPRINTFORM(@"%ANAME(勇儀)%が部下になりました", COLOR("注意"), "W")
				CFLAG:勇儀:所属 = CFLAG:MASTER:所属
				CFLAG:勇儀:特殊状態フラグ = 0
				CFLAG:勇儀:好感度 += 500
			CASE 1
				PRINTFORMW 「えっ、……あ、ああ、頼みってそういう？」
				PRINTFORMW 「うーん、英雄色を好むとか言ったりするけど……いや、まあ、約束だし、受け入れるけどさ」
				PRINTFORMW 「その……何というか、お手柔らかに頼むよ？」
				CALL COLORPRINTFORM(@"%ANAME(勇儀)%が部下になりました", COLOR("注意"), "W")
				CALL COLORPRINTFORM(@"%ANAME(勇儀)%の合意を得ました", COLOR("桃"), "W")
				CFLAG:勇儀:所属 = CFLAG:MASTER:所属
				CFLAG:勇儀:特殊状態フラグ = 0
				TALENT:勇儀:合意 = 1
			CASE 2
				PRINTFORMW 「ほう、私に勝っておいて、まだ力を欲するときた！　はは、アンタみたいな武辺者は大好きだよ」
				PRINTFORMW 「じゃあそうだな、鬼流の喧嘩の仕方を伝授してあげるよ」
				CALL COLORPRINTFORM(@"%ANAME(勇儀)%に喧嘩の仕方を教わりました", COLOR("注意"), "W")
				SIF !TALENT:MASTER:ステゴロ
					CALL COLORPRINTFORM(@"%ANAME(MASTER)%は＜ステゴロ＞を得ました", COLOR("注意"), "W")
				CALL COLORPRINTFORM(@"%ANAME(MASTER)%の武闘が3上昇しました", COLOR("注意"), "W")
				CFLAG:勇儀:好感度 += 300
				SETBIT TALENT:MASTER:ＳＬＧ素質, 素質_ステゴロ
				ABL:MASTER:武闘 += 3
				PRINTFORMW 「じゃ、私はこれで。楽しかったよ！」
				PRINTFORMW %ANAME(勇儀)%は立ち去っていった……
			CASE 3
				PRINTFORMW 「金か。堅実でいい望みじゃないか」
				PRINTFORMW 「鬼退治して財宝を得るってのは、昔話の定番だしね」
				PRINTFORMW 「わかった！　ほら、あるだけ持って行きなよ」
				CALL COLORPRINTFORM(@"金100000を得ました", COLOR("注意"), "W")
				MONEY += 100000
				PRINTFORMW 「じゃ、私はこれで。楽しかったよ！」
				PRINTFORMW %ANAME(勇儀)%は立ち去っていった……
		ENDSELECT
	ELSE
		PRINTFORMW 「ははは、勝ちだな！　私の！」
		PRINTFORML ……やはり、鬼には勝てなかった
		PRINTFORMW 杯の酒をこぼさないようにするというのは相当なハンデだと思うが、それでもなお駄目だとは……
		PRINTFORMW 「さてと、悪いけど約束は約束だ」
		IF 拉致
			PRINTFORMW 「あんたの身体を、もらうことにするよ」
			PRINTFORM 「くく、悪いようにはしないさ。
			CALL COLORPRINTFORM(@"10", COLOR("注意"))
			PRINTFORMW 期くらいしたら、放浪してもらっても構わないよ」
			PRINTFORMW 「ま、そういうことだから、これからよろしく！」
			PRINTFORMW %ANAME(勇儀)%に無理矢理引っ張られた……
			CALL COLORPRINTFORM(@"%ANAME(勇儀)%の配下にされました", COLOR("警告"), "W")
			CFLAG:MASTER:1 = CFLAG:勇儀:1
			PLAYER_HIRED_COUNTER = 10
			CALL FORCE_FREE(MASTER)
		ELSE
			PRINTFORMW 「有り金全部、もらっていくよ」
			PRINTFORMW 「昔なら命をもらってたとこだからさ、ま、拾いもんだと思いなよ」
			CALL COLORPRINTFORM(@"所持金を全て奪われました", COLOR("警告"), "W")
			PRINTFORMW 「じゃ、私はこれで。楽しかったよ！」
			PRINTFORMW %ANAME(勇儀)%は立ち去っていった……
			MONEY = 0
		ENDIF
	ENDIF
ENDIF

CALL COLORPRINTFORM(@"%ANAME(勇儀)%の面識を得ました", COLOR("注意"), "W")
CFLAG:勇儀:面識 = 1
KDVAR:勇儀:勇儀_力比べ = 1