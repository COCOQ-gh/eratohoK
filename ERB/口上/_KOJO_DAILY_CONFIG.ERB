@CONFIG_KOJO_DAILY
#DIM 行数
#DIM カウンタ
DRAWLINE
PRINTFORML 各口上のデイリーイベントの発生を制御できます（クリックでONOFF）
PRINTFORML 現在、キャラごとにまるっとONにするかOFFにするかのみ指定可能です
DRAWLINE

$SHOW_LOOP
REDRAW 0
行数 = 0
カウンタ = 0
FOR LOCAL, 0, 201
	CALL IS_KOJO(LOCAL)
	IF RESULT
		TRYCCALLFORM KOJO_DAILY_EXIST_K{LOCAL}
			SIF KDCONFIG:(LOCAL)
				SETCOLOR COLOR("選択不可")
			PRINTBUTTON @"%"[" + ANAME(LOCAL) + "]", 20, LEFT%", LOCAL + 100
			RESETCOLOR
			カウンタ ++
			IF カウンタ % 4 == 0
				PRINTL
				行数 ++
			ENDIF
		CATCH
			CONTINUE
		ENDCATCH
	ENDIF
NEXT
PRINTL
行数 ++
DRAWLINE
行数 ++
PRINTBUTTON "[決定]", 0
PRINT     
PRINTBUTTON "[全ON]", 1
PRINT     
PRINTBUTTON "[全OFF]", 2
PRINTL
行数 ++
$INPUT_LOOP
INPUT
行数 ++
IF RESULT == 0
	REDRAW 1
	RETURN
ELSEIF GROUPMATCH(RESULT, 1, 2)
	LOCAL:1 = RESULT
	FOR LOCAL, 0, 201
		CALL IS_KOJO(LOCAL)
		IF RESULT
			TRYCCALLFORM KOJO_DAILY_EXIST_K{LOCAL}
				KDCONFIG:(LOCAL) = LOCAL:1 -1
			CATCH
			ENDCATCH
		ENDIF
	NEXT
	CLEARLINE 行数
	GOTO SHOW_LOOP
ELSE
	LOCAL:2 = RESULT - 100
	TRYCCALLFORM KOJO_DAILY_EXIST_K{LOCAL:2}
		KDCONFIG:(LOCAL:2) = !KDCONFIG:(LOCAL:2)
		CLEARLINE 行数
		GOTO SHOW_LOOP
	CATCH
		CLEARLINE 1
		行数 --
		GOTO INPUT_LOOP
	ENDCATCH
ENDIF