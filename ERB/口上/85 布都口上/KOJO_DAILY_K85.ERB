@KOJO_DAILY_EXIST_K85()

@KOJO_DAILY_K85()
#DIM 発生フラグ
発生フラグ = 0

;一枚足りない
CALL KOJO_DAILY_K85_ITIMAI
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ


;----------------------------------
;一枚足りない
;----------------------------------
@KOJO_DAILY_K85_ITIMAI
#DIM 布都

布都 = NAME_TO_CHARA("布都")

;一発勝負
SIF KDVAR:布都:布都_一枚足りない == -1
	RETURN -1

;布都が同一勢力所属で捕虜でなく死んでいないなら3.0%で発生
SIF !CHECK_KOJO_DAILY_HAPPEN(布都, 30, 1, 0, 1)
	RETURN -1

;面識がないなら顔見知りになる
SIF CFLAG:布都:面識 == 0
	CFLAG:布都:面識 = 1

CALL KOJO_DAILY_START

PRINTFORML %ANAME(MASTER)%がぼんやり窓の外を見ていると、%ANAME(布都)%がキョロキョロしながらかけていくのが見えた
PRINTFORMW %ANAME(布都)%の放火癖は聞いたことがある……
PRINTFORMW %ANAME(MASTER)%は嫌な予感を覚え、%ANAME(布都)%の後を追った
PRINTFORMW 「おや、%ANAME(MASTER)%か。我に何か用か？」
PRINTFORMW 「放火？　馬鹿を言うな。そんなことをしているほど暇ではない」
PRINTFORML 「いやな、我の手下が『お皿が一枚足りない一枚足りない』とうるさくての」
PRINTFORMW 「おお！　そうじゃ！　%ANAME(MASTER)%が皿を用意すればいい！」
PRINTFORML %ANAME(布都)%は名案じゃ名案じゃとニコニコしている
PRINTFORMW 仕方ない。皿を用意してやるとしよう……

CALL ASK_MULTI("0枚", "1枚", "10枚", "100枚", "100000枚")
SELECTCASE RESULT
	CASE 0
		PRINTFORML 「なに！？　思った以上にケチで狭量な……」
		PRINTFORMW %ANAME(布都)%はブツブツ言いながら去っていった……
	CASE 1
		PRINTFORML 「おお！　言ってみるものじゃ！　でかした！」
		PRINTFORMW %ANAME(布都)%は太陽のような満面の笑顔で走り去っていった……
		CFLAG:布都:好感度 += 50
	CASE 2
		PRINTFORML 「ほう？　予備を用意するとは……なかなかデキるようだな？」
		PRINTFORML 「いいだろう！　お主を取り巻きとして考えてやっても……あれ？」
		PRINTFORMW 長くなりそうだったので%ANAME(MASTER)%はこっそり逃げた……
		CFLAG:布都:好感度 += 100
	CASE 3
		PRINTFORML 「こ、こんなに大量の皿、どこから出したのじゃ？　そちらの方が気になる……」
		PRINTFORML 「あ、いや。うむ。ありがたく頂戴するが……ううむ……」
		PRINTFORMW 釈然としない%ANAME(布都)%は100枚の皿をふらふらと運んでいった……
		CFLAG:布都:好感度 += 50
	CASEELSE
		PRINTFORML 「おおぉおぉおぉおおぉお！！！？！？？！？」
		PRINTFORMW ものすごい数の皿にさらわれて%ANAME(布都)%はどこかへ流れて行ってしまった……
		CFLAG:布都:好感度 += 10
		;助けるつもりはさらさらないです
ENDSELECT

KDVAR:布都:布都_一枚足りない = -1