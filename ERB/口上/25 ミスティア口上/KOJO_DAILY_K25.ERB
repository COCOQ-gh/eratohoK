;------------------------------------
;そのキャラがデイリーイベントをもつか判断する関数
;KOJO_EXISTとは別にこの関数も必要になるので注意
;------------------------------------
@KOJO_DAILY_EXIST_K25()


;------------------------------------
;そのキャラのデイリー本体を呼び出すハブとなる関数
;------------------------------------
@KOJO_DAILY_K25()
#DIM 発生フラグ
発生フラグ = 0

;デイリーイベント
DAILY_NAME = 作詞
CALL KOJO_DAILY_K25_COMPOSE
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

DAILY_NAME = 流行り屋台
CALL KOJO_DAILY_K25_UNAGI_STALL
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;;デイリーイベント
;DAILY_NAME = デイリーイベントの名前
;CALL KOJO_DAILY_K25_NAMAE_OF_DAILY_EVENT
;SIF RESULT != -1
;	発生フラグ = 1
;SIF DAILY_CANCEL
;	RETURN 1

RETURN 発生フラグ


;------------------------------------
;作詞
;アホみてぇな中身なのに内部処理は妙に大がかりなんDA
;-----------------------------------
@KOJO_DAILY_K25_COMPOSE
#DIM ミスティア
#DIM 選択肢, 5
#DIMS 歌詞, 5, 5
#DIM 正解パターン, 5, 5
#DIM 候補パターン
#DIM 制限時間 = 10000
#DIM FIRST_LINE
#DIM 現在ミリ秒

歌詞:0:0 = はるすぎて
歌詞:0:1 = TO
歌詞:0:2 = ヒト
歌詞:0:3 = この
歌詞:0:4 = このかみをぬいてな

歌詞:1:0 = なつきにけらし
歌詞:1:1 = ヒト
歌詞:1:2 = このかみをぬいてな
歌詞:1:3 = げんそうきょうでは
歌詞:1:4 = BE

歌詞:2:0 = かつらに
歌詞:2:1 = しろたえの
歌詞:2:2 = ヒト
歌詞:2:3 = OR
歌詞:2:4 = じょうしきに

歌詞:3:0 = とらわれては
歌詞:3:1 = にんげんを
歌詞:3:2 = NOT TO
歌詞:3:3 = ころもほすてふ
歌詞:3:4 = しようと

歌詞:4:0 = あまのかぐやま
歌詞:4:1 = BE
歌詞:4:2 = おもったのじゃ
歌詞:4:3 = いけないのですね
歌詞:4:4 = さらえ

正解パターン:0:0 = 0
正解パターン:0:1 = 0
正解パターン:0:2 = 1
正解パターン:0:3 = 3
正解パターン:0:4 = 0

正解パターン:1:0 = 2
正解パターン:1:1 = 1
正解パターン:1:2 = 2
正解パターン:1:3 = 1
正解パターン:1:4 = 4

正解パターン:2:0 = 4
正解パターン:2:1 = 2
正解パターン:2:2 = 0
正解パターン:2:3 = 4
正解パターン:2:4 = 2

正解パターン:3:0 = 1
正解パターン:3:1 = 4
正解パターン:3:2 = 3
正解パターン:3:3 = 2
正解パターン:3:4 = 1

正解パターン:4:0 = 3
正解パターン:4:1 = 3
正解パターン:4:2 = 4
正解パターン:4:3 = 0
正解パターン:4:4 = 3

ミスティア = NAME_TO_CHARA("ミスティア")

SIF !CHECK_KOJO_DAILY_HAPPEN(ミスティア, 50, 1, 0, 1)
	RETURN -1

SIF KDVAR:ミスティア:ミスティア_作詞
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORMW 「うーん……決まらないなぁ……」
PRINTFORMW %ANAME(ミスティア)%が何やら唸っているのを見かけた……
PRINTL
CALL ASK_YN("声をかける", "放っておく")

IF RESULT == 1
	PRINTFORMW 考え事を邪魔するのも無粋だろう
	PRINTFORMW そっとしておくことにした……
	KDVAR:ミスティア:ミスティア_作詞 = 1
	RETURN
ENDIF

PRINTFORMW 「あっ、%ANAME(MASTER)%」
PRINTFORMW 「実は、新しい曲の作詞をしてるんだけど……いい感じのが思いつかなくて」
PRINTFORMW 「一緒に考えてくれない？」
PRINTFORML
CALL ASK_YN("いいよ", "忙しいんだ")

IF RESULT == 1
	PRINTFORMW 残念だが今は忙しい
	PRINTFORMW 断った……
	KDVAR:ミスティア:ミスティア_作詞 = 1
	RETURN
ENDIF

PRINTFORMW 「ほんと！？　ありがと！」
PRINTFORMW 「じゃあちょっと挑戦してみてね！　準備できた！？」
PRINTFORMW 「いっくよー！」

PRINTL
CALL COLORPRINT("時間内にいい感じの歌詞を作れ！！", COLOR("注意"), "L")
PRINTL

FIRST_LINE = LINECOUNT

;TINPUTが残り時間を返さないので苦肉の策
;なぜ苦肉の策かというと、GETMILLISECONDは精度があまり良くないため
現在ミリ秒 = GETMILLISECOND()

FOR LOCAL, 0, VARSIZE("正解パターン", 1)

	FOR LOCAL:1, 0, VARSIZE("歌詞", 1)
		PRINTFORML [{LOCAL:1}] %歌詞:LOCAL:(LOCAL:1)%
	NEXT

	$INPUT_LOOP

	TINPUT 制限時間, -1, 1, ""

	;時間切れ
	IF RESULT == -1
		PRINTFORMW 必死に考えてみたが、良い詩が浮かばなかった……
		PRINTFORMW 「うーん……やっぱり難しい？」
		PRINTFORMW 「今日は調子が悪いみたいだね、また今度手伝ってくれると嬉しいな」
		PRINTFORMW 今日のところはお開きにしよう……
		RETURN
	ENDIF
	
	IF !INRANGE(RESULT, 0, 4)
		制限時間 = GETMILLISECOND() - 現在ミリ秒
		現在ミリ秒 = GETMILLISECOND()
		GOTO INPUT_LOOP
	ENDIF

	選択肢:LOCAL = RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	制限時間 -= GETMILLISECOND() - 現在ミリ秒
	現在ミリ秒 = GETMILLISECOND()

NEXT

PRINTFORMW 作り終えた！
PRINTFORMW 早速%ANAME(ミスティア)%に歌ってもらうとしよう……

PRINTFORML
FOR LOCAL, 0, VARSIZE("選択肢")
	PRINTFORMW 「%歌詞:LOCAL:(選択肢:LOCAL)%～♪」
NEXT
PRINTFORMW

FOR LOCAL, 0, VARSIZE("正解パターン", 1)
	IF 選択肢:0 == 正解パターン:LOCAL:0
		候補パターン = LOCAL
		BREAK
	ENDIF
NEXT

FOR LOCAL, 0, VARSIZE("正解パターン", 1)
	IF 選択肢:LOCAL != 正解パターン:候補パターン:LOCAL
		PRINTFORMW 「……うーん……」
		PRINTFORMW 「なんていうか、ちょっと違うかな」
		PRINTFORMW 「まぁ、そんなポンポンいいの作られても私の立場がないし、気にしないで」
		PRINTFORMW 「また挑戦してくれると嬉しいな！」
		PRINTFORMW どうやら%ANAME(ミスティア)%を納得させる出来ではなかったようだ
		PRINTFORMW 今日のところはお開きにしよう……
		RETURN
	ENDIF
NEXT

PRINTFORMW 「すごーい！　ばっちりだよ、ばっちり！」
PRINTFORMW どうやら%ANAME(ミスティア)%を満足させる出来のものが作れたようだ……
PRINTFORMW 「これなら次のライブで歌えるよ！　さっそく練習してくる！」
PRINTFORMW 「ありがとね、%ANAME(MASTER)%！」
PRINTFORMW %ANAME(ミスティア)%は勢いよく走り去っていった……
PRINTL
CALL COLORPRINT(@"%ANAME(ミスティア)%の好感度が上昇しました", COLOR("注意"), "W")
CALL COLORPRINT(@"%ANAME(MASTER)%の歌唱が5上昇しました", COLOR("注意"), "W")
CALL COLORPRINT(@"%ANAME(ミスティア)%の歌唱が5上昇しました", COLOR("注意"), "W")

CFLAG:ミスティア:好感度 += 300
ABL:MASTER:歌唱 += 5
ABL:ミスティア:歌唱 += 5
KDVAR:ミスティア:ミスティア_作詞 = 1

;------------------------------------
;作詞
;アホみてぇな中身なのに内部処理は妙に大がかりなんDA
;-----------------------------------
@KOJO_DAILY_K25_UNAGI_STALL
#DIM ミスティア

ミスティア = NAME_TO_CHARA("ミスティア")

;ミスティアが別勢力で捕虜でなく面識なし
SIF !CHECK_KOJO_DAILY_HAPPEN(ミスティア, 50, 0, 0, 0)
	RETURN -1

;カルテットと天敵はだめ
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("大妖精"), NAME_TO_CHARA("チルノ"), NAME_TO_CHARA("リグル"), NAME_TO_CHARA("幽々子"))
	RETURN -1

SIF GET_COUNTRY_BOSS(CFLAG:ミスティア:所属) == ミスティア
	RETURN -1

SIF KDVAR:ミスティア:ミスティア_屋台
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORML ……領地の見回りをしていると、なんだか美味しそうな香りが漂ってきた
PRINTFORML どうやら屋台の営業をしているようだ
PRINTFORML 営業許可を出した覚えはないが……
PRINTL
CALL ASK_YN("見に行く", "まあいいだろう")

IF RESULT == 1
	PRINTFORML 目くじらをたてるほどのことでもないだろう
	PRINTFORMW 放っておくことにした
	KDVAR:ミスティア:ミスティア_屋台 = 1
	RETURN 0
ENDIF

PRINTFORML 無許可営業は見過ごせない
PRINTFORMW 見に行くことにした……
PRINTFORMW 「あっ、お客さん？　いらっしゃい！」
PRINTFORML 暖簾をくぐると、夜雀の少女が出迎えた
PRINTFORML 少女は%NAME_FORMAL(ミスティア)%と名乗った
PRINTFORML どうやら八目鰻を焼いて出しているらしい
PRINTFORMW %ANAME(MASTER)%は自分の身分を明かし、このあたりでの営業に許可が必要なことを告げた
PRINTFORML 「えっ！　そうなの？　ごめんなさい、知らなくって……」
PRINTFORML %ANAME(ミスティア)%は眉尻をたれ下げ、申し訳なさそうな表情を浮かべている
PRINTFORML この様子だと、本当に知らなかったのだろう
PRINTFORMW 悪気はなかったようだが、どうしたものか……
PRINTFORML 
CALL ASK_MULTI("鰻で手を打つ", "営業代を回収", "捕らえる")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「……ありがたいけど、いいの？」
		PRINTFORML 無許可営業は問題だが、故意でないなら仕方あるまい
		PRINTFORML 鰻一匹で手を打つと伝えると、%ANAME(ミスティア)%は笑顔で仕込みを始め、焼き立てを渡してくれた
		PRINTFORMW 「ありがと！　熱いから気を付けて食べてね！」
		PRINTFORMW 鰻はなかなかの味だった……
		CFLAG:ミスティア:好感度 += 300
	CASE 1
		PRINTFORMW 「だよね……うーん、捕まらないだけましかぁ」
		PRINTFORMW 故意でないとはいえ規則は規則だ。営業代は払ってもらわなくてはならない
		PRINTFORMW そう伝えると、%ANAME(ミスティア)%は素直に金を差し出した……
		CALL ICPRINT("金<3000>を得ました", "W", COLOR("注意"))
		MONEY += 3000
	CASE 2
		PRINTFORMW 「えっ？　ちょ、ちょっと！」
		PRINTFORMW 悪気の有無など関係ない。違反は違反なのだ
		PRINTFORMW %ANAME(MASTER)%は%ANAME(ミスティア)%を捕らえると、牢獄へと連行した……
		CALL CAPTURE(ミスティア, CFLAG:MASTER:所属)
		CFLAG:ミスティア:好感度 -= 500
		CFLAG:ミスティア:従属度 -= 500
ENDSELECT



CFLAG:ミスティア:面識 = 1
KDVAR:ミスティア:ミスティア_屋台 = 1
