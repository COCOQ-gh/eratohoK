;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K149_MONOZUKI_RATE(対象)
#DIM 対象
RETURN 200


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K149_MONOZUKI_DECISION(対象)
#DIM 対象
#DIM 紫苑

紫苑 = NAME_TO_CHARA("紫苑")

SIF MASTER != NAME_TO_CHARA("あなた")
	RETURN 0

SIF KDVAR:対象:紫苑_物乞い != 2
	RETURN 0

SIF KDVAR:対象:女苑_物好き
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 0, 0) && CHECK_KOJO_DAILY_HAPPEN(紫苑)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K149_MONOZUKI_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K149_MONOZUKI(対象)
#DIM 対象
#DIM 紫苑
紫苑 = NAME_TO_CHARA("紫苑")

PRINTFORMW 「ん、あんたが%ANAME(MASTER)%？」
PRINTFORMW 街を巡回していると、いきなり少女に声をかけられた
PRINTFORMW なんだか豪奢な服装をしているが……
CALL ICPRINT(@"「ああ、私は<%NAME_FORMAL(対象)%>。あんたが拾った%ANAME(紫苑)%の妹よ」", "W", カラー_注意)
PRINTFORMW 「姉さんが何者かは知ってるでしょ？　貧乏神よ？　好き好んで仲間にするなんて、ほんっと奇特だこと」
PRINTFORMW 「そんな物好きの顔を、物見遊山で見に来たってわけ」
PRINTFORMW 「ま、せっかく引き入れたんだから、よくしてあげてよね。それじゃ」
PRINTFORMW 「……ああ、そうそう。まいどあり」
PRINTFORMW 姉のことを心配しているのかいないのか。言うだけ言って、%ANAME(対象)%は立ち去ってしまった
PRINTFORMW ……毎度ありというのは、何のことだったのだろう……
PRINTFORML
PRINTFORMW その後、%ANAME(MASTER)%は無性に散財したくなり、無駄遣いしてしまった
LOCAL = MIN(MONEY / 5, 30000)
MONEY -= LOCAL
CALL ICPRINT(@"金<{LOCAL}>を浪費した", "W", カラー_注意)
CALL COLOR_PRINTW(@"%ANAME(対象)%の面識を得ました", カラー_注意)
CFLAG:対象:面識 = 1
KDVAR:対象:女苑_物好き = 1


RETURN 1
