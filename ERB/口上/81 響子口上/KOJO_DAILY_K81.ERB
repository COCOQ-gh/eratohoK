@KOJO_DAILY_K81()

;ほあよう
CALL KOJO_DAILY_K81_HOAYOU


;----------------------------------
;ほあよう
;----------------------------------
@KOJO_DAILY_K81_HOAYOU
#DIM 響子
#DIM 判定値

響子 = NAME_TO_CHARA("響子")

;響子がいないと駄目
SIF 響子 == -1
	RETURN -1

SIF CFLAG:響子:面識 == 0
	CFLAG:響子:面識 = 1

;150日目以降は確率ダウン
IF DAY:0 < 150
	;響子が同一勢力所属で捕虜でなく死んでいないなら1.5%で発生
	IF !(RAND:1000 < 15 && CFLAG:響子:所属 == CFLAG:MASTER:所属 && !CFLAG:響子:捕虜先 && CFLAG:響子:特殊状態フラグ != 2)
		RETURN -1
	ENDIF
ELSE
	;響子あり同一勢力所属で捕虜でなく死んでいないなら0.5%で発生
	IF !(RAND:1000 < 5 && CFLAG:響子:所属 == CFLAG:MASTER:所属 && !CFLAG:響子:捕虜先 && CFLAG:響子:特殊状態フラグ != 2)
		RETURN -1
	ENDIF
ENDIF

DRAWLINE
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORMW 「ほあようごぁいまーーしゅ！！！！！！！！」
PRINTFORMW 朝早く、%ANAME(MASTER)%が顔を洗っていると、背後で声の暴力が轟いた
CALL ASK_YN("ほあようごあいまーーしゅ！！！！！！！！", "うるせえ！！")
IF RESULT == 0
	PRINTFORMW %ANAME(MASTER)%の元気な返事に満足したのか、%ANAME(響子)%はいい笑顔で次の標的を探しに行った
	PRINTFORMW 今日はいい日になるかもしれない
	CFLAG:響子:好感度 += 10
ELSE
	PRINTFORMW %ANAME(MASTER)%が怒鳴りつけると、%ANAME(響子)%は涙目でどこかへ行ってしまった……
ENDIF