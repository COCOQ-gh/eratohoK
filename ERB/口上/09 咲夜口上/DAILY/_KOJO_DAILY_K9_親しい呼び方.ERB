;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K9_ASK_MASTER_NAME_RATE(対象)
#DIM 対象
RETURN 1000

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K9_ASK_MASTER_NAME_DECISION(対象)
#DIM 対象

;対象口上デイリー入力系使用の設定が「使用する」でなければ戻る
SIF 咲夜_口上デイリー入力系使用 != 2
	RETURN 0

SIF !TALENT:対象:恋人 && !TALENT:対象:烙印
	RETURN 0

;一回きり
SIF KDVAR:対象:咲夜_親しい呼びかた
	RETURN 0


RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K9_ASK_MASTER_NAME_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K9_ASK_MASTER_NAME(対象)
#DIM 対象
#DIM 咲夜_対象
#DIMS NOW_CALLNAME_MASTER_K9
#DIMS NOW_CALLNAME_MASTER_RUBY_K9

;リセット
咲夜_対象 = MASTER


SETCOLOR 咲夜_口上カラー

IF CHECK_K9("敬語")
	PRINTFORML 「%CALLNAME_K9(咲夜_対象)%」
	PRINTFORMDW 拠点の見回りをしていると、%ANAME(対象)%が早足で近寄ってきた
	PRINTL 
	PRINTFORML 「その、お願いがあるんです。……分を弁えないお願いなのですが」
	PRINTFORML 「普段、%CALLNAME_K9(咲夜_対象)%のことを%CALLNAME_K9(咲夜_対象)%と呼ばせて頂いてますが」
	PRINTFORML 「できたら、もっと親しみをこめて呼んでみたいと思うことがあるんです」
	PRINTFORMDW %ANAME(対象)%は落ち着きなく目を泳がせて頬を染めている
	PRINTL 
	PRINTL 
	PRINTFORML 「%CALLNAME_K9(咲夜_対象)%のお気に召さない呼び方はしたくありません」
	PRINTFORML 「お好みの呼び方がありましたら、教えて頂けないでしょうか？」
ELSE
	PRINTFORML 「%CALLNAME_K9(咲夜_対象)%」
	PRINTFORMDW 拠点の見回りをしていると、%ANAME(対象)%が早足で近寄ってきた
	PRINTL 
	PRINTFORML 「その、お願いがあって」
	PRINTFORML 「いつもは私、%CALLNAME_K9(咲夜_対象)%のことを%CALLNAME_K9(咲夜_対象)%って呼んでるじゃない？」
	PRINTFORML 「もうちょっと親しみをこめて呼んでみたいのよね」
	PRINTFORMDW %ANAME(対象)%は頬を染めて微笑んでいる
	PRINTL 
	PRINTFORML 「でも、%CALLNAME_K9(咲夜_対象)%の気に入らない呼び方を勝手に決めたくはないから……」
	PRINTFORML 「好きな呼び方があったら、教えてくれないかしら？」
ENDIF

;CALLNAME_K9で現在呼称判定とフリガナの再取得が行われているのでここでリセット
NOW_CALLNAME_MASTER_K9 '= CALLNAME_K9(咲夜_対象)
NOW_CALLNAME_MASTER_RUBY_K9 '= 咲夜_主人公への呼称フリガナ

LOCAL:0 = LINECOUNT
$SHOW_LOOP

RESETCOLOR
CALL SINGLE_DRAWLINE

SETCOLOR カラー_選択不可
PRINTFORM 教えたい名称を入力・または決定
PRINTL 
PRINTFORM 現在の%ANAME(対象)%から%ANAME(咲夜_対象)%への呼称         : 
RESETCOLOR

PRINTFORM %NOW_CALLNAME_MASTER_K9, 20, LEFT%
PRINTBUTTON "  [入力]", "入力"
PRINTL 

SETCOLOR カラー_選択不可
PRINTFORM 現在の%ANAME(対象)%から%ANAME(咲夜_対象)%への呼称フリガナ : 
RESETCOLOR

PRINTFORM %NOW_CALLNAME_MASTER_RUBY_K9, 20, LEFT%
PRINTBUTTON "  [入力]", "入力フリガナ"
PRINTBUTTON "  [決定]", "決定"

PRINTL 
CALL SINGLE_DRAWLINE

;入力・初期値は"決定"
INPUTS 決定

IF RESULTS == "決定"
	咲夜_主人公への呼称 '= NOW_CALLNAME_MASTER_K9
	咲夜_主人公への呼称フリガナ '= NOW_CALLNAME_MASTER_RUBY_K9
	CLEARLINE LINECOUNT - LOCAL:0
	PRINTL 

ELSEIF RESULTS == "入力"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTFORML 呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_CALLNAME_MASTER_K9
		INPUTS %NOW_CALLNAME_MASTER_K9%
		LOCALS:0 '= RESULTS
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_CALLNAME_MASTER_K9 '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "入力フリガナ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTFORML 呼称フリガナ入力中
	RESETCOLOR
	IF RESULTS != NOW_CALLNAME_MASTER_RUBY_K9
		INPUTS %NOW_CALLNAME_MASTER_RUBY_K9%
		LOCALS:0 '= RESULTS
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_CALLNAME_MASTER_RUBY_K9 '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSE
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP
ENDIF

;※ここでフラグを立てないと下記テキスト内で変更後の呼称に対応できない
KDVAR:対象:咲夜_親しい呼びかた = 1

SETCOLOR カラー_選択不可
PRINTFORML ➡%CALLNAME_K9(咲夜_対象)%（%咲夜_主人公への呼称フリガナ%）
PRINTL 

SETCOLOR 咲夜_口上カラー

IF 咲夜_主人公への呼称 == "" && 咲夜_主人公への呼称フリガナ == ""
	PRINTFORMDW %ANAME(咲夜_対象)%は%ANAME(対象)%にこれまで通り『%CALLNAME_K9(咲夜_対象)%（%咲夜_主人公への呼称フリガナ%）』と呼んで欲しいと伝えた
	PRINTL 
	IF CHECK_K9("敬語")
		PRINTFORML 「かしこまりました。……ちょっと残念ですけれど」
		PRINTFORMW 「今の呼び方を気に入ってくださっているなら、無理に変えることはありませんよね」
		PRINTL 
		PRINTFORML 「聞いてくださってありがとうございます」
		PRINTFORML 「どうぞ、これからもよろしくお願いします。%CALLNAME_K9(咲夜_対象)%❤」
		PRINTFORMDL %ANAME(対象)%から丁寧におじぎをされて別れた
	ELSE
		PRINTFORML 「そう……ちょっと残念だけど」
		PRINTFORMW 「%CALLNAME_K9(咲夜_対象)%って呼び方を気に入ってくれてるなら、無理に変えることもないわよね」
		PRINTL 
		PRINTFORML 「聞いてくれてありがとう。これからもよろしくお願いするわね、%CALLNAME_K9(咲夜_対象)%❤」
		PRINTFORMDL %ANAME(対象)%から頬にキスをされて別れた
	ENDIF
ELSE
	PRINTFORMDW %ANAME(咲夜_対象)%は%ANAME(対象)%に『%CALLNAME_K9(咲夜_対象)%（%咲夜_主人公への呼称フリガナ%）』と呼んで欲しいと伝えた
	PRINTL 
	IF CHECK_K9("敬語")
		PRINTFORML 「%CALLNAME_K9(咲夜_対象)%ですね。%CALLNAME_K9(咲夜_対象)%、%CALLNAME_K9(咲夜_対象)%……」
		PRINTFORMDW %ANAME(対象)%は幸福に満ちた笑顔で%ANAME(咲夜_対象)%が教えた呼び名を口遊んでいる
		PRINTL 
		PRINTFORML 「考えてくださってありがとうございます。立ち話をしてしまってすみません」
		PRINTFORML 「これからもよろしくお願いします。%CALLNAME_K9(咲夜_対象)%❤」
		PRINTFORMDL %ANAME(対象)%から丁寧におじぎをされて別れた

	ELSE
		PRINTFORML 「%CALLNAME_K9(咲夜_対象)%でいいのね。%CALLNAME_K9(咲夜_対象)%、%CALLNAME_K9(咲夜_対象)%……」
		PRINTFORMDW %ANAME(対象)%は幸福に満ちた笑顔で%ANAME(咲夜_対象)%が教えた呼び名を口遊んでいる
		PRINTL 
		PRINTFORML 「考えてくれて嬉しいわ」
		PRINTFORML 「これからもよろしくお願いするわね、%CALLNAME_K9(咲夜_対象)%❤」
		PRINTFORMDL %ANAME(対象)%から頬にキスをされて別れた
	ENDIF
ENDIF
WAIT
RESETCOLOR
CFLAG:対象:好感度 = CFLAG:対象:好感度 + 100
CFLAG:対象:依存度 = CFLAG:対象:依存度 + 100
CFLAG:対象:従属度 = CFLAG:対象:従属度 + 100

RETURN 1

