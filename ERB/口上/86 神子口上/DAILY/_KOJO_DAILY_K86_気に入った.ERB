;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K86_GOODJOB_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K86_GOODJOB_DECISION(対象)
#DIM 対象
#DIM 白蓮
#DIM 青娥

白蓮 = NAME_TO_CHARA("白蓮")
青娥 = NAME_TO_CHARA("青娥")

SIF KDVAR:対象:神子_気にいった
	RETURN 0

SIF KDVAR:白蓮:白蓮_白蓮からの勧誘 != 2
	RETURN 0

SIF IS_LOVER(対象) || IS_SLAVE(対象)
	RETURN 0

SIF CFLAG:青娥:所属 != CFLAG:対象:所属
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K86_GOODJOB_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K86_GOODJOB(対象)
#DIM 対象
#DIM 白蓮
#DIM 青娥

白蓮 = NAME_TO_CHARA("白蓮")
青娥 = NAME_TO_CHARA("青娥")

CFLAG:対象:面識 = 1

PRINTFORMW 「ああ、ちょっと君、えー……%ANAME(MASTER)%、だったか？」
PRINTFORML 廊下を歩いていると、すれ違いざまに%ANAME(対象)%に声をかけられた
PRINTFORMW 「聞いたよ。あの生臭坊主の誘いを断ったそうじゃないか」
PRINTFORML %ANAME(対象)%の言葉に、%ANAME(MASTER)%は面食らう
PRINTFORMW 生臭坊主というのは%ANAME(白蓮)%のことだろう。彼女と会ったことは誰も知らないはずだが……
PRINTFORMW 「何を驚いている？　権力者には、優秀な"目"がつきものだ。私の"目"は、壁をすり抜けられるのさ」
PRINTFORMW 「ああ、後ろめたく思う必要はないよ。むしろ私は気分がいい。あの破戒僧の鼻を明かしてやったんだから」
PRINTFORMW 「あの鼠か、狸か……誰の入れ知恵か知らんが、さぞ悔しがったろうなぁ、くくっ！」
PRINTFORML %ANAME(対象)%は腹を抱え、肩を震わせる。しばしくっくっと笑ってから、不意に真顔に戻った。
PRINTFORML 「まぁ、そういうことだ。君は私への忠誠を示した」
PRINTFORMW 「君、気に入ったよ。私からの君への評価はより高まった。おめでとう」
PRINTFORMW 「話はそれだけだ。ではな」
PRINTFORML 言うだけ言って、%ANAME(対象)%は立ち去っていった
PRINTFORMW 思わぬ収穫になったようだ……
CALL COLORPRINT(@"%ANAME(対象)%の信頼を得ました", カラー_注意, "W")
CFLAG:対象:好感度 += 500
CFLAG:対象:支配度 += 500
CFLAG:対象:依存度 += 100
KDVAR:対象:神子_気にいった = 1

RETURN 1
