@KOJO_DAILY_K10()

;レミリアからの勧誘
CALL KOJO_DAILY_K10_INVITATION_FROM_REMILLIA

;レミリアからの勧誘
@KOJO_DAILY_K10_INVITATION_FROM_REMILLIA
#DIM ボス
#DIM レミリア
#DIM 紅魔館
レミリア = NAME_TO_CHARA("レミリア")
紅魔館 = GET_CITYNUMBER("紅魔館")

SIF レミリア == -1
	RETURN -1

;一回きり
SIF KDVAR:レミリア:レミリア_レミリアからの勧誘
	RETURN -1

;レミリアが君主であり、主人公が君主でない
SIF GET_COUNTRY_BOSS(CFLAG:レミリア:所属) != レミリア || GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
	RETURN -1

SIF 紅魔館 == -1
	RETURN -1

;レミリアと面識がなく、レミリア麾下ではなく、レミリアが捕虜でなく、死んでいない(捕虜と死亡については君主である限りセーフっぽいが） ５%
SIF !(RAND:1000 < 50 && !CFLAG:レミリア:面識 && CFLAG:レミリア:所属 != CFLAG:MASTER:所属 && !CFLAG:レミリア:捕虜先 && CFLAG:レミリア:特殊状態フラグ != 2)
	RETURN -1

;主人公は男であること
SIF !IS_MALE(MASTER)
	RETURN -1

ボス = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

DRAWLINE
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORML %ANAME(MASTER)%が仕事をしていると、部下が手紙を持ってきた
PRINTFORM 差出人は
CALL COLORPRINTFORM(@"%NAME_FORMAL(レミリア)%", COLOR("注意"))
PRINTFORMW 。強力な吸血鬼として聞いたことがあるが、面識はないはずだ
PRINTFORMW 何の用だろうと思って読んでみると、パーティの誘いだった
PRINTFORML 君主ならまだしも、自分のような一介の士官を招いてどうするつもりだろう……
PRINTFORML 疑問でしかないが、紅魔館といえば立派なお屋敷だと聞くし、そこでのパーティといえば各勢力の有力者たちがそろい踏みとなるだろう
PRINTFORMW 出席して顔を売っておくのもいいかもしれない……
PRINTFORML さて、どうしようか……
DRAWLINE
PRINTFORML
CALL ASK_YN("出席する", "やめておく")
IF RESULT == 1
	PRINTFORML お偉方の遊びに付き合ってやる必要もあるまい
	PRINTFORML そう考えた%ANAME(MASTER)%は、欠席にマルを付けて返事を出した……
	KDVAR:レミリア:レミリア_レミリアからの勧誘 = 1
	RETURN 0
ENDIF
PRINTFORML 折角誘われたのだ。行ってみるのも悪くないだろう……
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORML 紅魔館とやらは、噂に聞く以上に赤く、そして大きかった
PRINTFORMW 驚いたのは、その外見以上に中が広々としていたことだ。一体どういう仕組みなのだろう
PRINTFORML それはともかく、パーティはなかなかの賑わいを見せていた
PRINTFORMW 有力者の集いかと思いきや、案外見境なく声をかけていたらしく、妖精まで紛れ込んでいたが……
PRINTFORMW 「紅魔館へようこそ、客人。楽しんでくれているかしら？」
PRINTFORML 一人で酒を楽しんでいると、声をかけられる。青い髪に赤い瞳。背中には蝙蝠の翼。
PRINTFORMW 噂に聞く%NAME_FORMAL(レミリア)%その人だった
PRINTFORMW 挨拶しようとすると、彼女はそれを止める
PRINTFORMW 「堅苦しい挨拶は抜きよ。それより少しお話ししましょう。ついてらっしゃい」
PRINTFORML %ANAME(MASTER)%は%ANAME(レミリア)%に手を引かれ、バルコニーへと連れ出された
PRINTFORMW 今夜は月が綺麗に出ている代わりに肌寒く、そのためかバルコニーには誰もいない
PRINTFORMW ……密談にはちょうどいい場所だ
PRINTFORMW 「私がただ楽しみのためにパーティを開いただなんて、もちろん思ってはないでしょうね？」
PRINTFORML 頷いた。いくら社交的でも、見も知りもしない一介の士官にわざわざ招待状を送りつけるようなことはするまい
PRINTFORMW まして彼女は、いかにも気位の高そうなタイプだ。つまらない者が自分の館に入ることなど、許しはしないだろうし……
PRINTFORMW 「%ANAME(MASTER)%、優秀な武将だと聞いているわ」
PRINTFORMW 「私はそういう人間を手に入れるためなら、金も手間も惜しまない主義よ」
PRINTFORMW 「どうかしら。うちに来ない？　待遇も、%ANAME(ボス)%よりずっとよくしてあげる」
PRINTFORMW 「女がお望みなら、それも……ね」
PRINTFORMW 「ふふ、私も、どうせ成すなら優秀な子のほうがいいもの」
PRINTFORML %ANAME(レミリア)%の笑みは、外見とは不釣り合いな、妖艶なものだった
PRINTFORMW 女というのはつまり、彼女自身のことを示しているのだろう
PRINTFORMW ……それにしても、何かあると思っていたが、まさか引き抜きをかけられるとは
PRINTFORML 驚くほどのことではない。幻想郷は混迷の時代にある。勢力が生き延びるには、優秀な人材は必須だ
PRINTFORML こちらとしても、生き延びていくために、少しでも強力な勢力についておくのが得策だ
PRINTFORML わざわざこうして声をかけてくるほどだ。話を受ければ、その見返りは大層大きいだろう
PRINTFORMW 一方で、今の主君の方面との人間関係は、間違いなくこじれてしまうだろう
PRINTFORMW さて、どうしようか……
DRAWLINE
PRINTFORML %ANAME(レミリア)%の領地数:{GET_OWN_CITY(レミリア)} %ANAME(ボス)%の領地数:{GET_OWN_CITY(CFLAG:MASTER:所属)}
PRINTFORML 
CALL ASK_YN("話にのる", "やめておく")
IF RESULT == 0
	PRINTFORMW 「そう言ってくれると思ってたわ」
	PRINTFORMW 「それじゃあ……よろしくお願いするわね」
	PRINTFORMW 差し出された手を、%ANAME(MASTER)%は堅く握りしめた……
	LOCAL:0 = MIN((ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治) * 200, 50000)
	CALL COLORPRINTFORM(@"支度金として金{LOCAL:0}を受け取りました", COLOR("注意"), "W")
	CALL COLORPRINTFORM(@"%ANAME(レミリア)%の同意を得ました", COLOR("桃"), "W")
	CALL COLORPRINTFORM(@"%ANAME(ボス)%との外交関係が、非常にこじれました", COLOR("警告"), "W")
	MONEY += LOCAL:0
	TALENT:レミリア:合意 = 1
	CFLAG:レミリア:面識 = 1
	CFLAG:レミリア:好感度 = MAX(CFLAG:レミリア:好感度, 800)
	CFLAG:レミリア:依存度 = MAX(CFLAG:レミリア:依存度, 200)
	CALL CHANGE_RELATION_O_TO_O(ボス, レミリア, -500, 500)
	CALL CHANGE_RELATION_O_TO_O(ボス, MASTER, -500, 500)
	CFLAG:MASTER:所属 = CFLAG:レミリア:所属
ELSE
	PRINTFORML 「あら、そう」
	PRINTFORML 「ちょっと意外だわ。乗ってくれるものとばかり思ってたけど」
	PRINTFORML 「ま、仕方ないわね。そういうことなら、諦めるわ」
	PRINTFORML この手の話を断った以上、刃傷沙汰になるのを覚悟していたが、%ANAME(レミリア)%はあっさりと諦めた
	PRINTFORML 意外に思っていると、%ANAME(レミリア)%は唇を尖らせる
	PRINTFORML 「パーティに招いた客に暴力を振るうほど、私は無粋ではないわよ」
	PRINTFORML 「寒いところに連れ出して悪かったわね。パーティを楽しんでちょうだい」
	PRINTFORML その後、ホールに戻った%ANAME(MASTER)%は、今の話のことを胸にしまってパーティを楽しんだ……
ENDIF
KDVAR:レミリア:レミリア_レミリアからの勧誘 = 1