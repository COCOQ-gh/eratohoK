@KOJO_DAILY_EXIST_K10()

@KOJO_DAILY_K10()
#DIM 発生フラグ
発生フラグ = 0

;レミリアからの勧誘
DAILY_NAME = レミリアからの勧誘
CALL KOJO_DAILY_K10_INVITATION_FROM_REMILLIA
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;あいにくの天気
DAILY_NAME = あいにくの天気
CALL KOJO_DAILY_K10_BAD_WEATHER
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

RETURN 発生フラグ

;------------------------------------
;レミリアからの勧誘
;-----------------------------------
@KOJO_DAILY_K10_INVITATION_FROM_REMILLIA
#DIM ボス
#DIM レミリア
#DIM 紅魔館
レミリア = NAME_TO_CHARA("レミリア")
紅魔館 = GET_CITYNUMBER("紅魔館")

;レミリアと別勢力で、捕虜でなく、面識もない 5%
SIF !(CHECK_KOJO_DAILY_HAPPEN(レミリア, 50, 0, 0, 0) && CFLAG:レミリア:特殊状態 != 2)
	RETURN -1

;一回きり
SIF KDVAR:レミリア:レミリア_レミリアからの勧誘
	RETURN -1

;主人公は男であること
SIF !IS_MALE(MASTER)
	RETURN -1

;レミリアが君主であり、主人公が君主でない
SIF GET_COUNTRY_BOSS(CFLAG:レミリア:所属) != レミリア || GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
	RETURN -1

SIF 紅魔館 == -1
	RETURN -1

ボス = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

CALL KOJO_DAILY_START()

PRINTFORML %ANAME(MASTER)%が仕事をしていると、部下が手紙を持ってきた
PRINTFORM 差出人は
CALL COLORPRINT(@"%NAME_FORMAL(レミリア)%", カラー_注意)
PRINTFORMW 。強力な吸血鬼として聞いたことがあるが、面識はないはずだ
PRINTFORMW 何の用だろうと思って読んでみると、パーティの誘いだった
PRINTFORML 君主ならまだしも、自分のような一介の士官を招いてどうするつもりだろう……
PRINTFORML 疑問でしかないが、紅魔館といえば立派なお屋敷だと聞くし、そこでのパーティといえば各勢力の有力者たちがそろい踏みとなるだろう
PRINTFORMW 出席して顔を売っておくのもいいかもしれない……
PRINTFORML さて、どうしようか……
PRINTFORML
CALL ASK_YN("出席する", "やめておく")
IF RESULT == 1
	PRINTFORML お偉方の遊びに付き合ってやる必要もあるまい
	PRINTFORML そう考えた%ANAME(MASTER)%は、欠席にマルを付けて返事を出した……
	KDVAR:レミリア:レミリア_レミリアからの勧誘 = 1
	RETURN 0
ENDIF
PRINTFORML 折角誘われたのだ。行ってみるのも悪くないだろう……
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORML 紅魔館とやらは、噂に聞く以上に赤く、そして大きかった
PRINTFORMW 驚いたのは、その外見以上に中が広々としていたことだ。一体どういう仕組みなのだろう
PRINTFORML それはともかく、パーティはなかなかの賑わいを見せていた
PRINTFORMW 有力者の集いかと思いきや、案外見境なく声をかけていたらしく、妖精まで紛れ込んでいたが……
PRINTFORMW 「紅魔館へようこそ、客人。楽しんでくれているかしら？」
PRINTFORML 一人で酒を楽しんでいると、声をかけられる。青い髪に赤い瞳。背中には蝙蝠の翼。
PRINTFORMW 噂に聞く%NAME_FORMAL(レミリア)%その人だった
PRINTFORMW 挨拶しようとすると、彼女はそれを止める
PRINTFORMW 「堅苦しい挨拶は抜きよ。それより少しお話ししましょう。ついてらっしゃい」
PRINTFORML %ANAME(MASTER)%は%ANAME(レミリア)%に手を引かれ、バルコニーへと連れ出された
PRINTFORMW 今夜は月が綺麗に出ている代わりに肌寒く、そのためかバルコニーには誰もいない
PRINTFORMW ……密談にはちょうどいい場所だ
PRINTFORMW 「私がただ楽しみのためにパーティを開いただなんて、もちろん思ってはないでしょうね？」
PRINTFORML 頷いた。いくら社交的でも、見も知りもしない一介の士官にわざわざ招待状を送りつけるようなことはするまい
PRINTFORMW まして彼女は、いかにも気位の高そうなタイプだ。つまらない者が自分の館に入ることなど、許しはしないだろうし……
PRINTFORMW 「%ANAME(MASTER)%、優秀な武将だと聞いているわ」
PRINTFORMW 「私はそういう人間を手に入れるためなら、金も手間も惜しまない主義よ」
PRINTFORMW 「どうかしら。うちに来ない？　待遇も、%ANAME(ボス)%よりずっとよくしてあげる」
PRINTFORMW 「女がお望みなら、それも……ね」
PRINTFORMW 「ふふ、私も、どうせ成すなら優秀な子のほうがいいもの」
PRINTFORML %ANAME(レミリア)%の笑みは、外見とは不釣り合いな、妖艶なものだった
PRINTFORMW 女というのはつまり、彼女自身のことを示しているのだろう
PRINTFORMW ……それにしても、何かあると思っていたが、まさか引き抜きをかけられるとは
PRINTFORML 驚くほどのことではない。幻想郷は混迷の時代にある。勢力が生き延びるには、優秀な人材は必須だ
PRINTFORML こちらとしても、生き延びていくために、少しでも強力な勢力についておくのが得策だ
PRINTFORML わざわざこうして声をかけてくるほどだ。話を受ければ、その見返りは大層大きいだろう
PRINTFORMW 一方で、今の主君の方面との人間関係は、間違いなくこじれてしまうだろう
PRINTFORMW さて、どうしようか……
CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(レミリア)%の領地数:{GET_OWN_CITY(CFLAG:レミリア:所属)} %ANAME(ボス)%の領地数:{GET_OWN_CITY(CFLAG:MASTER:所属)}
PRINTFORML 
CALL ASK_YN("話にのる", "やめておく")
IF RESULT == 0
	PRINTFORMW 「そう言ってくれると思ってたわ」
	PRINTFORMW 「それじゃあ……よろしくお願いするわね」
	PRINTFORMW 差し出された手を、%ANAME(MASTER)%は堅く握りしめた……
	LOCAL:0 = MIN((ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + ABL:MASTER:防衛) * 200, 50000)
	CALL COLORPRINT(@"支度金として金{LOCAL:0}を受け取りました", カラー_注意, "W")
	CALL COLORPRINT(@"%ANAME(レミリア)%の合意を得ました", カラー_ピンク, "W")
	CALL COLORPRINT(@"%ANAME(ボス)%との外交関係が、非常にこじれました", カラー_警告, "W")
	MONEY += LOCAL:0
	TALENT:レミリア:合意 = 1
	TALENT:レミリア:チョロイン = 1
	CFLAG:レミリア:面識 = 1
	CFLAG:レミリア:好感度 = MAX(CFLAG:レミリア:好感度, 800)
	CFLAG:レミリア:依存度 = MAX(CFLAG:レミリア:依存度, 200)
	CALL CHANGE_RELATION_O_TO_O(ボス, レミリア, -500, 500)
	CALL CHANGE_RELATION_O_TO_O(ボス, MASTER, -500, 500)
	CALL CHANGE_COUNTRY(MASTER, CFLAG:レミリア:所属, 1)
	KDVAR:レミリア:レミリア_レミリアからの勧誘 = 1
ELSE
	PRINTFORMW 「あら、そう」
	PRINTFORMW 「ちょっと意外だわ。乗ってくれるものとばかり思ってたけど」
	PRINTFORMW 「ま、仕方ないわね。そういうことなら、諦めるわ」
	PRINTFORML この手の話を断った以上、刃傷沙汰になるのを覚悟していたが、%ANAME(レミリア)%はあっさりと諦めた
	PRINTFORMW 意外に思っていると、%ANAME(レミリア)%は唇を尖らせる
	PRINTFORMW 「パーティに招いた客に暴力を振るうほど、私は無粋ではないわよ」
	PRINTFORMW 「寒いところに連れ出して悪かったわね。パーティを楽しんでちょうだい」
	PRINTFORMW その後、ホールに戻った%ANAME(MASTER)%は、今の話のことを胸にしまってパーティを楽しんだ……
	KDVAR:レミリア:レミリア_レミリアからの勧誘 = 2
ENDIF

;------------------------------------
;あいにくの天気
;-----------------------------------
@KOJO_DAILY_K10_BAD_WEATHER
#DIM レミリア

レミリア = NAME_TO_CHARA("レミリア")

SIF !CHECK_KOJO_DAILY_HAPPEN(レミリア, 35, 1, 0, 1)
	RETURN -1

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("美鈴"), NAME_TO_CHARA("咲夜"), NAME_TO_CHARA("小悪魔"), NAME_TO_CHARA("フランドール"))
	RETURN -1

SIF !IS_LOVER(レミリア)
	RETURN -1

SIF KDVAR:レミリア:レミリア_あいにくの天気
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORML %ANAME(MASTER)%は%ANAME(レミリア)%と過ごしている……
PRINTFORMW といっても、あいにくの天気だったのため、屋内でだが
PRINTFORML 「やれやれ。この国ときたらすぐ降り始めるんだから」
PRINTFORMW 「嫌になるわね。そう思わない？」
PRINTFORMW ふたりで外出する予定だったのを潰され、%ANAME(レミリア)%は不満そうだ……
PRINTFORML

CALL ASK_MULTI("そうだな", "そうかな", "雨は嫌いか？")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「やっぱりそう思うわよね」
		PRINTFORMW 「いっそフランにでも言って、雨雲を破壊してもらおうかしら……」
		PRINTFORML そんなことをしたら異変扱いをされて、巫女が乗り込んでくるだろう
		PRINTFORMW 物騒な考えに至った%ANAME(レミリア)%を、どうにかなだめた……
	CASE 1
		PRINTFORMW 「む、なによ、私と出かけたくなかったの？」
		PRINTFORMW 「そんなこという奴のことは聞かないわ、もう」
		PRINTFORMW 唇を尖らせてすねる%ANAME(レミリア)%を、どうにかなだめた……
	CASE 2
		PRINTFORMW 「まぁね。吸血鬼の弱点……流れる水だし」
		PRINTFORMW 「それに、退屈でしょう？　外に出られないから、家の中でじっとしてるなんて」
		PRINTFORMW 「……でも実のところ、最近はそうでもないのよね」
		PRINTFORMW 「だって、%ANAME(MASTER)%がいるでしょう？」
		PRINTFORMW 「恋人と一緒に過ごしてて退屈だなんて、罰があたると思わない？」
		PRINTFORML ……嬉しいことを言ってくれる
		PRINTFORMW その後も、雨がやむまで二人で過ごした……
		CFLAG:レミリア:好感度 += 100
ENDSELECT

KDVAR:レミリア:レミリア_あいにくの天気 = 1
RETURN 

;----------------------------------
;愛称を教えて
;----------------------------------
@KOJO_DAILY_K10_ASK_MASTER_NAME
#DIM レミリア_対象
#DIM レミリア
#DIMS NOW_CALLNAME_MASTER_K10
#DIMS NOW_CALLNAME_MASTER_RUBY_K10

;リセット
レミリア_対象 = MASTER
レミリア = NAME_TO_CHARA("レミリア")

;レミリア口上デイリー入力系使用の設定が「使用する」でなければ戻る
SIF レミリア_口上デイリー入力系使用 != 2
	RETURN -1

;レミリアと同じ勢力、レミリアが捕虜でない、100%
SIF !(CHECK_KOJO_DAILY_HAPPEN(レミリア, 1000, 1, 0))
	RETURN -1

SIF !TALENT:レミリア:恋人 && !TALENT:レミリア:烙印
	RETURN -1

;一回きり
SIF KDVAR:レミリア:レミリア_愛称を教えて
	RETURN -1

CALL KOJO_DAILY_START()

SETCOLOR レミリア_口上カラー

PRINTFORML 「%CALLNAME_K10(レミリア_対象)%」
PRINTFORMDW 拠点の見回りをしていると、バルコニーからこちらに手招きしている%ANAME(レミリア)%が目に入った
PRINTL 
PRINTFORML 「ちょっとこっちへ来て。聞きたいことがあるの」
PRINTFORML 「ねぇ、%CALLNAME_K10(レミリア_対象)%って誰かに愛称をつけてもらったことはある？」
PRINTFORMDW %ANAME(レミリア)%はバルコニーから落ちそうに身を乗り出して、目を輝かせている
PRINTL 
PRINTFORML 「気に入ってる呼ばれ方があったら、私にも教えてよ」

;CALLNAME_K10で現在呼称判定とフリガナの再取得が行われているのでここでリセット
NOW_CALLNAME_MASTER_K10 '= CALLNAME_K10(レミリア_対象)
NOW_CALLNAME_MASTER_RUBY_K10 '= レミリア_主人公への呼称フリガナ

LOCAL:0 = LINECOUNT
$SHOW_LOOP

RESETCOLOR
CALL SINGLE_DRAWLINE

SETCOLOR カラー_選択不可
PRINTFORM 教えたい名称を入力・または決定
PRINTL 
PRINTFORM 現在の%ANAME(レミリア)%から%ANAME(レミリア_対象)%への呼称         : 
RESETCOLOR

PRINTFORM %NOW_CALLNAME_MASTER_K10, 20, LEFT%
PRINTBUTTON "  [入力]", "入力"
PRINTL 

SETCOLOR カラー_選択不可
PRINTFORM 現在の%ANAME(レミリア)%から%ANAME(レミリア_対象)%への呼称フリガナ : 
RESETCOLOR

PRINTFORM %NOW_CALLNAME_MASTER_RUBY_K10, 20, LEFT%
PRINTBUTTON "  [入力]", "入力フリガナ"
PRINTBUTTON "  [決定]", "決定"

PRINTL 
CALL SINGLE_DRAWLINE

;入力・初期値は"決定"
INPUTS 決定

IF RESULTS == "決定"
	レミリア_主人公への呼称 '= NOW_CALLNAME_MASTER_K10
	レミリア_主人公への呼称フリガナ '= NOW_CALLNAME_MASTER_RUBY_K10
	CLEARLINE LINECOUNT - LOCAL:0
	PRINTL 

ELSEIF RESULTS == "入力"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTFORML 呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_CALLNAME_MASTER_K10
		INPUTS %NOW_CALLNAME_MASTER_K10%
		LOCALS:0 '= RESULTS
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_CALLNAME_MASTER_K10 '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "入力フリガナ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTFORML 呼称フリガナ入力中
	RESETCOLOR
	IF RESULTS != NOW_CALLNAME_MASTER_RUBY_K10
		INPUTS %NOW_CALLNAME_MASTER_RUBY_K10%
		LOCALS:0 '= RESULTS
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_CALLNAME_MASTER_RUBY_K10 '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSE
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP
ENDIF

;※ここでフラグを立てないと下記テキスト内で変更後の呼称に対応できない
KDVAR:レミリア:レミリア_愛称を教えて = 1

SETCOLOR カラー_選択不可
PRINTFORML ➡%CALLNAME_K10(レミリア_対象)%（%レミリア_主人公への呼称フリガナ%）
PRINTL 

SETCOLOR レミリア_口上カラー

IF レミリア_主人公への呼称 == "" && レミリア_主人公への呼称フリガナ == ""
	PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(レミリア)%にこれまで通り『%CALLNAME_K10(レミリア_対象)%（%レミリア_主人公への呼称フリガナ%）』と呼ぶように伝えた
	PRINTL 
	PRINTFORML 「ないの？　つまんないわ」
	PRINTFORMW 「でも、%CALLNAME_K10(レミリア_対象)%って呼ぶのも好きよ。そのままでもいいわね」
	PRINTL 
	PRINTFORML 「じゃ、また遊びに来なさい。待っているわ。%CALLNAME_K10(レミリア_対象)%」
	PRINTFORMDL %ANAME(レミリア)%は大きく手を振って部屋に戻った
ELSE
	PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(レミリア)%に『%CALLNAME_K10(レミリア_対象)%（%レミリア_主人公への呼称フリガナ%）』と呼ぶように伝えた
	PRINTL 
	PRINTFORML 「%CALLNAME_K10(レミリア_対象)%？　それがお気に入り？」
	PRINTFORMW 「わかったわ。%CALLNAME_K10(レミリア_対象)%ね、%CALLNAME_K10(レミリア_対象)%。憶えたわ」
	PRINTL 
	PRINTFORML 「じゃ、また遊びに来なさい。待っているわ。%CALLNAME_K10(レミリア_対象)%」
	PRINTFORMDL %ANAME(レミリア)%は大きく手を振って部屋に戻った
ENDIF
WAIT
RESETCOLOR
CFLAG:レミリア:好感度 = CFLAG:レミリア:好感度 + 100
CFLAG:レミリア:依存度 = CFLAG:レミリア:依存度 + 100
CFLAG:レミリア:従属度 = CFLAG:レミリア:従属度 + 100

;----------------------------------
;ご褒美をあげる
;----------------------------------
@KOJO_DAILY_K10_ASK_DIRTY_NAME
#DIM レミリア_対象
#DIM レミリア
#DIMS BEFORE_DIRTY_P
#DIMS BEFORE_DIRTY_V
#DIMS BEFORE_DIRTY_A
#DIMS BEFORE_DIRTY_C
#DIMS BEFORE_DIRTY_B
#DIMS NOW_DIRTY_P
#DIMS NOW_DIRTY_V
#DIMS NOW_DIRTY_A
#DIMS NOW_DIRTY_C
#DIMS NOW_DIRTY_B

;リセット
レミリア_対象 = MASTER
レミリア = NAME_TO_CHARA("レミリア")

;レミリア口上デイリー入力系使用の設定が「使用する」でなければ戻る
SIF レミリア_口上デイリー入力系使用 != 2
	RETURN -1

;レミリアと同じ勢力、レミリアが捕虜でない、50%
SIF !(CHECK_KOJO_DAILY_HAPPEN(レミリア, 500, 1, 0))
	RETURN -1

;奴隷でも恋人でもない、または合意がなければ戻る
SIF ( !IS_SLAVE(レミリア) && !IS_LOVER(レミリア) ) || !TALENT:レミリア:合意
	RETURN -1

;一回きり
SIF KDVAR:レミリア:レミリア_ご褒美をあげる
	RETURN -1

CALL KOJO_DAILY_START()

SETCOLOR レミリア_口上カラー

;ロードデータ用の初期化
レミリア_淫語頻度 = 0
レミリア_淫語Ｐ '= "あれ"
レミリア_淫語Ｖ '= "なか"
レミリア_淫語Ｃ '= "そこ"
レミリア_淫語Ａ '= "そこ"
レミリア_淫語Ｂ '= "そこ"

;リセット
BEFORE_DIRTY_P '= レミリア_淫語Ｐ
BEFORE_DIRTY_V '= レミリア_淫語Ｖ
BEFORE_DIRTY_A '= レミリア_淫語Ａ
BEFORE_DIRTY_C '= レミリア_淫語Ｃ
BEFORE_DIRTY_B '= レミリア_淫語Ｂ
NOW_DIRTY_P '= レミリア_淫語Ｐ
NOW_DIRTY_V '= レミリア_淫語Ｖ
NOW_DIRTY_A '= レミリア_淫語Ａ
NOW_DIRTY_C '= レミリア_淫語Ｃ
NOW_DIRTY_B '= レミリア_淫語Ｂ

PRINTFORMDL 目を覚ますと、ドアの隙間から手紙が差し込まれていた
PRINTFORMDL 封蝋に%ANAME(レミリア)%の指輪印章が捺されている
PRINTFORMDW 部屋への招待状――どうやら呼び出しの手紙のようだ
PRINTL 
PRINTFORML 「来たわね、%CALLNAME_K10(レミリア_対象)%。喜びなさい」
PRINTFORMDL %ANAME(レミリア_対象)%が部屋に入ると、%ANAME(レミリア)%は目を輝かせて微笑んだ
PRINTFORMDW いつもより豪華な菓子がテーブルに飾られている
PRINTL 
PRINTFORML 「あなたにご褒美をあげる――今日からこの、%ANAME(レミリア)%が」
PRINTFORML 「%CALLNAME_K10(レミリア_対象)%の大好きな、卑しい言葉を使ってあげるわ！」
PRINTFORMDW %ANAME(レミリア)%は得意満面で声高らかに言い放った
PRINTL 
PRINTFORML 「……？　喜ばないの？　反応が薄いわね」
PRINTFORML 「たまに私に言ってもらおうとしてなかった？」
PRINTFORML 「何か特別な意味があるかと思っていたのだけど……違ったの？」
PRINTFORMDW %ANAME(レミリア)%はきょとんとして首を傾げている
PRINTL 
PRINTFORML 「あまり卑しい言葉は使いたくないのだけど」
PRINTFORML 「%CALLNAME_K10(レミリア_対象)%が気持ちよくなるなら、私も愉しいし」
PRINTFORMW 「好きな呼び方を言ってみていいのよ？　言いなさい」

LOCAL:0 = LINECOUNT
$SHOW_LOOP
RESETCOLOR
CALL SINGLE_DRAWLINE
CALL ICPRINT("<教えたい名称を><全角仮名><で入力・または決定する（平仮名・片仮名はどちらでも可能）>", "L", カラー_選択不可, カラー_オレンジ, カラー_選択不可)
PRINTL 
PRINTFORM   Ｐ呼称 : 
RESETCOLOR
PRINTFORM %NOW_DIRTY_P, 20, LEFT%
PRINTBUTTON "  [入力]", "入力Ｐ"
PRINTL 
PRINTFORM   Ｖ呼称 : 
RESETCOLOR
PRINTFORM %NOW_DIRTY_V, 20, LEFT%
PRINTBUTTON "  [入力]", "入力Ｖ"
PRINTL 
PRINTFORM   Ｃ呼称 : 
RESETCOLOR
PRINTFORM %NOW_DIRTY_C, 20, LEFT%
PRINTBUTTON "  [入力]", "入力Ｃ"
PRINTL 
PRINTFORM   Ａ呼称 : 
RESETCOLOR
PRINTFORM %NOW_DIRTY_A, 20, LEFT%
PRINTBUTTON "  [入力]", "入力Ａ"
PRINTL 
PRINTFORM   Ｂ呼称 : 
RESETCOLOR
PRINTFORM %NOW_DIRTY_B, 20, LEFT%
PRINTBUTTON "  [入力]", "入力Ｂ"

PRINTBUTTON "  [決定]", "決定"
PRINTL 
CALL SINGLE_DRAWLINE

;初期値指定入力
INPUTS 決定

IF RESULTS == "決定"
	レミリア_淫語Ｐ '= NOW_DIRTY_P
	レミリア_淫語Ｖ '= NOW_DIRTY_V
	レミリア_淫語Ａ '= NOW_DIRTY_A
	レミリア_淫語Ｃ '= NOW_DIRTY_C
	レミリア_淫語Ｂ '= NOW_DIRTY_B
	CLEARLINE LINECOUNT - LOCAL:0
	PRINTL 

ELSEIF RESULTS == "入力Ｐ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTL Ｐ呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_DIRTY_P
		INPUTS %NOW_DIRTY_P%
		LOCALS:0 = %RESULTS%
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_DIRTY_P '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "入力Ｖ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTL Ｖ呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_DIRTY_V
		INPUTS %NOW_DIRTY_V%
		LOCALS:0 = %RESULTS%
		SUBSTRING LOCALS:0, 0, 20
		NOW_DIRTY_V '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "入力Ａ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTL Ａ呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_DIRTY_A
		INPUTS %NOW_DIRTY_A%
		LOCALS:0 = %RESULTS%
		SUBSTRING LOCALS:0, 0, 20
		NOW_DIRTY_A '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "入力Ｃ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTL Ｃ呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_DIRTY_C
		INPUTS %NOW_DIRTY_C%
		LOCALS:0 = %RESULTS%
		SUBSTRING LOCALS:0, 0, 20
		NOW_DIRTY_C '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "入力Ｂ"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTL Ｂ呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_DIRTY_B
		INPUTS %NOW_DIRTY_B%
		LOCALS:0 = %RESULTS%
		SUBSTRING LOCALS:0, 0, 20
		NOW_DIRTY_B '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSE
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP
ENDIF

SETCOLOR カラー_選択不可
PRINTFORML ➡Ｐ呼称 : %レミリア_淫語Ｐ%
PRINTFORML   Ｖ呼称 : %レミリア_淫語Ｖ%
PRINTFORML   Ａ呼称 : %レミリア_淫語Ａ%
PRINTFORML   Ｃ呼称 : %レミリア_淫語Ｃ%
PRINTFORML   Ｂ呼称 : %レミリア_淫語Ｂ%
PRINTL 

SETCOLOR レミリア_口上カラー

IF NOW_DIRTY_P == BEFORE_DIRTY_P && NOW_DIRTY_V == BEFORE_DIRTY_V && NOW_DIRTY_A == BEFORE_DIRTY_A && NOW_DIRTY_C == BEFORE_DIRTY_C && NOW_DIRTY_B == BEFORE_DIRTY_B
	PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(レミリア)%に全てこれまで通りに呼んで欲しいと伝えた
	PRINTL 
	PRINTFORML 「なあに。いらないの？　つまんないわ。せっかく愉しませてあげたかったのに」
	PRINTFORML 「でも、%CALLNAME_K10(レミリア_対象)%は高貴な私が好きということね。それならそれでいいのよ」
	PRINTFORML 「せっかく来てくれたんだもの。寛いでいって」
	PRINTFORMDL %ANAME(レミリア)%はひとり満足そうに言い、用件は済んだとばかりにお茶を飲み始めた
ELSE
	PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(レミリア)%に呼び方を伝えた
	PRINTL 
	PRINTFORML 「それが%CALLNAME_K10(レミリア_対象)%の好きな呼び方なのね。憶えたわ」
	PRINTFORML 「たくさん呼んでもらえると嬉しいの？」
	CALL KOJO_ASK("そのとおり", "そうでもない")
	IF RESULT == 0
		PRINTFORML 「いいわ。次の機会を愉しみにしていなさい」
		PRINTFORML 「%CALLNAME_K10(レミリア_対象)%をたくさん悦ばせてあげる」
		PRINTFORML 「せっかく来てくれたんだもの。寛いでいって」
		PRINTFORMDL %ANAME(レミリア)%はひとり満足そうに言い、用件は済んだとばかりにお茶を飲み始めた
		レミリア_淫語頻度 = 2
	ELSE 
		PRINTFORML 「複雑なのね。わかったわ。次の機会を愉しみにしていなさい」
		PRINTFORML 「せっかく来てくれたんだもの。寛いでいって」
		PRINTFORMDL %ANAME(レミリア)%はひとり満足そうに言い、用件は済んだとばかりにお茶を飲み始めた
		レミリア_淫語頻度 = 1
	ENDIF
ENDIF
WAIT
RESETCOLOR
CFLAG:レミリア:好感度 = CFLAG:レミリア:好感度 + 100
CFLAG:レミリア:依存度 = CFLAG:レミリア:依存度 + 100
CFLAG:レミリア:従属度 = CFLAG:レミリア:従属度 + 100
KDVAR:レミリア:レミリア_ご褒美をあげる = 1

