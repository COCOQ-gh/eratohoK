;─────────────────────────────────────── 
;■日常_実行_実行前
;─────────────────────────────────────── 
@KOJO_K10_NOON_BEFORE_PLAYER(レミリア_対象)
#DIM レミリア
#DIM レミリア_対象
#DIMS レミリア機嫌

IF !レミリア_対象
	レミリア_対象 = MASTER
ENDIF

レミリア = NAME_TO_CHARA("レミリア")
レミリア機嫌 '= TOSTR_EMOTION(レミリア)

;─────────────────────────────────────── 
;●機嫌が悪ければ通常のコマンド口上は喋らない
;─────────────────────────────────────── 
SELECTCASE レミリア機嫌
	CASE "恨", "怒", "憤"
		IF PALAM:レミリア:怒主 <= PALAM:レミリア:怒外
			PRINTDATAL
				DATAFORM 「なんだかいらいらしていたの。くっついていて」
				DATAFORM 「いらいらしているの。%CALLNAME_K10(レミリア_対象)%に怒ってるのとは違うわ。聞いてくれる？」
				DATAFORM 「気に入らないことがあるのよ。聞いて」
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 「気にいらないわ」
				DATAFORM 「譲ってあげる気分じゃないの」
			ENDDATA
		ENDIF
		RETURN 0

	CASE "鬱", "悲", "憂"
		IF PALAM:レミリア:哀主 <= PALAM:レミリア:哀外
			PRINTDATAL
				DATAFORM 「なんだか悲しい気持ちだったの。くっついていて」
				DATAFORM 「つまんなかったの。一緒に遊んで」
				DATAFORM 「いっぱい抱きついていい？　悲しいことがあったの」
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 「そんなの、つまんないわ」
				DATAFORM 「何がそんなに気にいらないの？」
				DATAFORM 「…………」
			ENDDATA
		ENDIF
		RETURN 0

	CASE  "狂", "恐", "怯"
		IF PALAM:レミリア:怖主 <= PALAM:レミリア:怖外
			PRINTDATAL
				DATAFORM 「%CALLNAME_K10(レミリア_対象)%、私こういうの知らないわ」
				DATAFORM 「%CALLNAME_K10(レミリア_対象)%、ちゃんと撫でて」
				DATAFORM 「%CALLNAME_K10(レミリア_対象)%、私を助けてくれるでしょう？」
			ENDDATA
		ELSE
		PRINTDATAL
			DATAFORM 「…………」
			DATAFORM 「いやよ」
			DATAFORM 「よくわからないわ」
			DATAFORM 「そんな気分じゃないわ」
			DATAFORM 「ヘンよ」
		ENDDATA
		ENDIF
		RETURN 0
	CASEELSE
ENDSELECT

;─────────────────────────────────────── 
;●同一コマンド
;─────────────────────────────────────── 
IF SELECTCOM == PREVCOM && RAND:10 == 0
	;レミリアに主導権なし
	IF !IS_INITIATIVE(レミリア)
		PRINTDATAL
			DATAFORM 「もっと%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいでしょう」
			DATAFORM 「もっと？　そうね。いいわ」
			DATAFORM 「まだ？　いいわよ。%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%ましょう」
			DATAFORM 「%CALLNAME_K10(レミリア_対象)%以外に主導権は絶対に握らせないわ」
		ENDDATA
	;レミリアに主導権あり
	ELSE
		PRINTDATAL
			DATAFORM 「もっと%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%ましょう」
			DATAFORM 「もっと」
			DATAFORM 「主導権は握らせないわ」
			DATAFORM 「もっと%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいでしょう？」
		ENDDATA
	ENDIF
;─────────────────────────────────────── 
;●同一コマンドでない
;─────────────────────────────────────── 
ELSEIF RAND:10 == 0
	;レミリアに主導権なし
	IF !IS_INITIATIVE(レミリア)
		PRINTDATAL
			DATAFORM 「%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいのね。いいわ」
			DATAFORM 「そうね。%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%ましょう。わかってるじゃない」
		ENDDATA
	;レミリアに主導権あり
	ELSE
		PRINTDATAL
			DATAFORM 「%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%ましょう。いいわよね？」
			DATAFORM 「%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいでしょう？」
		ENDDATA
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●スキンシップ・背中を洗う
;─────────────────────────────────────── 
IF GROUPMATCH(SELECTCOM, 320, 358)
	;レミリアに主導権なし
	IF !IS_INITIATIVE(レミリア)
		PRINTDATAL
			DATAFORM 「こう？　……くすぐったいの？　このくらい我慢なさい」
			DATAFORM 「こうね。……落ち着くわ」
			DATAFORM 「こんな感じかしらね」
		ENDDATA
	;レミリアに主導権あり
	ELSE
		PRINTDATAL
			DATAFORM 「くすぐったいの？　このくらい我慢なさい」
			DATAFORM 「もっと傍に来て」
			DATAFORM 「もっとくっついてなさい」
			DATAFORM 「遠いわ。もっとこっちよ」
			DATAFORM 「お人形さん遊びさせてぇ」
		ENDDATA
	ENDIF
	RETURN 0
ENDIF

;─────────────────────────────────────── 
;●頭を撫でる
;─────────────────────────────────────── 
IF GROUPMATCH(SELECTCOM, 321)
	;レミリアに主導権なし
	IF !IS_INITIATIVE(レミリア)
		PRINTDATAL
			DATAFORM 「いいわ。もっと屈んで？」
			DATAFORM 「ふふ。よくできたわね。偉いわ」
			DATAFORM 「いいわ。褒めてあげる。いい子よ」
			DATAFORM 「そうなの。偉かったわね」
		ENDDATA
	;レミリアに主導権あり
	ELSE
		PRINTDATAL
			DATAFORM 「もっと屈んで？　撫でにくいわ」
			DATAFORM 「褒めてあげる。いい子、偉いわ」
			DATAFORM 「頑張ったのね。偉かったわね」
		ENDDATA
	ENDIF
	RETURN 0
ENDIF

;─────────────────────────────────────── 
;●髪を梳く・櫛で梳かす
;─────────────────────────────────────── 
IF GROUPMATCH(SELECTCOM, 322, 323)
	;初回かつスキンヘッドならマッサージの流れを作っておく
	;IS_COM_FIRST(1)はターゲット側の確認のためフラグを用意/一回限りなのでクリックあり
	IF !レミリア_髪梳き初回 && TALENT:レミリア_対象:髪の長さ < 25
		IF !IS_INITIATIVE(レミリア)
			PRINTFORML 「磨いて欲しいの？　%CALLNAME_K10(レミリア_対象)%、剃ってるじゃない」
			PRINTFORMDW %ANAME(レミリア)%は呆気にとられている
			PRINTL 
			PRINTFORML 「梳いてあげられないわ。撫でてあげる」
		ELSE
			PRINTFORML 「髪をとかしてあげ……られなかったわ、つまんない」
			PRINTFORMDW %ANAME(レミリア)%は首を傾げて考え込んでいる
			PRINTL 
			PRINTFORML 「そうだわ。撫でてあげる」
			PRINTFORMDL やがて得心がいったようにひとつ頷き、%ANAME(レミリア_対象)%に手を伸ばしてきた
		ENDIF
	;初回でないor初回だがスキンヘッドではない
	ELSE
		;スキンヘッド
		IF TALENT:レミリア_対象:髪の長さ < 25
			IF !IS_INITIATIVE(レミリア)
				PRINTFORML 「もうその冗談はいいわよ。いいわ。解してあげる」
			ELSE
				PRINTFORML 「疲れているでしょう？　解してあげるわ」
			ENDIF
			PRINTDATAL
				DATAFORM 「どう？　気持ちいいでしょう。……強過ぎた？」
				DATAFORM 「どう？　気持ちいいでしょう」
				DATAFORM 「えい……とんとん。こうかしら？」
			ENDDATA
		;スキンヘッドではない
		ELSE
			IF !IS_INITIATIVE(レミリア)
				PRINTFORML 「%CALLNAME_K10(レミリア_対象)%って、%KOJO_HAIR_NAME(レミリア_対象)%ね」
				PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(レミリア)%に頼んで、髪を梳かしてもらった
			ELSE
				PRINTFORML 「%KOJO_HAIR_NAME(レミリア_対象)%ね。触っていて気持ちがいいの」
				PRINTFORMDW %ANAME(レミリア)%は%ANAME(レミリア_対象)%の髪を梳かした
			ENDIF
			PRINTDATAL
				DATAFORM 「人形が欲しいけど、動かないのは嫌なの。つまらないでしょ」
				DATAFORM 「三つ編みにしてみてもいい？　後で解いてもいいから」
				DATAFORM 「切ったりもしてみたいわ」
			ENDDATA
		ENDIF
	ENDIF

	PRINTFORMD %ANAME(レミリア)%は%ANAME(レミリア_対象)%の
	;スキンヘッド
	IF TALENT:レミリア_対象:髪の長さ < 25
		PRINTDATADL
			DATAFORM 首を大切そうに揉み解している
			DATAFORM 耳朶を擽って幸せそうに微笑んでいる
			DATAFORM うなじに指を滑らせ密かな笑みを零している
			DATAFORM 首筋をそっとほぐすように揉んでいる
			DATAFORM 肩をとんとんと叩いている
			DATAFORM 肩甲骨をぐっとつかんで揉みほぐしている
		ENDDATA
	;長い
	ELSEIF TALENT:レミリア_対象:髪の長さ >= 475
		PRINTDATADL
			DATAFORM 毛束を手に取りそっとキスをしている
			DATAFORM 髪を慈しむように撫でている
			DATAFORM 髪を指に絡めて弄んでいる
			DATAFORM 髪を三つ編みにして遊び始めている
			DATAFORM 耳朶を擽って幸せそうに微笑んでいる
			DATAFORM うなじに指を滑らせ密かな笑みを零している
			DATAFORM 遅れ毛を摘まんでつんつんと引いて遊んでいる
			DATAFORM 髪に指を通して空気を含ませている
		ENDDATA
	;中間
	ELSE
		PRINTDATADL
			DATAFORM 毛先を手に取りそっとキスをしている
			DATAFORM 髪を慈しむように撫でている
			DATAFORM 耳朶を擽って幸せそうに微笑んでいる
			DATAFORM うなじに指を滑らせ密かな笑みを零している
			DATAFORM 髪を一筋摘まんでつんつんと引いて遊んでいる
			DATAFORM 髪に指を通して空気を含ませている
		ENDDATA
	ENDIF

	;フラグ立てがまだならフラグを立てておく
	;IS_COM_FIRST()はターゲット側の確認のためフラグ用意
	IF !レミリア_髪梳き初回
		レミリア_髪梳き初回 = 1
	ENDIF

	;描写オフ
	RETURN 1
ENDIF

;─────────────────────────────────────── 
;●見つめる・甘い言葉
;─────────────────────────────────────── 
IF GROUPMATCH(SELECTCOM, 324, 325)
	PRINTDATAL
		DATAFORM 「可愛がっているわよ」
		DATAFORM 「…………。ふふ」
		DATAFORM 「%CALLNAME_K10(レミリア_対象)%を気に入っているわ」
		DATAFORM 「%CALLNAME_K10(レミリア_対象)%はいちばんお気に入りよ」
	ENDDATA
	RETURN 0
ENDIF

;─────────────────────────────────────── 
;●訓練系　武闘86　防衛59　知略43　政治68　歌唱23　料理11
;─────────────────────────────────────── 
IF GROUPMATCH(SELECTCOM, 301, 302, 303, 304, 305, 306, 307)
	SELECTCASE SELECTCOM
		CASE 301
			PRINTFORML 「鬼ごっこしましょ。私が鬼ね。吸血『鬼』なんだから」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「がーおー。%CALLNAME_K10(レミリア_対象)%～っ。食べちゃうぞー！　あはははっ」
			ENDIF
		CASE 302
			PRINTFORML 「相手の力を利用するのよね。面白そうだわ」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「%CALLNAME_K10(レミリア_対象)%の力を利用して、押し倒してみたいわ」
			ENDIF
		CASE 303
			PRINTFORML 「大図書館に行きましょ。たまには本が読みたいわ」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「戦略的なことも少しは知らないと、%CALLNAME_K10(レミリア_対象)%のお話についていけないものね」
			ENDIF
		CASE 304
			PRINTFORML 「仕事ごっこをするの。大図書館に行きましょ」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「中身の確認は私の仕事じゃないわ」
				PRINTFORML 「書類ってつまらないし、無駄に文章が長いもの」
				PRINTFORML 「まとめた話を聞いて、捺印するのが私の仕事よ」
			ENDIF
		CASE 305
			PRINTFORML 「ちょっと、出掛けるよ。幻想郷巡りするの」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「デートよ。いいでしょう」
			ENDIF
		CASE 306
			PRINTFORML 「退屈だわ。歌ってよ。私も歌うから」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「お母さんが私を殺して♪　お父さんが私を食べている♪」
				PRINTFORML 「……マザーグースは嫌い？」
			ENDIF
		CASE 307
			PRINTFORML 「退屈だわ。料理をしてみましょう。何か面白いものを作って」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「私はだめよ。食べる役。赤ちゃんがレタスのどこにいるのか知らないもの」
			ENDIF
	ENDSELECT
	RETURN 0
ENDIF

;─────────────────────────────────────── 
;●お腹の音
;─────────────────────────────────────── 
IF SELECTCOM == 326
	PRINTFORML 「ここに人間がいるの？　レタスじゃなくて」
	RETURN 0

ENDIF

;─────────────────────────────────────── 
;●その他
;─────────────────────────────────────── 
PRINTDATAL
	DATAFORM 「私が主役。覚えておきなさい」
	DATAFORM 「簡単にやられてくれないかしら」
	DATAFORM 「%CALLNAME_K10(レミリア_対象)%、これやってみて」
	DATAFORM 「私をなめるとどういう目にあうか教えておかなくちゃ」
	DATAFORM 「こうしちゃえばいいのよ」
	DATAFORM 「さぁ、大人しくするのよ」
	DATAFORM 「よくわかってるわね。それで、いいのよ」
ENDDATA
RETURN 0

;─────────────────────────────────────── 
;●戻る
;─────────────────────────────────────── 
RETURN 0



;─────────────────────────────────────── 
;■日常_実行_実行後
;─────────────────────────────────────── 
@KOJO_K10_NOON_AFTER_PLAYER(レミリア_対象)
#DIM レミリア
#DIM レミリア_対象
#DIMS レミリア機嫌

IF !レミリア_対象
	レミリア_対象 = MASTER
ENDIF

レミリア = NAME_TO_CHARA("レミリア")
レミリア機嫌 '= TOSTR_EMOTION(レミリア)

;─────────────────────────────────────── 
;●機嫌が悪ければ通常のコマンド口上は喋らない
;─────────────────────────────────────── 
SELECTCASE レミリア機嫌
	CASE "恨", "怒", "憤"
		RETURN 0

	CASE "鬱", "悲", "憂"
		RETURN 0

	CASE  "狂", "恐", "怯"
		RETURN 0

	CASEELSE

ENDSELECT

;─────────────────────────────────────── 
;●髪を梳く・櫛で梳かす
;─────────────────────────────────────── 
IF GROUPMATCH(SELECTCOM, 322, 323)
	;描写オフ
	RETURN 1
ENDIF

;─────────────────────────────────────── 
;●Ｍ絶頂
;─────────────────────────────────────── 
IF NOWEX:レミリア:Ｍ絶頂 > NOWEX:レミリア:Ｃ絶頂 && NOWEX:レミリア:Ｍ絶頂 > NOWEX:レミリア:Ｂ絶頂 && NOWEX:レミリア:Ｍ絶頂 > NOWEX:レミリア:Ａ絶頂 && NOWEX:レミリア:Ｍ絶頂 > NOWEX:レミリア:Ｖ絶頂 && NOWEX:レミリア:Ｍ絶頂 > NOWEX:レミリア:射精
	IF NOWEX:レミリア:Ｍ絶頂 > 0
		PRINTFORM 「

		PRINTDATA
			DATAFORM んっ
			DATAFORM んぇれ
			DATAFORM はぁっ
			DATAFORM んぷっ
			DATAFORM んえうっ
			DATAFORM うむぁ、ぷ
		ENDDATA
		PRINTFORM %BREAK_K10("中", レミリア_対象)%

		PRINTDATA
			DATAFORM んぐ、んくぅっ
			DATAFORM あむっ
			DATAFORM ぷ、んっ
			DATAFORM んうぅ～……っ
			DATAFORM ふぢゅ
			DATAFORM んぐっ
		ENDDATA
		PRINTFORM %BREAK_K10("中", レミリア_対象)%

		PRINTDATA
			DATAFORM んく
			DATAFORM うふぐっ
			DATAFORM ふっ
			DATAFORM ぢゅっ
			DATAFORM ううっ
			DATAFORM んんんっ
		ENDDATA
		PRINTFORM %BREAK_K10("末", レミリア_対象)%

		PRINTFORML 」
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●戻る
;─────────────────────────────────────── 
RETURN 0

