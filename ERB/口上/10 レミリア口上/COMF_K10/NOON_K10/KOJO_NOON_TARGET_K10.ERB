;─────────────────────────────────────── 
;■日常_レミリア_対象_実行前
;─────────────────────────────────────── 
@KOJO_K10_NOON_BEFORE_TARGET(レミリア_対象)
#DIM レミリア
#DIM レミリア_対象
#DIMS レミリア機嫌

IF !レミリア_対象
	レミリア_対象 = MASTER
ENDIF

レミリア = NAME_TO_CHARA("レミリア")
レミリア機嫌 '= TOSTR_EMOTION(レミリア)

;─────────────────────────────────────── 
;●機嫌が悪ければ通常のコマンド口上は喋らない
;─────────────────────────────────────── 
レミリア機嫌 = %TOSTR_EMOTION(レミリア)%
SELECTCASE レミリア機嫌
	CASE "恨", "怒", "憤"
		IF PALAM:レミリア:怒主 <= PALAM:レミリア:怒外

		ELSE
			PRINTDATAL
				DATAFORM 「あいつは何やってるの」
				DATAFORM 「もう！　しっかりしてよね」
				DATAFORM 「短絡ね」
				DATAFORM 「理由が分からない」
				DATAFORM 「ふん」
			ENDDATA
		ENDIF
		RETURN 0

	CASE "鬱", "悲", "憂"
		IF PALAM:レミリア:哀主 <= PALAM:レミリア:哀外

		ELSE
			PRINTDATAL
				DATAFORM 「退屈なの」
				DATAFORM 「太陽は嫌いよ」
				DATAFORM 「眠いわ」
			ENDDATA
		ENDIF
		RETURN 0

	CASE  "狂", "恐", "怯"
		IF PALAM:レミリア:怖主 <= PALAM:レミリア:怖外

		ELSE
			PRINTDATAL
				DATAFORM 「おかしなことにしたでしょ」
				DATAFORM 「な、訳ない」
				DATAFORM 「何言ってるのよ」
			ENDDATA
		ENDIF
		RETURN 0

	CASEELSE

ENDSELECT

;─────────────────────────────────────── 
;●同一コマンド
;─────────────────────────────────────── 
IF SELECTCOM == PREVCOM
	;レミリアに主導権なし
	IF !IS_INITIATIVE(レミリア)
		PRINTDATAL
			DATAFORM 「もっと%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たいのね。いいわ」
			DATAFORM 「もっと%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たいかしら。いいわ」
			DATAFORM 「私もまだ、%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいわ」
			DATAFORM 「いいわ。このままね」
		ENDDATA
	;レミリアに主導権あり
	ELSE
		PRINTDATAL
			DATAFORM 「まだ%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たくはないかしら？」
			DATAFORM 「もっと%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいわ。いいわよね？」
			DATAFORM 「このまま？　いいわよ」
			DATAFORM 「もう少しこうして%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいわ」
		ENDDATA
	ENDIF
;─────────────────────────────────────── 
;●同一コマンドでない
;─────────────────────────────────────── 
ELSE
	;レミリアに主導権なし
	IF !IS_INITIATIVE(レミリア)
		PRINTDATAL
			DATAFORM 「%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たいの？　いいわよ」
			DATAFORM 「%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たいのね。わかったわ」
			DATAFORM 「%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たいかしら。そうね」
			DATAFORM 「私も%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいわ」
		ENDDATA
	;レミリアに主導権あり
	ELSE
		PRINTDATAL
			DATAFORM 「%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいわ。いいわよね？」
			DATAFORM 「%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいわ」
			DATAFORM 「%KOJO_COM_NAME_TARGET_K10(SELECTCOM)%たいから、付き合いなさい」
			DATAFORM 「%KOJO_COM_NAME_PLAYER_K10(SELECTCOM)%たくはない？」
		ENDDATA
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●会話
;─────────────────────────────────────── 
IF SELECTCOM == 300
	;レミリアに主導権あり（話し手）
	IF IS_INITIATIVE(レミリア)
		IF RAND:13 == 0
			IF CSTR:レミリア_対象:99 != "咲夜"
				PRINTFORML 「うちのメイドは優秀なんだけど、空間を広げ過ぎなのよね」
			ELSE
				PRINTFORML 「うちのメイドは優秀だと思ってるわよ。空間を広げ過ぎること以外はね」
			ENDIF
		ELSEIF RAND:12 == 0
			IF CSTR:レミリア_対象:99 != "フラン"
				PRINTFORML 「そうそう、いい家庭教師はいない？　たまに妹が私をあいつって呼ぶの」
			ELSE
				PRINTFORML 「そうそう、死なない家庭教師を探しているわ」
			ENDIF
		ELSE
			PRINTDATAL
				DATAFORM 「あーあ、月へ行きたいわ」
				DATAFORM 「うーむ。霧が薄いかしら」
				DATAFORM 「面白いことはないの」
				DATAFORM 「部屋？　紅いほうが血の汚れが目立たないでしょ」
				DATAFORM 「え？　蝙蝠は可愛いでしょ」
				DATAFORM 「大図書館？　あの部屋はかび臭いわ。何がいいの」
				DATAFORM 「そうね。人間の里にはあまり行かないわ」
				DATAFORM 「放っておくの？　知識人は本ばっかり読んでて、あんまり役に立たないわね」
				DATAFORM 「やるわよ。何時ぞやの借りを返すチャンスなの」
				DATAFORM 「棺桶は死人の入るものだったら」
				DATAFORM 「寝るのは４時くらい。早寝早起きが自慢なの」
				DATAFORM 「ねぇ鬼ごっこしない？　勝ったら血をちょうだい」
			ENDDATA
		ENDIF
	;レミリアに主導権なし（聞き手）
	ELSE
		PRINTDATAL
			DATAFORM 「部屋？　紅いほうが血が目立たないからよ」
			DATAFORM 「牙が短いのかしらね。零さないで飲むのは難しいわ」
			DATAFORM 「これ？　霧を作っているの」
			DATAFORM 「面白そうね、それ」
			DATAFORM 「他にもそういう話はある？　話してみなさいよ」
			DATAFORM 「それであんなに兵が増えてたのね」
			DATAFORM 「いいわ。その作戦、協力してあげる」
			DATAFORM 「楽しそうね。私にもさせて」
			DATAFORM 「私は小食なの」
			DATAFORM 「食べてもいいのよ」
			DATAFORM 「あんまり外に出して貰えないの。病弱っ娘なのよ」
		ENDDATA
	ENDIF
	RETURN 0

;─────────────────────────────────────── 
;●酒
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 310
	IF IS_INITIATIVE(レミリア)
		PRINTFORML 「いいもの持ってるじゃない」
		PRINTFORML 「紅いのがいいわ」
	ELSE
		PRINTFORML 「紅いのはある？　早くちょうだい」
	ENDIF

;─────────────────────────────────────── 
;●蜂蜜水
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 311
	PRINTFORML 「私は紅いほうが元気出るけど、気持ちは受け取っておくわ」

;─────────────────────────────────────── 
;●水浴び
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 354
	PRINTDATAL
		DATAFORM 「冷たぁい！　水かけたでしょ」
		DATAFORM 「服がぺたぺたくっつくわ」
		DATAFORM 「木陰で潜っちゃえば太陽も平気ね」
		DATAFORM 「こっちにいらっしゃいよ。日陰に休めそうな岩があるわ」
	ENDDATA

;─────────────────────────────────────── 
;●泳ぐ
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 355
	IF TFLAG:18 == 1
		PRINTFORML 「%CALLNAME_K10(レミリア_対象)%の泳ぎ上手だわ。教えてよぉ」
	ELSEIF TFLAG:18 == -1
		IF TFLAG:17 == 1
			PRINTFORML 「%CALLNAME_K10(レミリア_対象)%！？　何してるの」
		ELSEIF TFLAG:17 == 2
			PRINTFORML 「ごぽっ！　うぷっ！　う～～～っ！」
		ELSE
			PRINTFORML 「%CALLNAME_K10(レミリア_対象)%！？　何してるの！　こっちに……ごぽっ！　う～～～っ！」
		ENDIF
	ELSE
		PRINTFORML 「%CALLNAME_K10(レミリア_対象)%も泳げるのね」
	ENDIF

;─────────────────────────────────────── 
;●スキンシップ・頭を撫でる・髪を梳く・櫛で髪を梳く・背中を流す
;─────────────────────────────────────── 
ELSEIF GROUPMATCH(SELECTCOM, 320, 321, 322, 323, 358)
	PRINTDATAL
		DATAFORM 「%CALLNAME_K10(レミリア_対象)%に抱っこされるの好きよ」
		DATAFORM 「ずっとこうしていなさい」
		DATAFORM 「私の世話ができるなんて光栄でしょ」
		DATAFORM 「その従順さが必要なのよ」
	ENDDATA

;─────────────────────────────────────── 
;●見つめる・甘い言葉
;─────────────────────────────────────── 
ELSEIF GROUPMATCH(SELECTCOM, 324, 325)
	PRINTDATAL
		DATAFORM 「そんなに顔を見たって、喋らなくちゃわからないわよ」
		DATAFORM 「にらめっこなの？　私強いわよ」
		DATAFORM 「おなかが減ったのかしら？　私は減ってないわ」
	ENDDATA

;─────────────────────────────────────── 
;●訓練系
;─────────────────────────────────────── 
ELSEIF GROUPMATCH(SELECTCOM, 301, 302, 303, 304, 305, 306, 307)
	SELECTCASE SELECTCOM
		CASE 301
			PRINTFORML 「鬼ごっこしましょ。私が鬼ね。吸血『鬼』なんだから」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「がーおー。%CALLNAME_K10(レミリア_対象)%～っ。食べちゃうぞー！　あはははっ」
			ENDIF
		CASE 302
			PRINTFORML 「相手の力を利用するのね。面白そうだわ」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「%CALLNAME_K10(レミリア_対象)%の力を利用して、押し倒してみたいわ」
			ENDIF
		CASE 303
			PRINTFORML 「本？　遊びが面白くなること、書いてある？　見てみるわね」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「戦略的なことも少しは知りたいわ。%CALLNAME_K10(レミリア_対象)%のお話についていけないし」
			ENDIF
		CASE 304
			PRINTFORML 「大図書館に行くの？　私もするわ。仕事ごっこ」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「中身の確認は私の仕事じゃないわ」
				PRINTFORML 「書類ってつまらないし、無駄に文章が長いもの」
				PRINTFORML 「まとめた話を聞いて、捺印するのが私の仕事よ」
			ENDIF
		CASE 305
			PRINTFORML 「幻想郷巡り？　面白いことがあるといいわね」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「デートよ。いいでしょう」
			ENDIF
		CASE 306
			PRINTFORML 「いいけど、声に自信はあるの？　下手な歌は聴きたくないわ」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「お母さんが私を殺して♪　お父さんが私を食べている♪」
				PRINTFORML 「……マザーグースは嫌い？」
			ENDIF
		CASE 307
			PRINTFORML 「何を作ってくれるの？　面白いものを作って」
			IF CHECK_K10("陥落",レミリア_対象)
				PRINTFORML 「私はだめよ。食べる役。赤ちゃんがレタスのどこにいるのか知らないもの」
			ENDIF
	ENDSELECT

;─────────────────────────────────────── 
;●猥談
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 309
	;レミリアに主導権がある（話し手）
	IF IS_INITIATIVE(レミリア)
		IF MARK:レミリア:既成事実 == 0
			PRINTFORML 「この前読んだ漫画に唇を重ねる場面があったのだけど」
			PRINTFORML 「あれってどんな感じなの？　したくはないけど気になるの」
		ELSEIF MARK:レミリア:既成事実 == 1
			PRINTFORML 「キスって顔がすごく近づくじゃない？　目を開いてるとびっくりするわ」
		ELSEIF MARK:レミリア:既成事実 == 2
			PRINTFORML 「ハグって何か変わった意味があるの？　面白いことなら知りたいわ」
		ELSEIF MARK:レミリア:既成事実 == 3
			PRINTFORML 「生えてないのって……変なの？　普通でしょ」
		ENDIF
	;レミリアに主導権がない（聞き手）
	ELSE
		IF MARK:レミリア:既成事実 == 0
			PRINTFORML 「そのくらい知ってるわ。レタスから産まれるのよね」
		ELSEIF MARK:レミリア:既成事実 == 1
			PRINTFORML 「他に意味があるの？　唇を触れ合わせるのは約束の印でしょ」
		ELSEIF MARK:レミリア:既成事実 == 2
			PRINTFORML 「人間って奇妙なことをするのね」
		ELSEIF MARK:レミリア:既成事実 == 3
			PRINTFORML 「あれ気持ちよかったわ。またしなさい」
		ENDIF
	ENDIF

;─────────────────────────────────────── 
;●食事
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 351
	PRINTFORML 「たまには人間用の食事も食べてみたいわ。にんにくや鰯は嫌よ」
	;おごり発生の場合
	IF IS_MALE(レミリア_対象) && IS_FEMALE(レミリア)
		PRINTFORML 「いいの？　嬉しいわ。ご馳走になるわね」
	ENDIF

;─────────────────────────────────────── 
;●移動
;─────────────────────────────────────── 
ELSEIF GROUPMATCH(SELECTCOM, 390, 391)
	IF IS_INITIATIVE(レミリア)
		IF SELECTCOM == 390
			PRINTFORML 「出掛けるわよ。えーっと日傘……あったあった」
		ELSE
			PRINTFORML 「別のところへ行くわよ」
		ENDIF
	ELSE
		IF SELECTCOM == 390
			PRINTFORML 「出掛けるの？　日傘を用意するから待って」
		ELSE
			PRINTFORML 「別のところへ行くのね」
		ENDIF
	ENDIF

	;TFLAG:17は誘い方の種類。0=通常
	IF TFLAG:17 == 0
		IF TFLAG:18 == 1
			PRINTFORML 「%CALLNAME_K10(レミリア_対象)%は%GET_PLACENAME(TFLAG:54)%に行きたいのね」
		ELSEIF TFLAG:18 == 0
			PRINTFORML 「いいわ。%GET_PLACENAME(TFLAG:54)%に行きましょう」
		ELSE
			PRINTFORML 「%GET_PLACENAME(TFLAG:54)%？　うーむ……まぁいっか」
		ENDIF

	;TFLAG:17は誘い方の種類。1=レミリアが勝手に決定
	ELSEIF TFLAG:17 == 1
		PRINTFORML 「じゃあ%GET_PLACENAME(TFLAG:54)%へ行きましょう」

	;TFLAG:17は誘い方の種類。2=レミリアの行きたいデート先
	ELSEIF TFLAG:17 == 2
		IF TFLAG:18 == 1
			PRINTFORML 「%GET_PLACENAME(TFLAG:54)%でいいの？」
			PRINTFORML 「行きたいところがあったんじゃない？　付き合って貰うけど」
		ELSE
			PRINTFORML 「%GET_PLACENAME(TFLAG:54)%でいい？　悪いわね」
		ENDIF

	;TFLAG:17は誘い方の種類。3=レミリアが行きたいデート先以外
	ELSE
		IF TFLAG:18 == 1
			PRINTFORML 「%CALLNAME_K10(レミリア_対象)%は%GET_PLACENAME(TFLAG:54)%に行きたいのね」
			PRINTFORML 「まぁいいわ。%CALLNAME_K10(レミリア_対象)%が行きたいなら付き合ってあげるわよ」
		ELSEIF TFLAG:18 == 0
			PRINTFORML 「%CALLNAME_K10(レミリア_対象)%が%GET_PLACENAME(TFLAG:54)%に行きたいならいいわよ」
		ELSE
			PRINTFORML 「%GET_PLACENAME(TFLAG:54)%ー。まぁ付き合ってあげる」
		ENDIF
	ENDIF
	RETURN 0

;─────────────────────────────────────── 
;●帰還
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 392
	PRINTDATAL
		DATAFORM 「帰るのはやいのね」
		DATAFORM 「帰っちゃうの？」
		DATAFORM 「もう帰るの？　日はまだ昇らないのに」
	ENDDATA
	RETURN 0

;─────────────────────────────────────── 
;●お腹の音
;─────────────────────────────────────── 
ELSEIF SELECTCOM == 326
	PRINTFORML 「何か聴こえるの？　ちょっと怖いわ」
	RETURN 0

;─────────────────────────────────────── 
;●その他
;─────────────────────────────────────── 
ELSE
	PRINTDATAL
		DATAFORM 「うーむ」
		DATAFORM 「何か面白い奴もってきてよ」
		DATAFORM 「噂を聞いたけど」
		DATAFORM 「紅茶をこぼしちゃった」
		DATAFORM 「こうかな」
		DATAFORM 「%CALLNAME_K10(レミリア_対象)%、なにしてるの」
		DATAFORM 「平気よ」
		DATAFORM 「何処に置いたかなぁ」
		DATAFORM 「残念だったねぇ」
		DATAFORM 「しっかりしてよね」
		DATAFORM 「キビキビ動きなさい」
		DATAFORM 「しょうがないわね」
		DATAFORM 「さあね」
		DATAFORM 「天気悪い中出かけたくないのよ。晴れは悪い天気」
	ENDDATA
	RETURN 0
ENDIF

;─────────────────────────────────────── 
;●戻る
;─────────────────────────────────────── 
RETURN 0



;─────────────────────────────────────── 
;■日常_レミリア_対象_実行後
;─────────────────────────────────────── 
@KOJO_K10_NOON_AFTER_TARGET(レミリア_対象)
#DIM レミリア
#DIM レミリア_対象
#DIMS レミリア機嫌

IF !レミリア_対象
	レミリア_対象 = MASTER
ENDIF

レミリア = NAME_TO_CHARA("レミリア")
レミリア機嫌 '= TOSTR_EMOTION(レミリア)

;─────────────────────────────────────── 
;●機嫌が悪ければ通常のコマンド口上は喋らない
;─────────────────────────────────────── 
レミリア機嫌 = %TOSTR_EMOTION(レミリア)%
SELECTCASE レミリア機嫌
	CASE "恨", "怒", "憤"
		RETURN 0

	CASE "鬱", "悲", "憂"
		RETURN 0

	CASE  "狂", "恐", "怯"
		RETURN 0

	CASEELSE

ENDSELECT

;─────────────────────────────────────── 
;●主人公のＭ絶頂
;─────────────────────────────────────── 
IF NOWEX:レミリア_対象:Ｍ絶頂 > NOWEX:レミリア_対象:Ｃ絶頂 && NOWEX:レミリア_対象:Ｍ絶頂 > NOWEX:レミリア_対象:Ｂ絶頂 && NOWEX:レミリア_対象:Ｍ絶頂 > NOWEX:レミリア_対象:Ａ絶頂 && NOWEX:レミリア_対象:Ｍ絶頂 > NOWEX:レミリア_対象:Ｖ絶頂 && NOWEX:レミリア_対象:Ｍ絶頂 > NOWEX:レミリア_対象:射精
	IF NOWEX:レミリア_対象:Ｍ絶頂 > 0
		PRINTFORM 「

		PRINTDATA
			DATAFORM イッちゃったの
			DATAFORM 気持ち良くなっちゃったの
			DATAFORM イッたの
		ENDDATA
		PRINTFORM ？　

		PRINTDATA
			DATAFORM おくちで
			DATAFORM 舌で
			DATAFORM 喉で
		ENDDATA
		PRINTFORM %BREAK_K10("末", レミリア_対象)%

		PRINTFORML 」
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●戻る
;─────────────────────────────────────── 
RETURN 0

