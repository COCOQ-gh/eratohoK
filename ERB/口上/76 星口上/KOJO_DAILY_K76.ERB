@KOJO_DAILY_K76()

;またか！
CALL KOJO_DAILY_K76_MATAKA


;----------------------------------
;またか！
;----------------------------------
@KOJO_DAILY_K76_MATAKA
#DIM 星
#DIM 判定値

星 = NAME_TO_CHARA("星")

;星がいないと駄目
SIF 星 == -1
	RETURN -1

;150日目以降は確率ダウン
IF DAY:0 < 150
	;星と面識があり同一勢力所属で捕虜でなく死んでいないなら1.5%で発生
	IF !(RAND:1000 < 15 && CFLAG:星:面識 && CFLAG:星:所属 == CFLAG:MASTER:所属 && !CFLAG:星:捕虜先 && CFLAG:星:特殊状態フラグ != 2)
		RETURN -1
	ENDIF
ELSE
	;星と面識があり同一勢力所属で捕虜でなく死んでいないなら0.5%で発生
	IF !(RAND:1000 < 5 && CFLAG:星:面識 && CFLAG:星:所属 == CFLAG:MASTER:所属 && !CFLAG:星:捕虜先 && CFLAG:星:特殊状態フラグ != 2)
		RETURN -1
	ENDIF
ENDIF

DRAWLINE
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORMW %ANAME(MASTER)%がブラブラしていると、オロオロしている%ANAME(星)%を見つけた
PRINTFORMW 何事かと声をかけると、気まずそうに挨拶を返してきた
PRINTFORMW 「あっ、ど、どうかしましたか？　いえ、私はこれといって別に……」
PRINTFORMW 「不審な動き？　い、いえ、していませんよ。まさか毘沙門天代理がそう何度も失態を犯すような真似は……」
CALL ASK_YN("多分また忘れ物だろう", "毘沙門天様がそんなドジなはずがない")
IF RESULT == 0
	PRINTFORML 事情を察した%ANAME(MASTER)%はその場を離れ、あらゆる心当たりを駆け巡った……
	PRINTFORM 根気強く探し続け、
	SELECTCASE RAND:100
		CASE IS < 40
			PRINTFORM 宝塔を
		CASE IS < 60
			PRINTFORM 羽衣を
		CASE IS < 80
			PRINTFORM 槍を
		CASE IS < 100
			PRINTFORM 頭の飾りを
	ENDSELECT
	SELECTCASE RAND:100
		CASE IS < 40
			PRINTFORM 森林で
		CASE IS < 60
			PRINTFORM 川辺で
		CASE IS < 80
			PRINTFORM 城下町で
		CASE IS < 90
			PRINTFORM 花畑で
		CASEELSE
			PRINTFORM 温泉で
	ENDSELECT
	PRINTFORMW 見つけた！
	PRINTFORMW %ANAME(MASTER)%は%ANAME(星)%にブツを見つけたと申し出た
	PRINTFORMW 「あ！　それです！　大変お恥ずかしい話ですが……実は私はそれを探していたんです」
	PRINTFORMW 「見つけてくださってありがとうございます。これで叱られずに済みそうです」
	PRINTFORM 嬉しそうに去っていく%ANAME(星)%を見届けた%ANAME(MASTER)%は、忘れられた
	SELECTCASE RAND:100
		CASE IS < 40
			PRINTFORM 頭の飾りを
		CASE IS < 60
			PRINTFORM 槍を
		CASE IS < 80
			PRINTFORM 羽衣を
		CASE IS < 100
			PRINTFORM 宝塔を
	ENDSELECT
	PRINTFORMW 見つけた……
	CFLAG:星:好感度 += 200
ELSE
	PRINTFORMW 仮にも神なのだから、そんな隙まみれなはずがない。ましてや戦の神ともなればなおさらだ
	PRINTFORMW 「うう、困ったなあ……また叱られてしまいます……」
	PRINTFORMW 毘沙門天代理も色々大変なんだろうなぁとぼんやり考えながらその場を後にした……
ENDIF