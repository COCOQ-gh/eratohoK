;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K71_PROXY_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K71_PROXY_DECISION(対象)
#DIM 対象
#DIM 白蓮

白蓮 = NAME_TO_CHARA("白蓮")

SIF KDVAR:白蓮:白蓮_白蓮からの勧誘 != 1
	RETURN 0

SIF KDVAR:対象:ナズーリン_代わりに私が
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K71_PROXY_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K71_PROXY(対象)
#DIM 対象
#DIM 白蓮
#DIM 支度金

白蓮 = NAME_TO_CHARA("白蓮")

CFLAG:対象:面識 = 1

支度金 = (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + ABL:MASTER:防衛) * 100 * RAND(80, 120) / 100

CALL KOJO_DAILY_START()
PRINTFORMW 「ふむ。君が%ANAME(MASTER)%か」
PRINTFORML ある日、部屋に鼠の妖怪が訪ねてきた
PRINTFORML %PRONOUN(対象)%はこちらを見定めるように眺めてくる
PRINTFORMW 「失礼、私は%NAME_FORMAL(対象)%、この寺の関係者だ。諸事情あって、住み込んではいないが」
PRINTFORMW 「君が、聖の連れてきた助っ人かい？」
PRINTFORML いかにもそうだ、と頷く。
PRINTFORMW 「なるほどね。引き抜きは単にあのエセ聖人への揺さぶりのつもりだったが、中々できるようだ」
PRINTFORMW 「思ったよりもいい買い物になったな、これは……うん」
PRINTFORMW 「……ああ、すまない。君への用件がまだだったね」
PRINTFORMW 「私はダウザーでね。珍しいものを探しては拾っている。お近づきの印にこれをあげよう」
PRINTFORML そう言って%ANAME(対象)%が渡してきたのは、ずいぶん立派な宝石だった。相当の価値があるだろう
PRINTFORMW 少なくとも、お近づきの印にポンと渡すようなものではなかった
PRINTFORMW 「うん？　何を驚いているんだ。別に深い意味なんてないさ。私が度を越したお人好しというだけの話」
PRINTFORML 「でまぁ……これは独り言だが」
PRINTFORML 「聖は立場上、金や物をホイホイ渡せない。だがそれでは、優秀な人材を手元に留めておくのは難しい」
PRINTFORML 「だから、代わりにそういうことをする者が必要なんだな。あぁ誰かそんな者がいないかなぁ」
PRINTFORMW そういうことか……それなら、大人しく受け取っておくとしよう
PRINTFORMW 「そうそう、それでいいんだ。人からの裏のない純粋な善意は、受け取っておくべきだよ」
PRINTFORMW 「話はそれだけさ。私はこれで失礼するよ」
PRINTFORMW 言うだけ言って、%ANAME(対象)%は立ち去っていった
PRINTFORMW この件は、%ANAME(白蓮)%には黙っておこう……
CALL COLORPRINT(@"%ANAME(対象)%から、金{支度金}を得ました", カラー_注意, "L")
MONEY += 支度金
KDVAR:対象:ナズーリン_代わりに私が = 1

RETURN 1

