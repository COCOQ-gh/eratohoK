;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI_DECISION(対象)
#DIM 対象
#DIM 天子
天子 = NAME_TO_CHARA("天子")

;発生済みなら駄目
SIF KDVAR:対象:衣玖_たずねびと
	RETURN 0

;MASTERが天子なら駄目
SIF MASTER == 天子
	RETURN 0

;自分の勢力の捕虜なら駄目
SIF CFLAG:天子:捕虜先 == CFLAG:MASTER:所属
	RETURN 0

;天子が死んでたら駄目
SIF CFLAG:天子:特殊状態 == 2
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && CHECK_KOJO_DAILY_HAPPEN(天子, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI(対象)
#DIM 対象
#DIM 天子
天子 = NAME_TO_CHARA("天子")

PRINTFORML 部屋で仕事をしていると、%ANAME(対象)%が訪ねてきた
PRINTFORMW 「%ANAME(MASTER)%さん、実はお願いがありまして」
PRINTFORM 「私が目付役をしている、総領娘様……
CALL COLORPRINT(NAME_FORMAL(天子), カラー_注意)
PRINTFORMW のことですが」
PRINTFORMW 「%PRONOUN(天子)%を探し、私の目の届くところに確保していただきたいのです」
PRINTFORML 確保とはずいぶん物々しい言い方だ
PRINTFORML わけを問うと、%ANAME(対象)%は言葉を濁した
PRINTFORML 「その……なんといいますか、総領娘様は非常に奔放な方でして……」
PRINTFORMW 「%PRONOUN(天子)%を野放しにしておくのは……トラブルの元になるといいますか」
IF CFLAG:天子:面識
	PRINTFORMW 確かに、%PRONOUN(天子)%の性格を考えると、それは明らかなことだ……
ELSE
	PRINTFORMW %PRONOUN(天子)%と面識はないが、%ANAME(対象)%がここまで言うからにはそうなのだろう……
ENDIF
PRINTFORMW 「もちろん、作戦などもあるでしょうし、無理にとは言えません」
PRINTFORMW 「ですが、どうか考えていただけないでしょうか？」
PRINTFORML 
CALL ASK_YN("わかった", "無理だ")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「ありがとうございます。あちこちに頭を下げにいかずに済みそうです」
		PRINTFORMW 「では、すみませんがよろしくお願いします。私も可能な限り協力しますから」
		PRINTFORML 恭しく礼をし、%ANAME(対象)%は立ち去った……
		KDVAR:対象:衣玖_たずねびと = 1
	CASE 1
		PRINTFORMW 「そうですか……」
		PRINTFORMW 「仕方がありませんね。総領娘様のことは自分で探すことにします。お忙しいところ失礼しました」
		PRINTFORML %ANAME(対象)%は立ち去った……
		KDVAR:対象:衣玖_たずねびと = 2
ENDSELECT

RETURN 1

