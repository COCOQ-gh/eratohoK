@KOJO_DAILY_EXIST_K6()

@KOJO_DAILY_K6()
#DIM 発生フラグ
発生フラグ = 0

;太極拳
DAILY_NAME = 太極拳
CALL KOJO_DAILY_K6_TAIKYOKUKEN
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ

;----------------------------------
;太極拳
;----------------------------------
@KOJO_DAILY_K6_TAIKYOKUKEN
#DIM 美鈴
#DIM 経験値

美鈴 = NAME_TO_CHARA("美鈴")
	
;美鈴が同一勢力で捕虜でなく面識がある、3.5%
SIF !(CHECK_KOJO_DAILY_HAPPEN(美鈴, 35, 1, 0, 1))
	RETURN -1

IF CSTR:MASTER:99 == "レミリア"
	LOCALS:0 = お嬢様
ELSEIF CSTR:MASTER:99 == "パチュリー"
	LOCALS:0 = %ANAME(MASTER)%様
ELSE
	LOCALS:0 = %ANAME(MASTER)%さん
ENDIF

CALL KOJO_DAILY_START()

IF KDVAR:美鈴:美鈴_太極拳 == 0
	PRINTFORML %ANAME(MASTER)%が散歩をしていると、%ANAME(美鈴)%が庭先で怪しげな動きをしているのを見つけた
	PRINTFORMW 一体何事かと思っていると、向こうもこちらに気づいたようで、こちらに声をかけてきた
	PRINTFORMW 「%LOCALS:0%。どうしたんですか？　こっちをじっと見て」
	PRINTFORMW 「これですか？　これは太極拳といって、古くから伝わる拳法から編み出された、健康体操みたいなものです」
	PRINTFORMW 「%LOCALS:0%も一緒にどうですか？　気分がスッキリすると思いますよ」
	PRINTFORML なるほど、最近運動不足気味だし、それもいいかもしれない……
ELSE
	PRINTFORML %ANAME(MASTER)%が散歩をしていると、%ANAME(美鈴)%が太極拳をしているのを見つけた
	PRINTFORML 「あっ、%LOCALS:0%。どうですか？　太極拳」
ENDIF
PRINTFORML
CALL ASK_YN("やってみる", "今はいいや")
IF RESULT == 0
	SELECTCASE RAND:100
		CASE IS < 40
			経験値 = GET_EXP(GETNUM(ABL, "武闘"))
		CASE IS < 70
			経験値 = GET_EXP(GETNUM(ABL, "防衛"))
		CASE IS < 90
			経験値 = GET_EXP(GETNUM(ABL, "知略"))
		CASEELSE
			経験値 = GETNUM(GETNUM(ABL, "政治"))
	ENDSELECT
	PRINTFORML %ANAME(美鈴)%と太極拳を楽しんだ！
	CALL PRINT_ADD_EXP(MASTER, EXPNAME:経験値, RAND:5 + 1, 1)
	PRINTFORMW 「ふう、まぁ、こんなものでしょう」
	PRINTFORMW 「私はたまにここで太極拳してますから、また気が向いたらどうぞ！」
	PRINTFORMW 身体もスッキリしたし、仕事に励むとしよう……
ELSE
	PRINTFORMW 「あら、残念ですね、じゃあまた今度」
	PRINTFORMW 残念だが今は忙しい。美鈴の誘いを断った……
ENDIF

KDVAR:美鈴:美鈴_太極拳 = 1
