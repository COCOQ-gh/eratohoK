;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K122_SHOPPING_RATE(対象)
#DIM 対象
RETURN 20


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K122_SHOPPING_DECISION(対象)
#DIM 対象
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && !TALENT:対象:服従

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K122_SHOPPING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K122_SHOPPING(対象)
#DIM 対象

PRINTDATA
	DATAFORM 夕食の材料を買いに
	DATAFORM 夜食用の団子を買いに
	DATAFORM 美味な酒を求めて
	DATAFORM 新たな兵書を購入しに
ENDDATA
PRINTFORML 城下町へ行ったところ、同じく買い物中だった%ANAME(対象)%と遭遇した
IF TALENT:対象:正妻
	SELECTCASE RAND:5
	CASE 1
		PRINTFORMW 「夕飯の材料を買いに来たの、%ANAME(MASTER)%もいっしょにどう？」
	CASE 2
		PRINTFORMW 「%ANAME(MASTER)%の買い物についていってもいいかしら%UNICODE(0x2665)%」
	CASE 3
		PRINTFORMW 「買い物が終わったら甘味処でお茶にしましょう%UNICODE(0x2665)%」
	CASE 4
		PRINTFORMW 「今日は一緒に夕飯を作ろうね%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「あ%UNICODE(0x2665)% %ANAME(MASTER)%も買い物に来てたの%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW %ANAME(対象)%と一緒に買い物を楽しみました
	;正妻かつ好感度100万以上なら大幅に好感度が上がる
	IF CFLAG:対象:好感度 >= 1000000
		LOCAL:1 = MIN((CFLAG:対象:好感度 / 300), 10000)
	ELSE
		LOCAL:1 = 500
	ENDIF
ELSEIF TALENT:対象:恋人 && TALENT:対象:恋慕
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMW 「せっかくだし一緒に買い物しましょう%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「こんにちは%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW %ANAME(対象)%と一緒に買い物を楽しみました
	LOCAL:1 = 200
ELSEIF TALENT:対象:恋慕 || TALENT:対象:親友
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMW 「あ%UNICODE(0x2665)% %ANAME(MASTER)%じゃない%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「こんなところで会うなんて偶然ね%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW %ANAME(対象)%と暫くの間歓談しました
	LOCAL:1 = 100
ELSEIF CFLAG:対象:好感度 >= 500
	PRINTFORMW 「あら、あなたもお買い物？」
	PRINTFORMDW %ANAME(対象)%とそれなりに会話しました
	LOCAL:1 = 50
ELSE
	PRINTFORMW 「あら、奇遇ね」
	PRINTFORMDW %ANAME(対象)%と軽く会話しました
	LOCAL:1 = 5
ENDIF
PRINTFORML 
CALL COLOR_PRINTW(@"%NAME_FORMAL(対象)%の好感度が{LOCAL:1}上がりました", カラー_注意)
;そこそこ上昇するので有用か
CFLAG:対象:好感度 += LOCAL:1


RETURN 1

