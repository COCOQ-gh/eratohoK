@KOJO_DAILY_EXIST_K122()

@KOJO_DAILY_K122()
#DIM 発生フラグ
発生フラグ = 0
;エレン登用イベント１（未実装）
;CALL KOJO_DAILY_K122_APPOINT_1

;エレン登用イベント２（未実装）
;CALL KOJO_DAILY_K122_APPOINT_2

;買い物先で遭遇
DAILY_NAME = 買い物先で遭遇
CALL KOJO_DAILY_K122_SHOPPING
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ



;買い物先で遭遇イベント
@KOJO_DAILY_K122_SHOPPING

#DIM エレン
エレン = NAME_TO_CHARA("エレン")

;エレンに服従がついておらず、面識があり、同じ勢力に所属してるなら2%で発生
SIF !(CHECK_KOJO_DAILY_HAPPEN(エレン, 20, 1, 0, 1) && !TALENT:エレン:服従)
RETURN -1

CALL KOJO_DAILY_START
PRINTDATA
	DATAFORM 夕食の材料を買いに
	DATAFORM 夜食用の団子を買いに
	DATAFORM 美味な酒を求めて
	DATAFORM 新たな兵書を購入しに
ENDDATA
PRINTFORML 城下町へ行ったところ、同じく買い物中だった%ANAME(エレン)%と遭遇した
IF TALENT:エレン:正妻
	SELECTCASE RAND:5
	CASE 1
		PRINTFORMW 「夕飯の材料を買いに来たの、%ANAME(MASTER)%もいっしょにどう？」
	CASE 2
		PRINTFORMW 「%ANAME(MASTER)%の買い物についていってもいいかしら%UNICODE(0x2665)%」
	CASE 3
		PRINTFORMW 「買い物が終わったら甘味処でお茶にしましょう%UNICODE(0x2665)%」
	CASE 4
		PRINTFORMW 「今日は一緒に夕飯を作ろうね%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「あ%UNICODE(0x2665)% %ANAME(MASTER)%も買い物に来てたの%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW %ANAME(エレン)%と一緒に買い物を楽しみました
	;正妻かつ好感度100万以上なら大幅に好感度が上がる
	IF CFLAG:エレン:好感度 >= 1000000
		LOCAL:1 = MIN((CFLAG:エレン:好感度 / 300), 10000)
	ELSE
		LOCAL:1 = 500
	ENDIF
ELSEIF TALENT:エレン:恋人 && TALENT:エレン:恋慕
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMW 「せっかくだし一緒に買い物しましょう%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「こんにちは%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW %ANAME(エレン)%と一緒に買い物を楽しみました
	LOCAL:1 = 200
ELSEIF TALENT:エレン:恋慕 || TALENT:エレン:親友
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMW 「あ%UNICODE(0x2665)% %ANAME(MASTER)%じゃない%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「こんなところで会うなんて偶然ね%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW %ANAME(エレン)%と暫くの間歓談しました
	LOCAL:1 = 100
ELSEIF CFLAG:エレン:好感度 >= 500
	PRINTFORMW 「あら、あなたもお買い物？」
	PRINTFORMDW %ANAME(エレン)%とそれなりに会話しました
	LOCAL:1 = 50
ELSE
	PRINTFORMW 「あら、奇遇ね」
	PRINTFORMDW %ANAME(エレン)%と軽く会話しました
	LOCAL:1 = 5
ENDIF
PRINTFORML 
CALL COLORPRINT(@"%NAME_FORMAL(エレン)%の好感度が{LOCAL:1}上がりました", カラー_注意, "W")
;そこそこ上昇するので有用か
CFLAG:エレン:好感度 += LOCAL:1
