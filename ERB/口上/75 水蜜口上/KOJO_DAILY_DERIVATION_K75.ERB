@KOJO_DAILY_DERIVATION_K75()


DAILY_NAME = ダミー
CALL KOJO_DAILY_DERIVATION_K75_DUMMY()

DAILY_NAME = ダミー2
CALL KOJO_DAILY_DERIVATION_K75_DUMMY2()


DAILY_NAME = 仄暗い水の底から
CALL KOJO_DAILY_DERIVATION_K75_RETURN_FROM_THE_BOTTOM()

;----------------------------------
;ダミー
;----------------------------------
@KOJO_DAILY_DERIVATION_K75_DUMMY()
#DIM 水蜜
#DIM 白蓮
水蜜 = NAME_TO_CHARA("水蜜")
白蓮 = NAME_TO_CHARA("白蓮")
SIF 水蜜 == -1 || 白蓮 == -1
	RETURN -1

;開始イベント、終了イベント後ならだめ
SIF GROUPMATCH(KDVAR:水蜜:水蜜_仄暗い水の底から, 1, 2)
	RETURN -1
;白蓮が同じ勢力ならカウントは抑えられる
IF CFLAG:白蓮:所属 == CFLAG:水蜜:所属 && CFLAG:白蓮:捕虜先 == 0
	KDVAR:水蜜:水蜜_水底爆発カウント = 0
	RETURN -1
ENDIF

KDVAR:水蜜:水蜜_水底爆発カウント ++

;----------------------------------
;ダミー
;----------------------------------
@KOJO_DAILY_DERIVATION_K75_DUMMY2()
#DIM 水蜜
水蜜 = NAME_TO_CHARA("水蜜")

SIF 水蜜 == -1
	RETURN -1
;開始イベント前、終了イベント後ならだめ
SIF GROUPMATCH(KDVAR:水蜜:水蜜_仄暗い水の底から, 0, 2)
	RETURN -1
KDVAR:水蜜:水蜜_水底滞在カウント --


;----------------------------------
;水底より戻りて
;----------------------------------
@KOJO_DAILY_DERIVATION_K75_RETURN_FROM_THE_BOTTOM()
#DIM 水蜜
水蜜 = NAME_TO_CHARA("水蜜")

SIF 水蜜 == -1
	RETURN -1
;開始イベント前、終了イベント後ならだめ
SIF GROUPMATCH(KDVAR:水蜜:水蜜_仄暗い水の底から, 0, 2)
	RETURN -1
;滞在カウント残っててもだめ
SIF KDVAR:水蜜:水蜜_水底滞在カウント > 0
	RETURN -1

CALL KOJO_DAILY_START()
PRINTFORMW 「ただいま」
PRINTFORMW 行方不明になっていた%ANAME(水蜜)%が、ふらりと戻ってきた
PRINTFORMW 「急にごめんね、もう大丈夫だから」
PRINTFORMW 行方不明になる前の憔悴っぷりはどこへやら、なにかつやつやしてすらいる
PRINTFORMW どこへ行っていたのかと尋ねても、%ANAME(水蜜)%は首を横に振った
PRINTFORMW （言えるわけないじゃん……？　血の池地獄に沈んできてたなんて）
PRINTFORMW （あそこなら私でも『浮けない』し『溺れる』し……どっちも、普通の水じゃできないことだし）
PRINTFORMW （誰にだって、ストレス解散の方法くらいあるよねってことで、ひとつ）

CALL COLORPRINT(@"%ANAME(水蜜)%が職場復帰しました", COLOR("注意"), "W")
CALL COLORPRINT(@"%ANAME(水蜜)%の能力・素質が変化しました", COLOR("注意"), "W")

CALL SKILL_LEARN_BY_NAME(水蜜, スキル_ジャンル_SP, NO:水蜜, "幽霊船永久停泊")
SIF RESULT
	CALL COLORPRINT(@"%ANAME(水蜜)%がスキル＜幽霊船永久停泊＞を習得しました", COLOR("注意"), "W")


ABL:水蜜:武闘 += 10
ABL:水蜜:防衛 += 10
ABL:水蜜:知略 += 10
TALENT:水蜜:妖術知識 = 1
ABL:水蜜:妖術 += 5

TALENT:水蜜:男嫌い = 0
TALENT:水蜜:解放 = 1
TALENT:水蜜:Ｓ気質 = 1
TALENT:水蜜:Ｍ気質 = 1

CALL CHANGE_COUNTRY(水蜜, CFLAG:MASTER:所属)


KDVAR:水蜜:水蜜_仄暗い水の底から = 2

