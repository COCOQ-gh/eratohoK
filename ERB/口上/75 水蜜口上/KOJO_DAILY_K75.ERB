@KOJO_DAILY_EXIST_K75()

@KOJO_DAILY_K75()
#DIM 発生フラグ
発生フラグ = 0

;進路相談
DAILY_NAME = 進路相談
CALL KOJO_DAILY_K75_SHINRO
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ


;----------------------------------
;進路相談
;----------------------------------
@KOJO_DAILY_K75_SHINRO
#DIM 水蜜
#DIM 判定値

水蜜 = NAME_TO_CHARA("水蜜")

;水蜜が同一勢力所属で捕虜でなく面識があるなら1.5%で発生
SIF !CHECK_KOJO_DAILY_HAPPEN(水蜜, 15, 1, 0, 1)
	RETURN -1

;おわってたら出ない
SIF KDVAR:水蜜:水蜜_進路相談 == -1
	RETURN -1

CALL KOJO_DAILY_START
PRINTFORMW %ANAME(MASTER)%が退屈していると、%ANAME(水蜜)%が何やら頭をひねっていた
PRINTFORMW 「……ああ、%ANAME(MASTER)%さんですか。いえ、ちょっと悩んでいることがありまして」
PRINTL
CALL ASK_YN("話を聞く", "興味ないね")
IF RESULT == 0
	PRINTFORMW 「ありがとうございます。実は、船を動かすこともない今、私にできることは何かないかと考えておりまして」
	PRINTFORMW 「船に関わることなら多少の覚えはあるのですが、船も海もありませんので……」
	PRINTFORMW 何かあるだろうか……
		CALL ASK_MULTI("セイレーンに倣う", "陸でも星は見えるだろう", "ない。現実は非情である")
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 船を沈める妖怪なら美しい歌声がつきものだろう
				PRINTFORMW それを聞いた%ANAME(水蜜)%は恭しく礼を述べ、どこかで練習してきますと言い去っていった
				ABL:水蜜:歌唱 += 5
				CALL COLORPRINTFORM(@"%ANAME(水蜜)%の歌唱が5上昇しました", COLOR("注意"), "W")
			CASE 1
				PRINTFORMW 船の進路を決める時、星見をしたりはしていなかったのか
				PRINTFORMW それを聞いた%ANAME(水蜜)%は目を輝かせて礼を言い、練習のために去っていった
				SETBIT TALENT:水蜜:ＳＬＧ素質, 素質_ＳＬＧ_疾風迅雷
				CALL COLORPRINTFORM(@"%ANAME(水蜜)%は＜疾風迅雷＞を得ました", COLOR("注意"), "W")
			CASE 2
				PRINTFORMW 「ううん、なかなか思いつかないものですね……」
		ENDSELECT
ELSE
	PRINTFORMW あまり軽率に人の悩みに首を突っ込むべきではないだろう(人じゃないけど)
	PRINTFORMW %ANAME(MASTER)%はうんうん唸る%ANAME(水蜜)%を放置してその場を去った
ENDIF

KDVAR:水蜜:水蜜_進路相談 = -1