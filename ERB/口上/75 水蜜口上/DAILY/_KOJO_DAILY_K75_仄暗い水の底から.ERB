;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K75_FROM_THE_DEEP_RATE(対象)
#DIM 対象
RETURN 250


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K75_FROM_THE_DEEP_DECISION(対象)
#DIM 対象
#DIM 白蓮

白蓮 = NAME_TO_CHARA("白蓮")

SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
	RETURN 0

;開始イベント、終了イベント後ならだめ
SIF GROUPMATCH(KDVAR:対象:水蜜_仄暗い水の底から, 1, 2)
	RETURN 0

;爆発カウント最低4必要
SIF KDVAR:対象:水蜜_水底爆発カウント < 4
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0) && CHECK_KOJO_DAILY_HAPPEN(白蓮, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K75_FROM_THE_DEEP_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K75_FROM_THE_DEEP(対象)
#DIM 対象
#DIM 白蓮

白蓮 = NAME_TO_CHARA("白蓮")

PRINTFORML 「沈めたい……」
PRINTFORMW 自室で一人、%ANAME(対象)%はそのようにつぶやいた
PRINTFORML 船幽霊である彼女にとって、船を沈めたり沈んだりすることは呼吸や食事に等しい
PRINTFORML あまりにも物騒なそれは、普段であれば%ANAME(白蓮)%の力によって抑制されている
PRINTFORMW だが、今はその%ANAME(白蓮)%がいない
PRINTFORML いかに意思の強い者でも、それで呼吸を止められたりはしない
PRINTFORMW %ANAME(対象)%のこれも同じことだ。%PRONOUN(対象)%は限界を迎えていた
PRINTFORMW 「また、あそこ、いこっかな」
PRINTFORML 衝動に侵されぼんやりとした頭で、%PRONOUN(対象)%はそのようにつぶやいた
PRINTFORML そうしてふらりと部屋を、拠点を後にすると、一人どこかへ出かけていった
PRINTFORMW 誰にもとがめられることなく、沈め沈められることのできる場所へ……
PRINTFORML 
CALL COLORPRINT(@"%ANAME(対象)%が行方不明になりました", カラー_警告, "W")
KDVAR:対象:水蜜_仄暗い水の底から = 1
KDVAR:対象:水蜜_水底滞在カウント = 5
CALL CHANGE_COUNTRY(対象, 0)
CFLAG:対象:特殊状態 = 0

RETURN 1

