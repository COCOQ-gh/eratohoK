;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:藍_エクセレントさんすう教室
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("紫"), NAME_TO_CHARA("マミゾウ"), NAME_TO_CHARA("橙"), NAME_TO_CHARA("幽々子"))
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)


;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS(対象)
#DIM 対象
#DIM かける数
#DIM かけられる数
#DIM 答え

SIF !CFLAG:対象:面識
	CFLAG:対象:面識 = 1


CALL KOJO_DAILY_START()

PRINTFORMW 「ふーむ……暇だ」
PRINTFORMW 「おや？　%ANAME(MASTER)%じゃないか。どうだ、ちょっと暇つぶしにつきあってくれないか？」
PRINTFORMW 拠点の見回りをしていると、%ANAME(対象)%に出くわした
PRINTFORML さて、どうしようか……
PRINTFORML 
CALL ASK_YN("つきあう", "忙しい")

IF RESULT == 1
	PRINTFORMW 「む、そうか……」
	PRINTFORMW 残念ながら今は忙しい
	PRINTFORMW 断り、立ち去った……
	KDVAR:対象:藍_エクセレントさんすう教室 = 1
	RETURN 1
ENDIF

PRINTFORMW 「おお、そうか、つきあってくれるか」
PRINTFORMW 「なに、簡単なクイズに付き合ってもらうだけさ」
PRINTFORMW 「ではいくぞ？」

かけられる数 = RAND(1, 100)
かける数 = RAND(1, 100)
答え = かけられる数 * かける数

CALL COLORPRINT(@"「{かけられる数} ×　{かける数}の答えは！？」", カラー_注意, "L")

TINPUT 10000, -1, 1, ""

IF RESULT != 答え
	PRINTFORMW 「残念、外れだ」
	PRINTFORMW 「まぁ、また挑戦してくれよ？」
	PRINTFORMW 残念ながら間違えてしまったようだ……
	PRINTFORMW しかし、%ANAME(対象)%の暇つぶしにはなったようだ
	RETURN 1
ENDIF

PRINTFORMW 「おお、その通り。正解だ。やるじゃないか」
PRINTFORMW 「ふむ。良い暇つぶしになったよ。ありがとう」
PRINTFORMW どうやら正解したようだ……
CALL COLORPRINT(@"%ANAME(MASTER)%の知略が3上昇した！", カラー_注意, "W")
ABL:MASTER:知略 += 3
KDVAR:対象:藍_エクセレントさんすう教室 = 1

RETURN 1


