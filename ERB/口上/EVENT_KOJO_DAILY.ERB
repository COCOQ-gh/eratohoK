@EVENT_KOJO_DAILY(SLGフラグ)
#DIM SLGフラグ
#DIMS SLG
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM EVENT_COUNT
#DIM EVENT_CHARA, VARSIZE("SHAFFLE_ARRAY")
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")
#DIM 対象
#DIM 対象NO

SIF !SLGフラグ && !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF SLGフラグ && FLAG:観戦モード
	RETURN 0

SIF SLGフラグ && CFLAG:MASTER:捕虜先
	RETURN 0

CALL KOJO_DAILY_DERIVATION(SLGフラグ)

DAILY_CANCEL = 0
EVENT_COUNT = KOJO_DAILY_EVENT_COUNT
SLG = %(SLGフラグ ? "SLG_" # "" )%


;口上用デイリー MASTERのものは発動しない
;順番決定
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_CHARA"

FOR LOCAL, 1, CHARANUM
	対象 = EVENT_CHARA:LOCAL
	対象NO = NO:対象
	SIF !IS_FIXED_CHARA(対象)
		CONTINUE

	SIF KOJO_DAILY_EVENT_NUM:対象NO:(SLGフラグ ? デイリー_ＳＬＧ # デイリー_通常) == 0
		CONTINUE

	EVENT_NUM = KOJO_DAILY_EVENT_NUM:対象NO:(SLGフラグ ? デイリー_ＳＬＧ # デイリー_通常)

	CALL FISHER_YATES_SHAFFLE(EVENT_NUM)
	ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_ARRAY"

	FOR LOCAL:1, 0, EVENT_NUM
		IF SLGフラグ
			NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:対象NO:デイリー_ＳＬＧ:(EVENT_ARRAY:(LOCAL:1))
			NAME_JAP '= KOJO_DAILY_EVENT_NAME:対象NO:デイリー_ＳＬＧ:(EVENT_ARRAY:(LOCAL:1))
		ELSE
			NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:対象NO:デイリー_通常:(EVENT_ARRAY:(LOCAL:1))
			NAME_JAP '= KOJO_DAILY_EVENT_NAME:対象NO:デイリー_通常:(EVENT_ARRAY:(LOCAL:1))
		ENDIF
		IF !SLGフラグ && KOJO_DAILY_GET_DISABLE_CONFIG(対象, NAME_ENG)
			CONTINUE
		ELSEIF SLGフラグ && SLG_KOJO_DAILY_GET_DISABLE_CONFIG(対象, NAME_ENG)
			CONTINUE
		ENDIF

		TRYCCALLFORM %SLG%KOJO_DAILY_K{対象NO}_%NAME_ENG%_RATE(対象)
			SIF RESULT * KOJO_DAILY_EVENT_ODDS <= RAND:1000
				CONTINUE

			RESULT = 1
			TRYCALLFORM %SLG%KOJO_DAILY_K{対象NO}_%NAME_ENG%_DECISION(対象)
			SIF RESULT == 0
				CONTINUE

			VARSET DAILY_TARGET, -1
			VARSET DAILY_TARGET_NUM
			RESULT = 1
			TRYCALLFORM %SLG%KOJO_DAILY_K{対象NO}_%NAME_ENG%_SETTARGET(対象)
			SIF RESULT == 0
				CONTINUE

			RESULTS = 
			TRYCALLFORM %SLG%KOJO_DAILY_K{対象NO}_%NAME_ENG%_NAME(対象)
			IF RESULTS == ""
				CALL KOJO_DAILY_START(NAME_JAP)
			ELSE
				CALL KOJO_DAILY_START(RESULTS)
			ENDIF
			CALLFORM %SLG%KOJO_DAILY_K{対象NO}_%NAME_ENG%(対象)

			IF RESULT == 1
				EVENT_COUNT --
			ENDIF
			SIF DAILY_CANCEL
				RETURN
			SIF EVENT_COUNT == 0
				BREAK
		CATCH
			DEBUGPRINTFORML %SLG%KOJO_DAILY_{対象NO}_%NAME_ENG%_RATEみつからず
		ENDCATCH
	NEXT
NEXT



;共通デイリーイベント：派生処理
@KOJO_DAILY_DERIVATION(SLGフラグ)
#DIM SLGフラグ
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIMS SLG
#DIM EVENT_CHARA, VARSIZE("SHAFFLE_ARRAY")
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")
#DIM 対象
#DIM 対象NO

SIF !SLGフラグ && !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF SLGフラグ && FLAG:観戦モード
	RETURN 0

SIF SLGフラグ && CFLAG:MASTER:捕虜先
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0


DAILY_CANCEL = 0
SLG = %(SLGフラグ ? "SLG_" # "" )%


;口上用デイリー MASTERのものは発動しない
;順番決定
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_CHARA"

FOR LOCAL, 1, CHARANUM
	対象 = EVENT_CHARA:LOCAL
	対象NO = NO:対象
	SIF !IS_FIXED_CHARA(対象)
		CONTINUE

	EVENT_NUM = KOJO_DAILY_EVENT_NUM:対象NO:(SLGフラグ ? デイリー_ＳＬＧ派生 # デイリー_通常派生)

	FOR LOCAL:1, 0, EVENT_NUM
		IF SLGフラグ
			NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:対象NO:デイリー_ＳＬＧ派生:LOCAL
			NAME_JAP '= KOJO_DAILY_EVENT_NAME:対象NO:デイリー_ＳＬＧ派生:LOCAL
		ELSE
			NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:対象NO:デイリー_通常派生:LOCAL
			NAME_JAP '= KOJO_DAILY_EVENT_NAME:対象NO:デイリー_通常派生:LOCAL
		ENDIF

		RESULT = 0
		TRYCCALLFORM %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_DISABLE()
			SIF RESULT == 1
				CONTINUE


			TRYCALLFORM %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_DECISION()
			SIF RESULT == 0
				CONTINUE

			VARSET DAILY_TARGET, -1
			VARSET DAILY_TARGET_NUM
			RESULT = 1
			TRYCALLFORM %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_SETTARGET()
			SIF RESULT == 0
				CONTINUE

			RESULTS = 
			TRYCALLFORM %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_NAME()
			IF RESULTS == ""
				CALL KOJO_DAILY_START_DERIVATION(NAME_JAP)
			ELSE
				CALL KOJO_DAILY_START_DERIVATION(RESULTS)
			ENDIF

			CALLFORM %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%()
			SIF DAILY_CANCEL
				RETURN
			CALLFORM %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%()
			SIF DAILY_CANCEL
				RETURN
		CATCH
			DEBUGPRINTFORML %SLG%KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_DECISIONみつからず
		ENDCATCH
	NEXT
NEXT
