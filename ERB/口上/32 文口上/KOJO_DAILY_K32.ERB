@KOJO_DAILY_EXIST_K32()

@KOJO_DAILY_K32()
#DIM 発生フラグ
発生フラグ = 0

;新聞勧誘
DAILY_NAME = 新聞勧誘
CALL KOJO_DAILY_K32_INVITATION_FROM_AYA
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;水遊び
DAILY_NAME = 水遊び
CALL KOJO_DAILY_K32_MIZUASOBI
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ


;------------------------------------
;新聞勧誘
;-----------------------------------
@KOJO_DAILY_K32_INVITATION_FROM_AYA
#DIM ボス
#DIM 文
文 = NAME_TO_CHARA("文")

;文と別勢力で、捕虜でない 4%
SIF !CHECK_KOJO_DAILY_HAPPEN(文, 40, 0, 0, -1)
	RETURN -1

SIF KDVAR:文:文_新聞勧誘
	RETURN -1

;主人公は男であること
SIF !IS_MALE(MASTER)
	RETURN -1

;主人公が君主でない
SIF GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
	RETURN -1

;三能力の合計が200以上
SIF ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 < 200
	RETURN -1
	
ボス = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

CALL KOJO_DAILY_START()

PRINTFORML %ANAME(MASTER)%が仕事をしていると、部下が来客を告げた……
PRINTFORMW 「どうも！　こんにちは！　清く正しい射命丸です！」
PRINTFORMW 「新聞はいかがですか？　今ならお得な割引キャンペーン中ですよ！」
PRINTFORML 通せと伝えるより先に、%ANAME(文)%は部屋に入っていた
PRINTFORMW %ANAME(MASTER)%は部下を下げさせ、来客用の椅子に%ANAME(文)%を座らせる
PRINTFORMW 「人払いしちゃうんですか？　不用心ですねぇ、私が害意を抱いていたらどうするつもりです？」
PRINTFORML 流石にここまで堂々と要人暗殺に及ぶ奴もいるまい
PRINTFORMW それに、いざという時は自分一人で厄介事を退けるくらいの腕はあるつもりだ……
PRINTFORMW そう返すと、%ANAME(文)%はニヤリと笑った
PRINTFORMW 「ふぅん、それなりに考えの回るほうなのかしらね、わざわざ足を運んだのは無駄じゃなかったかしら」
PRINTFORMW 彼女の雰囲気が変わった。こちらの方が素なのだろう。特に気にすることもなく、話を促す
PRINTFORMW 「%ANAME(MASTER)%。優秀な武将だそうね？」
PRINTFORML 優秀かどうかはともかく、そこらの凡百に後れをとるつもりはなかった
PRINTFORMW 「こんな時代だからね、そういう人材を得るためなら、私は手間暇を惜しむつもりはないわ」
PRINTFORMW 「もちろん、ただでとは言わない。そうね、幾ばくかの支度金、それに、女なんてどう？　英雄色を好むともいうしね」
PRINTFORML 口端から舌をちろりとうかがわせる文は、快活な新聞記者としての印象とは裏腹な、妖艶な雰囲気を纏っていた
PRINTFORMW 女というのは、つまり彼女のことなのだろう
PRINTFORML ……それにしても、何の密談かと思ったら、どうやら自分を引き抜きにきたらしい
PRINTFORML 驚くようなことではない。幻想郷は混迷を極めている。生き延びるには、優秀な人材は必須だ
PRINTFORMW こちらとしても、生き残っていくためには、少しでも強力な勢力の庇護を求めるのが得策だ
PRINTFORMW ……だが、この話を受ければ、今の主君の方面との人間関係は、間違いなくこじれてしまうだろう
PRINTFORML さて、どうしようか……
DRAWLINE
PRINTFORML %ANAME(文)%の領地数:{GET_OWN_CITY(CFLAG:文:所属)} %ANAME(ボス)%の領地数:{GET_OWN_CITY(CFLAG:MASTER:所属)}
PRINTFORML 
CALL ASK_YN("話を受ける", "断る")
IF RESULT == 1
	PRINTFORMW 「……あら、そう？　それは残念」
	PRINTFORML %ANAME(ボス)%を裏切るわけにはいかない……
	PRINTFORMW そう答えると、%ANAME(文)%は案外あっさりと引いてみせた
	PRINTFORMW 「そうですか、それなら仕方ありませんね！　文々。新聞はいい新聞なんですが、必要とされない方に押しつけることもないでしょう」
	PRINTFORMW 「では私はこれで！　……次に会うとき、敵同士になっていないことを祈ってるわよ？」
	PRINTFORMW 不敵な笑みを浮かべ、%ANAME(文)%は風のように立ち去っていった……
ELSE
	PRINTFORMW 「……ふふ、そう言ってくれると思ってたわ」
	PRINTFORMW 「これから、どうぞよろしく」
	PRINTFORMW 差し出された手を握る。柔らかく、温かい手だった
	LOCAL:0 = MIN((ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治) * 200, 50000)
	CALL COLORPRINTFORM(@"支度金として金{LOCAL:0}を受け取りました", COLOR("注意"), "W")
	CALL COLORPRINTFORM(@"%ANAME(文)%の合意を得ました", COLOR("桃"), "W")
	CALL COLORPRINTFORM(@"%ANAME(ボス)%との外交関係が、非常にこじれました", COLOR("警告"), "W")
	MONEY += LOCAL:0
	TALENT:文:合意 = 1
	CFLAG:文:面識 = 1
	CFLAG:文:好感度 = MAX(CFLAG:文:好感度, 800)
	CFLAG:文:従属度 = MAX(CFLAG:文:従属度, 600)
	CFLAG:文:依存度 = MAX(CFLAG:文:依存度, 200)
	CALL CHANGE_RELATION_O_TO_O(ボス, 文, -500, 500)
	CALL CHANGE_RELATION_O_TO_O(ボス, MASTER, -500, 500)
	CALL CHANGE_COUNTRY(MASTER, CFLAG:文:所属, 1)
	PRINTFORMW 「それじゃあ、約束の『女』だけど……」
	PRINTFORMW 纏う衣服を、%ANAME(文)%は脱ぎ捨てていく
	PRINTFORMW 「ふふ、天狗の閨技、たっぷりと味わわせてあげる……」
	PRINTFORMW %ANAME(MASTER)%は力を抜き、%ANAME(文)%に身をゆだねた……
	;天狗の閨技
	ABL:文:性知識 = MAX(ABL:文:性知識, 4)
	ABL:文:欲望 = MAX(ABL:文:欲望, 3)
	ABL:文:性技 = MAX(ABL:文:性技, 3)
	ABL:文:奉仕 = MAX(ABL:文:奉仕, 2)
	ABL:文:性交 = MAX(ABL:文:性交, 2)
	ABL:文:精愛 = MAX(ABL:文:精愛, 1)
	TALENT:文:快感に素直 = 1
	;全員の調教参加フラグをクリア
	CVARSET CFLAG, 6, 0
	CFLAG:文:調教参加フラグ = 1
	CFLAG:文:93 = 1
	CFLAG:文:調教中帰らない = 1
	BASE:文:体力 = MAXBASE:文:体力
	BASE:文:気力 = MAXBASE:文:気力
	;強制逆調教フラグをオン
	FLAG:強制調教フラグ = 1
	;それ以降のデイリーイベント発生を潰す
	DAILY_CANCEL = 1
ENDIF

;フラグ
KDVAR:文:文_新聞勧誘 = 1

;----------------------------------
;水遊び
;----------------------------------
@KOJO_DAILY_K32_MIZUASOBI
#DIM 文
文 = NAME_TO_CHARA("文")

;文が同じ勢力で、捕虜でないなら 3.5%
SIF !(CHECK_KOJO_DAILY_HAPPEN(文, 35, 1, 0, 1))
	RETURN -1

;好感度が必要
SIF CFLAG:文:好感度 >= 500
	RETURN -1

;一回きり
SIF KDVAR:文:文_水遊び
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORMW 「はぁ～、暑い……あの、%ANAME(MASTER)%、もう帰りませんか？」
PRINTFORMW 「空を飛んでる間はいいけれど、こうやって地を歩くと、本当にこの暑さが嫌になってくるわ……」
PRINTFORML うだるように暑い中、%ANAME(MASTER)%と%ANAME(文)%は領地の見回りをしていた
PRINTFORML %ANAME(文)%は大汗をかいている。暑がりなのだそうだ
PRINTFORMW 襟元をぱたぱたとあおぐものだから、目のやり場に困ってしまうが……
PRINTFORML 「ん……あ、ちょっと、%ANAME(MASTER)%、見てくださいよ、ちょうどいいところに川がありましたよ」
PRINTFORMW 「この暑さ、そして涼しげな川！　するべきことといったら一つですよねぇ？」
PRINTFORMW 「さ、さ、参りましょう、この程度ならサボりのうちにも入りませんって！　ね？」
PRINTFORML %ANAME(文)%の言うとおり、目の前には行水におあつらえ向きの小川が横たわっている
PRINTFORML 
PRINTFORML さて、どうしたものか……
PRINTFORML
CALL ASK_YN("水遊びする", "やめておく")
IF RESULT == 0
	PRINTFORML 「さすが%ANAME(MASTER)%！　そう言ってくれると思いましたよ！」
	PRINTFORML 「そうと決まればさあ、善は急げですよ！」
	PRINTFORML 文に手を引かれ、水遊びを楽しんだ……
	PRINTFORMW
	PRINTFORMW
	PRINTFORMW
	PRINTFORML 拠点に戻った%ANAME(MASTER)%は、なんだか寒気を覚えた
	PRINTFORML どうやら風邪をひいてしまったようだ……
	CALL COLORPRINTFORM(@"%ANAME(MASTER)%に3ターンのクールタイムがつきました", COLOR("警告"), "W")
	CALL ADD_COOLTIME(MASTER, 3)
	KDVAR:文:文_水遊び = 1
ELSE
	PRINTFORMW 「えぇー？　ちょっとくらいいいじゃないですか、ね？」
	PRINTFORMW 「……駄目？　はぁ、しょうがないか……」
	PRINTFORML 仕事をさぼるわけにはいかない
	PRINTFORMW 誘惑を断ち切り、見回りに戻ることにした……
	KDVAR:文:文_水遊び = 2
ENDIF
