@KOJO_DAILY_EXIST_K35()

@KOJO_DAILY_K35()
#DIM 発生フラグ
発生フラグ = 0

;
DAILY_NAME = 昼寝
CALL KOJO_DAILY_K35_SABORI
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ

;-----------------------------
;鋼鉄巨人バトルガイオン
;-----------------------------
@KOJO_DAILY_K35_SABORI
#DIM 小町
#DIM 四季映姫
小町 = NAME_TO_CHARA("小町")
四季映姫 = NAME_TO_CHARA("四季映姫")

;小町と四季映姫が同一勢力で捕虜でない。小町とは面識が必要。確率判定はイベント進行で変動させるのでダミー
SIF !CHECK_KOJO_DAILY_HAPPEN(小町, 1000, 1, 0, 1) || !CHECK_KOJO_DAILY_HAPPEN(四季映姫, 1000, 1, 0, -1)
	RETURN -1

;MASTERが四季映姫だとおかしなことになるんで駄目
SIF MASTER == NAME_TO_CHARA("四季映姫")
	RETURN -1

;終わってたら発生しない
SIF KDVAR:小町:小町_昼寝 == -1
	RETURN -1

;サボり魔が直ってたら駄目
SIF !TALENT:小町:サボり魔
	RETURN -1

;最初は5%
SIF KDVAR:小町:小町_昼寝 == 0 && RAND:1000 >= 50
	RETURN -1

;それ以降は40%
SIF KDVAR:小町:小町_昼寝 > 0 && RAND:1000 >= 400
	RETURN -1

CALL KOJO_DAILY_START
SELECTCASE KDVAR:小町:小町_昼寝
	CASE 0
		PRINTFORMW 「おっ、%ANAME(MASTER)%じゃないか」
		PRINTFORML ある日、裏庭を散歩していると、%ANAME(小町)%を見かけた
		PRINTFORMW この時間帯は彼女は警邏の当番だったはずだが……？
		PRINTFORMW 「いやほら、今日はぽかぽかとしてて気持ちがいいだろう？」
		PRINTFORMW 「こんな日に働くなんて間違ってるよ。ここは休息して、英気を養うべきだ」
		PRINTFORML ……要はサボって昼寝をしようということらしい
		PRINTFORMW どうしたものかと思っていると、%ANAME(小町)%は手招きしてきた
		PRINTFORMW 「%ANAME(MASTER)%もさ。働き過ぎはよくないよ？　あたいが正しい休息の仕方ってのをレクチャーしたげるよ」
		PRINTFORMW 「いや、共犯者を得ようとかそんなこと考えてないから、マジで」
		PRINTFORMW そう言う%ANAME(小町)%は、地面に布を敷いて、昼寝する気まんまんでいる
		PRINTFORML さて、どうしようか……
		PRINTFORML
		CALL ASK_MULTI("誘いに乗る", "やめておく", "四季映姫に報告だ")
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「おっ、話が分かるねぇ」
				PRINTFORMW 「ほら、おいでよ、一緒に昼寝しようじゃないか」
				PRINTFORML %ANAME(小町)%と一緒に昼寝した……
				CALL COLORPRINTFORM(@"%ANAME(MASTER)%が＜サボり魔＞になりました", COLOR("警告"), "W")
				CALL COLORPRINTFORM(@"サボって腕が鈍った%ANAME(MASTER)%と%ANAME(小町)%の武闘が3下がりました", COLOR("警告"), "W")
				TALENT:MASTER:サボり魔 = 1
				ABL:MASTER:武闘 = MIN_DECREASE(ABL:MASTER:武闘, 3, 1)
				ABL:小町:武闘 = MIN_DECREASE(ABL:小町:武闘, 3, 1)
				CFLAG:小町:好感度 += 50
				KDVAR:小町:小町_昼寝 = 1
			CASE 1
				PRINTFORMW 「む……そうかい？」
				PRINTFORMW 「ならしょうがないね。ここはあたいの秘密スポットだから、誰にも言わないどいとくれよ？」
				PRINTFORMW 言うだけ言って、%ANAME(小町)%はグースカ寝始めた……
				KDVAR:小町:小町_昼寝 = -1
			CASE 2
				PRINTFORMW 「ちょ……それは駄目だって……！」
				PRINTFORMW 「何が駄目だというんです？」
				PRINTFORMW 「おあぁ四季様！！？　いつからいらしたんです！！！！？」
				PRINTFORMW 「ほんの一部始終ですよ。『おっ、%ANAME(MASTER)%じゃないか』のくだりから」
				PRINTFORMW 「全部じゃないですか！　うわぁすみませんお説教は勘弁して下さい！」
				PRINTFORMW 「だまらっしゃい！　今日という今日は改心してもらいますからね！」
				PRINTFORMW その後、%ANAME(小町)%は%ANAME(四季映姫)%にこってりとしぼられたようだ……
				CALL COLORPRINTFORM(@"%ANAME(小町)%が＜サボり魔＞ではなくなりました", COLOR("注意"), "W")
				TALENT:小町:サボり魔 = 0
				KDVAR:小町:小町_昼寝 = -1
		ENDSELECT
	CASEELSE
		PRINTFORMW 「おっ、%ANAME(MASTER)%」
		PRINTFORMW 「また昼寝するかい？」
		PRINTFORML
		CALL ASK_MULTI("誘いに乗る", "やめておく")
		SELECTCASE RESULT
			CASE 0
				IF KDVAR:小町:小町_昼寝 * 10 < RAND:100
					PRINTFORMW 「そう来なくっちゃね」
					PRINTFORMW 「ささ、おいでよ、一緒に寝ようじゃないか♪」
					PRINTFORMW 小町と昼寝して英気を養った……
					CALL COLORPRINTFORM(@"サボって腕が鈍った%ANAME(MASTER)%と%ANAME(小町)%の武闘が3下がりました", COLOR("警告"), "W")
					TALENT:MASTER:サボり魔 = 1
					ABL:MASTER:武闘 = MIN_DECREASE(ABL:MASTER:武闘, 3, 1)
					ABL:小町:武闘 = MIN_DECREASE(ABL:小町:武闘, 3, 1)
					CFLAG:小町:好感度 += 50
					KDVAR:小町:小町_昼寝 ++
				ELSE
					PRINTFORMW 「なぁにが『誘いに乗る』ですか。二人揃って嘆かわしい」
					PRINTFORMW ……ふと、後ろから声をかけられた
					PRINTFORMW おそるおそる振り返る……%ANAME(四季映姫)%だ！
					PRINTFORMW 「げぇっ　四季様！　いつからいらしたんです！？」
					PRINTFORMW 「だまらっしゃい！　二人揃ってサボるとは言語道断です！　そのたるんだ精神を徹底的にたたき直してあげますからね！」
					PRINTFORMW 二人揃って、%ANAME(四季映姫)%にたっぷりとしぼられた……
					LOCAL = 3 * KDVAR:小町:小町_昼寝 + KDVAR:小町:小町_昼寝
					CALL COLORPRINTFORM(@"%ANAME(四季映姫)%のお説教で、二人の武闘が{LOCAL}上昇しました", COLOR("注意"), "W")
					CALL COLORPRINTFORM(@"二人は＜サボり魔＞ではなくなりました", COLOR("注意"), "W")
					TALENT:MASTER:サボり魔 = 0
					TALENT:小町:サボり魔 = 0
					ABL:MASTER:武闘 += LOCAL
					ABL:小町:武闘 += LOCAL
					CFLAG:小町:好感度 += 50
					KDVAR:小町:小町_昼寝 = -1
				ENDIF
			CASE 1
				PRINTFORMW 「おや、そうかい？」
				PRINTFORMW 「それなら仕方ない。あたい一人で寝るとするかね」
				PRINTFORMW 言うなり、%ANAME(小町)%はグースカと眠り始めた……
				KDVAR:小町:小町_昼寝 = -1
		ENDSELECT
ENDSELECT
