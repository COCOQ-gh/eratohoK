@KOJO_DAILY_EXIST_K5()

@KOJO_DAILY_K5()
#DIM 発生フラグ
発生フラグ = 0

;最強伝説
DAILY_NAME = 最強伝説
CALL KOJO_DAILY_K5_SAIKYOU_LEGEND
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;----------------------------------
;最強伝説
;----------------------------------
@KOJO_DAILY_K5_SAIKYOU_LEGEND
#DIM チルノ
#DIMS 答え文字列 = "左", "右", "上", "下"
#DIM アップ能力
#DIM 答え

チルノ = NAME_TO_CHARA("チルノ")

;チルノが同一勢力、面識あり、捕虜でない、6%
SIF !(CHECK_KOJO_DAILY_HAPPEN(チルノ, 60, 1, 0, 1))
	RETURN -1

;主人公は大妖精、レティ以外
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("大妖精"), NAME_TO_CHARA("レティ"))
	RETURN -1

;5能力の合計が250以上必要
SIF ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + ABL:MASTER:歌唱 + ABL:MASTER:料理 < 250
	RETURN -1

;一回きり
SIF KDVAR:チルノ:チルノ_最強伝説
	RETURN -1


;上がる能力を出しておく
SELECTCASE RAND:100
	CASE IS < 40
		アップ能力 = GETNUM(ABL, "武闘")
	CASE IS < 60
		アップ能力 = GETNUM(ABL, "知略")
	CASE IS < 80
		アップ能力 = GETNUM(ABL, "政治")
	CASE IS < 90
		アップ能力 = GETNUM(ABL, "料理")
	CASEELSE
		アップ能力 = GETNUM(ABL, "歌唱")
ENDSELECT

CALL KOJO_DAILY_START()

PRINTFORMW 「やいやい！　%ANAME(MASTER)%！」
PRINTFORMW 散歩をしていると、突然%ANAME(チルノ)%がつっかかってきた
PRINTFORMW 「ちょっと強いからっていい気になってるってね！」
PRINTFORMW 「あたいの方が強いんだから！　勝負だ！」
PRINTFORMW ……誰かは知らないが、%ANAME(チルノ)%を焚きつけたようだ
PRINTFORML すっかりやる気のようだが……
PRINTL
CALL ASK_YN("付き合う", "付き合ってられるか")
IF RESULT == 1
	PRINTFORMW 「え？　勝負しないの？」
	PRINTFORMW 「……あっ！　あたいの力に恐れをなしたってわけね！」
	PRINTFORMW 「やっぱりあたいが一番強いってわけ！　ふふーん！」
	PRINTFORMW 勝ち誇る%ANAME(チルノ)%を放って、その場を立ち去った……
	KDVAR:チルノ:チルノ_最強伝説 = 1
	RETURN
ENDIF

PRINTFORMW その思い上がりは危険だ。正してやらなくてはなるまい
PRINTFORMW %ANAME(MASTER)%は頷いた……
PRINTFORMW 「そう来なくっちゃ！　いくよ！」
PRINTFORML %ANAME(チルノ)%はアイシクルフォールを放ってきた！

;4回、正しい選択肢を選ぶ
FOR LOCAL, 0, 4
	;答えを出しておく
	答え = RAND:4
	CALL COLORPRINT(@"%答え文字列:答え%に避けろ！", COLOR("警告"), "L")
	FOR LOCAL:1, 0, VARSIZE("答え文字列")
		PRINTFORML [{LOCAL:1}] %答え文字列:(LOCAL:1)%
	NEXT
	TINPUT 2000, -1, 1
	;失敗したら強制終了
	SIF RESULT != 答え
		BREAK
	SIF LOCAL != 3
		PRINTFORML 「そう来なくっちゃ！　もっといくよ！」
NEXT

;失敗したパターン
IF LOCAL != 4
	PRINTFORML %ANAME(MASTER)%は回避しようと試みた！
	PRINTFORMW だが、弾幕はものの見事に%ANAME(MASTER)%に命中した！
	PRINTFORMW 「やったー！　あたいの勝ちね！」
	PRINTFORMW 「やっぱりあたいが一番強いんだから！」
	CALL COLORPRINT(@"%ANAME(MASTER)%は怪我をしてしまいました", COLOR("警告"), "W")
	CALL ADD_COOLTIME(MASTER, 3)
ELSE
	;成功
	PRINTFORML 弾幕を見事回避した！
	PRINTFORMW 「むっ……！　なかなかやるじゃんか……！」
	PRINTFORMW 「ふんっ！　今のは本気じゃなかったもん！　せーぜー自慢してなさい！」
	PRINTFORMW %ANAME(チルノ)%は逃げていった……
	CALL COLORPRINT(@"%ANAME(チルノ)%はその後、こっそり特訓したようです", COLOR("注意"), "W")
	CALL COLORPRINT(@"%ANAME(チルノ)%の%ABLNAME:アップ能力%が5上昇しました", COLOR("注意"), "W")
	ABL:チルノ:アップ能力 += 5
ENDIF

KDVAR:チルノ:チルノ_最強伝説 = 1
RETURN
