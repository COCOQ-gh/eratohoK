;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K5_SAIKYOU_LEGEND_RATE(対象)
#DIM 対象
RETURN 60


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K5_SAIKYOU_LEGEND_DECISION(対象)
#DIM 対象

;主人公は大妖精、レティ以外
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("大妖精"), NAME_TO_CHARA("レティ"))
	RETURN 0

;5能力の合計が250以上必要
SIF ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + ABL:MASTER:歌唱 + ABL:MASTER:料理 < 250
	RETURN 0

;一回きり
SIF KDVAR:対象:チルノ_最強伝説
	RETURN 0


RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K5_SAIKYOU_LEGEND_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K5_SAIKYOU_LEGEND(対象)
#DIM 対象
#DIMS 答え文字列 = "左", "右", "上", "下"
#DIM アップ能力
#DIM 答え

;上がる能力を出しておく
SELECTCASE RAND:100
	CASE IS < 40
		アップ能力 = GETNUM(ABL, "武闘")
	CASE IS < 60
		アップ能力 = GETNUM(ABL, "知略")
	CASE IS < 80
		アップ能力 = GETNUM(ABL, "政治")
	CASE IS < 90
		アップ能力 = GETNUM(ABL, "料理")
	CASEELSE
		アップ能力 = GETNUM(ABL, "歌唱")
ENDSELECT

PRINTFORMW 「やいやい！　%ANAME(MASTER)%！」
PRINTFORMW 拠点の見回りをしていると、突然%ANAME(対象)%がつっかかってきた
PRINTFORMW 「ちょっと強いからっていい気になってるってね！」
PRINTFORMW 「あたいの方が強いんだから！　勝負だ！」
PRINTFORMW ……誰かは知らないが、%ANAME(対象)%を焚きつけたようだ
PRINTFORML すっかりやる気のようだが……
PRINTL
CALL ASK_YN("付き合う", "付き合ってられるか")
IF RESULT == 1
	PRINTFORMW 「え？　勝負しないの？」
	PRINTFORMW 「……あっ！　あたいの力に恐れをなしたってわけね！」
	PRINTFORMW 「やっぱりあたいが一番強いってわけ！　ふふーん！」
	PRINTFORMW 勝ち誇る%ANAME(対象)%を放って、その場を立ち去った……
	KDVAR:対象:チルノ_最強伝説 = 1
	RETURN 1
ENDIF

PRINTFORMW その思い上がりは危険だ。正してやらなくてはなるまい
PRINTFORMW %ANAME(MASTER)%は頷いた……
PRINTFORMW 「そう来なくっちゃ！　いくよ！」
PRINTFORML %ANAME(対象)%はアイシクルフォールを放ってきた！

;4回、正しい選択肢を選ぶ
FOR LOCAL, 0, 4
	;答えを出しておく
	答え = RAND:4
	CALL COLORPRINT(@"%答え文字列:答え%に避けろ！", カラー_警告, "L")
	FOR LOCAL:1, 0, VARSIZE("答え文字列")
		PRINTFORML [{LOCAL:1}] %答え文字列:(LOCAL:1)%
	NEXT
	TINPUT 2000, -1, 1
	;失敗したら強制終了
	SIF RESULT != 答え
		BREAK
	SIF LOCAL != 3
		PRINTFORML 「そう来なくっちゃ！　もっといくよ！」
NEXT

;失敗したパターン
IF LOCAL != 4
	PRINTFORML %ANAME(MASTER)%は回避しようと試みた！
	PRINTFORMW だが、弾幕はものの見事に%ANAME(MASTER)%に命中した！
	PRINTFORMW 「やったー！　あたいの勝ちね！」
	PRINTFORMW 「やっぱりあたいが一番強いんだから！」
	CALL COLORPRINT(@"%ANAME(MASTER)%は怪我をしてしまいました", カラー_警告, "W")
	CALL ADD_COOLTIME(MASTER, 3)
ELSE
	;成功
	PRINTFORML 弾幕を見事回避した！
	PRINTFORMW 「むっ……！　なかなかやるじゃんか……！」
	PRINTFORMW 「ふんっ！　今のは本気じゃなかったもん！　せーぜー自慢してなさい！」
	PRINTFORMW %ANAME(対象)%は逃げていった……
	CALL COLORPRINT(@"%ANAME(対象)%はその後、こっそり特訓したようです", カラー_注意, "W")
	CALL COLORPRINT(@"%ANAME(対象)%の%ABLNAME:アップ能力%が5上昇しました", カラー_注意, "W")
	ABL:対象:アップ能力 += 5
ENDIF

KDVAR:対象:チルノ_最強伝説 = 1
RETURN 1
