;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K95_SHIMAI_DON_RATE(対象)
#DIM 対象
RETURN 35


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K95_SHIMAI_DON_DECISION(対象)
#DIM 対象
#DIM 八橋

八橋 = NAME_TO_CHARA("八橋")

SIF MASTER == 八橋
	RETURN 0

SIF !(MAX(CFLAG:対象:好感度, CFLAG:対象:従属度) >= 500 && MAX(CFLAG:八橋:好感度, CFLAG:八橋:従属度) >= 500)
	RETURN 0

SIF !(ABL:対象:欲望 >= 2 && ABL:八橋:欲望 >= 2)
	RETURN 0

SIF KDVAR:対象:弁々_シマイガッキ == -1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && CHECK_KOJO_DAILY_HAPPEN(八橋, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K95_SHIMAI_DON_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_エロ

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K95_SHIMAI_DON(対象)
#DIM 対象
#DIM 八橋
八橋 = NAME_TO_CHARA("八橋")

IF KDVAR:対象:弁々_シマイガッキ
	PRINTFORMW %ANAME(対象)%と%ANAME(八橋)%が寝室を訪ねてきた……
	PRINTFORMW 「また、駄目かしら……」
	PRINTFORMW 「私と%ANAME(八橋)%と、二人まとめて、ね？」
ELSE
	PRINTFORMW 深夜。%ANAME(対象)%と%ANAME(八橋)%が寝室を訪ねてきた……
	PRINTFORMW 「%ANAME(MASTER)%、実はお願いがあるのだけど」
	PRINTFORMW 「人っていうのは、お互いの信頼を確かめ合うために、身体を重ねる？　ということをするそうね」
	PRINTFORMW 「私と八橋は付喪神として一緒に目覚めた姉妹よ。だから信頼を確かめ合うっていうそれをしてみたいのだけど……」
	PRINTFORMW 知性を得てから時間が短いので、やり方が分からない、ということだろう
	PRINTFORMW %ANAME(八橋)%が言葉を引き継いだ
	PRINTFORMW 「それで%ANAME(MASTER)%に教えてもらおうって、姉さんが言うからさ」
	PRINTFORMW 「二人で来たってわけ。そしたら、三人でできるでしょ？　……できるよね？」
	PRINTFORMW 「それで、どう？」
	PRINTFORMW 「私たちとその、身体を重ねる？　っていうの、してくれるの？」
	PRINTFORMW ……どうやら二人は、それがどういう行為のことなのかまるで知らないらしい
	PRINTFORMW さて、どうするか……
ENDIF

PRINTFORML 
CALL ASK_YN("する", "やめておく")

IF RESULT == 1
	PRINTFORMW 「……そう？　残念ね」
	PRINTFORMW 「それなら仕方ないわ。お邪魔したわね」
	PRINTFORMW 二人は立ち去った……
	IF KDVAR:対象:弁々_シマイガッキ == 0
		KDVAR:対象:弁々_シマイガッキ = -1
	ENDIF
	RETURN 1
ENDIF

IF KDVAR:対象:弁々_シマイガッキ == 0
	PRINTFORMW 「ありがとう。それでえっと、具体的にはどうしたら……」
	PRINTFORMW 「服を脱ぐの？　なんだか変わってるのね」
	PRINTFORMW 二人を寝床に招き、押し倒した……
	CALL COLORPRINT(@"%ANAME(対象)%の合意を得ました", カラー_注意, "W")
	CALL COLORPRINT(@"%ANAME(八橋)%の合意を得ました", カラー_注意, "W")
	TALENT:対象:合意 = 1
	TALENT:八橋:合意 = 1
	TALENT:対象:チョロイン = 1
	TALENT:八橋:チョロイン = 1
ELSEIF RAND:2
	PRINTFORMW 「……痛くしないでよ？」
	PRINTFORMW 二人を寝床に招き、押し倒した……
ELSE
	PRINTFORMW 「ええと、その……よろしくね？」
	PRINTFORMW 二人を寝床に招き、押し倒した……
ENDIF

;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0

CFLAG:八橋:調教参加フラグ = 1
CFLAG:八橋:調教中帰らない = 1
CFLAG:八橋:強制友好化 = 1
BASE:八橋:体力 = MAXBASE:八橋:体力
BASE:八橋:気力 = MAXBASE:八橋:気力

CFLAG:対象:調教参加フラグ = 1
CFLAG:対象:調教中帰らない = 1
CFLAG:対象:強制友好化 = 1
BASE:対象:体力 = MAXBASE:対象:体力
BASE:対象:気力 = MAXBASE:対象:気力

;強制逆調教フラグをオン
FLAG:ターンエンド調教 = 1
;それ以降のデイリーイベント発生を潰す
DAILY_CANCEL = 1
;フラグ
KDVAR:対象:弁々_シマイガッキ = 1

RETURN 1

