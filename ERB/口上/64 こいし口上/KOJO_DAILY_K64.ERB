@KOJO_DAILY_K64()

;おやつ泥棒
CALL KOJO_DAILY_K64_OYATU_THEIF

;脱獄
CALL KOJO_DAILY_K64_JAILBREAK

;----------------------------------
;おやつ泥棒
;----------------------------------
@KOJO_DAILY_K64_OYATU_THEIF
#DIM こいし
#DIM 判定値

こいし = NAME_TO_CHARA("こいし")

;こいしがいないと駄目
SIF こいし == -1

;こいしが君主ではない
SIF GET_COUNTRY_BOSS(CFLAG:こいし:所属) != こいし
	RETURN -1

;おわってたら出ない
SIF KDVAR:こいし:こいし_おやつ泥棒 == -1
	RETURN -1

;こいしとMASTERが別勢力で、こいしが捕虜でなく、生存していて、6%
SIF !(RAND:1000 < 6 && !CFLAG:こいし:所属 != CFLAG:MASTER:所属 && !CFLAG:こいし:捕虜先 && CFLAG:こいし:特殊状態フラグ != 2)
	RETURN -1

判定値 = MIN(ABL:MASTER:知略 + KDVAR:こいし:こいし_おやつ泥棒 * 20, 40)

DRAWLINE
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
;判定をクリアしたらとっ捕まえる。
IF KDVAR:こいし:こいし_おやつ泥棒 != 0 && (RAND:100 < 判定値 || KDVAR:こいし:こいし_おやつ泥棒 == 5)
	PRINTFORML 静まりかえった夜の宮廷。そこに、怪しく動く影があった
	PRINTFORMW 影は戸棚を開き……%ANAME(MASTER)%のおやつを取り出した！
	PRINTFORMW ――そこまでだ！
	PRINTFORMW 「いったーい！　放してよー！」
	PRINTFORM %ANAME(MASTER)%は飛びかかり、影を押さえつける。影の正体は――
	CALL COLORPRINTFORM(@"%NAME_FORMAL(こいし)%", COLOR("注意"))
	PRINTFORM だ！
	PRINTFORML なるほど、無意識を操る彼女の能力なら、宮廷に忍び込んでおやつをつまみ食いすることなど容易だ
	PRINTFORMW そのことを理解すると同時に、%ANAME(MASTER)%の背筋が寒くなる
	PRINTFORMW この子の能力をもってすれば、要人暗殺すらも赤子の手をひねるようなものなのでは？
	PRINTFORMW 「ねー、放してってばー。盗み食いしたのは謝るからー」
	PRINTFORML こいしはじたばたと暴れている。ただのおやつ泥棒なら笑って許してやってもいいが……本当にいいのだろうか
	PRINTFORML 彼女の能力が、%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%に牙をむくことにならないだろうか……？
	DRAWLINE
	PRINTFORML
	CALL ASK_YN("許してやる", "衛兵を呼ぶ")
	IF RESULT == 0
		PRINTFORMW いやいや。無邪気な子のようだし、杞憂だろう
		PRINTFORMW そう考えた%ANAME(MASTER)%は、%ANAME(こいし)%を放してやった
		PRINTFORMW 「ありがと！　今度お礼するね！」
		PRINTFORMW %ANAME(こいし)%はぺこりとお辞儀すると、そのまま消え去った。無意識に潜んだのだろう……
		PRINTFORML
		IF GET_COUNTRY_BOSS(CFLAG:こいし:所属) != -1
			PRINTFORML
			PRINTFORMW 後日、%ANAME(GET_COUNTRY_BOSS(CFLAG:こいし:所属))%から、お詫びの銘菓が届いた
			PRINTFORMW まぁまぁの味だった……
			CALL CHANGE_RELATION_C_TO_C(CFLAG:こいし:所属, CFLAG:MASTER:所属, 200, -200)
		ELSE
			PRINTFORMW 後日、%ANAME(MASTER)%の枕元に、飴ちゃんが置かれていた
			PRINTFORMW いちご味だった……
		ENDIF
		CFLAG:こいし:好感度 += 200
	ELSE
		PRINTFORMW この子がもし、悪意を覚えたら。大層危険なことになる……
		PRINTFORMW そう考えた%ANAME(MASTER)%は、衛兵を呼びつけ、%ANAME(こいし)%を投獄させた……
		CFLAG:こいし:捕虜先 = CFLAG:MASTER:所属
		CFLAG:こいし:好感度 -= 300
		CALL CHANGE_RELATION_O_TO_O(こいし, MASTER, -300, 300)
	ENDIF
	IF !CFLAG:こいし:面識
		CALL COLORPRINTFORM(@"%ANAME(こいし)%の面識を得ました", COLOR("注意"), "W")
		CFLAG:こいし:面識 = 1
	ENDIF
	KDVAR:こいし:こいし_おやつ泥棒 = -1
	RETURN
ELSE
	CALL COLORPRINTFORM(@"アレッ！！！！！！！！？", COLOR("警告"), "W")
	PRINTFORML 楽しみにしておいたおやつが無くなっている！
	PRINTFORMW 確かに戸棚にしまっておいたはずなのに！
	PRINTFORML %ANAME(MASTER)%は慌てて部下や同僚に聞いて回るが、誰も食べていないという
	PRINTFORML まさかおやつに足が生えて逃げていったりはするまいし、誰かが食べてしまったのだろう
	PRINTFORMW 許すまじ、おやつ泥棒……！
	CALL COLORPRINTFORM(@"おやつを食べられなかったショックで、%ANAME(MASTER)%に1ターンのクールタイムがつきました", COLOR("警告"), "W")
	COOLTIME:MASTER:0 += 1
	KDVAR:こいし:こいし_おやつ泥棒 ++
ENDIF

;----------------------------------
;脱獄
;----------------------------------
@KOJO_DAILY_K64_JAILBREAK
#DIM こいし

こいし = NAME_TO_CHARA("こいし")

;こいしがいないと駄目
SIF こいし == -1
	RETURN -1

;こいしが捕まっていないと駄目
SIF !(RAND:100 < 8 && CFLAG:こいし:捕虜先)
	RETURN -1

DRAWLINE
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORMW 「こんなとこ、薄暗くてじめじめしててつまんなーい。遊びに行っちゃお！」
PRINTFORM 牢につながれていた
CALL COLORPRINTFORM(@"%NAME_FORMAL(こいし)%", COLOR("注意"))
PRINTFORML は、持ち前の能力で、誰にも気づかれることなく脱獄した……
CALL COLORPRINTFORM(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:こいし:捕虜先))%に囚われていた%ANAME(こいし)%が、捕虜ではなくなりました", COLOR("注意"), "W")
CFLAG:こいし:捕虜先 = 0
SIF CFLAG:こいし:所属 == 0
	CFLAG:こいし:特殊状態フラグ = 1
RETURN 1