;能力アップ画面の処理

;-------------------------------------------------
;コマンド実行時の自動能力上昇 ARG:0……キャラ番号 ARG:1……1ならメッセージを非表示
;-------------------------------------------------
@TRAIN_AUTO_ABLUP(ARG:0, ARG:1 = 0)
;能力固定フラグが立っているなら処理しない
IF CFLAG:(ARG:0):65
	RETURN
ENDIF

FOR LOCAL:0, 0, 70
	IF ABLNAME:(LOCAL:0) == "性知識"
		;性知識は別枠
		CALL TRAIN_AUTO_ABLUP_KNOWLEDGE(ARG:0, ARG:1)
	ELSE
		;能力に対応する経験値の名称を取得
		RESULTS = 
		TRYCALLFORM ABLUP_EXPNAME{LOCAL:0}
		IF RESULTS != ""

			LOCAL:1 = NAME_TO_NUM("EXP", RESULTS)

			WHILE ABL:(ARG:0):(LOCAL:0) < 9999
				;必要な経験値の計算
				RESULT = 0
				TRYCALLFORM CALC_ABLUP_EXP{LOCAL:0}(ARG:0)
				LOCAL:5 = RESULT

				IF LOCAL:5 >= 0 && EXP:(ARG:0):(LOCAL:1) >= LOCAL:5
					ABL:(ARG:0):(LOCAL:0) ++
					EXP:(ARG:0):(LOCAL:1) -= LOCAL:5
					IF ARG:1 == 0
						SETCOLOR COLOR("オレンジ")
						PRINTFORML %ANAME(ARG:0)%の%ABLNAME:(LOCAL:0)%が上昇しました(Lv{ABL:(ARG:0):(LOCAL:0)})
						RESETCOLOR
					ENDIF
				ELSE
					BREAK
				ENDIF
			WEND
		ENDIF
	ENDIF
NEXT


;-------------------------------------------------
;性知識の特殊なLv上昇処理 ARG:0……キャラ番号 ARG:1……1ならメッセージを非表示
;-------------------------------------------------
@TRAIN_AUTO_ABLUP_KNOWLEDGE(ARG:0, ARG:1)
;能力固定フラグが立っているなら処理しない
IF CFLAG:(ARG:0):65
	RETURN
ENDIF

WHILE ABL:(ARG:0):性知識 < 5
	;常に経験値100でLv上昇
	IF EXP:(ARG:0):性知識経験値 >= 100
		ABL:(ARG:0):性知識 ++
		EXP:(ARG:0):性知識経験値 = 0
		IF ARG:1 == 0
			SETCOLOR COLOR("オレンジ")
			PRINTFORML %ANAME(ARG:0)%の性知識が上昇しました(Lv{ABL:(ARG:0):性知識})
			RESETCOLOR
		ENDIF
	ELSE
		BREAK
	ENDIF
WEND


