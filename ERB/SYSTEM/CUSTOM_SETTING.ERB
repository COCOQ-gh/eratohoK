;-------------------------------------------------
;キャラARG:0にARG:1番目のカスタムデータを適用
;-------------------------------------------------
@APPLY_CUSTOM_DATA(ARG:0, ARG:1)
;基本名(NAME)
NAME:(ARG:0) = %CUSTOM_NAME:(ARG:1):0%
;呼び名(CALLNAME)（旧真名）
CALLNAME:(ARG:0) = %CUSTOM_NAME:(ARG:1):1%
;姓
CSTR:(ARG:0):0 = %CUSTOM_NAME:(ARG:1):2%
;名
CSTR:(ARG:0):1 = %CUSTOM_NAME:(ARG:1):3%
;字
CSTR:(ARG:0):2 = %CUSTOM_NAME:(ARG:1):4%
;基本名(NAME)のフリガナ
CSTR:(ARG:0):3 = %CUSTOM_NAME:(ARG:1):5%
;呼び名(CALLNAME)のフリガナ
CSTR:(ARG:0):4 = %CUSTOM_NAME:(ARG:1):6%
;姓のフリガナ
CSTR:(ARG:0):5 = %CUSTOM_NAME:(ARG:1):7%
;名のフリガナ
CSTR:(ARG:0):6 = %CUSTOM_NAME:(ARG:1):8%
;字のフリガナ
CSTR:(ARG:0):7 = %CUSTOM_NAME:(ARG:1):9%
;愛称
CSTR:(ARG:0):98 = %CUSTOM_NAME:(ARG:1):10%
FOR LOCAL:0, 0, MAX_TAG
	TAG:(ARG:0):(LOCAL:0) = CUSTOM_TAG:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 100
	MAXBASE:(ARG:0):(LOCAL:0) = CUSTOM_MAXBASE:(ARG:1):(LOCAL:0)
	BASE:(ARG:0):(LOCAL:0) = MAXBASE:(ARG:0):(LOCAL:0)
	ABL:(ARG:0):(LOCAL:0) = CUSTOM_ABL:(ARG:1):(LOCAL:0)
	EXP:(ARG:0):(LOCAL:0) = CUSTOM_EXP:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 1000
	TALENT:(ARG:0):(LOCAL:0) = CUSTOM_TALENT:(ARG:1):(LOCAL:0)
NEXT

FOR LOCAL:0, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		SKILL_NO_SLOT:(ARG:0):(LOCAL:0):(LOCAL:1) = CUSTOM_SKILL_NO:(ARG:1):(LOCAL:0):(LOCAL:1)
		SKILL_ID_SLOT:(ARG:0):(LOCAL:0):(LOCAL:1) = CUSTOM_SKILL_ID:(ARG:1):(LOCAL:0):(LOCAL:1)
	NEXT
NEXT

;キス未経験でないならキス経験を不明として記録
IF !TALENT:(ARG:0):キス未経験
	SEXUAL_EXPERIENCE:(ARG:0):初体験_ファーストキス = 不明
ELSE
	SEXUAL_EXPERIENCE:(ARG:0):初体験_ファーストキス = ----
ENDIF

IF HAS_VAGINA(ARG:0) && !TALENT:(ARG:0):処女
	SEXUAL_EXPERIENCE:(ARG:0):初体験_処女 = 不明
ELSE
	TALENT:(ARG:0):処女 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_処女 = ----
ENDIF

IF HAS_PENIS(ARG:0) && !TALENT:(ARG:0):童貞
	SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = 不明
ELSE
	TALENT:(ARG:0):童貞 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = ----
ENDIF

IF !TALENT:(ARG:0):アナル処女
	SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女 = 不明
ELSE
	TALENT:(ARG:0):アナル処女 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女 = ----
ENDIF

;-------------------------------------------------
;カスタムデータARG:0にARG:1番目のキャラデータを適用
;-------------------------------------------------
@REVERSE_APPLY_CUSTOM_DATA(ARG:0, ARG:1)
;基本名(NAME)
CUSTOM_NAME:(ARG:0):0 = %NAME:(ARG:1)%
;呼び名(CALLNAME)（旧真名）
CUSTOM_NAME:(ARG:0):1 = %CALLNAME:(ARG:1)%
;姓
CUSTOM_NAME:(ARG:0):2 = %CSTR:(ARG:1):0%
;名
CUSTOM_NAME:(ARG:0):3 = %CSTR:(ARG:1):1%
;字
CUSTOM_NAME:(ARG:0):4 = %CSTR:(ARG:1):2%
;基本名(NAME)のフリガナ
CUSTOM_NAME:(ARG:0):5 = %CSTR:(ARG:1):3%
;呼び名(CALLNAME)のフリガナ
CUSTOM_NAME:(ARG:0):6 = %CSTR:(ARG:1):4%
;姓のフリガナ
CUSTOM_NAME:(ARG:0):7 = %CSTR:(ARG:1):5%
;名のフリガナ
CUSTOM_NAME:(ARG:0):8 = %CSTR:(ARG:1):6%
;字のフリガナ
CUSTOM_NAME:(ARG:0):9 = %CSTR:(ARG:1):7%
;愛称
CUSTOM_NAME:(ARG:0):10 = %CSTR:(ARG:1):98%

FOR LOCAL:0, 0, MAX_TAG
	CUSTOM_TAG:(ARG:0):(LOCAL:0) = TAG:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 100
	CUSTOM_MAXBASE:(ARG:0):(LOCAL:0) = MAXBASE:(ARG:1):(LOCAL:0)
	CUSTOM_ABL:(ARG:0):(LOCAL:0) = ABL:(ARG:1):(LOCAL:0)
	CUSTOM_EXP:(ARG:0):(LOCAL:0) = EXP:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 1000
	CUSTOM_TALENT:(ARG:0):(LOCAL:0) = TALENT:(ARG:1):(LOCAL:0)
NEXT

FOR LOCAL:0, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		CUSTOM_SKILL_NO:(ARG:0):(LOCAL:0):(LOCAL:1) = SKILL_NO_SLOT:(ARG:1):(LOCAL:0):(LOCAL:1)
		CUSTOM_SKILL_ID:(ARG:0):(LOCAL:0):(LOCAL:1) = SKILL_ID_SLOT:(ARG:1):(LOCAL:0):(LOCAL:1)
	NEXT
NEXT
SAVEGLOBAL

;-------------------------------------------------
;キャラARG:0番目のカスタムデータを削除
;-------------------------------------------------
@DELETE_CUSTOM(ARG:0)
;カスタムデータを前に１つ詰める
FOR LOCAL:1, ARG:0, 29
	FOR LOCAL:0, 0, 100
		CUSTOM_MAXBASE:(LOCAL:1):(LOCAL:0) = CUSTOM_MAXBASE:(LOCAL:1 + 1):(LOCAL:0)
		CUSTOM_ABL:(LOCAL:1):(LOCAL:0) = CUSTOM_ABL:(LOCAL:1 + 1):(LOCAL:0)
		CUSTOM_EXP:(LOCAL:1):(LOCAL:0) = CUSTOM_EXP:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, 1000
		CUSTOM_TALENT:(LOCAL:1):(LOCAL:0) = CUSTOM_TALENT:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, 10
		CUSTOM_NAME:(LOCAL:1):(LOCAL:0) = %CUSTOM_NAME:(LOCAL:1 + 1):(LOCAL:0)%
	NEXT
	FOR LOCAL:0, 0, MAX_TAG
		CUSTOM_TAG:(LOCAL:1):(LOCAL:0) = CUSTOM_TAG:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	CUSTOM_NAMETYPE:(LOCAL:1) = CUSTOM_NAMETYPE:(LOCAL:1 + 1)
NEXT
FOR LOCAL:0, 0, MAX_TAG
	CUSTOM_TAG:29:(LOCAL:0) = -1
NEXT
FOR LOCAL:0, 0, 100
	CUSTOM_MAXBASE:29:(LOCAL:0) = 0
	CUSTOM_ABL:29:(LOCAL:0) = 0
	CUSTOM_EXP:29:(LOCAL:0) = 0
NEXT
FOR LOCAL:0, 0, 1000
	CUSTOM_TALENT:29:(LOCAL:0) = 0
NEXT
FOR LOCAL:0, 0, 10
	CUSTOM_NAME:29:(LOCAL:0) = 
NEXT
CUSTOM_NAMETYPE:29 = 0

;-------------------------------------------------
;配列のARG:0番目にカスタムを新規作成 ARG:1に1を設定すると修正モード
;-------------------------------------------------
@CREATE_CUSTOM(ARG:0, ARG:1 = 0)
;名前の一時設定
#DIMS TMP_NAME, 30
#DIM FIRST_LINE
VARSET TMP_NAME, ""

LOCAL:20 = 0

;修正の場合
IF ARG:1
	IF CUSTOM_NAMETYPE:(ARG:0) == 0
		TMP_NAME:0 = %CUSTOM_NAME:(ARG:0):0%
		TMP_NAME:1 = %CUSTOM_NAME:(ARG:0):10%
	ELSEIF CUSTOM_NAMETYPE:(ARG:0) == 1
		TMP_NAME:10 = %CUSTOM_NAME:(ARG:0):1%
		TMP_NAME:11 = %CUSTOM_NAME:(ARG:0):2%
		TMP_NAME:12 = %CUSTOM_NAME:(ARG:0):3%
		TMP_NAME:13 = %CUSTOM_NAME:(ARG:0):4%
		TMP_NAME:14 = %CUSTOM_NAME:(ARG:0):6%
		TMP_NAME:15 = %CUSTOM_NAME:(ARG:0):7%
		TMP_NAME:16 = %CUSTOM_NAME:(ARG:0):8%
		TMP_NAME:17 = %CUSTOM_NAME:(ARG:0):9%
		TMP_NAME:18 = %CUSTOM_NAME:(ARG:0):10%
	ELSE
		TMP_NAME:20 = %CUSTOM_NAME:(ARG:0):2%
		TMP_NAME:21 = %CUSTOM_NAME:(ARG:0):3%
		TMP_NAME:22 = %CUSTOM_NAME:(ARG:0):7%
		TMP_NAME:23 = %CUSTOM_NAME:(ARG:0):8%
		TMP_NAME:24 = %CUSTOM_NAME:(ARG:0):10%
	ENDIF
	LOCAL:20 = CUSTOM_NAMETYPE:(ARG:0)

;新規作成の場合
ELSE
	;デフォルト能力の設定
	CUSTOM_MAXBASE:(ARG:0):0 = 2000
	CUSTOM_MAXBASE:(ARG:0):1 = 1500
	CUSTOM_MAXBASE:(ARG:0):2 = 2500

	CUSTOM_TALENT:(ARG:0):0 = 0
	CUSTOM_TALENT:(ARG:0):1 = 1
	CUSTOM_TALENT:(ARG:0):2 = 1
	CUSTOM_TALENT:(ARG:0):3 = 1
	CUSTOM_TALENT:(ARG:0):4 = 1
	CUSTOM_TALENT:(ARG:0):200 = 1
	CUSTOM_TALENT:(ARG:0):300 = 0
	CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛現在値") = 陰毛_標準
	CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛目標値") = 陰毛_標準
	CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ｖ締まり") = GET_DEFAULT_TIGHTNESS("普通")
	CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ａ締まり") = GET_DEFAULT_TIGHTNESS("普通")
	CUSTOM_ABL:(ARG:0):14 = 5
	CUSTOM_ABL:(ARG:0):50 = 60
	CUSTOM_ABL:(ARG:0):51 = 60
	CUSTOM_ABL:(ARG:0):52 = 60
	CUSTOM_ABL:(ARG:0):53 = 60
	CUSTOM_ABL:(ARG:0):60 = 25
	CUSTOM_ABL:(ARG:0):61 = 25
	FOR LOCAL, 0, MAX_TAG
		CUSTOM_TAG:(ARG:0):LOCAL = -1
	NEXT
	FOR LOCAL, 0, SKILL_GENRE_NUM
		FOR LOCAL:1, 0, MAX_SKILL_SLOT
			CUSTOM_SKILL_NO:(ARG:0):(LOCAL:0):(LOCAL:1) = -1
			CUSTOM_SKILL_ID:(ARG:0):(LOCAL:0):(LOCAL:1) = -1
		NEXT
	NEXT
	TMP_NAME:0 = あなた
ENDIF

$SHOW_LOOP1

CALL SINGLE_DRAWLINE
PRINTFORML ★★カスタムキャラの素質・能力を設定して下さい★★
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

;妖術が1以上なら<妖術知識>を付与
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "妖術知識") = CUSTOM_ABL:(ARG:0):GETNUM(ABL, "妖術") >= 1

PRINT   名前の入力方式:
CALL PRINT_SELECT_BUTTON(@" 0\{カナ名・特殊名\}", 0, LOCAL:20 == 0)
CALL PRINT_SELECT_BUTTON(@" 1\{中国名\}", 1, LOCAL:20 == 1)
CALL PRINT_SELECT_BUTTON(@" 2\{日本名\}", 2, LOCAL:20 == 2)
PRINTL 

IF LOCAL:20 == 0
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTFORM     呼称 -- %TMP_NAME:0, 14, LEFT%
	PRINTBUTTON " 10[入力]", 10
	PRINTBUTTON " 11[カナ名ランダム]", 11
	PRINTBUTTON " 100[消去]", 100
	PRINTL 
	PRINTFORM     愛称 -- %TMP_NAME:1, 14, LEFT%
	PRINTBUTTON " 12[入力]", 12
	PRINTBUTTON " 101[消去]", 101
	PRINTL 
ELSEIF LOCAL:20 == 1
	PRINTL 
	PRINTFORM       姓 -- %TMP_NAME:11, 10, LEFT%%TMP_NAME:15, 14, LEFT%
	PRINTBUTTON " 12[入力]", 12
	PRINTBUTTON " 13[ランダム]", 13
	PRINTBUTTON " 101[消去]", 101
	PRINTL 
	PRINTFORM       名 -- %TMP_NAME:12, 10, LEFT%%TMP_NAME:16, 14, LEFT%
	PRINTBUTTON " 14[入力]", 14
	PRINTBUTTON " 15[ランダム]", 15
	PRINTBUTTON " 102[消去]", 102
	PRINTL 
	PRINTFORM       字 -- %TMP_NAME:13, 10, LEFT%%TMP_NAME:17, 14, LEFT%
	PRINTBUTTON " 16[入力]", 16
	PRINTBUTTON " 17[ランダム]", 17
	PRINTBUTTON " 103[消去]", 103
	PRINTL 
	PRINTFORM     愛称 -- %TMP_NAME:18, 14, LEFT%
	PRINTBUTTON " 18[入力]", 18 
	PRINTBUTTON " 104[消去]", 104
	PRINTL
ELSEIF LOCAL:20 == 2
	PRINTL 
	PRINTL 
	PRINTFORM       姓 -- %TMP_NAME:20, 10, LEFT%%TMP_NAME:22, 14, LEFT%
	PRINTBUTTON " 10[入力]", 10
	PRINTBUTTON " 14[ランダム]", 14
	PRINTBUTTON " 100[消去]", 100
	PRINTL 
	PRINTFORM       名 -- %TMP_NAME:21, 10, LEFT%%TMP_NAME:23, 14, LEFT%
	PRINTBUTTON " 12[入力]", 12
	PRINTBUTTON " 15[ランダム]", 15
	PRINTBUTTON " 101[消去]", 101
	PRINTL 
	PRINTFORM     愛称 -- %TMP_NAME:24, 14, LEFT%
	PRINTBUTTON " 13[入力]", 13
	PRINTBUTTON " 102[消去]", 102
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE

PRINT     性別 -- 
CALL PRINTBUTTON_EX("20{  男性} ", 20, 1, CUSTOM_TALENT:(ARG:0):0 == 0)
CALL PRINTBUTTON_EX("21{  女性} ", 21, 1, CUSTOM_TALENT:(ARG:0):0 == 1)
CALL PRINTBUTTON_EX("22{  女双} ", 22, 1, CUSTOM_TALENT:(ARG:0):0 == 2)
CALL PRINTBUTTON_EX("23{  男双} ", 23, 1, CUSTOM_TALENT:(ARG:0):0 == 3)
CALL PRINTBUTTON_EX("24{男の娘} ", 24, 1, CUSTOM_TALENT:(ARG:0):0 == 4)
CALL PRINTBUTTON_EX("25{男の娘双} ", 25, 1, CUSTOM_TALENT:(ARG:0):0 == 5)
PRINTL 

PRINT キス経験 -- 
CALL PRINTBUTTON_EX("30{  なし} ", 30, 1, CUSTOM_TALENT:(ARG:0):3)
CALL PRINTBUTTON_EX("31{  あり} ", 31, 1, !CUSTOM_TALENT:(ARG:0):3)
PRINTL 

PRINT 性経験♂ -- 
CALL PRINTBUTTON_EX("40{  童貞} ", 40, 1, CUSTOM_TALENT:(ARG:0):2)
CALL PRINTBUTTON_EX("41{非童貞} ", 41, 1, !CUSTOM_TALENT:(ARG:0):2)
PRINTL 

PRINT 性経験♀ -- 
CALL PRINTBUTTON_EX("50{  処女} ", 50, 1, CUSTOM_TALENT:(ARG:0):1)
CALL PRINTBUTTON_EX("51{非処女} ", 51, 1, !CUSTOM_TALENT:(ARG:0):1)
PRINTL 

PRINT   バスト -- 
LOCAL:0 = CUSTOM_TALENT:(ARG:0):140 || CUSTOM_TALENT:(ARG:0):141 || CUSTOM_TALENT:(ARG:0):142 || CUSTOM_TALENT:(ARG:0):143
CALL PRINTBUTTON_EX("60{  絶壁} ", 60, 1, CUSTOM_TALENT:(ARG:0):140)
CALL PRINTBUTTON_EX("61{  貧乳} ", 61, 1, CUSTOM_TALENT:(ARG:0):141)
CALL PRINTBUTTON_EX("62{  並乳} ", 62, 1, !LOCAL:0)
CALL PRINTBUTTON_EX("63{  巨乳} ", 63, 1, CUSTOM_TALENT:(ARG:0):142)
CALL PRINTBUTTON_EX("64{  爆乳} ", 64, 1, CUSTOM_TALENT:(ARG:0):143)
PRINTL 

PRINT   ペニス -- 
LOCAL:0 = CUSTOM_TALENT:(ARG:0):120 || CUSTOM_TALENT:(ARG:0):121 || CUSTOM_TALENT:(ARG:0):122 || CUSTOM_TALENT:(ARG:0):123
CALL PRINTBUTTON_EX("65{超短小} ", 65, 1, CUSTOM_TALENT:(ARG:0):120)
CALL PRINTBUTTON_EX("66{  短小} ", 66, 1, CUSTOM_TALENT:(ARG:0):121)
CALL PRINTBUTTON_EX("67{  普通} ", 67, 1, !LOCAL:0)
CALL PRINTBUTTON_EX("68{  巨根} ", 68, 1, CUSTOM_TALENT:(ARG:0):122)
CALL PRINTBUTTON_EX("69{超巨根} ", 69, 1, CUSTOM_TALENT:(ARG:0):123)
PRINTL 


PRINT 天の御遣い- 
CALL PRINTBUTTON_EX("70{  あり} ", 70, 1, CUSTOM_TALENT:(ARG:0):200)
CALL PRINTBUTTON_EX("71{  なし} ", 71, 1, !CUSTOM_TALENT:(ARG:0):200)
PRINTL 

PRINT 君主の器- 
CALL PRINTBUTTON_EX("72{  あり} ", 72, 1, CUSTOM_TALENT:(ARG:0):203)
CALL PRINTBUTTON_EX("73{  なし} ", 73, 1, !CUSTOM_TALENT:(ARG:0):203)
PRINTL 

PRINT 求心力- 
CALL PRINTBUTTON_EX("74{  あり} ", 74, 1, CUSTOM_TALENT:(ARG:0):204)
CALL PRINTBUTTON_EX("75{  なし} ", 75, 1, !CUSTOM_TALENT:(ARG:0):204)
PRINTL 


PRINTL   ※童貞・処女・バスト・ペニスは性別によって非表示。特殊なアイテムで性別変更した場合などに反映

CALL SINGLE_DRAWLINE
PRINTFORML 種族 -- 
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	LOCAL:2 = 0
	FOR LOCAL:1, 0, MAX_TAG
		IF CUSTOM_TAG:(ARG:0):(LOCAL:1) == LOCAL
			LOCAL:2 = 1
			BREAK
		ENDIF
	NEXT
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 300 + LOCAL, LOCAL:2)
NEXT
PRINTL
PRINTFORML ※最大{MAX_TAG}個まで
PRINTFORML ※矛盾したタグが付与できる？　まぁ気にするなよ

CALL SINGLE_DRAWLINE
PRINTFORML ※武闘・防衛・知略・政治は52、歌唱・料理は27程度が全キャラの平均です
PRINTFORM 武闘:{CUSTOM_ABL:(ARG:0):50, 3} -- 
PRINTBUTTON "[ +1]", 200
PRINTBUTTON "[ -1]", 201
PRINTBUTTON "[+10]", 202
PRINTBUTTON "[-10]", 203
PRINT   
PRINTBUTTON "80[直接入力]", 80
PRINTL 
PRINTFORM 防衛:{CUSTOM_ABL:(ARG:0):51, 3} -- 
PRINTBUTTON "[ +1]", 210
PRINTBUTTON "[ -1]", 211
PRINTBUTTON "[+10]", 212
PRINTBUTTON "[-10]", 213
PRINT   
PRINTBUTTON "81[直接入力]", 81
PRINTL 
PRINTFORM 知略:{CUSTOM_ABL:(ARG:0):52, 3} -- 
PRINTBUTTON "[ +1]", 220
PRINTBUTTON "[ -1]", 221
PRINTBUTTON "[+10]", 222
PRINTBUTTON "[-10]", 223
PRINT   
PRINTBUTTON "82[直接入力]", 82
PRINTL 
PRINTFORM 政治:{CUSTOM_ABL:(ARG:0):53, 3} -- 
PRINTBUTTON "[ +1]", 230
PRINTBUTTON "[ -1]", 231
PRINTBUTTON "[+10]", 232
PRINTBUTTON "[-10]", 233
PRINT   
PRINTBUTTON "83[直接入力]", 83
PRINTL 
PRINTFORM 妖術:{CUSTOM_ABL:(ARG:0):54, 3} -- 
PRINTBUTTON "[ +1]", 240
PRINTBUTTON "[ -1]", 241
PRINTBUTTON "[+10]", 242
PRINTBUTTON "[-10]", 243
PRINT   
PRINTBUTTON "84[直接入力]", 84
PRINT  ※1以上に設定すると素質<妖術知識>が付与
PRINTL 
PRINTFORM 歌唱:{CUSTOM_ABL:(ARG:0):60, 3} -- 
PRINTBUTTON "[ +1]", 250
PRINTBUTTON "[ -1]", 251
PRINTBUTTON "[+10]", 252
PRINTBUTTON "[-10]", 253
PRINT   
PRINTBUTTON "85[直接入力]", 85
PRINTL 
PRINTFORM 料理:{CUSTOM_ABL:(ARG:0):61, 3} -- 
PRINTBUTTON "[ +1]", 260
PRINTBUTTON "[ -1]", 261
PRINTBUTTON "[+10]", 262
PRINTBUTTON "[-10]", 263
PRINT   
PRINTBUTTON "86[直接入力]", 86
PRINTL 
PRINTFORM 野心:{CUSTOM_ABL:(ARG:0):99, 3} -- 
PRINTBUTTON "[ +1]", 270
PRINTBUTTON "[ -1]", 271
PRINTBUTTON "[+10]", 272
PRINTBUTTON "[-10]", 273
PRINT   
PRINTBUTTON "87[直接入力]", 87
PRINTL 

CALL SINGLE_DRAWLINE
PRINTBUTTON "  90[詳細設定]", 90
PRINT     
PRINTBUTTON "  92[スキル作成]", 92
PRINTL 
CALL SINGLE_DRAWLINE
PRINTBUTTON "  91[情報確認]", 91
PRINTL 
PRINTBUTTON "  99[    登録]", 99
PRINT                     
PRINTBUTTON "  95[キャンセル]", 95

;名前の調整
FOR LOCAL:0, 0, 10
	CUSTOM_NAME:(ARG:0):(LOCAL:0) = 
NEXT
IF LOCAL:20 == 0
	CUSTOM_NAME:(ARG:0):0 = %TMP_NAME:0%
	CUSTOM_NAME:(ARG:0):1 = %TMP_NAME:0%
	CUSTOM_NAME:(ARG:0):10 = %TMP_NAME:1%
ELSEIF LOCAL:20 == 1
	CUSTOM_NAME:(ARG:0):0 = %TMP_NAME:11%%TMP_NAME:12%
	;真名が無くなったので暫定的にCALLNAMEは「字」を使います
	CUSTOM_NAME:(ARG:0):1 = %TMP_NAME:13%
	CUSTOM_NAME:(ARG:0):2 = %TMP_NAME:11%
	CUSTOM_NAME:(ARG:0):3 = %TMP_NAME:12%
	CUSTOM_NAME:(ARG:0):4 = %TMP_NAME:13%
	CUSTOM_NAME:(ARG:0):5 = %TMP_NAME:15%%TMP_NAME:16%
	;真名が無くなったので暫定的にCALLNAMEは「字」を使います（こちらはフリガナ）
	CUSTOM_NAME:(ARG:0):6 = %TMP_NAME:17%
	CUSTOM_NAME:(ARG:0):7 = %TMP_NAME:15%
	CUSTOM_NAME:(ARG:0):8 = %TMP_NAME:16%
	CUSTOM_NAME:(ARG:0):9 = %TMP_NAME:17%
	CUSTOM_NAME:(ARG:0):10 = %TMP_NAME:18%
ELSEIF LOCAL:20 == 2
	;日本人名のNAMEはNPCに合わせて「名」を使います
	CUSTOM_NAME:(ARG:0):0 = %TMP_NAME:21%
	;日本人名のCALLNAMEはNPCに合わせて「名」を使います
	CUSTOM_NAME:(ARG:0):1 = %TMP_NAME:21%
	CUSTOM_NAME:(ARG:0):2 = %TMP_NAME:20%
	CUSTOM_NAME:(ARG:0):3 = %TMP_NAME:21%
	;日本人名のNAMEはNPCに合わせて「名」を使います（こちらはフリガナ）
	CUSTOM_NAME:(ARG:0):5 = %TMP_NAME:23%
	;日本人名のCALLNAMEはNPCに合わせて「名」を使います（こちらはフリガナ）
	CUSTOM_NAME:(ARG:0):6 = %TMP_NAME:23%
	CUSTOM_NAME:(ARG:0):7 = %TMP_NAME:22%
	CUSTOM_NAME:(ARG:0):8 = %TMP_NAME:23%
	CUSTOM_NAME:(ARG:0):10 = %TMP_NAME:24%
ENDIF
CUSTOM_NAMETYPE:(ARG:0) = LOCAL:20
REDRAW 0

$INPUT_LOOP2
INPUT

;登録
IF RESULT == 99
	SAVEGLOBAL
	RETURN 1

;戻る
ELSEIF RESULT == 95
	PRINTL 登録せずに戻ります。よろしいですか？
	CALL ASK_YN()
	IF RESULT == 0
		;新規追加なら削除
		IF ARG:1 == 0
			CALL DELETE_CUSTOM(ARG:0)
		;修正ならグローバルを読み戻す
		ELSE
			LOADGLOBAL
		ENDIF
		RETURN 0
	ELSE
		GOTO SHOW_LOOP1
	ENDIF

;情報表示
ELSEIF RESULT == 91
	ADDVOIDCHARA
	CALL APPLY_CUSTOM_DATA(CHARANUM - 1, ARG:0)


	CALL SINGLE_DRAWLINE
	CALL SHOW_INFO(CHARANUM - 1)
	WAIT

	;特例(通常は危険なのでDELCHARA禁止)
	DELCHARA CHARANUM - 1
	GOTO SHOW_LOOP1

;詳細設定
ELSEIF RESULT == 90
	CALL CREATE_CUSTOM_DETAIL(ARG:0)
	GOTO SHOW_LOOP1
ELSEIF RESULT == 92
	CALL CUSTOM_SKILL_SETTING(ARG:0)
	GOTO SHOW_LOOP1
ELSEIF RESULT >= 80 && RESULT <= 87
	LOCAL:0 = RESULT
	IF LOCAL:0 == 80
		PRINT 武闘
	ELSEIF LOCAL:0 == 81
		PRINT 防衛
	ELSEIF LOCAL:0 == 82
		PRINT 知略
	ELSEIF LOCAL:0 == 83
		PRINT 政治
	ELSEIF LOCAL:0 == 84
		PRINT 妖術
	ELSEIF LOCAL:0 == 85
		PRINT 歌唱
	ELSEIF LOCAL:0 == 86
		PRINT 料理
	ELSEIF LOCAL:0 == 87
		PRINT 野心
	ENDIF
	PRINTL の値を入力して下さい(0～120)
	INPUT
	IF LOCAL:0 == 80
		CUSTOM_ABL:(ARG:0):50 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 81
		CUSTOM_ABL:(ARG:0):51 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 82
		CUSTOM_ABL:(ARG:0):52 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 83
		CUSTOM_ABL:(ARG:0):53 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 84
		CUSTOM_ABL:(ARG:0):54 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 85
		CUSTOM_ABL:(ARG:0):60 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 86
		CUSTOM_ABL:(ARG:0):61 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 87
		CUSTOM_ABL:(ARG:0):99 = LIMIT(RESULT, 0, 120)
	ENDIF
	GOTO SHOW_LOOP1
ELSE
	IF RESULT >= 10 && RESULT <= 19
		IF LOCAL:20 == 0 && RESULT == 10
			PRINTFORML カスタムの呼称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:0 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 0 && RESULT == 12
			PRINTFORML カスタムの愛称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:1 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 12
			PRINTFORML カスタムの姓を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:11 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:15 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 14
			PRINTFORML カスタムの名を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:12 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:16 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 16
			PRINTFORML カスタムの字を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:13 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:17 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 18
			PRINTFORML カスタムの愛称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:18 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 2 && RESULT == 10
			PRINTFORML カスタムの姓を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:20 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:22 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 2 && RESULT == 12
			PRINTFORML カスタムの名を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:21 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:23 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 2 && RESULT == 13
			PRINTFORML カスタムの愛称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:24 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ENDIF
	ENDIF

	CLEARLINE LINECOUNT - FIRST_LINE
	IF RESULT >= 0 && RESULT <= 3
		LOCAL:20 = RESULT
	ELSEIF LOCAL:20 == 0 && RESULT == 11
		IF GROUPMATCH(CUSTOM_TALENT:(ARG:0):0, 0, 3, 4, 5)
			CALL GENERATE_FIRST_NAME_KM
		ELSE
			CALL GENERATE_FIRST_NAME_KF
		ENDIF
		TMP_NAME:0 = %RESULTS:0%
	ELSEIF LOCAL:20 == 1 && RESULT == 11
		IF GROUPMATCH(CUSTOM_TALENT:(ARG:0):0, 0, 3, 4, 5)
			CALL GENERATE_TRUE_NAME_M
		ELSE
			CALL GENERATE_TRUE_NAME_F
		ENDIF
		TMP_NAME:10 = %RESULTS:0%
		TMP_NAME:14 = %RESULTS:1%
	ELSEIF LOCAL:20 == 1 && RESULT == 13
		CALL GENERATE_FAMILY_NAME
		TMP_NAME:11 = %RESULTS:0%
		TMP_NAME:15 = %RESULTS:1%
	ELSEIF LOCAL:20 == 1 && RESULT == 15
		CALL GENERATE_FIRST_NAME
		TMP_NAME:12 = %RESULTS:0%
		TMP_NAME:16 = %RESULTS:1%
	ELSEIF LOCAL:20 == 1 && RESULT == 17
		CALL GENERATE_MIDDLE_NAME
		TMP_NAME:13 = %RESULTS:0%
		TMP_NAME:17 = %RESULTS:1%
	ELSEIF LOCAL:20 == 2 && RESULT == 14
		CALL GENERATE_FAMILY_NAME_J
		TMP_NAME:20 = %RESULTS:0%
		TMP_NAME:22 = %RESULTS:1%
	ELSEIF LOCAL:20 == 2 && RESULT == 15
		IF GROUPMATCH(CUSTOM_TALENT:(ARG:0):0, 0, 3, 4, 5)
			CALL GENERATE_FIRST_NAME_JM
		ELSE
			CALL GENERATE_FIRST_NAME_JF
		ENDIF
		TMP_NAME:21 = %RESULTS:0%
		TMP_NAME:23 = %RESULTS:1%
	ELSEIF RESULT >= 20 && RESULT <= 25
		CUSTOM_TALENT:(ARG:0):0 = RESULT - 20
	ELSEIF RESULT >= 30 && RESULT <= 31
		CUSTOM_TALENT:(ARG:0):3 = 31 - RESULT
	ELSEIF RESULT >= 40 && RESULT <= 41
		CUSTOM_TALENT:(ARG:0):2 = 41 - RESULT
	ELSEIF RESULT >= 50 && RESULT <= 51
		CUSTOM_TALENT:(ARG:0):1 = 51 - RESULT
	ELSEIF RESULT >= 60 && RESULT <= 64
		CUSTOM_TALENT:(ARG:0):140 = 0
		CUSTOM_TALENT:(ARG:0):141 = 0
		CUSTOM_TALENT:(ARG:0):142 = 0
		CUSTOM_TALENT:(ARG:0):143 = 0
		SELECTCASE RESULT - 60
			CASE 0
				CUSTOM_TALENT:(ARG:0):140 = 1
			CASE 1
				CUSTOM_TALENT:(ARG:0):141 = 1
			CASE 3
				CUSTOM_TALENT:(ARG:0):142 = 1
			CASE 4
				CUSTOM_TALENT:(ARG:0):143 = 1
		ENDSELECT
	ELSEIF RESULT >= 65 && RESULT <= 69
		CUSTOM_TALENT:(ARG:0):120 = 0
		CUSTOM_TALENT:(ARG:0):121 = 0
		CUSTOM_TALENT:(ARG:0):122 = 0
		CUSTOM_TALENT:(ARG:0):123 = 0
		SELECTCASE RESULT - 65
			CASE 0
				CUSTOM_TALENT:(ARG:0):120 = 1
			CASE 1
				CUSTOM_TALENT:(ARG:0):121 = 1
			CASE 3
				CUSTOM_TALENT:(ARG:0):122 = 1
			CASE 4
				CUSTOM_TALENT:(ARG:0):123 = 1
		ENDSELECT
	ELSEIF RESULT >= 70 && RESULT <= 71
		CUSTOM_TALENT:(ARG:0):200 = 71 - RESULT
	ELSEIF RESULT >= 72 && RESULT <= 73
		CUSTOM_TALENT:(ARG:0):203 = 73 - RESULT
	ELSEIF RESULT >= 74 && RESULT <= 75
		CUSTOM_TALENT:(ARG:0):204 = 75 - RESULT
	ELSEIF RESULT >= 100 && RESULT <= 103
		IF LOCAL:20 == 0 && RESULT == 100
			TMP_NAME:0 = 
		ELSEIF LOCAL:20 == 0 && RESULT == 101
			TMP_NAME:1 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 100
			TMP_NAME:10 = 
			TMP_NAME:14 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 101
			TMP_NAME:11 = 
			TMP_NAME:15 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 102
			TMP_NAME:12 = 
			TMP_NAME:16 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 103
			TMP_NAME:13 = 
			TMP_NAME:17 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 104
			TMP_NAME:18 = 
		ELSEIF LOCAL:20 == 2 && RESULT == 100
			TMP_NAME:20 = 
			TMP_NAME:22 = 
		ELSEIF LOCAL:20 == 2 && RESULT == 101
			TMP_NAME:21 = 
			TMP_NAME:23 = 
		ELSEIF LOCAL:20 == 2 && RESULT == 102
			TMP_NAME:24 = 
		ENDIF
	ELSEIF RESULT >= 200 && RESULT < 300
		LOCAL:4 = (RESULT - 200) / 10
		LOCAL:5 = RESULT % 10
		IF LOCAL:4 >= 0 && LOCAL:4 <= 7
			IF LOCAL:4 < 5
				LOCAL:4 += 50
			ELSEIF LOCAL:4 < 7
				LOCAL:4 += 55
			ELSE
				LOCAL:4 = 99
			ENDIF
			SELECTCASE LOCAL:5
				CASE 0
					CUSTOM_ABL:(ARG:0):(LOCAL:4) += 1
				CASE 1
					CUSTOM_ABL:(ARG:0):(LOCAL:4) -= 1
				CASE 2
					CUSTOM_ABL:(ARG:0):(LOCAL:4) += 10
				CASE 3
					CUSTOM_ABL:(ARG:0):(LOCAL:4) -= 10
			ENDSELECT
			CUSTOM_ABL:(ARG:0):(LOCAL:4) = LIMIT(CUSTOM_ABL:(ARG:0):(LOCAL:4), 0, 120)
		ENDIF
	ELSEIF RESULT >= 300 && RESULT < 300 + VARSIZE("TAG_NAME") + 1
		LOCAL:1 = RESULT - 300
		FOR LOCAL, 0, MAX_TAG
			IF CUSTOM_TAG:(ARG:0):(LOCAL:0) == LOCAL:1
				CUSTOM_TAG:(ARG:0):(LOCAL:0) = -1
				GOTO SHOW_LOOP2
			ENDIF
		NEXT
		FOR LOCAL, 0, MAX_TAG
			IF CUSTOM_TAG:(ARG:0):(LOCAL:0) == -1
				CUSTOM_TAG:(ARG:0):(LOCAL:0) = LOCAL:1
				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF
GOTO SHOW_LOOP2

;-------------------------------------------------
;カスタムの詳細設定
;-------------------------------------------------
@CREATE_CUSTOM_DETAIL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_ABL_LIST, 100
#DIM FIRST_LINE
;髪のHSVカラー
#DIM HAIR_HSV, 3

;TALENTNAMEの一時保存用
#DIMS TN
#DIMS STN
#DIM PAGE = 1, 1
#DIM MAX_PAGE = 0, 0

VARSET SHOW_ABL_LIST, -1

SHOW_ABL_LIST:0 = GETNUM(ABL, "Ｃ感")
SHOW_ABL_LIST:1 = GETNUM(ABL, "Ｖ感")
SHOW_ABL_LIST:2 = GETNUM(ABL, "Ａ感")
SHOW_ABL_LIST:3 = GETNUM(ABL, "Ｂ感")
SHOW_ABL_LIST:4 = GETNUM(ABL, "Ｍ感")

SHOW_ABL_LIST:10 = GETNUM(ABL, "欲望")
SHOW_ABL_LIST:11 = GETNUM(ABL, "性技")
SHOW_ABL_LIST:12 = GETNUM(ABL, "性知識")
SHOW_ABL_LIST:13 = GETNUM(ABL, "奉仕")
SHOW_ABL_LIST:14 = GETNUM(ABL, "性交")
SHOW_ABL_LIST:20 = GETNUM(ABL, "レズ")
SHOW_ABL_LIST:21 = GETNUM(ABL, "ＢＬ")
SHOW_ABL_LIST:22 = GETNUM(ABL, "露出")
SHOW_ABL_LIST:23 = GETNUM(ABL, "自慰")
SHOW_ABL_LIST:24 = GETNUM(ABL, "精愛")
SHOW_ABL_LIST:30 = GETNUM(ABL, "射精")
SHOW_ABL_LIST:31 = GETNUM(ABL, "噴乳")
SHOW_ABL_LIST:32 = GETNUM(ABL, "排泄")
SHOW_ABL_LIST:33 = GETNUM(ABL, "触手")
SHOW_ABL_LIST:34 = GETNUM(ABL, "出産")
SHOW_ABL_LIST:35 = GETNUM(ABL, "肝臓")
SHOW_ABL_LIST:36 = GETNUM(ABL, "マゾ")
SHOW_ABL_LIST:37 = GETNUM(ABL, "サド")
SHOW_ABL_LIST:38 = GETNUM(ABL, "催眠")
IF CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:0 = RGB_TO_H(CUSTOM_TALENT:(ARG:0):302)
	HAIR_HSV:1 = RGB_TO_S(CUSTOM_TALENT:(ARG:0):302)
	HAIR_HSV:2 = RGB_TO_V(CUSTOM_TALENT:(ARG:0):302)
ELSE
	VARSET HAIR_HSV, 0
ENDIF

$SHOW_LOOP1

CALL SINGLE_DRAWLINE
PRINTL カスタムの詳細設定
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

IF HAIR_HSV:2 > 0
	CUSTOM_TALENT:(ARG:0):302 = HSV_TO_RGB(HAIR_HSV:0, HAIR_HSV:1, HAIR_HSV:2)
ENDIF

PRINT □ 能力 □
FOR LOCAL:0, 0, 3
	PRINTL 
	PRINTFORM %BASENAME:(LOCAL:0), 8%最大値:{CUSTOM_MAXBASE:(ARG:0):(LOCAL:0), 4} -- 
	PRINTBUTTON "[+100]", LOCAL:0 + 2000
	PRINTBUTTON "[-100]", LOCAL:0 + 2100
	PRINT  
	PRINTBUTTON @"{LOCAL:0 + 700}[入力]", LOCAL:0 + 700
NEXT
LOCAL:3 = 0
FOR LOCAL:0, 0, 100
	LOCAL:2 = SHOW_ABL_LIST:(LOCAL:0)
	IF LOCAL:2 >= 0
		IF LOCAL:0 % 10 == 0 || LOCAL:3 == 5
			LOCAL:3 = 0
			PRINTL 
			PRINT   
		ELSE
			PRINT  
		ENDIF
		PRINTFORM %ABLNAME:(LOCAL:2), 4, RIGHT%:{CUSTOM_ABL:(ARG:0):(LOCAL:2), 2}
		PRINTBUTTON "[+]", LOCAL:0 + 1000
		PRINTBUTTON "[-]", LOCAL:0 + 1100
		PRINTBUTTON "[入力]", LOCAL:0 + 1200
		LOCAL:3 ++
	ENDIF
NEXT
PRINTL 
PRINTFORM     陰毛:%GET_INMOU_NAME(CUSTOM_TALENT:(ARG:0):332), 12, LEFT%
PRINTBUTTON "[変更]", 332

PRINTL
PRINTFORM 膣締まり:%TOSTR_TIGHTNESS(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ｖ締まり")), 12, LEFT%
PRINTBUTTON "[変更]", 333

PRINTL
PRINTFORM Ａ締まり:%TOSTR_TIGHTNESS(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ａ締まり")), 12, LEFT%
PRINTBUTTON "[変更]", 334


PRINTL
CALL SINGLE_DRAWLINE

PRINT □ 素質 □
LOCAL:3 = 0
LOCAL:4 = 0
FOR LOCAL:0, 1, 300
	TN = %TALENTNAME:(LOCAL:0)%
	IF TN != ""
		;非表示素質のスキップ
		IF GROUPMATCH(TN, "絶壁", "貧乳", "巨乳", "爆乳", "超短小", "短小", "巨根", "超巨根")
			CONTINUE
		ELSEIF 150 <= LOCAL:0 && LOCAL:0 <= 200
			CONTINUE
		ELSEIF 280 <= LOCAL:0
			CONTINUE
		ENDIF

		IF LOCAL:3 >= (PAGE - 1) * 44 && LOCAL:3 < PAGE * 44
			;改行判定
			IF LOCAL:3 % 4 == 0
				PRINTL 
				PRINT   
				LOCAL:4 ++
			ENDIF
			IF CUSTOM_TALENT:(ARG:0):(LOCAL:0)
				SETCOLOR カラー_選択中
			ENDIF
			PRINTBUTTON @"{LOCAL:0, 3}\{%TN, 14%\}", LOCAL:0
			RESETCOLOR
			PRINT    
		ENDIF
		LOCAL:3 ++
	ENDIF
NEXT
FOR LOCAL:0, 0, 11 - LOCAL:4
	PRINTL 
NEXT
;最大ページ数が設定されていない場合、ここで初めて設定
IF MAX_PAGE <= 0
	MAX_PAGE = (LOCAL:3 - 1) / 44 + 1
ENDIF

PRINTL 
PRINT   
PRINTBUTTON "901[前のページ]", 901
PRINT   
PRINTBUTTON "902[次のページ]", 902
PRINTL 
CALL SINGLE_DRAWLINE
LOCAL:0 = CUSTOM_TALENT:(ARG:0):300
LOCALS:0 = (%TOSTR_GROWTYPE_B(LOCAL:0)%に適性)
PRINTFORM    成長型:%TOSTR_GROWTYPE(LOCAL:0)%%LOCALS:0, 25, LEFT%
PRINTBUTTON "300[変更]", 300
PRINTL 
PRINTFORM      髪色:
CALL PRINT_HAIR_COLOR(CUSTOM_TALENT:(ARG:0):302)
PRINTFORM %TOSTR_SPACE(19)%
PRINTBUTTON "310[ランダム]", 310
IF CUSTOM_TALENT:(ARG:0):302 != 0
	PRINT  
	PRINTBUTTON "319[不明にする]", 319
ENDIF
PRINTL 
IF CUSTOM_TALENT:(ARG:0):302 != 0
	PRINT           
	PRINTBUTTON "311[色相+10]", 311
	PRINT  
	PRINTBUTTON "312[色相-10]", 312
	PRINT  
	PRINTBUTTON "313[彩度+5]", 313
	PRINT  
	PRINTBUTTON "314[彩度-5]", 314
	PRINT  
	PRINTBUTTON "315[明度+5]", 315
	PRINT  
	PRINTBUTTON "316[明度-5]", 316
	PRINT  
ENDIF
PRINTL 
LOCALS:0 = %TOSTR_HAIR_LENGTH(CUSTOM_TALENT:(ARG:0):303)%({CUSTOM_TALENT:(ARG:0):303})
PRINTFORM  髪の長さ:%LOCALS:0, 29, LEFT%
PRINTBUTTON "320[ランダム]", 320
PRINT  
PRINTBUTTON "321[直接入力]", 321
PRINTL 
PRINTL  ※髪色・髪の長さはコンフィグの「外見に関する詳細の表示」がONのときのみ表示
CALL SINGLE_DRAWLINE

PRINTBUTTON "999[決定]", 999

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW 1
	RETURN
ELSEIF RESULT == 901
	PAGE --
	IF PAGE < 1
		PAGE = MAX_PAGE
	ENDIF
ELSEIF RESULT == 902
	PAGE ++
	IF PAGE > MAX_PAGE
		PAGE = 1
	ENDIF
ELSEIF RESULT >= 700 && RESULT <= 702
	LOCAL:0 = RESULT - 700
	PRINTFORML %BASENAME:(LOCAL:0)%の最大値を入力して下さい(500～5000)
	INPUT
	IF RESULT != 0
		CUSTOM_MAXBASE:(ARG:0):(LOCAL:0) = LIMIT(RESULT, 500, 5000)
	ENDIF
	GOTO SHOW_LOOP1
ELSEIF INRANGE(RESULT, 1000, 1099) && SHOW_ABL_LIST:(RESULT - 1000) >= 0
	LOCAL:0 = SHOW_ABL_LIST:(RESULT - 1000)
	CUSTOM_ABL:(ARG:0):(LOCAL:0) = LIMIT(CUSTOM_ABL:(ARG:0):(LOCAL:0) + 1, 0, MAX(ABL_MAX:(LOCAL:0), 15))
ELSEIF INRANGE(RESULT, 1100, 1199) && SHOW_ABL_LIST:(RESULT - 1100) >= 0
	LOCAL:0 = SHOW_ABL_LIST:(RESULT - 1100)
	CUSTOM_ABL:(ARG:0):(LOCAL:0) = LIMIT(CUSTOM_ABL:(ARG:0):(LOCAL:0) - 1, 0, MAX(ABL_MAX:(LOCAL:0), 15))
ELSEIF INRANGE(RESULT, 1200, 1299) && SHOW_ABL_LIST:(RESULT - 1200) >= 0
	LOCAL:0 = SHOW_ABL_LIST:(RESULT - 1200)
	PRINTFORML %ABLNAME:(LOCAL:0)%の値を入力して下さい(0 ~ {MAX(ABL_MAX:(LOCAL:0), 15)})
	INPUT
	IF INRANGE(RESULT, 0, MAX(ABL_MAX:(LOCAL:0), 15))
		CUSTOM_ABL:(ARG:0):(LOCAL:0) = RESULT
	ENDIF
ELSEIF INRANGE(RESULT, 2000, 2002)
	CUSTOM_MAXBASE:(ARG:0):(RESULT - 2000) = LIMIT(CUSTOM_MAXBASE:(ARG:0):(RESULT - 2000) + 100, 500, 5000)
ELSEIF INRANGE(RESULT, 2100, 2102)
	CUSTOM_MAXBASE:(ARG:0):(RESULT - 2100) = LIMIT(CUSTOM_MAXBASE:(ARG:0):(RESULT - 2100) - 100, 500, 5000)
ELSEIF RESULT == 300
	CUSTOM_TALENT:(ARG:0):300 ++
	IF CUSTOM_TALENT:(ARG:0):300 >= 10
		CUSTOM_TALENT:(ARG:0):300 = 0
	ENDIF
ELSEIF RESULT == 310
	HAIR_HSV:0 = RAND:360
	HAIR_HSV:1 = RAND:93
	HAIR_HSV:2 = RAND:68 + 30
ELSEIF RESULT == 319 && CUSTOM_TALENT:(ARG:0):302 != 0
	CUSTOM_TALENT:(ARG:0):302 = 0
	VARSET HAIR_HSV, 0
ELSEIF RESULT == 311 && CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:0 += 10
	IF HAIR_HSV:0 >= 360
		HAIR_HSV:0 -= 360
	ENDIF
ELSEIF RESULT == 312 && CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:0 -= 10
	IF HAIR_HSV:0 < 0
		HAIR_HSV:0 += 360
	ENDIF
ELSEIF RESULT == 313 && CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:1 = LIMIT(HAIR_HSV:1 + 5, 0, 95)
ELSEIF RESULT == 314 && CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:1 = LIMIT(HAIR_HSV:1 - 5, 0, 95)
ELSEIF RESULT == 315 && CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:2 = LIMIT(HAIR_HSV:2 + 5, 30, 100)
ELSEIF RESULT == 316 && CUSTOM_TALENT:(ARG:0):302 != 0
	HAIR_HSV:2 = LIMIT(HAIR_HSV:2 - 5, 30, 100)
ELSEIF RESULT == 320
	CUSTOM_TALENT:(ARG:0):303 = RAND:990 + 10
ELSEIF RESULT == 321
	CALL SINGLE_DRAWLINE
	PRINTL 髪の長さを数値で入力して下さい
	PRINTL ------長さの表記------
	PRINTL   2～ 24 スキンヘッド
	PRINTL  25～174 超ショート
	PRINTL 175～274 ショート
	PRINTL 275～474 ミドル
	PRINTL 475～674 ロング
	PRINTL 675～999 超ロング
	INPUT
	CUSTOM_TALENT:(ARG:0):303 = LIMIT(RESULT, 2, 999)
ELSEIF RESULT == 332
	CALL SINGLE_DRAWLINE
	PRINTL 陰毛について数値で入力して下さい
	PRINTL -1 パイパン（生えません。永久脱毛状態です）
	PRINTL 0  つるつる（剃ったorまだ生えていないパイパンです）
	PRINTL 1  うっすら
	PRINTL 2  柔毛
	PRINTL 3  ふんわり
	PRINTL 4  標準
	PRINTL 5  多め
	PRINTL 6  ふさふさ
	PRINTL 7  もっさり
	PRINTL 8  剛毛
	PRINTL 9  尻毛も生えるくらい
	PRINTL 10 ハート型に整える
	$INPUT_INMOU_LOOP
	INPUT
	IF -1 <= RESULT && RESULT <= 10
		CUSTOM_TALENT:(ARG:0):332 = RESULT
		CUSTOM_TALENT:(ARG:0):333 = RESULT
	ELSE
		GOTO INPUT_INMOU_LOOP
	ENDIF
ELSEIF RESULT == 333
	CALL SINGLE_DRAWLINE
	PRINTL 膣の締まり具合について数値で入力して下さい
	PRINTL 0 崩壊
	PRINTL 1 がばがば
	PRINTL 2 ぽっかり
	PRINTL 3 ゆるゆる
	PRINTL 4 ゆるめ
	PRINTL 5 ふつう
	PRINTL 6 名器
	PRINTL 7 きゅっきゅっ
	PRINTL 8 きつきつ
	PRINTL 9 ぎちぎち
	$INPUT_LOOP_TIGHTNESS_V
	INPUT
	IF 0 <= RESULT && RESULT <= 9
		CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ｖ締まり") = RESULT * 100 + 50
	ELSE
		GOTO INPUT_LOOP_TIGHTNESS_V
	ENDIF
ELSEIF RESULT == 334
	CALL SINGLE_DRAWLINE
	PRINTL 膣の締まり具合について数値で入力して下さい
	PRINTL 0 崩壊
	PRINTL 1 がばがば
	PRINTL 2 ぽっかり
	PRINTL 3 ゆるゆる
	PRINTL 4 ゆるめ
	PRINTL 5 ふつう
	PRINTL 6 名器
	PRINTL 7 きゅっきゅっ
	PRINTL 8 きつきつ
	PRINTL 9 ぎちぎち
	$INPUT_LOOP_TIGHTNESS_A
	INPUT
	IF 0 <= RESULT && RESULT <= 9
		CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ａ締まり") = RESULT * 100 + 50
	ELSE
		GOTO INPUT_LOOP_TIGHTNESS_A
	ENDIF
ELSEIF RESULT >= 1 && RESULT < 300 && TALENTNAME:RESULT != ""
	IF !GROUPMATCH(TALENTNAME:RESULT, "絶壁", "貧乳", "巨乳", "爆乳", "超短小", "短小", "巨根", "超巨根")
		IF (150 <= RESULT && RESULT <= 200) || 280 <= RESULT
		ELSE
			CUSTOM_TALENT:(ARG:0):RESULT = !CUSTOM_TALENT:(ARG:0):RESULT
		ENDIF
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP2
