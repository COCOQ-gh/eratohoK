;個別にコンフィグを割り振るまでもない「小さな」イベントを扱う。
@DAILY_MISC
#DIM 対象

;能力の上昇
IF RAND:100 < 5
	CALL DAILY_EVENT_RAND_CHARASELECT(0)
	IF RESULT >= 0
		対象 = RESULT
		SELECTCASE RAND:100
			CASE 0 TO 69
				LOCAL:1 = 1
			CASE 70 TO 89
				LOCAL:1 = 2
			CASE 90 TO 99
				LOCAL:1 = 3
		ENDSELECT
		SELECTCASE RAND:3
			CASE 0
				LOCALS = 武闘
				ABL:対象:武闘 += LOCAL:1
				LOCAL:2 = 50
			CASE 1
				LOCALS = 知略
				ABL:対象:知略 += LOCAL:1
				LOCAL:2 = 51
			CASE 2
				LOCALS = 政治
				ABL:対象:政治 += LOCAL:1
				LOCAL:2 = 52
		ENDSELECT
		DRAWLINE
		PRINTFORML %ANAME(対象)%は秘密の特訓にいそしんでいる……
		SETCOLOR COLOR("オレンジ")
		PRINTFORML %ANAME(対象)%の%LOCALS%が{LOCAL:1}上がり、{ABL:対象:(LOCAL:2)}になった！
		RESETCOLOR
	ENDIF
ENDIF

;好景気
IF RAND:100 < 4
	CALL DAILY_EVENT_RAND_CITYSELECT(0)
	対象 = RESULT
	IF 対象 > 0
		;表示される数字の100倍がCITY_ECONOMYには保存されている　つまり下二桁は実質の小数点二桁
		;最初に100で割って100をかけるのは、実質のFLOOR
		LOCAL:1 = RAND(CITY_ECONOMY_LIMIT:(対象) / 100 * 100 / 10 , CITY_ECONOMY_LIMIT:(対象) / 100 * 100 / 10 + 1000)
		DRAWLINE
		PRINTFORML %CITY_NAME:対象%は好景気なようだ……
		SETCOLOR COLOR("オレンジ")
		;経済が成長しきってたら限界を伸ばす
		IF CITY_ECONOMY:対象 == CITY_ECONOMY_LIMIT:対象
			CITY_ECONOMY_LIMIT:対象 += LOCAL:1
			PRINTFORML %CITY_NAME:対象%の経済規模の上限が{LOCAL:1 / 100}上昇し、{CITY_ECONOMY_LIMIT:対象 / 100}になった！
		;成長しきってなければ限界まで伸ばす
		ELSE
			SIF CITY_ECONOMY:対象 + LOCAL:1 >= CITY_ECONOMY_LIMIT:対象
				LOCAL:1 = MIN(LOCAL:1, CITY_ECONOMY_LIMIT:対象 - CITY_ECONOMY)
			CITY_ECONOMY:対象 += LOCAL:1
			PRINTFORML %CITY_NAME:対象%の経済規模が{LOCAL:1 / 100}上昇し、{CITY_ECONOMY:対象 / 100}になった！
		ENDIF
		RESETCOLOR
		ENDIF
ENDIF

;兵数が増える
IF RAND:100 < 4
	LOCALS = %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:1))%
	LOCAL:1 = GET_OWN_CITY(CFLAG:MASTER:1)
	LOCAL:2 = RAND(LOCAL:1 * 300, LOCAL:1 * 300 + 500)
	COUNTRY_SOLDIER:(CFLAG:MASTER:1) += LOCAL:2
	DRAWLINE
	PRINTFORML %LOCALS%の人望に、義勇軍が結成された……
	SETCOLOR COLOR("オレンジ")
	PRINTFORML 兵数が{LOCAL:2}増え、{COUNTRY_SOLDIER:(CFLAG:MASTER:1)}になった！
	RESETCOLOR
ENDIF


;サボり魔を取得するか、削除する
IF RAND:1000 < 5
	CALL DAILY_EVENT_RAND_CHARASELECT(0)
	対象 = RESULT
	IF 対象 >= 0
		DRAWLINE
		IF TALENT:対象:サボり魔
			PRINTFORML 最近の%ANAME(対象)%はなんだかやる気に満ちあふれている……
			SETCOLOR COLOR("オレンジ")
			PRINTFORML %ANAME(対象)%は【サボり魔】を克服した！
			RESETCOLOR
			TALENT:対象:サボり魔 = 0
		ELSE
			PRINTFORML 最近の%ANAME(対象)%はなんだかやる気がみられない……
			SETCOLOR COLOR("警告")
			PRINTFORML %ANAME(対象)%は【サボり魔】になってしまった！
			RESETCOLOR
			TALENT:対象:サボり魔 = 1
		ENDIF
	ENDIF
ENDIF

;ケガをするか、ケガから復帰
IF RAND:100 < 1
	CALL DAILY_EVENT_RAND_CHARASELECT(0)
	対象 = RESULT
	IF 対象 >= 0
		DRAWLINE
		IF COOLTIME:対象:0
			PRINTFORML %ANAME(対象)%のケガは予想以上に早く治ったようだ
			SETCOLOR COLOR("オレンジ")
			PRINTFORML %ANAME(対象)%はクールタイムから復帰した！
			RESETCOLOR
			COOLTIME:対象:0 = 0
		ELSE
			LOCAL:1 = RAND:3 + 1
			PRINTFORML %ANAME(対象)%は訓練中にケガをしてしまったようだ
			SETCOLOR COLOR("警告")
			PRINTFORML %ANAME(対象)%は{LOCAL:1}ターンのクールタイムに入った！
			RESETCOLOR
			COOLTIME:対象:0 = LOCAL:1
		ENDIF
	ENDIF
ENDIF

;国家の兵力が増加する
IF RAND:100 < 4
	CALL DAILY_EVENT_RAND_COUNTRYSELECT()
	対象 = RESULT
	IF 対象 >= 0 && IS_COUNTRY(対象)
		LOCAL:1 = RAND(MIN(DAY * 100, 5000), MIN(DAY * 200, 10000))
		COUNTRY_SOLDIER:(対象) += LOCAL:1
		DRAWLINE
		PRINTFORML %ANAME(GET_COUNTRY_BOSS(対象))%は徴兵を行った！
		CALL COLORPRINTFORM(@"兵数が{LOCAL:1}増加した", COLOR("注意"), "L")
	ENDIF
ENDIF
		