@TALENT_TRAINER
#DIM キャラ
#DIM 素質
#DIM 人数
#DIM LCOUNT
VARSET LOCAL, -1
VARSET 人数

;15期以降で2.5%
IF !(DAY >= 14 && RAND:1000 < 25)
	RETURN 0
ENDIF

;対象キャラ選出
FOR LCOUNT, 0, CHARANUM
	IF CFLAG:LCOUNT:所属 == CFLAG:MASTER:所属 && !CFLAG:LCOUNT:9
		LOCAL:人数 = LCOUNT
		人数 ++
	ENDIF
NEXT
CALL FISHER_YATES_SHAFFLE(人数)
キャラ = LOCAL:(SHAFFLE_ARRAY:0)

;対象素質選出
SELECTCASE RAND:1000
	;治療
	CASE IS < 100
		素質 = 203
	;努力家
	CASE IS < 175
		素質 = 205
	;策士
	CASE IS < 250
		素質 = 219
	;明察
	CASE IS < 300
		素質 = 220
	;ステゴロ
	CASE IS < 375
		素質 = 221
	;三種の神器
	CASE IS < 450
		素質 = 222
	;下克上等
	CASE IS < 490
		素質 = 223
	;魔力転換
	CASE IS < 540
		素質 = 224
	;突貫
	CASE IS < 640
		素質 = 225
	;バンドガール
	CASE IS < 680
		素質 = 226
	;哨戒
	CASE IS < 750
		素質 = 227
	;防衛戦術
	CASE IS < 800
		素質 = 228
	;暗殺
	CASE  IS < 840
		素質 = 229
	;弁舌
	CASE IS < 880
		素質 = 230
	;早足
	CASEELSE
		素質 = 240
ENDSELECT

;もう持ってる場合は中止
IF TALENT:キャラ:素質
	RETURN 0
ENDIF
DRAWLINE
IF DVAR:素質トレーナー == 0
	PRINTFORML %ANAME(MASTER)%が仕事をしていると、侍従が来客を告げた
	PRINTFORML なんでも「やけにうるさい男が来た」のだそうだ
	PRINTFORMW どれくらいうるさいのかと思った%ANAME(MASTER)%は、会うことにしてみた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML 「ウオーーーッ！！！！！！　君が%ANAME(MASTER)%か！！！！！　会えて光栄だ！！！！！」
	PRINTFORMW 「私は『ムキムキマッチョ☆筋肉マッスル塾』塾長！！！　男山　隆太郎だ！！！！！！！」
	PRINTFORML 「実は今日は！！！！　君にいい話を持ってきた！！！！！　まぁ聞いてくれ！！！！！！」
	PRINTFORML 「君のところにだ！！！！　%ANAME(キャラ)%っているだろ！！！？　あの人なんだが！！！」
	PRINTFORMW 「僕の見立てでは＜%TALENTNAME:素質%＞を覚える素質がある！！！！　それでだ！！！！！」
ELSE
	PRINTFORML 「やあ！！！！！！！！！　また会ったな！！！！！！！！！！！！！！！！」
	PRINTFORML 「覚えているかい！！！！！！？！？　ムキムキ筋肉♂マッスルハート塾の男山だ！！！！！！」
	PRINTFORMW 「また特殊能力を教えに来たぞ！！！！！！！！！！！！！！！！！！！！！！！！！！！」
ENDIF

PRINTFORM 「金
SETCOLOR COLOR("注意")
PRINTFORM 50000
RESETCOLOR
PRINTFORML で、%ANAME(キャラ)%に＜%TALENTNAME:素質%＞を教えてやろうじゃないか！！！！！！！！」
PRINTFORML 「素晴らしい素質だぞ！！！！！！！！！
PRINTFORMW 「どうだ！！！？　いい話だと思わないか！！！！！！？！！！？！？！！！？？？？」
PRINTFORML 男は終始うるさかったが、持ってきた話自体は悪いものではなかった
PRINTFORML 提示されたのは安くない額だが、考える価値はあるだろう
PRINTFORMW さて、どうするか……
PRINTFORML
DRAWLINE
PRINTFORML 現在の金:{MONEY}
IF MONEY >= 50000
	PRINTFORML [0] %ANAME(キャラ)%を教育してもらう
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAINFORM [0] 金50000が必要だが、そんなに持っていない 現在:{MONEY}
	PRINTL
ENDIF
PRINTFORML [1] やめておく
$INPUT_LOOP
INPUT
IF RESULT == 1
	PRINTFORMW 「ムムムッ！！！！！！？　そうか！！！！！！　そういうこともあるよな！！！！！」
	PRINTFORMW 「それならば仕方あるまい！！！！！　私は立ち去るとするよ！！！！！！！！！！！」
ELSEIF RESULT == 0 && MONEY >= 50000
	PRINTFORML 「あいわかった！！！！！！！！！　ではちょっと%ANAME(キャラ)%をお借りするぞ！！！！！」
	PRINTFORMW %ANAME(キャラ)%は男山と訓練を始めた……
	PRINTFORML
	PRINTFORMW 「いいか！！！！！！　筋肉だ！！！！！　全ては筋肉なのだ！！！！！！！」
	PRINTFORMW 「＜%TALENTNAME:素質%＞だって基本は筋肉！！！！！！　そのことを忘れるな！！！！！！！」
	PRINTFORML
	PRINTFORML 「いや素晴らしいな！！！！！！　実に飲み込みの早い生徒だ！！！！！！！！！」
	PRINTFORMW 「＜%TALENTNAME:素質%＞のことはバッチリみたいだぞ！！！！！！　よかったな！！！！！」
	SETCOLOR COLOR("注意")
	PRINTFORML %ANAME(キャラ)%は＜%TALENTNAME:素質%＞を得た
	PRINTFORMW ……ついでに声がうるさくなった気もする？
	RESETCOLOR
	PRINTFORML 「では私はこれで！！！！！！！！　また来るよ！！！！！！！！！！！！！！！！！！」
	PRINTFORMW 男は終始やかましく、立ち去っていった……
	MONEY -= 50000
	TALENT:キャラ:素質 = 1
ELSE
	GOTO INPUT_LOOP
ENDIF
DVAR:素質トレーナー = 1