;複数シナリオにまたがるイベント処理
@EVENT_DAILY()
#DIM EVENT_LIST, 100
#DIM EVENT_PICK, 1000
#DIM EVENT_ODDS
#DIM EVENT_COUNT, 2
VARSET LOCAL
VARSET EVENT_PICK

DAILY_CANCEL = 0

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0


;派生イベント関係の処理
CALL DAILY_DERIVATION

;口上デイリー用の派生イベント関係の処理
CALL KOJO_DAILY_DERIVATION

;イベント発生数のセット
EVENT_COUNT:0 = DCONFIG:951 + 1, DCONFIG:952 + 1

;イベントリストの作成（起動後作成していなければ）
;イベント追加時は系列に合わせて定数に使用したものを突っ込んでおいてください
;金貸しイベント(ID=1)が登録されていなければ処理を実行(薬売りはID=0なので利用できない)
IF !EVENT_LIST:1
	;0が薬売りで使用されているので-1で初期化
	VARSET EVENT_LIST, -1
	;スケベ系:0-8
	EVENT_LIST:0 = DCON_薬売り, DCON_金貸し, DCON_夜遊び, DCON_富豪, DCON_募金, DCON_催眠, DCON_接待, DCON_恩返し, DCON_カルマ, DCON_河童娘, DCON_闇討ち, DCON_睡姦, DCON_賞金首討伐, DCON_お手伝い, DCON_酒場, DCON_家庭教師, DCON_夜伽
	;雇用系:20-22
	EVENT_LIST:20 = DCON_傭兵, DCON_用心棒, DCON_仕官の誘い
	;トレーナー:30-33
	EVENT_LIST:30 = DCON_素質トレーナー, DCON_セックストレーナー, DCON_外界の医者, DCON_麻薬治療医, DCON_触手トレーナー
	;外交:40-41
	EVENT_LIST:40 = DCON_人材要求, DCON_資金援助要求
	;野盗:50-52
	EVENT_LIST:50 = DCON_野盗討伐, DCON_野盗身代金, DCON_野盗蜂起, DCON_放浪捕縛, DCON_野盗略奪, DCON_村落襲撃, DCON_性奴隷要求
	;素質系:60-64
	EVENT_LIST:60 = DCON_肉便器, DCON_薬物依存, DCON_雌犬, DCON_苗床
	;その他90-93
	EVENT_LIST:90 = DCON_シャブセックス, DCON_触手討伐任務, DCON_触手兵, DCON_ランダム謀反, DCON_MISC
ENDIF

;デイリーイベント発生チェック
;例の関数で順番を決めてSELECTCASEにぶん投げる
;順番にイベントフラグ及び発生期間の確認と確率ロールを行い、大丈夫であればイベント本体に移動
;その後、終了フラグが立っていればループを脱出する

;イベント発生倍率のセット
;2^(コンフィグ値-1)+1、つまりDCONFIG:950が0/1/2/3/4の場合1/2/3/5/10倍になる
;なお1000を超えた場合、そのイベントが必ず発生するだけで何ら問題はない（ぉ
EVENT_ODDS = POWER(2, DCONFIG:DCON_通常発生頻度 - 1) + 1

;いつもの順番決定、拡張性を考えて枠は100個用意させていただきました
CALL FISHER_YATES_SHAFFLE(100)

;念のため順番を記録
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_PICK"

FOR LOCAL:0, 0, 100
	RESULT = -1
	SELECTCASE EVENT_LIST:(EVENT_PICK:(LOCAL:0))
	;薬売り
	;8期以降で2.5%
	CASE DCON_薬売り
		SIF !DCONFIG:DCON_薬売り && DAY >= 8 && RAND:1000 < 25 * EVENT_ODDS
			CALL SMUGGLER

	;金貸し
	;15期以降で2%
	CASE DCON_金貸し
		SIF !DCONFIG:DCON_金貸し && DAY >= 15 && RAND:1000 < 20 * EVENT_ODDS
			CALL LENDER

	;夜遊び
	;5期以降で4%
	CASE DCON_夜遊び
		SIF !DCONFIG:DCON_夜遊び && DAY >= 5 && RAND:1000 < 40 * EVENT_ODDS
			CALL NIGHTLIFE

	;富豪
	;20期以降で3%（お気に入りがいれば25%）
	CASE DCON_富豪
		SIF !DCONFIG:DCON_富豪 && DAY >= 20 && RAND:1000 < (DVAR:富豪お気に入り > 0 ? 250 # 30) * EVENT_ODDS
			CALL MILLIONAIRE

	;募金
	;10期以降で3%（抱くたびに確率アップ）
	CASE DCON_募金
		SIF !DCONFIG:DCON_募金 && DAY >= 10 && RAND:1000 < (30 + INRANGE(DVAR:募金抱いた回数, 0, 10) * 7) * EVENT_ODDS
			CALL FUND_RAISING

	;催眠
	;10期以降で3%
	CASE DCON_催眠
		SIF !DCONFIG:DCON_催眠 && DAY >= 10 && RAND:1000 < 30 * EVENT_ODDS
			CALL BLAIN_WASH

	;接待
	;5期以降で2%
	CASE DCON_接待
		SIF !DCONFIG:DCON_接待 && DAY >= 5 && RAND:1000 < 20 * EVENT_ODDS
			CALL RECEPTION

	;恩返し
	;10期以降、あなたが男性の場合、確率3%で発生、イベント中は発生確率アップ
	CASE DCON_恩返し
		SIF !DCONFIG:DCON_恩返し && DAY >= 10 && IS_MALE(MASTER) && RAND:1000 < (DVAR:恩返しフラグ || DVAR:通い妻フラグ > 0 ? 250 # 30) * EVENT_ODDS
			CALL REPAYMENT

	;カルマ
	;10期以降、あなたが男性の場合、確率3%で発生、イベント中は発生確率アップ
	CASE DCON_カルマ
		SIF !DCONFIG:DCON_カルマ && DAY >= 10 && IS_MALE(MASTER) && RAND:1000 < ( DVAR:カルマ進行度 > 0 ? 250 # 30 ) * EVENT_ODDS
			CALL KARMA

	;河童娘
	;10期以降、確率3%で発生
	CASE DCON_河童娘
		SIF !DCONFIG:DCON_河童娘 && DAY >= 10 && RAND:1000 < 30 * EVENT_ODDS
			CALL KAPPAPPA

	;闇討ち
	;15期以降、確率2.5%で発生
	CASE DCON_闇討ち
		SIF !DCONFIG:DCON_闇討ち && DAY >= 15 && RAND:1000 < 25 * EVENT_ODDS
			CALL ASSASSIN_CHALLENGE

	;睡姦
	;10期以降、あなたが男性の場合、確率3%で発生
	CASE DCON_睡姦
		SIF !DCONFIG:DCON_睡姦 && IS_MALE(MASTER) && DAY >= 10 && RAND:1000 < 30 * EVENT_ODDS
			CALL DRUNK_RAPE

	;賞金首討伐
	;15期以降、確率3%で発生
	CASE DCON_賞金首討伐
		SIF !DCONFIG:DCON_賞金首討伐 && DAY >= 15 && RAND:1000 < 30 * EVENT_ODDS
			CALL BOUNTY_HUNTER

	;お手伝い
	;15期以降、確率2.5%で発生
	CASE DCON_お手伝い
		SIF !DCONFIG:DCON_お手伝い && DAY >= 15 && RAND:1000 < 25 * EVENT_ODDS
			CALL WORK_HELPER

	;酒場
	;10期以降、確率2%で発生
	CASE DCON_酒場
		SIF !DCONFIG:DCON_酒場 && DAY >= 10 && RAND:1000 < 20 * EVENT_ODDS
			CALL TAVERN_SEX

	;家庭教師
	;15期以降、確率2%で発生、お気に入りがいる場合は25%にアップ
	CASE DCON_家庭教師
		SIF !DCONFIG:DCON_酒場 && DAY >= 15 && RAND:1000 < (DVAR:娘お気に入り > 0 ? 250 # 20) * EVENT_ODDS
			CALL TUTOR

	;夜伽
	;10期以降、主人公が女の場合、確率15%で発生
	CASE DCON_夜伽
		SIF !DCONFIG:DCON_夜伽 && DAY >= 10 && IS_FEMALE(MASTER) && RAND:1000 < 150 * EVENT_ODDS
			CALL VIGIL

	;傭兵
	;5期以降で3%
	CASE DCON_傭兵
		SIF !DCONFIG:DCON_傭兵 && DAY >= 5 && RAND:1000 < 30 * EVENT_ODDS
			CALL MERCENARY

	;用心棒
	;10期以降で2%
	CASE DCON_用心棒
		SIF !DCONFIG:DCON_用心棒 && DAY >= 10 && RAND:1000 < 20 * EVENT_ODDS
			CALL BOUNCER

	;筋肉馬鹿
	;15期以降で2.5%
	CASE DCON_素質トレーナー
		SIF !DCONFIG:DCON_素質トレーナー && DAY >= 15 && RAND:1000 < 25 * EVENT_ODDS
			CALL TALENT_TRAINER

	;セックストレーナー
	;15期以降で3%の確率
	CASE DCON_セックストレーナー
		SIF !DCONFIG:DCON_セックストレーナー && DAY >= 15 && RAND:1000 < 30 * EVENT_ODDS
			CALL SEX_TRAINER

	;外界の医者
	;15期以降で3.5%
	CASE DCON_外界の医者
		SIF !DCONFIG:DCON_外界の医者 && DAY >= 15 && RAND:1000 < 35 * EVENT_ODDS
			CALL DOCTOR_GAIRAI

	;麻薬治療医
	;15期以降で7.5%
	CASE DCON_麻薬治療医
		SIF !DCONFIG:DCON_麻薬治療医 && DAY >= 15 && RAND:1000 < 75 * EVENT_ODDS
			CALL DOCTOR_DRUG

	;触手トレーナー
	;触手部屋がある場合、15期以降で2.5%
	CASE DCON_触手トレーナー
		SIF !DCONFIG:DCON_触手トレーナー && DAY >= 15 && RAND:1000 < 25 * EVENT_ODDS && ITEM:触手部屋
			CALL TENTACLE_TRAINER

	;人材要求
	;外交解禁後3.5%
	CASE DCON_人材要求
		SIF !DCONFIG:DCON_人材要求 && DAY >= SLG_PP:0 && RAND:1000 < 35 * EVENT_ODDS
			CALL DAILY_DIPLO_REQUEST_CHARA

	;資金援助要求
	;外交解禁後3.5%
	CASE DCON_資金援助要求
		SIF !DCONFIG:DCON_資金援助要求 && DAY >= SLG_PP:0 && RAND:1000 < 35 * EVENT_ODDS
			CALL DAILY_DIPLO_REQUEST_MONEY

	;野盗討伐イベント
	CASE DCON_野盗討伐
		SIF !DCONFIG:DCON_野盗討伐 && DAY >= 15 && RAND:1000 < 40 * EVENT_ODDS
			CALL BANDIT_CONQUER

	;野盗の身代金要求処理
	;乱数が特殊かつ解禁時期がないため関数側で処理
	CASE DCON_野盗身代金
		SIF !DCONFIG:DCON_野盗身代金
			CALL BANDIT_REQUIRE_RANSOM

	;野盗の放浪キャラ捕縛処理
	;とりあえずキャラ別判定は関数に任せて5%の部分だけ引き出す
	CASE DCON_放浪捕縛
		SIF !DCONFIG:DCON_放浪捕縛 && RAND:1000 < 50 * EVENT_ODDS
			CALL BANDIT_ARREST

	CASE DCON_野盗略奪
		;略奪
		SIF !DCONFIG:DCON_野盗略奪 && DAY >= 12 && RAND:1000 < 40 * EVENT_ODDS
			CALL BANDIT_PILLAGE

	;村落襲撃イベント、12期以降3%で発動
	CASE DCON_村落襲撃
		SIF !DCONFIG:DCON_村落襲撃 && DAY >= 12 && RAND:1000 < 30 * EVENT_ODDS
			CALL BANDIT_LOOT

	;性奴隷要求イベント、30期以3%で発動、野盗と停戦・同盟状態でなければデイリーの方ではじかれる
	CASE DCON_性奴隷要求
		SIF !DCONFIG:DCON_性奴隷要求 && DAY >= 30 && RAND:1000 < 30 * EVENT_ODDS
			CALL BANDIT_LOOT

	;肉便器イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE DCON_肉便器
		SIF !DCONFIG:DCON_肉便器
			CALL PUBLIC_TOILET

	;薬物依存イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE DCON_薬物依存
		SIF !DCONFIG:DCON_薬物依存
			CALL DRUG_ADDICT

	;雌犬用イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE DCON_雌犬
		SIF !DCONFIG:DCON_雌犬
			CALL ZOOPHILIA

	;苗床用イベント
	;キャラごとに乱数判定のため関数側で処理
	CASE DCON_苗床
		SIF !DCONFIG:DCON_苗床
			CALL SEEDBED

	;モブ支援会話
	;10期以降、確率3%で発生、イベント中は発生確率アップ
;	CASE DCON_モブ支援会話
;		SIF !DCONFIG:DCON_モブ支援会話 && DAY >= 10 && RAND:1000 < ( DVAR:支援会話進行度 > 0 ? 250 # 30 ) * EVENT_ODDS
;			CALL MOBU_LOVER

	;シャブセックス
	;2%で発動
	CASE DCON_シャブセックス
		SIF !DCONFIG:DCON_シャブセックス && RAND:1000 < 20 * EVENT_ODDS
			CALL DRUG_SEX

	;触手兵イベント
	;10期以降で2%
	CASE DCON_触手兵
		SIF !DCONFIG:DCON_触手兵 && DAY >= 10 && RAND:1000 < 20 * EVENT_ODDS
			CALL TENTACLE_SOLDIERS

	;触手討伐任務イベント
	;10期以降で2%
	CASE DCON_触手討伐任務
		SIF !DCONFIG:DCON_触手討伐任務 && DAY >= 10 && RAND:1000 < 20 * EVENT_ODDS
			CALL TENTACLE_SUPPRESSION

	;小型イベント
	;複数のイベントが併発しなくなった代わりにこちらで確率を制御（10%）
	CASE DCON_MISC
		SIF !DCONFIG:DCON_MISC && RAND:1000 < 100 * EVENT_ODDS
			CALL DAILY_MISC

	;未定義
	CASEELSE
		CONTINUE
	ENDSELECT

	;正常終了していたらカウントを減らす
	;イベントに入る前に弾かれていた場合はカウントしない
	SIF RESULT != -1
		EVENT_COUNT:0 --
	;終了フラグが立っていれば口上デイリー処理へ
	SIF DAILY_CANCEL || !EVENT_COUNT:0
		BREAK

NEXT

;口上用デイリー MASTERのものは発動しない
;順番決定
CALL FISHER_YATES_SHAFFLE(CHARANUM)
;念のため順番を記録
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_PICK"

FOR LOCAL, 1, CHARANUM
	SIF NO:(EVENT_PICK:LOCAL) > 200 || (NO:(EVENT_PICK:LOCAL) <= 200 && KDCONFIG:(NO:(EVENT_PICK:LOCAL)))
		CONTINUE
	CALL KOJO_DAILY(EVENT_PICK:LOCAL)
	;正常終了していたらカウントを減らす
	SIF RESULT
		EVENT_COUNT:1 --
	;終了フラグもしくはカウントが0になっていたら抜ける
	SIF DAILY_CANCEL || EVENT_COUNT:1 == 0
		BREAK
NEXT

;-------------------------------------------------------------------------------
;指定キャラについて、「放浪したり死んだりしていない」「捕虜でない」「どこかの勢力に所属している」を満たしているなら真
;-------------------------------------------------------------------------------
@IS_DAILY_EVENT_HAPPEN(ARG:0)
#FUNCTION
IF CFLAG:(ARG:0):12 == 0 && CFLAG:(ARG:0):9 == 0 && CFLAG:(ARG:0):1
	RETURNF 1
ENDIF
RETURNF 0


;-------------------------------------------------------------------------------
;デイリーイベントにおいて、ランダムに対象キャラを選出する
;捕虜でなく、死んだりしていないのは前提
;ARG:0 0自勢力キャラ　1全勢力から選択
;RESULT 選出された対象キャラ
;-------------------------------------------------------------------------------
@DAILY_EVENT_RAND_CHARASELECT(ARG:0 = 0)
#DIM 対象, 1000
#DIM カウンタ, 1
VARSET 対象
VARSET カウンタ
;シャッフル用の配列を作成する
FOR LOCAL, 0, CHARANUM
	IF (ARG:0 || (!ARG:0 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1)) && !CFLAG:(LOCAL:0):9 && !CFLAG:(LOCAL:0):12
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT

RETURN カウンタ ? 対象:(RAND:カウンタ) # -1

;-------------------------------------------------------------------------------
;デイリーイベントにおいて、ランダムに対象都市を選出する
;ARG:0 0自勢力都市　1全勢力から選択
;RESULT 選出された対象都市
;-------------------------------------------------------------------------------
@DAILY_EVENT_RAND_CITYSELECT(ARG:0 = 0)
#DIM 対象, MAX_CITY
#DIM カウンタ, 1
VARSET 対象
VARSET カウンタ

;シャッフル用の配列を作成する
FOR LOCAL, 0, MAX_CITY
	IF CITY_TYPE:LOCAL == 1
		BREAK
	ENDIF
	IF ARG:0 || (!ARG:0 && CITY_OWNER:LOCAL == CFLAG:MASTER:1)
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT

RETURN カウンタ ? 対象:(RAND:カウンタ) # -1

;-------------------------------------------------------------------------------
;デイリーイベントにおいて、ランダムに対象国家を選出する
;RESULT 選出された対象国家
;-------------------------------------------------------------------------------
@DAILY_EVENT_RAND_COUNTRYSELECT
#DIM 対象, MAX_COUNTRY
#DIM カウンタ
VARSET 対象
VARSET カウンタ

;シャッフル用の配列を作成する
FOR LOCAL, 0, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL)
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT

RETURN カウンタ ? 対象:(RAND:カウンタ) # -1
