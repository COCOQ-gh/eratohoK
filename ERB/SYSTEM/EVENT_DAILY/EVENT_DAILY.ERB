@EVENT_DAILY(SLGフラグ)
#DIM SLGフラグ
#DIMS SLG
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM FIRST_LINE
#DIM EVENT_COUNT
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")

DAILY_CANCEL = 0
EVENT_COUNT = SLGフラグ ? SLG_DAILY_EVENT_COUNT # DAILY_EVENT_COUNT
SLG = %(SLGフラグ ? "SLG_" # "" )%

SIF !SLGフラグ && !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

CALL DAILY_DERIVATION(SLGフラグ)

SIF DAILY_CANCEL
	RETURN 0

EVENT_NUM = SLGフラグ ? VARSIZE("SLG_DAILY_EVENT_NAME_ENG") # VARSIZE("DAILY_EVENT_NAME_ENG")

CALL FISHER_YATES_SHAFFLE(EVENT_NUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_ARRAY"

FOR LOCAL, 0, EVENT_NUM
	SIF (SLGフラグ == 0 && DAILY_DISABLE:(EVENT_ARRAY:LOCAL)) || (SLGフラグ == 1 && SLG_DAILY_DISABLE:(EVENT_ARRAY:LOCAL))
		CONTINUE
	IF SLGフラグ
		NAME_ENG = %SLG_DAILY_EVENT_NAME_ENG:(EVENT_ARRAY:LOCAL)%
		NAME_JAP = %SLG_DAILY_EVENT_NAME:(EVENT_ARRAY:LOCAL)%
	ELSE
		NAME_ENG = %DAILY_EVENT_NAME_ENG:(EVENT_ARRAY:LOCAL)%
		NAME_JAP = %DAILY_EVENT_NAME:(EVENT_ARRAY:LOCAL)%
	ENDIF
	TRYCCALLFORM %SLG%EVENT_DAILY_%NAME_ENG%_RATE()

		SIF RESULT * (SLGフラグ ? SLG_DAILY_EVENT_ODDS # DAILY_EVENT_ODDS) < RAND:1000
			CONTINUE

		FIRST_LINE = LINECOUNT

		RESULT = 1
		TRYCALLFORM %SLG%EVENT_DAILY_%NAME_ENG%_DECISION()
		SIF RESULT == 0
			CONTINUE

		VARSET DAILY_TARGET, -1
		VARSET DAILY_TARGET_NUM
		RESULT = 1
		TRYCALLFORM %SLG%EVENT_DAILY_%NAME_ENG%_SETTARGET()
		SIF RESULT == 0
			CONTINUE

		CALL DAILY_START(NAME_JAP)
		CALLFORM %SLG%EVENT_DAILY_%NAME_ENG%()

		IF RESULT == 1
			EVENT_COUNT --
		ENDIF
		SIF DAILY_CANCEL
			RETURN
		SIF EVENT_COUNT == 0
			BREAK
	CATCH
		DEBUGPRINTFORML %SLG%EVENT_DAILY_%NAME_ENG%_RATEみつからず
	ENDCATCH
NEXT


@EVENT_KOJO_DAILY()
#DIM EVENT_NUM
#DIM EVENT_COUNT
#DIM EVENT_PICK, VARSIZE("SHAFFLE_ARRAY")
EVENT_COUNT = KOJO_DAILY_EVENT_COUNT

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

CALL KOJO_DAILY_DERIVATION

;口上用デイリー MASTERのものは発動しない
;順番決定
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_PICK"

FOR LOCAL, 1, CHARANUM
	SIF !IS_FIXED_CHARA(EVENT_PICK:LOCAL) || (!IS_FIXED_CHARA(EVENT_PICK:LOCAL) && KOJO_DAILY_DISABLE:(NO:(EVENT_PICK:LOCAL)))
		CONTINUE
	CALL KOJO_DAILY(EVENT_PICK:LOCAL)
	;正常終了していたらカウントを減らす
	SIF RESULT
		EVENT_COUNT --
	;終了フラグもしくはカウントが0になっていたら抜ける
	SIF DAILY_CANCEL || EVENT_COUNT == 0
		BREAK
NEXT

;-------------------------------------------------------------------------------
;指定キャラについて、「放浪したり死んだりしていない」「捕虜でない」「どこかの勢力に所属している」を満たしているなら真
;-------------------------------------------------------------------------------
@IS_DAILY_EVENT_HAPPEN(ARG:0)
#FUNCTION
IF CFLAG:(ARG:0):特殊状態 == 0 && CFLAG:(ARG:0):捕虜先 == 0 && CFLAG:(ARG:0):1
	RETURNF 1
ENDIF
RETURNF 0


;-------------------------------------------------------------------------------
;デイリーイベントにおいて、ランダムに対象キャラを選出する
;捕虜でなく、死んだりしていないのは前提
;ARG:0 0自勢力キャラ　1全勢力から選択
;RESULT 選出された対象キャラ
;-------------------------------------------------------------------------------
@DAILY_EVENT_RAND_CHARASELECT(ARG:0 = 0)
#DIM 対象, 3000
#DIM カウンタ, 1
VARSET 対象
VARSET カウンタ
;シャッフル用の配列を作成する
FOR LOCAL, 0, CHARANUM
	IF (ARG:0 || (!ARG:0 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1)) && !CFLAG:(LOCAL:0):捕虜先 && !CFLAG:(LOCAL:0):特殊状態
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT

RETURN カウンタ ? 対象:(RAND:カウンタ) # -1

;-------------------------------------------------------------------------------
;デイリーイベントにおいて、ランダムに対象都市を選出する
;ARG:0 0自勢力都市　1全勢力から選択
;RESULT 選出された対象都市
;-------------------------------------------------------------------------------
@DAILY_EVENT_RAND_CITYSELECT(ARG:0 = 0)
#DIM 対象, MAX_CITY
#DIM カウンタ, 1
VARSET 対象
VARSET カウンタ

;シャッフル用の配列を作成する
FOR LOCAL, 0, MAX_CITY
	IF CITY_TYPE:LOCAL == 1
		BREAK
	ENDIF
	IF ARG:0 || (!ARG:0 && CITY_OWNER:LOCAL == CFLAG:MASTER:1)
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT

RETURN カウンタ ? 対象:(RAND:カウンタ) # -1

;-------------------------------------------------------------------------------
;デイリーイベントにおいて、ランダムに対象国家を選出する
;RESULT 選出された対象国家
;-------------------------------------------------------------------------------
@DAILY_EVENT_RAND_COUNTRYSELECT
#DIM 対象, MAX_COUNTRY
#DIM カウンタ
VARSET 対象
VARSET カウンタ

;シャッフル用の配列を作成する
FOR LOCAL, 0, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL)
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT

RETURN カウンタ ? 対象:(RAND:カウンタ) # -1
