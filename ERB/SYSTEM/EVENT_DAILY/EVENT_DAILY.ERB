;複数シナリオにまたがるイベント処理
@EVENT_DAILY()
VARSET LOCAL

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

;薬売り
CALL SMUGGLER

;金貸し
CALL LENDER
CALL TRAIN_BY_LENDER(DVAR:金貸し娘)

;野盗討伐イベント
CALL BANDIT_CONQUER

;野盗の身代金要求処理
CALL BANDIT_REQUIRE_RANSOM

;肉便器イベント
CALL PUBLIC_TOILET

;薬物依存イベント
CALL DRUG_ADDICT

;外界の医者
CALL DOCTOR_GAIRAI

;薬物用医者
CALL DOCTOR_DRUG

;筋肉馬鹿
CALL TALENT_TRAINER

;人材要求
CALL REQUESTED_CHARA

;風説流布の効果
CALL SPREAD_OF_RUMORS

;能力が上昇
IF RAND:100 < 5
	CALL DAILY_EVENT_RAND_CHARASELECT
	LOCAL = RESULT
	SELECTCASE RAND:100
		CASE 0 TO 69
			LOCAL:1 = 1
		CASE 70 TO 89
			LOCAL:1 = 2
		CASE 90 TO 99
			LOCAL:1 = 3
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			LOCALS = 武闘
			ABL:LOCAL:武闘 += LOCAL:1
			LOCAL:2 = 50
		CASE 1
			LOCALS = 知略
			ABL:LOCAL:知略 += LOCAL:1
			LOCAL:2 = 51
		CASE 2
			LOCALS = 政治
			ABL:LOCAL:政治 += LOCAL:1
			LOCAL:2 = 52
	ENDSELECT
	DRAWLINE
	PRINTFORML %ANAME(LOCAL)%は秘密の特訓にいそしんでいる……
	SETCOLOR COLOR("オレンジ")
	PRINTFORML %ANAME(LOCAL)%の%LOCALS%が{LOCAL:1}上がり、{ABL:LOCAL:(LOCAL:2)}になった！
	RESETCOLOR
ENDIF

CALL DAILY_EVENT_RAND_CITYSELECT
LOCAL = RESULT
IF LOCAL >= 0
	;経済が拡張
	IF RAND:100 < 2
		DRAWLINE
		LOCAL:1 = RAND(CITY_ECONOMY_LIMIT:(LOCAL) / 100 * 100 / 10 , CITY_ECONOMY_LIMIT:(LOCAL) / 100 * 100 / 10 + 1000)
		PRINTFORML %CITY_NAME:LOCAL%は好景気なようだ……
		SETCOLOR COLOR("オレンジ")
		IF CITY_ECONOMY:LOCAL == CITY_ECONOMY_LIMIT:LOCAL
			CITY_ECONOMY_LIMIT:LOCAL += LOCAL:1
			PRINTFORML %CITY_NAME:LOCAL%の経済規模の上限が{LOCAL:1 / 100}上昇し、{CITY_ECONOMY_LIMIT:LOCAL / 100}になった！
		ELSE
			SIF CITY_ECONOMY:LOCAL + LOCAL:1 >= CITY_ECONOMY_LIMIT:LOCAL
				LOCAL:1 = MIN(LOCAL:1, CITY_ECONOMY_LIMIT:LOCAL - CITY_ECONOMY)
			CITY_ECONOMY:LOCAL += LOCAL:1
			PRINTFORML %CITY_NAME:LOCAL%の経済規模が{LOCAL:1 / 100}上昇し、{CITY_ECONOMY:LOCAL / 100}になった！
		ENDIF
		RESETCOLOR
	ENDIF
ENDIF

;兵数が増える
IF RAND:100 < 2
	LOCALS = %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:1))%
	LOCAL:1 = 0
	FOR LOCAL, 0, MAX_CITY
		IF CITY_OWNER:LOCAL == CFLAG:MASTER:1
			LOCAL:1 ++
		ENDIF
	NEXT
	LOCAL:2 = RAND(LOCAL:1 * 300, LOCAL:1 * 300 + 500)
	COUNTRY_SOLDIER:(CFLAG:MASTER:1) += LOCAL:2
	DRAWLINE
	PRINTFORML %LOCALS%の人望に、義勇軍が結成された……
	SETCOLOR COLOR("オレンジ")
	PRINTFORML 兵数が{LOCAL:2}増え、{COUNTRY_SOLDIER:(CFLAG:MASTER:1)}になった！
	RESETCOLOR
ENDIF

CALL DAILY_EVENT_RAND_CHARASELECT
LOCAL = RESULT

IF LOCAL >= 0
	;サボり魔を取得するか、削除する
	IF !RAND:200
	DRAWLINE
		IF TALENT:LOCAL:サボり魔
			PRINTFORML 最近の%ANAME(LOCAL)%はなんだかやる気に満ちあふれている……
			SETCOLOR COLOR("オレンジ")
			PRINTFORML %ANAME(LOCAL)%は【サボり魔】を克服した！
			RESETCOLOR
			TALENT:LOCAL:サボり魔 = 0
		ELSE
			PRINTFORML 最近の%ANAME(LOCAL)%はなんだかやる気がみられない……
			SETCOLOR COLOR("警告")
			PRINTFORML %ANAME(LOCAL)%は【サボり魔】になってしまった！
			RESETCOLOR
			TALENT:LOCAL:サボり魔 = 1
		ENDIF
	ENDIF

	;ケガをするか、ケガから復帰
	IF !RAND:100
		DRAWLINE
		CALL DAILY_EVENT_RAND_CHARASELECT
		LOCAL = RESULT
		IF SLG_COOLTIME:LOCAL:0
			PRINTFORML %ANAME(LOCAL)%のケガは予想以上に早く治ったようだ
			SETCOLOR COLOR("オレンジ")
			PRINTFORML %ANAME(LOCAL)%はクールタイムから復帰した！
			RESETCOLOR
			SLG_COOLTIME:LOCAL:0 = 0
		ELSE
			LOCAL:1 = RAND:3 + 1
			PRINTFORML %ANAME(LOCAL)%は訓練中にケガをしてしまったようだ
			SETCOLOR COLOR("警告")
			PRINTFORML %ANAME(LOCAL)%は{LOCAL:1}ターンのクールタイムに入った！
			RESETCOLOR
			SLG_COOLTIME:LOCAL:0 = LOCAL:1
		ENDIF
	ENDIF
ENDIF





;指定キャラについて、「放浪したり死んだりしていない」「捕虜でない」「どこかの勢力に所属している」を満たしているなら真
@IS_DAILY_EVENT_HAPPEN(ARG:0)
#FUNCTION
IF CFLAG:(ARG:0):12 == 0 && CFLAG:(ARG:0):9 == 0 && CFLAG:(ARG:0):1
	RETURNF 1
ENDIF
RETURNF 0



;デイリーイベントにおいて、ランダムに対象キャラを選出する
;捕虜でなく、死んだりしていないのは前提
;ARG:0 0自勢力キャラ　1全勢力から選択
;RESULT 選出された対象キャラ
@DAILY_EVENT_RAND_CHARASELECT(ARG:0 = 0)
#DIM 対象, 1000
#DIM カウンタ, 1
VARSET 対象
VARSET カウンタ
;シャッフル用の配列を作成する
FOR LOCAL, 0, CHARANUM
	IF (ARG:0 || (!ARG:0 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1)) && !CFLAG:(LOCAL:0):9 && !CFLAG:(LOCAL:0):12
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT
IF カウンタ > 0
	CALL FISHER_YATES_SHAFFLE(カウンタ)
	RETURN 対象:(SHAFFLE_ARRAY:0)
ELSE
	RETURN -1
ENDIF

;デイリーイベントにおいて、ランダムに対象都市を選出する
;ARG:0 0自勢力都市　1全勢力から選択
;RESULT 選出された対象都市
@DAILY_EVENT_RAND_CITYSELECT(ARG:0 = 0)
#DIM 対象, MAX_CITY
#DIM カウンタ, 1
VARSET 対象
VARSET カウンタ

;シャッフル用の配列を作成する
FOR LOCAL, 0, MAX_CITY
	IF CITY_TYPE:LOCAL == 1
		BREAK
	ENDIF
	IF ARG:0 || (!ARG:0 && CITY_OWNER:LOCAL == CFLAG:MASTER:1)
		対象:カウンタ = LOCAL
		カウンタ ++
	ENDIF
NEXT
IF カウンタ > 0
	CALL FISHER_YATES_SHAFFLE(カウンタ)
	RETURN 対象:(SHAFFLE_ARRAY:0)
ELSE
	RETURN -1
ENDIF