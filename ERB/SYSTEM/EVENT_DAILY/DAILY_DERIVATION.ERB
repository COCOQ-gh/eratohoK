;共通デイリーイベント：派生処理
@DAILY_DERIVATION(SLGフラグ)
#DIM SLGフラグ
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIMS SLG
#DIM EVENT_COUNT
#DIM FIRST_LINE

SIF !SLGフラグ && !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0

EVENT_NUM = SLGフラグ ? VARSIZE("SLG_DAILY_EVENT_DERIVATION_NAME_ENG") # VARSIZE("DAILY_EVENT_DERIVATION_NAME_ENG")
SLG = %SLGフラグ ? "SLG_" # ""%

CALL FISHER_YATES_SHAFFLE(EVENT_NUM)

FOR LOCAL, 0, EVENT_NUM
	IF SLGフラグ
		NAME_ENG = %SLG_DAILY_EVENT_DERIVATION_NAME_ENG:(SHAFFLE_ARRAY:LOCAL)%
		NAME_JAP = %SLG_DAILY_EVENT_DERIVATION_NAME:(SHAFFLE_ARRAY:LOCAL)%
	ELSE
		NAME_ENG = %DAILY_EVENT_DERIVATION_NAME_ENG:(SHAFFLE_ARRAY:LOCAL)%
		NAME_JAP = %DAILY_EVENT_DERIVATION_NAME:(SHAFFLE_ARRAY:LOCAL)%
	ENDIF

	TRYCCALLFORM %SLG%EVENT_DAILY_DERIVATION_%NAME_ENG%_DECISION()
	SIF RESULT == 0
		CONTINUE

	VARSET DAILY_TARGET, -1
	VARSET DAILY_TARGET_NUM
	RESULT = 1
	TRYCALLFORM %SLG%EVENT_DAILY_DERIVATION_%NAME_ENG%_SETTARGET()
	SIF RESULT == 0
		CONTINUE

	FIRST_LINE = LINECOUNT

	CALL DAILY_START(NAME_JAP)
	CALLFORM %SLG%EVENT_DAILY_DERIVATION_%NAME_ENG%()
	IF RESULT == 0
		CLEARLINE LINECOUNT - FIRST_LINE
		CONTINUE
	ENDIF
	SIF DAILY_CANCEL
		RETURN
	CATCH
		DEBUGPRINTFORML %SLG%EVENT_DAILY_DERIVATION_%NAME_ENG%_DECISIONみつからず
	ENDCATCH
NEXT
