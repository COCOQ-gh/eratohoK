

;---------------------------------------------------------------
;淫紋イベント
;---------------------------------------------------------------
@LEWDNESS_CURSE
#DIM 対象
IF DVAR:悪魔契約 == 0
	PRINTFORML ある日%ANAME(MASTER)%の夢の中に悪魔が現れた
	PRINTFORML その悪魔は契約をすれば強大な力をくれると囁いてきた
	PRINTFORML 彼の言葉には妙な説得力を感じた…
	DVAR:悪魔契約 = 1
ELSEIF DVAR:悪魔契約 == 1 && GETBIT(TALENT:MASTER:デイリー陥落系, 素質_デイリー_淫紋)
	PRINTFORML 再びあの悪魔が現れた
	PRINTFORML その姿を見るだけで%ANAME(MASTER)%の子宮が疼き、雌犬の様に発情してしまった
	PRINTFORML %ANAME(MASTER)%のその姿を見て悪魔は満足そうに笑い、下腹部を撫で回してくる
	PRINTFORML ブルブルと震える%ANAME(MASTER)%の耳元で更なる契約を迫ってきた
ELSE
	PRINTFORML 再びあの悪魔が現れた
	PRINTFORML 心に響く声で契約を迫ってくる…
ENDIF
PRINTFORMW どうしよう？
IF GETBIT(TALENT:MASTER:デイリー陥落系, 素質_デイリー_淫紋)
	CALL ASK_MULTI(@"契約する" ,"断る" ,"犯してもらう")
ELSE
	CALL ASK_MULTI(@"契約する" ,"断る")
ENDIF
IF RESULT == 1
	PRINTFORML そんな契約を受けるわけにはいかない
	PRINTFORML %ANAME(MASTER)%が断ると悪魔は地の底から響くような声で笑った
	PRINTFORMW そしてまた来ることを告げると黒い靄となって消え去った
	RETURN
ELSEIF RESULT == 2
	PRINTFORML 疼きが止まらない%ANAME(MASTER)%は悪魔に犯してもらうように縋り付いた
	PRINTFORML 悪魔は大きく口を歪めて、ニヤリと笑うと%ANAME(MASTER)%の眼前に降りてくる
	PRINTFORML そしてどぎまぎする%ANAME(MASTER)%の目の前に凶悪なペニスをさらけ出してきた
	PRINTFORML それは赤黒くグロテスクでドクンドクンと激しく脈動していた
	PRINTFORMW %ANAME(MASTER)%はあまりに濃厚な雄の臭いに子宮が排卵を始めるのを感じながら、恍惚の表情でそれに口づけをした
	PRINTFORML 
	PRINTFORML %ANAME(MASTER)%は悪魔のペニスを深々と突き刺されながら狂ったように髪を振り乱して喘いでいる
	PRINTFORML 悪魔とのセックスは想像を絶する快楽を与え、一突き毎に%ANAME(MASTER)%の理性が破壊されるのを感じる
	PRINTFORML しかしそんな事は今の%ANAME(MASTER)%にとってはどうでも良い事で、ひたすら与えられる快楽にヨガり狂っている
	PRINTFORMW どちゅん！と胎内にねじ込まれる程に一際深くペニスをねじ込まれ、%ANAME(MASTER)%は大きく背中を反らして絶頂する
	PRINTFORML アヘ顔を晒してビクンビクンと震える%ANAME(MASTER)%だが、彼がそれを気遣うはずもなく更に激しいピストンを開始する
	PRINTFORML 今だ絶頂を続ける%ANAME(MASTER)%はペニスが出入りする度に更に強烈な絶頂を与えられ、全身を痙攣させて泡を吹く
	PRINTFORML あまりの快楽に涙を流しながら許しを請う%ANAME(MASTER)%だが、その願いは聞き届けられず延々と絶頂させられる
	PRINTFORMW %ANAME(MASTER)%はすっかり悪魔の玩具になりさがり、一晩中彼の暴力的なセックスで身も心も征服されてしまった
	CALL FORCE_FUCK(MASTER, SP_NAME_TO_ID("不明"), @"悪魔の\@ RAND:2 ? ペニス # 唇\@", @"悪魔", 4)
	CALL FORCE_FUCK(MASTER, SP_NAME_TO_ID("不明"), @"悪魔の\@ RAND:2 ? ペニス # 唇\@", @"悪魔", 4)
	PRINTFORML 
	PRINTFORML 翌日、目覚めた%ANAME(MASTER)%は子宮から伝わる強烈な快楽で思わず絶頂した
	PRINTFORMW 息を荒げながら下腹部に手を当てると、ドクンドクンと悪魔の精液がそこで暴れているのを感じた
	RETURN
ELSE
	PRINTFORML 力の誘惑は抗いがたく、%ANAME(MASTER)%は契約する事にした
	PRINTFORML 悪魔は頷くと、契約の代償として女の身体を差し出せと言ってきた
	PRINTFORML %ANAME(MASTER)%はゴクリと生唾を飲む
	PRINTFORMW 誰にしよう？
	CALL LEWDNESS_CURSE_CHALASELECT
	対象 = RESULT
	IF 対象 == -1
		PRINTFORML ……いや、やはりそんな契約を受けるわけにはいかない
		PRINTFORML %ANAME(MASTER)%が断ると悪魔は地の底から響くような声で笑った
		PRINTFORMW そしてまた来ることを告げると黒い靄となって消え去った
		RETURN
	ELSE
		IF 対象 == MASTER
			PRINTFORMW %ANAME(MASTER)%は震える声で自らの身体を捧げると告げた
			PRINTFORML すると悪魔はニヤリと不気味な笑みを浮かべて了承した
			PRINTFORML 彼が%ANAME(MASTER)%の下腹部に掌をかざすと、%ANAME(MASTER)%は強烈な熱を感じて思わずお腹を押さえてうずくまった
			PRINTFORML 「確かに贄をいただいた、これで契約は成立した」
			PRINTFORMW 戸惑う%ANAME(MASTER)%に向けて悪魔が掌をかざすと、%ANAME(MASTER)%の身体に力がみなぎってきた！
		ELSE
			PRINTFORMW %ANAME(対象)%を贄として差し出した
			PRINTFORML 悪魔は満足そうに笑うと、ぱちんと指を鳴らした
			PRINTFORML 「確かに贄をいただいた、これで契約は成立した」
			PRINTFORMW 戸惑う%ANAME(MASTER)%に向けて悪魔が掌をかざすと、%ANAME(MASTER)%の身体に力がみなぎってきた！
		ENDIF
		LOCAL = (100 - ABL:MASTER:武闘) / 6
		SIF LOCAL <= 0
			LOCAL = 2
		ABL:MASTER:武闘 += LOCAL
		LOCAL:1 = (100 - ABL:MASTER:知略) / 6
		SIF LOCAL:1 <= 0
			LOCAL:1 = 2
		ABL:MASTER:知略 += LOCAL:1
		LOCAL:2 = (100 - ABL:MASTER:政治) / 6
		SIF LOCAL:2 <= 0
			LOCAL:2 = 2
		ABL:MASTER:政治 += LOCAL:2
		CALL COLORPRINTFORM(@"%ANAME(MASTER)%の武闘値が{LOCAL}上昇した！", COLOR("オレンジ"))
		PRINTFORML 
		CALL COLORPRINTFORM(@"%ANAME(MASTER)%の知略値が{LOCAL:1}上昇した！", COLOR("オレンジ"))
		PRINTFORML 
		CALL COLORPRINTFORM(@"%ANAME(MASTER)%の政治値が{LOCAL:2}上昇した！", COLOR("オレンジ"))
		PRINTFORMW 
		PRINTFORML 悪魔は黒い靄となって消え去った
		IF 対象 == MASTER
			PRINTFORMW %ANAME(MASTER)%は高揚感に包まれながら、再び夢の中へと意識が溶けて行った
			PRINTFORML 
			PRINTFORML …翌日、目覚めた%ANAME(MASTER)%は自らの下腹部を摩った
			PRINTFORML 何の変化もないが、あの時の熱を思い出しかすかな恐怖が頭をもたげる
			PRINTFORML しかし%ANAME(MASTER)%は不安を振り払うとベッドを抜けて着替えの為に寝間着を脱いだ
			PRINTFORMW その下腹部には、悪魔の契約印がうっすらと浮かび上がっていた……
			CALL COLORPRINTFORM(@"%ANAME(対象)%に淫紋が刻まれた", COLOR("桃"))
			PRINTFORMW 
			SETBIT TALENT:対象:デイリー陥落系, 素質_デイリー_淫紋
		ELSE
			PRINTFORMW %ANAME(MASTER)%は高揚感に包まれながら、再び夢の中へと意識が溶けて行った
			PRINTFORML 
			PRINTFORML …同時刻、%ANAME(対象)%は下腹部に強烈な熱を感じて飛び起きた
			PRINTFORML 汗だくで何事かと下腹部を摩るが先程感じた熱は嘘の様に去っていた
			PRINTFORMW 気のせいかと訝しみながらも再び横になる彼女の下腹部には、悪魔の契約印がうっすらと浮かび上がっていた……
			CALL COLORPRINTFORM(@"%ANAME(対象)%に淫紋が刻まれた", COLOR("桃"))
			PRINTFORMW 
			SETBIT TALENT:対象:デイリー陥落系, 素質_デイリー_淫紋
		ENDIF
		DVAR:浸食中キャラ = GET_ID(対象)
		DVAR:浸食値 = 1
	ENDIF
ENDIF
RETURN

;---------------------------------------------------------------
;キャラ選択
;---------------------------------------------------------------
@LEWDNESS_CURSE_CHALASELECT
#DIM 消去行
#DIM 対象
#DIM 表示位置
#DIM 表示ページ
#DIM 総ページ数
#DIM キャラ数
#DIM キャラカウンタ
#DIM 表示開始位置
#DIM 表示終了位置
VARSET キャラ数
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		キャラ数 ++
NEXT
総ページ数 = (キャラ数 - 1) / 40 + 1
表示ページ = 1

PRINTFORML %TOSTR_SPACE(23)%   武|  知|  政|  妖|  歌|  料 %TOSTR_SPACE(26)%   武|  知|  政|  妖|  歌|  料

$SHOW_LOOP

VARSET 消去行
VARSET 表示位置
VARSET キャラカウンタ
表示開始位置 = (表示ページ - 1) * 40
表示終了位置 = 表示ページ * 40


FOR LOCAL:0, 0, CHARANUM
	;育児されている子供は弾く
	SIF CFLAG:(LOCAL:0):16 == 4
		CONTINUE
	;所属が同じのキャラのみ
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		IF 表示開始位置 <= キャラカウンタ && キャラカウンタ < 表示終了位置
			IF 表示位置 % 2 != 0
				PRINT 　　
			ELSEIF 表示位置 > 0
				PRINTL 
				消去行 ++
			ENDIF
			IF !CFLAG:LOCAL:捕虜先 && IS_FEMALE(LOCAL:0) && !GETBIT(TALENT:LOCAL:デイリー陥落系, 素質_デイリー_淫紋) && !GETBIT(TALENT:LOCAL:肉体陥落系, 素質_陥落_肉便器) && !GETBIT(TALENT:LOCAL:肉体陥落系, 素質_陥落_淫乱)
				PRINTFORM [{LOCAL:0 + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%
				PRINTFORM   
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):武闘)
				PRINTFORM {ABL:(LOCAL:0):武闘, 3}|
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):知略)
				PRINTFORM {ABL:(LOCAL:0):知略, 3}|
				CALL PRINT_ALPHABET_RANK(0, ABL:(LOCAL:0):政治)
				PRINTFORM {ABL:(LOCAL:0):政治, 3}|
				CALL PRINT_ALPHABET_RANK(1, ABL:(LOCAL:0):妖術)
				PRINTFORM {ABL:(LOCAL:0):妖術, 3}|
				CALL PRINT_ALPHABET_RANK(2, ABL:(LOCAL:0):歌唱)
				PRINTFORM {ABL:(LOCAL:0):歌唱, 3}|
				CALL PRINT_ALPHABET_RANK(2, ABL:(LOCAL:0):料理)
				PRINTFORM {ABL:(LOCAL:0):料理, 3}
			ELSE
				LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%  %ALPHABET_RANK(0, ABL:LOCAL:武闘)%{ABL:(LOCAL:0):武闘, 3}|%ALPHABET_RANK(0, ABL:LOCAL:知略)%{ABL:(LOCAL:0):知略, 3}|
				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:政治)%{ABL:(LOCAL:0):政治, 3}|%ALPHABET_RANK(1, ABL:LOCAL:妖術)%{ABL:(LOCAL:0):妖術, 3}|%ALPHABET_RANK(2, ABL:LOCAL:歌唱)%{ABL:(LOCAL:0):歌唱, 3}|%ALPHABET_RANK(2, ABL:LOCAL:料理)%{ABL:(LOCAL:0):料理, 3}"
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM %LOCALS:0%
				RESETCOLOR
			ENDIF
			表示位置 ++
		ENDIF
		キャラカウンタ ++
	ENDIF
NEXT

PRINTL 
DRAWLINE

IF 総ページ数 >= 2
	消去行 += 2
	IF 表示ページ > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = ページ{表示ページ}/{総ページ数}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF 表示ページ < 総ページ数
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF

PRINTL [  0] やっぱりやめる

REDRAW 0

消去行 += 4

$INPUT_LOOP
INPUT
IF RESULT == 8 && 表示ページ > 1
	表示ページ --
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ELSEIF RESULT == 9 && 表示ページ < 総ページ数
	表示ページ ++
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	RETURN -1
ENDIF

対象 = RESULT - 99
IF 対象 > CHARANUM || 対象 < 0
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ELSEIF CFLAG:MASTER:所属 != CFLAG:対象:所属 || CFLAG:対象:捕虜先 || !IS_FEMALE(対象) || GETBIT(TALENT:対象:デイリー陥落系, 素質_デイリー_淫紋) || GETBIT(TALENT:対象:肉体陥落系, 素質_陥落_肉便器) || GETBIT(TALENT:対象:肉体陥落系, 素質_陥落_淫乱)
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ENDIF

RETURN 対象