@STEMA
#DIM 対象
#DIM 候補キャラ, 1000
#DIM 候補数
VARSET 候補キャラ
VARSET 候補数

;他国に所属している自分へ恋慕or服従を持つキャラからランダム選択、対象がいない場合はイベントキャンセル
FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):所属 != CFLAG:MASTER:所属 && IS_COUNTRY(CFLAG:LOCAL:所属) && (IS_LOVER(LOCAL) || IS_SLAVE(LOCAL)) && !CFLAG:(LOCAL):捕虜先 && !IS_ANIMAL(LOCAL)
		候補キャラ:LOCAL = RAND:1000 
		候補数 ++
	ENDIF
NEXT

SIF 候補数 == 0
	RETURN -1

対象 = FINDELEMENT(候補キャラ, MAXARRAY(候補キャラ))

DAILY_NAME = ステマ
CALL DAILY_START()

PRINTFORML %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%勢力にいる%ANAME(対象)%は、同勢力の仲間に対して
PRINTFORM %ANAME(MASTER)%の事を
IF IS_SLAVE(対象)
	PRINTFORM 陶酔するような表情で熱っぽく
ELSE
	PRINTFORM 頬を赤らめながら熱心に
ENDIF
PRINTFORMW 宣伝して回っている

FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):所属 == CFLAG:(対象):所属 && !CFLAG:(LOCAL):捕虜先 && !IS_ANIMAL(LOCAL)
		CFLAG:(LOCAL):好感度 += (51 + RAND:100)
	ENDIF
NEXT
CALL CHANGE_RELATION_C_TO_O(CFLAG:対象:所属, MASTER, 50, -50)
CALL COLORPRINTFORM(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%勢力に所属する武将たちの%ANAME(MASTER)%に対する評価が上がった", COLOR("注意"))
PRINTFORMW 

RETURN
