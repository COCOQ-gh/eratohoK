;---------------------
;対応するデイリーのDISABLEを返す。設定しない場合、イベントは発生しない。
;---------------------
@EVENT_DAILY_DERIVATION_RETURN_HOSTAGE_DISABLE()
RETURN DAILY_GET_DISABLE_CONFIG("REQUEST_MONEY")

;---------------------
;発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;対応するデイリーのDISABLEチェックを規約として必須とする
;---------------------
@EVENT_DAILY_DERIVATION_RETURN_HOSTAGE_DECISION()
#DIM 君主

;資金援助要求カウンタが残っていればデクリメント（0になるまでイベントは再発生しない）
IF DVAR:資金援助要求カウンタ > 0
	DVAR:資金援助要求カウンタ --
	;カウンタが0になったなら、返却期限を迎えたとみなす
	IF DVAR:資金援助要求カウンタ == 0 && DVAR:資金援助担保
		君主 = GET_COUNTRY_BOSS(DVAR:資金援助担保)
		;君主が見つからなかったら（対象の元所属国家が滅ぶかなにかしていたら）駄目
		IF 君主 == -1
			FOR LOCAL, 0, CHARANUM
				SIF GETBIT(TALENT:(LOCAL:0):デイリー系, 素質_デイリー_借り物)
					CLEARBIT TALENT:(LOCAL:0):デイリー系, 素質_デイリー_借り物
			NEXT
			DVAR:資金援助担保 = 0
			DVAR:資金援助担保時所属国 = 0
			RETURN 0
		ENDIF
		RETURN 1
	ENDIF
ENDIF

RETURN 0

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_DERIVATION_RETURN_HOSTAGE_SETTARGET()

FOR LOCAL, 0, CHARANUM
	IF GETBIT(TALENT:(LOCAL:0):デイリー系, 素質_デイリー_借り物)
		DAILY_TARGET:0 = LOCAL
		DAILY_TARGET_NUM = 1
		BREAK
	ENDIF
NEXT
;対象が見つからなかったら抜ける
IF DAILY_TARGET_NUM < 1
	DVAR:資金援助担保 = 0
	DVAR:資金援助担保時所属国 = 0
	RETURN 0
ENDIF

RETURN 1

;---------------------
;本体
;---------------------
@EVENT_DAILY_DERIVATION_RETURN_HOSTAGE()
#DIM 対象
#DIM 君主
対象 = DAILY_TARGET:0
君主 = GET_COUNTRY_BOSS(DVAR:資金援助担保)

;担保をとったときの所属国と今の所属が違っていたら、簡易イベントで済ませる
IF CFLAG:MASTER:所属 != DVAR:資金援助担保時所属国
	PRINTFORMW 資金援助のため貸し出されていた%ANAME(対象)%は、%ANAME(君主)%の元へ戻っていったそうだ……
	CALL CHANGE_COUNTRY(対象, DVAR:資金援助担保, 1) 
	CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	RETURN 1
ENDIF

PRINTFORMW ……そろそろ%ANAME(対象)%の返却の期限だ
PRINTFORMW %ANAME(MASTER)%は%ANAME(君主)%の元へ向かった……
PRINTFORML
;自国の捕虜にしている
IF CFLAG:対象:捕虜先 == CFLAG:MASTER:所属
	PRINTFORMW ……%ANAME(対象)%は今、牢屋に幽閉している状態だ
	PRINTFORMW 戦死してしまったことにして、幽閉したままにするのもありかもしれない
	PRINTFORMW もちろん、%ANAME(君主)%との関係は、めちゃめちゃにこじれてしまうことだろうが……
	PRINTFORML
	CALL ASK_YN("戦死したと嘘をつく", "正直に返却する")
	LOCAL = RESULT
	IF RESULT == 0
		PRINTFORMW %ANAME(MASTER)%は%ANAME(君主)%との会談に臨んだ
		PRINTFORMW そして、%ANAME(対象)%が戦死してしまった旨を伝えた
		PRINTFORMW 話を聞いた%ANAME(君主)%は怒り狂い、お前達の顔など二度と見たくないと、部屋を後にした……
		PRINTFORML
		PRINTFORMW 拠点に戻った%ANAME(MASTER)%は、牢につながれた%ANAME(対象)%を見、舌なめずりした
		PRINTFORMW 邪魔者もいなくなった今、こいつをどのように調理してやろうか……
		CALL COLORPRINT(@"%ANAME(君主)%との関係が非常に悪化しました", カラー_警告, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が無所属の自国捕虜となりました", カラー_警告, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -1000, 800)
		CALL CHANGE_COUNTRY(対象, 0, 1)
		CALL CAPTURE(対象, CFLAG:MASTER:所属)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ELSEIF RESULT == 1
		PRINTFORMW %ANAME(MASTER)%は%ANAME(君主)%に%ANAME(対象)%を返却した
		PRINTFORMW 幽閉していたのもあって心証は最高とはいかなかったが、一応、感謝はされたようだ……
		CALL COLORPRINT(@"%ANAME(君主)%との関係が多少改善されました", カラー_注意, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が帰って行きました", カラー_警告, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, 300, -100)
		CALL CHANGE_COUNTRY(対象, DVAR:資金援助担保, 1)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ENDIF
;死んだ
ELSEIF CFLAG:対象:特殊状態 == 2
	PRINTFORMW ……%ANAME(対象)%は、残念ながら死亡してしまった
	PRINTFORMW 言い逃れるわけにもいかず、%ANAME(MASTER)%は正直に伝えた
	PRINTFORMW 話を聞いた%ANAME(君主)%は激怒し、お前達に預けたのが間違いだったと非難した……
	CALL COLORPRINT(@"%ANAME(君主)%との関係がかなり悪化しました", カラー_警告, "W")
	CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -800, 700)
	CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
;他国の捕虜にされている
ELSEIF CFLAG:対象:捕虜先
	PRINTFORMW ……%ANAME(対象)%は今、%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:捕虜先))%に幽閉されている
	PRINTFORMW 言い逃れるわけにもいかず、%ANAME(MASTER)%は正直に伝えた
	PRINTFORMW 話を聞いた%ANAME(君主)%は怒り、なぜ身柄を確かにしておいてくれなかったのだと非難した……
	CALL COLORPRINT(@"%ANAME(君主)%との関係が悪化しました", カラー_警告, "W")
	CALL COLORPRINT(@"%ANAME(対象)%が担保ではなくなりました", カラー_警告, "W")
	CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -500, 500)
	;CHANGE_COUNTRYすると捕虜先消えるのでこうする
	CFLAG:対象:所属 = DVAR:資金援助担保
	CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
;他国に移籍した（捕虜からの登用、野盗による拉致などで）
ELSEIF CFLAG:対象:所属 != CFLAG:MASTER:所属
	PRINTFORMW ……%ANAME(対象)%は今、\@ IS_COUNTRY(CFLAG:対象:所属) ? %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%に仕えている # 放浪している \@
	PRINTFORMW 言い逃れるわけにもいかず、%ANAME(MASTER)%は正直に伝えた
	PRINTFORMW 話を聞いた%ANAME(君主)%は怒り、なぜ身柄を確かにしておいてくれなかったのだと非難した……
	CALL COLORPRINT(@"%ANAME(君主)%との関係が悪化しました", カラー_警告, "W")
	CALL COLORPRINT(@"%ANAME(対象)%が担保ではなくなりました", カラー_警告, "W")
	CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -500, 500)
	CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
;帰りたくない・恋慕ルート
ELSEIF IS_LOVER(対象)
	PRINTFORMW ……%ANAME(君主)%との会談の場で、当の%ANAME(対象)%が帰りたくないと言い始めた
	PRINTFORMW %ANAME(MASTER)%と触れあう中で、慕うようになったと
	PRINTFORMW できればこのまま、%ANAME(MASTER)%に仕えたいというのだが……
	PRINTFORML
	CALL ASK_MULTI("ダメ元で残留を頼んでみる", "諦めて国元へ返す", "写真を送りつける")
	IF RESULT == 0
		PRINTFORMW %ANAME(MASTER)%としても、%ANAME(対象)%を失うことは色々つらい
		PRINTFORMW そのことを、%ANAME(君主)%に素直に、誠実に伝えた
		PRINTFORMW 話を聞いた%ANAME(君主)%は、複雑な思いだったようだが、
		PRINTFORMW それなら祝福するしかあるまいと、最終的には移籍を受け入れてくれた……
		CALL COLORPRINT(@"%ANAME(君主)%との関係がやや改善されました", カラー_注意, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が正式に自国に移籍しました", カラー_注意, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, 300, -300)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ELSEIF RESULT == 1
		PRINTFORMW %ANAME(MASTER)%は%ANAME(君主)%に%ANAME(対象)%を返却した
		PRINTFORMW %ANAME(対象)%は寂しがったが、少なくとも%ANAME(君主)%には感謝された……
		CALL COLORPRINT(@"%ANAME(君主)%との関係が改善されました", カラー_注意, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が帰って行きました", カラー_警告, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, 500, -500)
		CALL CHANGE_COUNTRY(対象, DVAR:資金援助担保, 1)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ELSEIF RESULT == 2
		PRINTFORMW %ANAME(MASTER)%としても、%ANAME(対象)%を失うことは色々つらい
		PRINTFORMW %ANAME(対象)%を説得するということで、%ANAME(君主)%に一旦帰ってもらった
		PRINTFORMW 後日、%ANAME(君主)%へ%ANAME(対象)%から一通の封筒が届いた
		PRINTFORMW そこには%ANAME(MASTER)%の横で幸せそうな笑顔をした%ANAME(対象)%の写った一枚の写真があった
		PRINTFORMW 裏には謝罪の言葉から始まり、もう%ANAME(MASTER)%と離れられないこと
		PRINTFORMW そして資金のために送り出したことへの批判が綴られていた……
		CALL COLORPRINT(@"%ANAME(君主)%との関係がこじれきりました", カラー_警告, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が正式に自国に移籍しました", カラー_注意, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -1500, 1500)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ENDIF
;帰りたくない・屈服ルート
ELSEIF IS_SLAVE(対象)
	PRINTFORMW ……%ANAME(君主)%との会談の場で、当の%ANAME(対象)%が帰りたくないと言い始めた
	PRINTFORMW %ANAME(MASTER)%と触れあう中で、忠誠を誓ったのだと
	PRINTFORMW できればこのまま、%ANAME(MASTER)%に仕えたいというのだが……
	PRINTFORML
	CALL ASK_MULTI(@"%ANAME(対象)%を奪い取る", "諦めて国元へ返す", "写真を送りつける")
	IF RESULT == 0
		PRINTFORMW %ANAME(MASTER)%としても、%ANAME(対象)%のような優秀なしもべは手元に置いておきたい
		PRINTFORMW そのことを、%ANAME(君主)%に一方的に伝えた
		PRINTFORMW さらに、%ANAME(対象)%が屈服した証を見せつけるように、%ANAME(君主)%の前で自らに奉仕させた
		PRINTFORMW %ANAME(君主)%は呆然とし、そしてその後激怒し、
		PRINTFORMW お前達の顔などもう見たくもないと叫んで、部屋を後にした……
		CALL COLORPRINT(@"%ANAME(君主)%との関係がこじれきりました", カラー_警告, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が正式に自国に移籍しました", カラー_注意, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -1500, 1500)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ELSEIF RESULT == 1
		PRINTFORMW %ANAME(MASTER)%は%ANAME(君主)%に%ANAME(対象)%を返却した
		PRINTFORMW %ANAME(対象)%は寂しがったが、少なくとも%ANAME(君主)%には感謝された……
		CALL COLORPRINT(@"%ANAME(君主)%との関係が改善されました", カラー_注意, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が帰って行きました", カラー_警告, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, 500, -500)
		CALL CHANGE_COUNTRY(対象, DVAR:資金援助担保, 1)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ELSEIF RESULT == 2
		PRINTFORMW %ANAME(MASTER)%としても、%ANAME(対象)%のような優秀なしもべは手元に置いておきたい
		PRINTFORMW %ANAME(対象)%を説得するということで、%ANAME(君主)%に一旦帰ってもらった
		PRINTFORMW 後日、%ANAME(君主)%へ%ANAME(対象)%から一通の封筒が届いた
		PRINTFORMW そこには%ANAME(MASTER)%と繋がっている%ANAME(対象)%が写った一枚の写真があった
		PRINTFORMW 裏には謝罪の言葉から始まり、もう%ANAME(MASTER)%と離れられないこと
		PRINTFORMW そして資金のために送り出したことへの批判が綴られていた……
		CALL COLORPRINT(@"%ANAME(君主)%との関係がこじれきりました", カラー_警告, "W")
		CALL COLORPRINT(@"%ANAME(対象)%が正式に自国に移籍しました", カラー_注意, "W")
		CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, -1500, 1500)
		CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
	ENDIF
;帰れ
ELSE
	PRINTFORMW ……約束通り、%ANAME(対象)%を%ANAME(君主)%に返却した
	PRINTFORMW %ANAME(君主)%から感謝された……


	CALL COLORPRINT(@"%ANAME(君主)%との関係が改善されました", カラー_注意, "W")
	CALL COLORPRINT(@"%ANAME(対象)%が帰って行きました", カラー_警告, "W")
	CALL CHANGE_RELATION_C_TO_C(DVAR:資金援助担保, CFLAG:MASTER:所属, 500, -500)
	CALL CHANGE_COUNTRY(対象, DVAR:資金援助担保, 1)
	CLEARBIT TALENT:対象:デイリー系, 素質_デイリー_借り物
ENDIF
DVAR:資金援助担保 = 0



RETURN 1
