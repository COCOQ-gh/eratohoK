@TALENT_TRAINER
#DIM 対象


DAILY_NAME = 素質トレーナー
CALL DAILY_START()

IF DVAR:素質トレーナー == 0
	PRINTFORML %ANAME(MASTER)%が仕事をしていると、侍従が来客を告げた
	PRINTFORML なんでも「やけにうるさい男が来た」のだそうだ
	PRINTFORMW どれくらいうるさいのかと思った%ANAME(MASTER)%は、会うことにしてみた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML 「ウオーーーッ！！！！　君が%ANAME(MASTER)%か！！！！！　会えて光栄だ！！！！！！！！」
	PRINTFORMW 「私は『ムキムキマッチョ☆筋肉マッスル塾』塾長！！！　男山　隆太郎だ！！！！！！！！」
	PRINTFORML 「実は今日は！！　君にいい話を持ってきた！！！　まぁ聞いてくれ！！！！！！！！！」
	PRINTFORML 「僕は色んな人に素質を教えて回っている！！！　戦いに役立つ素質をね！！！！！！！！」
	PRINTFORMW 「こんなご時世だ！！！！！！　身を守るためにも是非とも覚えてみないか！！！！！！？」
ELSE
	PRINTFORML 「やあ！！！！！！！！！　また会ったな！！！！！！！！！！！！！！！！」
	PRINTFORML 「覚えているかい！！！！？！？　ムキムキ筋肉♂マッスルハート塾の男山だ！！！！！！」
	PRINTFORMW 「また特殊能力を教えに来たぞ！！！！！！！！！！！！！！！！！！！！！！！！！！！」
ENDIF
PRINTFORML 「まぁ正直安くはないけど！！！！！！！！　でも！！素晴らしい素質ばかりだ！！！！！！！
PRINTFORMW 「どうだ！！！？　いい話だと思わないか！！！！！！？！！！？！？！！！？？？？」
PRINTFORML 男は終始うるさかったが、持ってきた話自体は悪いものではなかった
PRINTFORML 安くないらしいが、考える価値はあるだろう
PRINTFORMW さて、どうするか……
PRINTFORML
CALL ASK_YN("おしえてもらう！！！！！！！！！！！！！！", "暑苦しいのでやめておく")

IF RESULT == 1
	$CANCEL
	PRINTFORMW 「ムムムッ！！！！！！？　そうか！！！！！！　そういうこともあるよな！！！！！」
	PRINTFORMW 「それならば仕方あるまい！！！！！　私は立ち去るとするよ！！！！！！！！！！！」
ELSEIF RESULT == 0
	$TRAIN_LOOP
	PRINTFORML 「あいわかった！！！！！！！！！　では誰に教えたいのか決めてシルブプレ！！！！！！！！！！！」
	DRAWLINE
	;対象を決める
	CALL SELECT_CHARA_LIST_SLG
	SIF RESULT == -1
		GOTO CANCEL
	対象 = RESULT
	PRINTFORMW 「%ANAME(対象)%さんか！！！！！！！　あい分かった！！！！！！！！！！！！１！！！！！」
	PRINTFORMW 「では何を教えたいのか決めてシルブプレ！！！！！！！！！！！！！！！！！！！！！！！！」
	PRINTFORML 現在の金:{MONEY}
	DRAWLINE
	;種類を決める
	FOR LOCAL, 0, VARSIZE("SLG_TALENT_NAME")
		IF MONEY >= (SLG_TALENT_PRICE:LOCAL / 2) && !GETBIT(TALENT:対象:ＳＬＧ素質, LOCAL)
			PRINTFORML [{LOCAL, 2, RIGHT}] %SLG_TALENT_NAME:LOCAL, 14, LEFT% 価格:{SLG_TALENT_PRICE:LOCAL / 2}
		ELSE
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM [{LOCAL, 2, RIGHT}] %SLG_TALENT_NAME:LOCAL, 14, LEFT% 価格:{SLG_TALENT_PRICE:LOCAL / 2}
			RESETCOLOR
			PRINTL
		ENDIF
	NEXT
	PRINTFORML [99] 戻る！！！！！！！！！！！！！！！！！！！！
	$INPUT_LOOP
	INPUT
	IF RESULT == 99
		GOTO TRAIN_LOOP
	ELSEIF  RESULT < 0 || VARSIZE("SLG_TALENT_NAME") <= RESULT || GETBIT(TALENT:対象:ＳＬＧ素質, RESULT) || MONEY < (SLG_TALENT_PRICE:RESULT / 2)
		GOTO INPUT_LOOP
	ELSE
		LOCAL:1 = RESULT
		LOCAL:2 = SLG_TALENT_PRICE:RESULT / 2
		PRINTFORMW 「%SLG_TALENT_NAME:(LOCAL:1)%だな！！！！！！！！！！！　いいよね！！！！！！！！！　筋肉って感じもするしな！！！！！！！！！！！！」
		PRINTFORMW 「じゃあちょっと%ANAME(対象)%さんをお借りするよ！！！！！！！！！！！！！　ちょっと待っててくれな！！！！！！！！！！！！！１」
		PRINTFORMW ・
		PRINTFORMW ・
		PRINTFORMW ・

		SELECTCASE RAND:3
			CASE 0	
				PRINTFORMW 「いいか！！！！！！！！！！！！！　筋肉だ！！！！！！！！！！！！！１　筋肉が全てなんだ！！！！！！！」
				PRINTFORMW 「筋肉に全てを捧げろ！！！！！！！！！１　ｳｵｰｰｯ！！！！！　マッスルマッスル！！！！！！！！！！！！！」
			CASE 1
				PRINTFORMW 「いいか！！！！！！！！！！！　筋肉がコツだ！！！！！！！！！！　頑張れ！！！！！！！１」
				PRINTFORMW 「持てる筋肉を全て使え！！！！！！！！！！！！！！！　ぬおおおおおおお！！！！！！！！！」
			CASE 2
				PRINTFORMW 「筋肉！！！！！！！！１　肉！！！！！！！！　マッスル！！！！！！！！　ぬうう！！！！！」
				PRINTFORMW 「どうした！！！！　声が小さいぞ！！！！！！！！　ほら！！！！　恥ずかしがらないで！！！！！！！」
				PRINTFORMW 「筋肉！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！１１」
		ENDSELECT
		PRINTFORMW ・
		PRINTFORMW ・
		PRINTFORMW ・
		PRINTFORMW 「ふう！！！！！！！！！！　いい汗かいた！！！！！！！！！」
		PRINTFORMW 「結果はバッチリだったよ！！！！！！！！！！　飲み込みが早くて素晴らしいな！！！！！！！！！！」
		CALL COLORPRINTFORM(@"%ANAME(対象)%は%SLG_TALENT_NAME:(LOCAL:1)%を習得した！！！！！！！！！！！！！！", COLOR("注意"), "W")
		PRINTFORMW 「じゃあ僕は失礼するよ！！！！！！！！！！！！　また会えるといいな！！！！！！！！！１」
		PRINTFORMW 男は立ち去っていった！！！！！！！！！！！！！！！！！！！！！！！！！！！！！
		SETBIT TALENT:対象:ＳＬＧ素質, LOCAL:1
		MONEY -= LOCAL:2
	ENDIF
ENDIF
DVAR:素質トレーナー = 1