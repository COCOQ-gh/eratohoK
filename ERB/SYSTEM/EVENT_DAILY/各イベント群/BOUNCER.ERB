;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_BOUNCER_RATE()
RETURN 20


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_BOUNCER_DECISION()
RETURN DAY >= 10

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_BOUNCER_GENRE()
RETURN デイリー_ジャンル_雇用

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_BOUNCER()
#DIM 対象
#DIM 素質
#DIM 料金

CALL CREATE_RANDOM_CHARA()
対象 = RESULT

;生成に失敗したらRETURN 0
SIF RESULT == -1
	RETURN 0

ABL:対象:武闘 = RAND(60, 85)
ABL:対象:防衛 = RAND(60, 85)
ABL:対象:知略 = RAND(60, 80)
ABL:対象:政治 = 280 - (ABL:対象:武闘 + ABL:対象:知略 + ABL:対象:防衛)
ABL:対象:歌唱 = RAND(20, 40)
ABL:対象:料理 = 60 - ABL:対象:歌唱

;さらに、SLG用素質を一つ取得させる
SELECTCASE RAND:1000
	;治療
	CASE IS < 100
		素質 = 素質_ＳＬＧ_治療
	;努力家
	CASE IS < 175
		素質 = 素質_ＳＬＧ_努力家
	;策士
	CASE IS < 250
		素質 = 素質_ＳＬＧ_策士
	;明察
	CASE IS < 300
		素質 = 素質_ＳＬＧ_明察
	;ステゴロ
	CASE IS < 375
		素質 = 素質_ＳＬＧ_ステゴロ
	;威圧
	CASE IS < 450
		素質 = 素質_ＳＬＧ_威圧
	;下克上等
	CASE IS < 490
		素質 = 素質_ＳＬＧ_下克上等
	;魔力転換
	CASE IS < 540
		素質 = 素質_ＳＬＧ_魔力転換
	;突貫
	CASE IS < 640
		素質 = 素質_ＳＬＧ_突貫
	;バンドガール
	CASE IS < 680
		素質 = 素質_ＳＬＧ_バンドガール
	;哨戒
	CASE IS < 750
		素質 = 素質_ＳＬＧ_哨戒
	;防衛戦術
	CASE IS < 800
		素質 = 素質_ＳＬＧ_防衛戦術
	;暗殺
	CASE  IS < 840
		素質 = 素質_ＳＬＧ_暗殺
	;弁舌
	CASE IS < 880
		素質 = 素質_ＳＬＧ_弁舌
	;風説流布
	CASE IS < 920
		素質 = 素質_ＳＬＧ_風説流布
	;手練手管
	CASE IS < 960
		素質 = 素質_ＳＬＧ_手練手管
	;早足
	CASEELSE
		素質 = 素質_ＳＬＧ_早足
ENDSELECT
SETBIT TALENT:対象:ＳＬＧ素質, 素質

料金 = RAND(20000, 30000)

PRINTFORML %ANAME(MASTER)%が仕事をしていると、従者が来客を告げた
PRINTFORMW なんでも、腕に自信のある者が士官したいと言ってきたらしい
PRINTFORML 優秀な人材なら大歓迎だ……それが本当に優秀ならの話だが
PRINTFORMW ともかく、%ANAME(MASTER)%は会ってみることにしてみた……
PRINTFORML
PRINTFORM 客は名前を
CALL COLORPRINTFORM(@"%NAME:(対象)%", COLOR("注意"))
PRINTFORMW といった
PRINTFORML 軽く実力をみせてもらったが、なるほど、中々できる奴のようだ
PRINTFORMW これならば、%ANAME(MASTER)%の勢力に迎えても、十分活躍してくれるだろう……
PRINTFORM %ANAME(対象)%は、支度金として
CALL COLORPRINTFORM(@"{料金}", COLOR("注意"))
PRINTFORML を要求している
PRINTFORMW 安くない額だが、実力のある者を迎えられることには代えられないか……
DRAWLINE
PRINTFORML 現在の資金:{MONEY}
CALL ASK_MULTI_JUDGE(@"金{料金}を払って雇う", MONEY >= 料金, "断る", 1)
IF RESULT == 0
	PRINTFORMW %ANAME(対象)%を迎え入れることにした……
	CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
	MONEY -= 料金
ELSE
	PRINTFORMW 帰ってもらうことにした……
	CALL DELETE_CHARA(対象)
ENDIF
RETURN 1
