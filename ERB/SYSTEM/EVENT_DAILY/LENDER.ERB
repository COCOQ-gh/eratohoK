@LENDER
#DIM LCOUNT, 3
#DIM 融資額
#DIM 返済額
#DIM 返済期限
#DIM 担保
VARSET 融資額
VARSET 返済額
VARSET 返済期限
DRAWLINE

SIF 金貸しフラグ == -1
	RETURN -1

;返済期限が0日かつ返済額が0ならスキップ
SIF !(DVAR:金貸し期限 == 0 && DVAR:金貸し現在額 == 0) 
	RETURN -1

;借りていないパターン
IF DVAR:金貸し期限 == 0 && DVAR:金貸し現在額 == 0 && DVAR:金貸し総額 == 0
	;融資額と返済期限の計算
	;融資額は現在資金の10% + 俸給 * 期限日分
	;返済額はその2割増し
	返済期限 = RAND(5, 10)
	融資額 = MONEY / 10 + GET_SUM_ECONOMY(CFLAG:MASTER:1) * (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + 100) / 40000 * (返済期限)
	返済額 = 融資額 * 12 / 10
	PRINTFORMW %ANAME(MASTER)%が仕事をしていると、侍従が来客を告げた
	PRINTFORMW なんでも「恰幅の良い人の良さそうな中年が来た」のだそうだ
	PRINTFORMW 何の用かと訝りながら、%ANAME(MASTER)%は、会うことにしてみた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORMW 「どうも、初めまして。%ANAME(MASTER)%様ですな。お会いできて光栄ですぞ」
	PRINTFORMW 揉み手をしながら現れたのは、なるほど非常にふくよかな、いかにも人のよさげな中年男だ
	PRINTFORMW その身なりといい、決して低くない社会的立場にいることがわかる。羽振りも良さそうだ
	PRINTFORMW 「わたくし、金貸しをしておりましてな。このたびは%ANAME(MASTER)%様のお手伝いをさせていただきたく」
	PRINTFORMW つまり、資金援助の申し出だろう。といっても金貸しだ。当然、融資という形になるのだろうが……
	PRINTFORM 「そうですな、金
	CALL COLORPRINTFORM(@"{融資額}", COLOR("注意"))
	PRINTFORM の融資で、返済額は
	CALL COLORPRINTFORM(@"{返済額}", COLOR("注意"))
	PRINTFORM 、返済時期は
	CALL COLORPRINTFORM(@"{返済期限}期", COLOR("注意"))
	PRINTFORMW でいかがですかな？」
	PRINTFORMW 「%ANAME(MASTER)%様も、何かとご入り用でしょう？」
	PRINTFORMW その通りだ。徴兵に訓練、外交、何事にも金は必要だ
	PRINTFORMW 借金はあまりしたくないが、生き抜くために手段は選んでいられまい
	PRINTFORMW さて、どうしようか……
	DRAWLINE
	PRINTFORML 現在の金:{MONEY}
	CALL ASK_MULTI(@"金{融資額}を借りる", "やめておく", "うざいので斬る")
	IF RESULT == 1
		PRINTFORMW 「ふむ。必要でないとおっしゃるなら、仕方ありますまい」
		PRINTFORMW 「ではわたくしはこのへんで。また伺いますぞ」
		PRINTFORMW 男は早々に立ち去っていった……
		RETURN
	ELSEIF RESULT == 0
		PRINTFORMW 「契約成立ですな」
		PRINTFORM 「返済額は
		CALL COLORPRINTFORM(@"{返済額}", COLOR("注意"))
		PRINTFORM 、返済期限は
		CALL COLORPRINTFORM(@"{返済期限}期", COLOR("注意"))
		PRINTFORMW ですからな。お間違いないよう」
		PRINTFORMW 種々の書類に記入させたあと、男は立ち去っていった……
		PRINTFORM 金
		CALL COLORPRINTFORM(@"{融資額}", COLOR("注意"))
		PRINTFORMW を手に入れた
		MONEY += 融資額
		DVAR:金貸し期限 = 返済期限
		DVAR:金貸し現在額 = 返済額
		DVAR:金貸し総額 = 返済額
		RETURN
	ELSEIF RESULT == 2
		融資額 = RAND(100000, 300000)
		PRINTFORMW 「おやおや！　なんてことをなさるのです！　怪我をしたら困るでしょう」
		PRINTFORMW 斬りかかった%ANAME(MASTER)%だが、男は見た目から想像つかない俊敏さでかわしてみせた
		PRINTFORM 「まったく信じられない方だ。慰謝料として、金
		CALL COLORPRINTFORM(@"{融資額}", COLOR("注意"))
		PRINTFORMW を払っていただきますぞ？
		CALL LENDER_CHOOSE_GIRL
		担保 = RESULT
		DVAR:金貸しフラグ = 1
		IF MONEY >= 融資額
			PRINTFORM 男は本当に、金
			CALL COLORPRINTFORM(@"{融資額}", COLOR("注意"))
			PRINTFORMW をもっていってしまった……
			MONEY -= 融資額
		ELSEIF 担保 != -1 && !CONFIG:200
			PRINTFORMW 「……なに？　金が足りない？」
			PRINTFORMW 「では%ANAME(担保)%をうちの店で働かせていただきましょうか」
			PRINTFORMW 「なぁに、ちゃんと額ぶん働いたらお返ししますので……」
			PRINTFORMW 男はそう言って、%ANAME(担保)%を連れて行ってしまった……
			DVAR:金貸し期限 = -1
			DVAR:金貸し娘 = GET_ID(担保)
			DVAR:金貸し総額 = 融資額
			DVAR:金貸し現在額 = 融資額
		ELSE
			PRINTFORMW 「……なに？　金が足りない？」
			PRINTFORMW 「仕方ない、今回は不問ということにしてさしあげましょう」
			PRINTFORMW 「しかし、次はありませんからね！」
			PRINTFORMW 男は立ち去っていった……
			RETURN
		ENDIF
	ENDIF
ENDIF

@LENDER_CHOOSE_GIRL
#DIM 女いるフラグ
#DIM 担保
#DIM 好感度ランキング, 5
#DIM LCOUNT, 3
VARSET 女いるフラグ
VARSET 好感度ランキング, -1
担保 = -1
FOR LCOUNT:0, 0, CHARANUM
	IF LCOUNT != MASTER && CFLAG:LCOUNT:1 == CFLAG:MASTER:1 && CFLAG:LCOUNT:捕虜先 == 0 && IS_FEMALE(LCOUNT)
		女いるフラグ = 1
	ENDIF
NEXT
;女がいる場合、担保の選択開始
IF 女いるフラグ
	;担保にする女を選択
	;自国仕官の好感度高い女上位5人からランダム
	FOR LCOUNT:0, 0, CHARANUM
		IF (LCOUNT:0) != MASTER && CFLAG:(LCOUNT:0):1 == CFLAG:MASTER:1 &&  CFLAG:LCOUNT:捕虜先 == 0 && IS_FEMALE(LCOUNT:0)
			FOR LCOUNT:1, 0, 5
				IF 好感度ランキング:(LCOUNT:1) == -1 || CFLAG:(LCOUNT:0):好感度 >= CFLAG:(好感度ランキング:(LCOUNT:1)):好感度
					FOR LCOUNT:2, 3, LCOUNT:1 - 1, -1
						好感度ランキング:(LCOUNT:2 + 1) = 好感度ランキング:(LCOUNT:2)
					NEXT
					好感度ランキング:(LCOUNT:1) = LCOUNT:0
					BREAK
				ENDIF
			NEXT
		ENDIF
	NEXT
	CALL FISHER_YATES_SHAFFLE(5)
	FOR LCOUNT, 0, 5
		IF 好感度ランキング:(SHAFFLE_ARRAY:LCOUNT) != -1
			担保 = 好感度ランキング:(SHAFFLE_ARRAY:LCOUNT)
			BREAK
		ENDIF
	NEXT
ENDIF
RETURN 担保