;-------------------------------------------------
;キャラリスト表示　誰かを選べばそのキャラ番号が、「やめる」を選ぶと-1が返る

;---使い方---
;渡されたキャラについて表示/選択可にするなら1を返し、そうでないなら0を返す関数、
;SELECT_CHARA_LIST_SHOW_LOGIC_%呼び出し関数名%(キャラ番号)ならびにSELECT_CHARA_LIST_SELECT_LOGIC_%呼び出し関数名%(キャラ番号)
;を作っておく 呼び出し関数名として渡すことで、SELECT_CHARA_LISTがええ感じにしてくれる
;表示文字にはカラムとして表示したいABLを文字列で指定する
;-------------------------------------------------
@SELECT_CHARA_LIST(表示関数名 = "", 選択関数名="", 表示文字:0 = "", 表示文字:1 = "", 表示文字:2 = "", 表示文字:3 = "", 表示文字:4 = "", 表示文字:5 = "", 表示文字:6 = "")
#DIMS 表示関数名
#DIMS 選択関数名
#DIMS 表示文字, 7
#DIM 表示数値, 7
#DIM FIRST_LINE
#DIM 対象
#DIM 表示位置
#DIM 表示ページ
#DIM 総ページ数
#DIM キャラ数
#DIM キャラカウンタ
#DIM 表示開始位置
#DIM 表示終了位置
#DIM ランク種別

VARSET キャラ数
VARSET 表示数値, -1

TRYCCALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(MASTER)
CATCH
	THROW 指定された関数SELECT_CHARA_LIST_SHOW_LOGIC_%選択関数名%が存在しません
ENDCATCH

TRYCCALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%表示関数名%(MASTER)
CATCH
	THROW 指定された関数SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%が存在しません
ENDCATCH


;一行目を用意
FOR LOCAL, 0, VARSIZE("表示文字")
	FOR LOCAL:1, 0, VARSIZE("ABL")
		SIF ABLNAME:(LOCAL:1) == ""
			CONTINUE
		IF 表示文字:LOCAL == ABLNAME:(LOCAL:1)
			表示文字:LOCAL = %TOSTR_SPACE(2)%%SUBSTRINGU(ABLNAME:(LOCAL:1), 0, 1)%\@ LOCAL == VARSIZE("表示文字") - 1 ?   # | \@
			表示数値:LOCAL = LOCAL:1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:1 == VARSIZE("ABL")
		表示文字:LOCAL = %TOSTR_SPACE(5)%
NEXT

FOR LOCAL, 0, CHARANUM
	CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(LOCAL)
	SIF RESULT
		キャラ数 ++
NEXT

総ページ数 = (キャラ数 - 1) / 40 + 1
表示ページ = 1

PRINTFORM %TOSTR_SPACE(15)%
FOR LOCAL:1, 0, VARSIZE("表示文字")
	PRINTFORM %表示文字:(LOCAL:1)%
NEXT
PRINTFORM %TOSTR_SPACE(19)%
FOR LOCAL:1, 0, VARSIZE("表示文字")
	PRINTFORM %表示文字:(LOCAL:1)%
NEXT

PRINTL
FIRST_LINE = LINECOUNT
$SHOW_LOOP

VARSET 表示位置
VARSET キャラカウンタ
表示開始位置 = (表示ページ - 1) * 40
表示終了位置 = 表示ページ * 40


FOR LOCAL:0, 0, CHARANUM
	CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(LOCAL)
	SIF !RESULT
		CONTINUE
	IF 表示開始位置 <= キャラカウンタ && キャラカウンタ < 表示終了位置
		IF 表示位置 % 2 != 0
			PRINT   
		ELSEIF 表示位置 > 0
			PRINTL 
		ENDIF
		CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(LOCAL)
		IF RESULT
			PRINTFORM [{LOCAL:0 + 99, 4}]%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH/2, RIGHT%
			PRINTFORM %TOSTR_SPACE(1)%
			FOR LOCAL:1, 0, VARSIZE("表示数値")
				IF 表示数値:(LOCAL:1) != -1
					SELECTCASE 表示数値:(LOCAL:1)
						CASE GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "妖術"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱")
							ランク種別 = ランク_ＳＬＧ
						CASE GETNUM(ABL, "性知識")
							ランク種別 = ランク_性知識
						CASEELSE
							ランク種別 = ランク_その他
					ENDSELECT
					CALL PRINT_ALPHABET_RANK(ランク種別, ABL:(LOCAL:0):(表示数値:(LOCAL:1)))
					PRINTFORM {ABL:(LOCAL:0):(表示数値:(LOCAL:1)), 3}
					SIF LOCAL:1 != VARSIZE("表示数値") -1
						PRINT |
				ELSE
					PRINTFORM %TOSTR_SPACE(5)%
				ENDIF
			NEXT
			PRINTBUTTON " 情", LOCAL:0 + 10099
		ELSE
			LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH/2, RIGHT%%TOSTR_SPACE(1)%
			FOR LOCAL:1, 0, VARSIZE("表示数値")
				IF 表示数値:(LOCAL:1) != -1
					SELECTCASE 表示数値:(LOCAL:1)
						CASE GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "妖術"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱")
							ランク種別 = ランク_ＳＬＧ
						CASE GETNUM(ABL, "性知識")
							ランク種別 = ランク_性知識
						CASEELSE
							ランク種別 = ランク_その他
					ENDSELECT
					LOCALS:0 = %LOCALS:0%%ALPHABET_RANK(ランク種別, ABL:(LOCAL:0):(表示数値:(LOCAL:1)))%{ABL:(LOCAL:0):(表示数値:(LOCAL:1)), 3}
					SIF LOCAL:1 != VARSIZE("表示数値") -1
						LOCALS:0 = %LOCALS:0%|
				ELSE
					 LOCALS:0 = %LOCALS:0%%TOSTR_SPACE(5)%
				ENDIF
			NEXT
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM %LOCALS:0%
			PRINTPLAINFORM    
			RESETCOLOR
		ENDIF
		表示位置 ++
	ENDIF
	キャラカウンタ ++
NEXT

PRINTL 
DRAWLINE

IF 総ページ数 >= 2
	IF 表示ページ > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = ページ{表示ページ}/{総ページ数}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF 表示ページ < 総ページ数
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF
PRINTL [  0] やっぱりやめる

REDRAW 0


$INPUT_LOOP
INPUT
IF RESULT == 8 && 表示ページ > 1
	表示ページ --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 9 && 表示ページ < 総ページ数
	表示ページ ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	RETURN -1
ENDIF

IF RESULT >= 10099
	対象 = RESULT - 10099
	IF 対象 > CHARANUM || 対象 < 0
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(対象)
	IF !RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(対象)
	IF !RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	VARSET SHOP_CHARA_LIST, -1
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(対象)
		SIF !RESULT
			CONTINUE
		CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(対象)
		SIF !RESULT
			CONTINUE
		IF RESULT
			SHOP_CHARA_LIST:(LOCAL:1) = LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
	CALL SHOW_INFO_WITH_UI(対象, 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF


対象 = RESULT - 99
IF 対象 > CHARANUM || 対象 < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(対象)
IF !RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(対象)
IF !RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RETURN 対象

;-------------------------------------------------
;キャラリスト表示　大まかな使い方はSELECT_CHARA_LISTと同様
;人数に-1を指定すれば無制限になる
;選択したキャラはSELECTED_CHARAにぶち込まれる
;選択した人数はSELECTED_CHARA_NUMにぶち込まれる
;-------------------------------------------------
@SELECT_CHARA_LIST_MULTI(人数, 表示関数名="", 選択関数名 = "", 表示文字:0 = "", 表示文字:1 = "", 表示文字:2 = "", 表示文字:3 = "", 表示文字:4 = "", 表示文字:5 = "", 表示文字:6 = "")
#DIMS 表示関数名
#DIMS 選択関数名
#DIMS 表示文字, 7
#DIM 表示数値, 7
#DIM FIRST_LINE
#DIM 対象
#DIM 表示位置
#DIM 表示ページ
#DIM 総ページ数
#DIM キャラ数
#DIM キャラカウンタ
#DIM 選択フラグ, VARSIZE("SELECTED_CHARA")
#DIM 人数
#DIM 表示開始位置
#DIM 表示終了位置
#DIM ランク種別
VARSET 選択フラグ
VARSET キャラ数
VARSET 表示数値, -1

VARSET SELECTED_CHARA
VARSET SELECTED_CHARA_NUM

TRYCCALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(MASTER)
CATCH
	THROW 指定された関数SELECT_CHARA_LIST_SHOW_LOGIC_%選択関数名%が存在しません
ENDCATCH

TRYCCALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%表示関数名%(MASTER)
CATCH
	THROW 指定された関数SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%が存在しません
ENDCATCH


;一行目を用意
FOR LOCAL, 0, VARSIZE("表示文字")
	FOR LOCAL:1, 0, VARSIZE("ABL")
		SIF ABLNAME:(LOCAL:1) == ""
			CONTINUE
		IF 表示文字:LOCAL == ABLNAME:(LOCAL:1)
			表示文字:LOCAL = %TOSTR_SPACE(2)%%SUBSTRINGU(ABLNAME:(LOCAL:1), 0, 1)%\@ LOCAL == VARSIZE("表示文字") - 1 ?   # | \@
			表示数値:LOCAL = LOCAL:1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:1 == VARSIZE("ABL")
		表示文字:LOCAL = %TOSTR_SPACE(5)%
NEXT

FOR LOCAL, 0, CHARANUM
	CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(LOCAL)
	SIF RESULT
		キャラ数 ++
NEXT

総ページ数 = (キャラ数 - 1) / 40 + 1
表示ページ = 1

PRINTFORM %TOSTR_SPACE(15)%
FOR LOCAL:1, 0, VARSIZE("表示文字")
	PRINTFORM %表示文字:(LOCAL:1)%
NEXT
PRINTFORM %TOSTR_SPACE(20)%
FOR LOCAL:1, 0, VARSIZE("表示文字")
	PRINTFORM %表示文字:(LOCAL:1)%
NEXT

PRINTL
FIRST_LINE = LINECOUNT
$SHOW_LOOP

VARSET 表示位置
VARSET キャラカウンタ
表示開始位置 = (表示ページ - 1) * 40
表示終了位置 = 表示ページ * 40


FOR LOCAL:0, 0, CHARANUM
	CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(LOCAL)
	SIF !RESULT
		CONTINUE
	IF 表示開始位置 <= キャラカウンタ && キャラカウンタ < 表示終了位置
		IF 表示位置 % 2 != 0
			PRINT   
		ELSEIF 表示位置 > 0
			PRINTL 
		ENDIF
		CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(LOCAL)
		IF RESULT	
			IF 選択フラグ:LOCAL
				SETCOLOR COLOR("選択中")
			ELSE
				RESETCOLOR
			ENDIF
			PRINTFORM [{LOCAL:0 + 99, 4}]%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH/2, RIGHT%
			PRINTFORM %TOSTR_SPACE(1)%
			FOR LOCAL:1, 0, VARSIZE("表示数値")
				IF 表示数値:(LOCAL:1) != -1
					SELECTCASE 表示数値:(LOCAL:1)
						CASE GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "妖術"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱")
							ランク種別 = ランク_ＳＬＧ
						CASE GETNUM(ABL, "性知識")
							ランク種別 = ランク_性知識
						CASEELSE
							ランク種別 = ランク_その他
					ENDSELECT
					CALL PRINT_ALPHABET_RANK(ランク種別, ABL:(LOCAL:0):(表示数値:(LOCAL:1)))
					PRINTFORM {ABL:(LOCAL:0):(表示数値:(LOCAL:1)), 3}
					SIF LOCAL:1 != VARSIZE("表示数値") -1
						PRINT |
				ELSE
					PRINTFORM %TOSTR_SPACE(5)%
				ENDIF
			NEXT
			PRINTBUTTON " 情", LOCAL:0 + 10099
		ELSE
			LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH/2, RIGHT%%TOSTR_SPACE(1)%
			FOR LOCAL:1, 0, VARSIZE("表示数値")
				IF 表示数値:(LOCAL:1) != -1
					SELECTCASE 表示数値:(LOCAL:1)
						CASE GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "妖術"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱")
							ランク種別 = ランク_ＳＬＧ
						CASE GETNUM(ABL, "性知識")
							ランク種別 = ランク_性知識
						CASEELSE
							ランク種別 = ランク_その他
					ENDSELECT
					LOCALS:0 = %LOCALS:0%%ALPHABET_RANK(ランク種別, ABL:(LOCAL:0):(表示数値:(LOCAL:1)))%{ABL:(LOCAL:0):(表示数値:(LOCAL:1)), 3}
					SIF LOCAL:1 != VARSIZE("表示数値") -1
						LOCALS:0 = %LOCALS:0%|
				ELSE
					 LOCALS:0 = %LOCALS:0%%TOSTR_SPACE(5)%
				ENDIF
			NEXT
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM %LOCALS:0%
			PRINTPLAINFORM    
			RESETCOLOR
		ENDIF
		表示位置 ++
	ENDIF
	キャラカウンタ ++
NEXT

PRINTL 
DRAWLINE

IF 総ページ数 >= 2
	IF 表示ページ > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = ページ{表示ページ}/{総ページ数}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF 表示ページ < 総ページ数
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF
SIF 人数 != -1
	PRINTFORM {MATCH(選択フラグ, 1)} / {人数}    
PRINTL [  0] これでよし

REDRAW 0


$INPUT_LOOP
INPUT
IF RESULT == 8 && 表示ページ > 1
	表示ページ --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 9 && 表示ページ < 総ページ数
	表示ページ ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	FOR LOCAL, 0, VARSIZE("SELECTED_CHARA")
		IF 選択フラグ:LOCAL
			SELECTED_CHARA:SELECTED_CHARA_NUM = LOCAL
			SELECTED_CHARA_NUM ++
		ENDIF
	NEXT
	RETURN
ENDIF

IF RESULT >= 10099
	対象 = RESULT - 10099
	IF 対象 > CHARANUM || 対象 < 0
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(対象)
	IF !RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(対象)
	IF !RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	VARSET SHOP_CHARA_LIST, -1
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(LOCAL)
		SIF !RESULT
			CONTINUE
		CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(LOCAL)
		SIF !RESULT
			CONTINUE
		IF RESULT
			SHOP_CHARA_LIST:(LOCAL:1) = LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
	CALL SHOW_INFO_WITH_UI(対象, 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

対象 = RESULT - 99
IF 対象 > CHARANUM || 対象 < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

IF 選択フラグ:対象
	選択フラグ:対象 = 0
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

CALLFORM SELECT_CHARA_LIST_SHOW_LOGIC_%表示関数名%(対象)
IF !RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALLFORM SELECT_CHARA_LIST_SELECT_LOGIC_%選択関数名%(対象)
IF !RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
IF 選択フラグ:対象 == 0
	SIF (人数 != -1 && MATCH(選択フラグ, 1) < 人数) || 人数 == -1
	選択フラグ:対象 = 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

CLEARLINE 1
GOTO INPUT_LOOP



;-------------------------------------------------
;キャラリスト用のロジック関数
;-------------------------------------------------
@SELECT_CHARA_LIST_LOGIC(対象, 利用ロジック指定文字列)
#DIMS 利用ロジック指定文字列
#DIMS 利用ロジック, 100
#DIM 対象
;利用ロジックをリセット。渡された利用ロジック指定文字列を分割し、利用ロジックにそれぞれ格納。
VARSET 利用ロジック
SPLIT 利用ロジック指定文字列, ",", 利用ロジック

FOR LOCAL, 0, VARSIZE("利用ロジック")
	SIF 利用ロジック:LOCAL == ""
		CONTINUE
	;スペースを除去
	利用ロジック:LOCAL = %REPLACE(利用ロジック:LOCAL, " ", "")%
	利用ロジック:LOCAL = %REPLACE(利用ロジック:LOCAL, "　", "")%
	;利用できるロジック一覧　必要に応じて追加してくれていいです
	;「除外するキャラを」指定することに注意
	;「死亡」はMASTERがどこかに所属している時に「別勢力」指定で自然と除外できたり、そういうのがあるので注意

	SIF 利用ロジック:LOCAL == "男" && IS_MALE(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "女" && IS_FEMALE(対象)
		RETURN 0
	SIF 利用ロジック:LOCAL == "異性" && !IS_SAMESEX(MASTER, 対象)
		RETURN 0
		
	SIF 利用ロジック:LOCAL == "同性" && IS_SAMESEX(MASTER, 対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "育児対象" && CFLAG:対象:行動不能状態 == 4
		RETURN 0

	SIF 利用ロジック:LOCAL == "動物" && IS_ANIMAL(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "君主" && GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
		RETURN 0

	SIF 利用ロジック:LOCAL == "別勢力" && CFLAG:対象:所属 != CFLAG:MASTER:所属
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "同勢力" && CFLAG:対象:所属 == CFLAG:MASTER:所属
		RETURN 0

	SIF 利用ロジック:LOCAL == "所属なし" && !CFLAG:対象:所属
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "放浪していない" && CFLAG:対象:特殊状態 != 1
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "!IS_FREE" && !IS_FREE(対象)
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "捕虜" && CFLAG:対象:捕虜先
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "捕虜でない" && !CFLAG:対象:捕虜先
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "自国捕虜以外" && !CFLAG:対象:捕虜先
		RETURN 0

	SIF 利用ロジック:LOCAL == "!PENIS" && !PENIS(対象)
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "!VAGINA" && !VAGINA(対象)
		RETURN 0
		
	SIF 利用ロジック:LOCAL == "生存" && CFLAG:対象:特殊状態 != 2
		RETURN 0
		
	SIF 利用ロジック:LOCAL == "死亡" && CFLAG:対象:特殊状態 == 2
		RETURN 0

	SIF 利用ロジック:LOCAL == "放浪" && CFLAG:対象:特殊状態 == 1
		RETURN 0
		
	SIF 利用ロジック:LOCAL == "未登場" && ALLSAMES(0, CFLAG:対象:特殊状態, CFLAG:対象:所属)
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "特殊キャラ" && IS_SP_CHARA(対象)
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "妊娠" && TALENT:対象:妊娠
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "MASTER" && 対象 == MASTER
		RETURN 0
	
	SIF 利用ロジック:LOCAL == "淫紋" && GETBIT(TALENT:対象:デイリー系, 素質_デイリー_淫紋)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!TALK_CAN_GIVE_SEX" && !TALK_CAN_GIVE_SEX(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!IS_LOVER" && !IS_LOVER(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!IS_SLAVE" && !IS_SLAVE(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!IS_LOVER&&!IS_SLAVE" && !IS_LOVER(対象) && !IS_SLAVE(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!IS_CHILD" && !IS_CHILD(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!IS_RANDOM_CHARA" && !IS_RANDOM_CHARA(対象)
		RETURN 0

	SIF 利用ロジック:LOCAL == "!IS_CHILD&&!IS_RANDOM_CHARA" && !IS_CHILD(対象) && !IS_RANDOM_CHARA(対象)
		RETURN 0

NEXT
RETURN 1

;-------------------------------------------------
;キャラリスト表示用の汎用関数の表示ロジック
;-------------------------------------------------
@SELECT_CHARA_LIST_SHOW_LOGIC_DEFAULT(対象)
#DIM 対象
;育児対象,別勢力,放浪,脂肪,未登場,動物をはじく
SIF CFLAG:対象:行動不能状態 == 4
	RETURN 0
SIF CFLAG:対象:所属 != CFLAG:MASTER:所属
	RETURN 0
SIF GROUPMATCH(CFLAG:対象:特殊状態, 1, 2)
	RETURN 0
SIF CFLAG:対象:特殊状態 == 0 && !IS_COUNTRY(CFLAG:対象:所属)
	RETURN 0
SIF IS_ANIMAL(対象)
	RETURN 0
RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_NONE(対象)
#DIM 対象
RETURN 1

;-------------------------------------------------
;キャラリスト表示用の汎用関数の洗濯用ロジック
;-------------------------------------------------
@SELECT_CHARA_LIST_SELECT_LOGIC_DEFAULT(対象)
#DIM 対象
;フリーでない、捕虜をはじく
SIF !IS_FREE(対象) || CFLAG:対象:捕虜先
	RETURN 0
RETURN 1

@SELECT_CHARA_LIST_SELECT_LOGIC_NONE(対象)
#DIM 対象
RETURN 1

;-------------------------------------------------
;キャラリスト表示用の汎用関数 能力表示SLG版
;-------------------------------------------------
@SELECT_CHARA_LIST_SLG()
CALL SELECT_CHARA_LIST("DEFAULT", "DEFAULT", "武闘", "防衛", "知略", "政治", "妖術", "歌唱", "料理")
RETURN RESULT

@SELECT_CHARA_LIST_ONLY_LOGIC_SLG(表示関数名, 選択関数名)
#DIMS 表示関数名
#DIMS 選択関数名
CALL SELECT_CHARA_LIST(表示関数名, 選択関数名, "武闘", "防衛", "知略", "政治", "妖術", "歌唱", "料理")
RETURN RESULT

;-------------------------------------------------
;キャラリスト表示用の汎用関数 能力表示すけべ版
;-------------------------------------------------
@SELECT_CHARA_LIST_SEX()
CALL SELECT_CHARA_LIST("DEFAULT", "DEFAULT", "Ｃ感", "Ｖ感", "Ｂ感", "Ａ感", "Ｍ感", "欲望")
RETURN RESULT

@SELECT_CHARA_LIST_ONLY_LOGIC_SEX(表示関数名, 選択関数名)
#DIMS 表示関数名
#DIMS 選択関数名
CALL SELECT_CHARA_LIST(表示関数名, 選択関数名, "Ｃ感", "Ｖ感", "Ｂ感", "Ａ感", "Ｍ感", "欲望")
RETURN RESULT


;-------------------------------------------------
;キャラリスト表示用の汎用関数 マルチ版SLG
;-------------------------------------------------
@SELECT_CHARA_LIST_MULTI_SLG(人数)
#DIM 人数
CALL SELECT_CHARA_LIST_MULTI(人数, "DEFAULT", "DEFAULT", "武闘", "防衛", "知略", "政治", "妖術", "歌唱", "料理")

@SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(人数, 表示関数名, 選択関数名)
#DIM 人数
#DIMS 表示関数名
#DIMS 選択関数名
CALL SELECT_CHARA_LIST_MULTI(人数, 表示関数名, 選択関数名, "武闘", "防衛", "知略", "政治", "妖術", "歌唱", "料理")

;-------------------------------------------------
;キャラリスト表示用の汎用関数 マルチ版すけべ
;-------------------------------------------------
@SELECT_CHARA_LIST_MULTI_SEX(人数)
#DIM 人数
CALL SELECT_CHARA_LIST_MULTI(人数, "DEFAULT", "DEFAULT", "Ｃ感", "Ｖ感", "Ｂ感", "Ａ感", "Ｍ感", "欲望")

@SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SEX(人数, 表示関数名, 選択関数名)
#DIM 人数
#DIMS 表示関数名
#DIMS 選択関数名
CALL SELECT_CHARA_LIST_MULTI(人数, 表示関数名, 選択関数名, "Ｃ感", "Ｖ感", "Ｂ感", "Ａ感", "Ｍ感", "欲望")

