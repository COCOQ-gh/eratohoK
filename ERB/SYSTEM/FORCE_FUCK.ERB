;-------------------------
;性処理と同様の経験をさせる汎用関数FORCE_FUCK
;対象は対象となるキャラ、IDには膣内射精したことにしたいキャラのIDを入力
;レズフラグを立てると、IDと膣内射精フラグは無効
;-------------------------
@FORCE_FUCK(対象, ID, キス, セックス, シチュ, CFLAG減少させないフラグ = 0, 膣内射精させないフラグ = 0, レズフラグ = 0)
#DIM 対象
#DIM ID
#DIMS キス
#DIMS セックス
#DIM シチュ
#DIM CFLAG減少させないフラグ
#DIM 膣内射精させないフラグ
#DIM レズフラグ
VARSET LOCAL
IF !CFLAG減少させないフラグ
	;好感度と従属度を失っていく
	CFLAG:対象:好感度 = MAX(CFLAG:対象:好感度 - MAX(CFLAG:対象:好感度 / 10, 200) , -1000)
	CFLAG:対象:依存度 = MAX(CFLAG:対象:依存度 - MAX(CFLAG:対象:依存度 / 10, 200) , -1000)
	CFLAG:対象:従属度 = MAX(CFLAG:対象:従属度 - MAX(CFLAG:対象:従属度 / 10, 200) , -1000)
ENDIF

LOCAL:1 = 0
LOCAL:1 += MAX(ABL:対象:Ｃ感 - 1, 0) 
LOCAL:1 += MAX(ABL:対象:Ｖ感 - 1, 0)
LOCAL:1 += MAX(ABL:対象:Ａ感 - 1, 0)
LOCAL:1 += MAX(ABL:対象:Ｂ感 - 1, 0)
LOCAL:1 += MAX(ABL:対象:Ｍ感 - 1, 0) / 2
LOCAL:1 = LOCAL:1 * (ABL:対象:欲望 + 2) / 2

IF PENIS(対象)
	EXP:対象:Ｃ感経験値 += ABL:対象:Ｃ感 + 3 + RAND:2
	EXP:対象:Ａ感経験値 += ABL:対象:Ａ感 * 10 + 15 + RAND:10
ELSE
	EXP:対象:Ｃ感経験値 += ABL:対象:Ｃ感 * 4 + 5 + RAND:7
	EXP:対象:Ｖ感経験値 += ABL:対象:Ｖ感 * 10 + 10 + RAND:7
	EXP:対象:Ａ感経験値 += ABL:対象:Ａ感 * 10 + 10 + RAND:7
ENDIF

EXP:対象:Ｂ感経験値 += ABL:対象:Ｂ感 * 5 + 4 + RAND:6
EXP:対象:Ｍ感経験値 += ABL:対象:Ｍ感 * 5 + 4 + RAND:6
EXP:対象:欲望経験値 += ABL:対象:欲望 * 3 + 2 + LOCAL:1 + RAND:6
EXP:対象:奉仕経験値 += ABL:対象:奉仕 * 2 + 5 + RAND:7
EXP:対象:性交経験値 += ABL:対象:性交 * 4 + 10 + RAND:7
EXP:対象:性技経験値 += ABL:対象:性技 + 2 + RAND:4
EXP:対象:自慰経験値 += ABL:対象:自慰 * 3 + 5 + RAND:8
EXP:対象:精愛経験値 += ABL:対象:精愛 * 7 + 10 + RAND:10

SIF !RAND:3
	EXP:対象:露出経験値 += ABL:対象:露出 * 2 + 5 + RAND:8
SIF !RAND:3
	EXP:対象:排泄経験値 += ABL:対象:排泄 * 2 + 5 + RAND:8


IF IS_MALE(対象)
	EXP:対象:ＢＬ経験値 += 20 + RAND:11
ENDIF

IF IS_FEMALE(対象) && レズフラグ
	EXP:対象:レズ経験値 += 15 + RAND:10
ENDIF

IF PENIS(対象)
	EXP:対象:射精経験値 += ABL:対象:射精 * 7 + 20 + RAND:10
ENDIF

SIF VAGINA(対象) && RAND:100 < 15
	EXP:対象:Ｖ拡張経験 += RAND:3

SIF RAND:100 < 15
	EXP:対象:Ａ拡張経験 += RAND:3

EXP:対象:苦痛快楽経験 += LOCAL:1 * (30 + RAND:40) / 100
EXP:対象:緊縛経験 += 3 + RAND:4
EXP:対象:キス経験 += 2 + RAND:8
EXP:対象:口淫経験 += 3 + RAND:10
EXP:対象:絶頂経験 += LOCAL:1 * (60 + RAND:81) / 100
EXP:対象:輪姦経験 += 3 + RAND(2, MAX(3, MIN(8, ABL:対象:欲望)))
;中出しと妊娠判定
IF !レズフラグ && (CONFIG:1 == 0 && (VAGINA(対象) || TALENT:対象:孕み可能) && !膣内射精させないフラグ)
	FOR LOCAL, 440, 480
		TCVAR:対象:(LOCAL) = 0
	NEXT
	CALL RECORD_INJECTION(対象, ID, 3)
	CALL CHECK_PREGNANT(対象)
ENDIF

;能力上昇メッセ
CALL TRAIN_AUTO_ABLUP(対象, 0)

;処女とキス未経験を失う
CALL KISS_COMMON(対象, @"%キス%", シチュ)
CALL VIRGIN_COMMON(対象, @"%セックス%", シチュ)

SELECTCASE RAND:100
	CASE IS < 20
		LOCAL = 性的嗜好_愛撫される
	CASE IS < 40
		LOCAL = 性的嗜好_奉仕する
	CASE IS < 80
		LOCAL = 性的嗜好_性交される
	CASEELSE
		LOCAL = 性的嗜好_ＳＭされる
ENDSELECT
CALL GET_RANDOM_SEXUAL_PREFERENCE_EXP(対象, LOCAL, RAND(5, 9))
CALL CHECK_SEXUAL_PREFERENCE(対象, 1)


