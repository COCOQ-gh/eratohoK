;オープニング、エンディングに関する処理

;-------------------------------------------------
;オープニング
;-------------------------------------------------
@OPENING_STORY
;2週目以降用のオープニング口上
;IF FLAG:26 >= 2
;	RETURN 1
;ENDIF

IF NO:MASTER == 0
ELSE
ENDIF
RETURN 1

;-------------------------------------------------
;エンディングのチェック
;-------------------------------------------------
@ENDING_CHECK
;天下を統一した国家の番号
#DIM UNIFY

;クリア済みは除く
IF !FLAG:28
	;主人公が死亡した場合
	IF FLAG:27 || FLAG:34
		DRAWLINE

		;処刑
		IF FLAG:27
			PRINTFORMW 処刑を命じられた%ANAME(MASTER)%の首元に、ひんやりと冷たい鉄の刀が押し当てられる…
		;戦死
		ELSEIF FLAG:34
			PRINTFORMW 戦いの中で致命傷を負った%ANAME(MASTER)%は、傷口から大量の血を噴き出し地面に倒れ伏していた
			PRINTFORMW 馬のいななきや刀が触れ合う金属音、悲鳴や狂喜の入り混じった歓声が響く中、%ANAME(MASTER)%の意識は薄れていく…
		ENDIF

		PRINTL 
		PRINTW ………………
		PRINTW …………
		PRINTW ……
		PRINTL 

		;好感度が最も高いキャラとその好感度を求める
		LOCAL:1 = 0
		LOCAL:2 = -1
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):2 > LOCAL:1 && LOCAL:0 != MASTER && GROUPMATCH(CFLAG:(LOCAL:0):12, 0, 1)
				LOCAL:1 = CFLAG:(LOCAL:0):2
				LOCAL:2 = LOCAL:0
			ENDIF
		NEXT

		;好感度5000以上のキャラがいる場合
		IF LOCAL:1 >= 5000
			;口上の専用エンディングがあれば呼び出す
			RESULT = 0
			CALL KOJO_DEAD_ENDING(LOCAL:2)
			IF !RESULT
				PRINTFORMW ここに、%ANAME(MASTER)%の歴史は幕を閉じた…
				PRINTFORMW                             - 終 -
				WAIT
			ENDIF
		;好感度5000以上のキャラがいない場合
		ELSE
			PRINTFORMW ここに、%ANAME(MASTER)%の歴史は幕を閉じた…
			PRINTFORMW                             - 終 -
			WAIT
		ENDIF

		FOR LOCAL:0, 0, 60
			PRINTL 
		NEXT
		FLAG:27 = 9999
		RETURN
	ENDIF

	;天下を統一した国家の番号を取得
	UNIFY = GET_UNIFY_COUNTRY()

	;一国による統一
	IF UNIFY >= 1
		;無所属の都市の統合
		CALL UNIFY_FREE_CITY(UNIFY)

		LOCAL:3 = GET_COUNTRY_BOSS(UNIFY)

		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORMW %NAME:(LOCAL:3)%が天下を統一しました！
		RESETCOLOR
		PRINTL 
		
		IF UNIFY == GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
			CUSTOMDRAWLINE *-
			WAIT
			PRINTFORML 幻想郷に瞬く間に広がった野盗の勢力は、
			PRINTFORMW あっという間に幻想郷全土を覆い尽くした
			PRINTFORML 彼らの拠点には何人、いや何十人という少女が捕らえられ、彼らの性欲の犠牲となっている
			PRINTFORMW だが、それを止める者はいない。彼らに刃向かう者は全て、滅ぼされてしまったのだから……
			IF CFLAG:MASTER:所属 == UNIFY && IS_FEMALE(MASTER)
				PRINTFORMW 
				PRINTFORMW 幻想郷統一を祝う酒宴に、%ANAME(MASTER)%の姿もあった
				PRINTFORMW 彼女は野盗勢力がここまで広まる立役者であり、この軍において、女でありながら比較的待遇は良い
				PRINTFORMW ……女にしては、という但し書きつきであるが
				PRINTFORML 酔っ払った野盗の一人が、意味深な目配せをする
				PRINTFORML すると、%ANAME(MASTER)%は服を脱ぎ捨て、音をたてて彼の一物をしゃぶりはじめる
				PRINTFORMW その様を見て興奮した別の男が、%ANAME(MASTER)%の秘部に肉棒をねじ込むと、%ANAME(MASTER)%はたまらないといった声を上げ、腰を振る
				PRINTFORML ……野盗の中心にいるということは、一番深く彼らの思想に染まっているということでもある
				PRINTFORMW 女は男の性奴隷。%ANAME(MASTER)%の頭には、それが心底まで刻み込まれているのだ……
			ENDIF
			PRINTFORML 
			PRINTFORMW もはや救世主などない地に、ただただ女の嬌声と男の罵声が響き渡り続ける……
			PRINTL 
			ALIGNMENT CENTER
			PRINTL ――――― つづく ―――――
			ALIGNMENT LEFT
			PRINTL 
			WAIT
			FOR LOCAL:0, 0, 20
				PRINTL 
			NEXT
		ELSEIF UNIFY == GET_COUNTRY_FROM_ID(COUNTRY_GAIRAI)
			CUSTOMDRAWLINE *-
			WAIT
			PRINTFORML 外来人たちの幻想郷侵攻計画は成功した
			PRINTFORMW 彼らは科学の力で、幻想郷全土を支配下においた
			PRINTFORML 今日も彼らは少女達を使い、非道な「実験」を繰り返している
			PRINTFORML 彼らの研究所には、少女の悲鳴や嬌声が響き渡る
			PRINTFORMW だが、それを止める者はいない。彼らに刃向かう者は全て、滅ぼされてしまったのだから……
			IF CFLAG:MASTER:所属 == UNIFY && IS_FEMALE(MASTER)
				PRINTFORMW 
				PRINTFORMW 研究用奴隷たちのなかに、%ANAME(MASTER)%の姿もあった
				PRINTFORMW 研究に協力的な模範的奴隷ということで、比較的待遇は良い
				PRINTFORMW ……奴隷にしては、という但し書きつきであるが
				PRINTFORML 今日の彼女に課せられたのは、異種生物との交配実験だ
				PRINTFORMW 特殊な排卵誘発剤をあらかじめ下腹に子宮に注射されている%ANAME(MASTER)%は、妊娠の期待に下腹を疼かせている
				PRINTFORML 彼女が実験室に入るなり、大型犬が彼女を組み伏せ、犯し始める
				PRINTFORMW %ANAME(MASTER)%は、たまらないといった声をあげ、何度も放たれる精液を肉穴で受け止めていく 
				PRINTFORMW 科学者たちは、それを無感動に眺め、時折記録をとっていた……
			ENDIF
			PRINTFORML 
			PRINTFORMW もはや救世主などない地に、ただただ女の嬌声が響き渡り続ける……
			PRINTL 
			ALIGNMENT CENTER
			PRINTL ――――― つづく ―――――
			ALIGNMENT LEFT
			PRINTL 
			WAIT
			FOR LOCAL:0, 0, 20
				PRINTL 
			NEXT
		ELSEIF UNIFY == GET_COUNTRY_FROM_ID(COUNTRY_GOBLIN)
			CUSTOMDRAWLINE *-
			WAIT
			PRINTFORML ホフゴブリンたちの底力は、幻想郷を転覆させるほどのものだった
			PRINTFORML 名だたる強者たちは数の暴力の前に駆逐され、彼らに捕らえられていった
			PRINTFORMW ……そして、彼らが幻想郷の支配者となる日が、とうとう訪れた
			PRINTFORML 彼らの拠点に、少女たちの嬌声が響き渡る
			PRINTFORML 最初は抵抗していた彼女らも、ホフゴブリンの強烈な性欲による、徹底した調教の前に、いつしか屈していた
			PRINTFORMW 今や彼女らは、彼らに使える無価値な性奴隷に過ぎない。そこに、かつての面影などなかった……
			IF CFLAG:MASTER:所属 == UNIFY && IS_FEMALE(MASTER)
				PRINTFORMW 
				PRINTFORML ……奴隷どものなかに、%ANAME(MASTER)%の姿もあった
				PRINTFORMW ホフゴブリンどもに取り囲まれながら、懸命に腰を振っている
				PRINTFORML 膣穴がなかなかいい具合だと評判の彼女は、奴隷どもの中でも比較的ましな扱いを受けていた
				PRINTFORMW ……奴隷にしては、という但し書きつきであるが
				PRINTFORML 二穴に、そして口に肉棒を咥える彼女の頭の中には、種を付けられること、そして妊娠することへの期待しかなかった
				PRINTFORMW 子宮を小突かれ絶頂する%ANAME(MASTER)%をよそに、ホフゴブリンは猛然と腰を振りたくって行く
				PRINTFORMW %ANAME(MASTER)%は、たまらないといった声をあげ、何度も放たれる精液を肉穴で受け止めていく
				PRINTFORMW 終わることのない悦びに、%ANAME(MASTER)%はこれからも浸り続けるのだ……
			ENDIF
			PRINTFORML 
			PRINTFORMW もはや救世主などない地に、ただただ女の嬌声が響き渡り続ける……
			PRINTL 
			ALIGNMENT CENTER
			PRINTL ――――― つづく ―――――
			ALIGNMENT LEFT
			PRINTL 
			WAIT
			FOR LOCAL:0, 0, 20
				PRINTL 
			NEXT
		;外交禁止フラグが立っている国家が統一した場合、エンディングメッセージを表示させない
		ELSEIF !COUNTRY_IS_CLOSED:UNIFY
			CUSTOMDRAWLINE *-
			WAIT
			PRINTFORML 幻想郷を焼き尽くさんばかりに燃え広がった戦乱の炎は、
			PRINTFORMW 一人の英雄の手によって打ち払われた。
			IF NO:(LOCAL:3) == 0
				PRINTFORMW その英雄の名は――……
				PRINTW 
			ELSE
				PRINTFORMW その英雄の名は%NAME_FORMAL(LOCAL:3)%。
			ENDIF
			PRINTFORMW %TOSTR_THIRD(LOCAL:3)%の偉業は、長く後世に語り継がれることとなるだろう。
			PRINTFORMW ここに天下は定まり、幻想郷にしばしの平穏の時代が訪れたのであった……
			CUSTOMDRAWLINE *-
			PRINTL 
			PRINTW ……………………
			PRINTL 

			;主人公の所属する国家が統一した場合
			IF UNIFY == CFLAG:MASTER:1
				;好感度が最も高いキャラとその好感度を求める
				LOCAL:1 = 0
				LOCAL:2 = -1
				FOR LOCAL:0, 0, CHARANUM
					IF CFLAG:(LOCAL:0):2 > LOCAL:1 && LOCAL:0 != MASTER && CFLAG:(LOCAL:0):12 == 0 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1
						LOCAL:1 = CFLAG:(LOCAL:0):2
						LOCAL:2 = LOCAL:0
					ENDIF
				NEXT

				;好感度5000以上のキャラがいる場合
				IF LOCAL:1 >= 5000
					;口上の専用エンディングがあれば呼び出す
					RESULT = 0
					CALL KOJO_SINGLE_ENDING(LOCAL:2)
					IF !RESULT
						PRINTFORMW ひっそりと祝賀の宴から抜け出した%CALLNAME:MASTER%と%ANAME(LOCAL:2)%は、共に星空を見上げていた。
						PRINTFORMW ここに来るまで、二人で様々な苦難を乗り越えてきたが、今思えばあっという間だったような気もする。
						PRINTFORMW 一つの時代が、一つの物語が、今、終わりを告げたのだ。
						PRINTFORMW 不意に、%ANAME(LOCAL:2)%が%CALLNAME:MASTER%の手を、ぎゅっと、握った。
						PRINTFORMW その手を%CALLNAME:MASTER%も握り返す。
						PRINTFORMW 決して離れぬように、決して離さぬように……
						IF TALENT:MASTER:3 || TALENT:(LOCAL:2):3
							PRINTFORMW 優しい月の光に照らされながら――
							PRINTFORMW 二人はお互いの確かな存在を感じ合っていた。
						ELSE
							PRINTFORMW 二人は、お互いの存在を確かめ合うように、抱きしめ合い――
							PRINTFORMW そっと唇を重ねた。
							;キスの共通処理
							CALL KISS_COMMON(MASTER, @"%ANAME(LOCAL:2)%の唇")
							CALL KISS_COMMON(LOCAL:2, @"%ANAME(MASTER)%の唇")
							EXP:MASTER:キス経験 += 1
							EXP:(LOCAL:2):キス経験 += 1
						ENDIF
					ENDIF
				;好感度5000以上のキャラがいない場合
				ELSE
					PRINTFORMW ひっそりと祝賀の宴から抜け出した%CALLNAME:MASTER%は、一人天を見上げていた。
					PRINTFORMW 天下が定まったこと、それ自体は喜ばしいことに違いない。
					PRINTFORMW しかし、ずっと戦乱の中で居場所を見つけてきた%CALLNAME:MASTER%は、心の何処かで虚しさを感じていた。
					PRINTFORMW これからの平穏な世の中に、自分のいるべき場所はあるのだろうか……？
					PRINTL 
					;好感度50以上のキャラがいる場合
					IF LOCAL:1 >= 50
						PRINTFORMW 誰かが%CALLNAME:MASTER%を呼ぶ声がする。
						PRINTFORMW どうやら%ANAME(LOCAL:2)%が%CALLNAME:MASTER%を探しに来たようだ。
						PRINTFORMW 新しい居場所……そんなものはこれから探していけばいいのだろう。
						PRINTFORMW 戦いは終わった。時間はいくらでもある。
						PRINTFORMW %CALLNAME:MASTER%は%ANAME(LOCAL:2)%に返事をすると、酔い潰れる覚悟で宴の席へ戻っていった。
					ELSE
						PRINTFORMW 宴はまだ続いているようだ。
						PRINTFORMW ……こんな目出度い日に、辛気臭いことを考えても仕方ない。
						PRINTFORMW %CALLNAME:MASTER%は酔い潰れる覚悟を決めると、宴の席へ戻っていった。
					ENDIF
				ENDIF
				PRINTL 
				ALIGNMENT CENTER
				PRINTL ――――― つづく ―――――
				ALIGNMENT LEFT
				PRINTL 
				WAIT
				FOR LOCAL:0, 0, 20
					PRINTL 
				NEXT
			ENDIF
		ENDIF

		;部隊を解散
		CALL CLEAR_ALL_UNIT(UNIFY)
		;守将を解除
		VARSET CITY_COMMANDER, 0

		;野盗の捕虜を解放
		IF CFLAG:(MASTER):1 != GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
			FOR LOCAL:0, 0, CHARANUM
				IF CFLAG:(LOCAL:0):9 == GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
					CFLAG:(LOCAL:0):9 = 0
				ENDIF
			NEXT
		ENDIF

		;残党の一斉検挙
		CALL CAPTURE_WANDERER(UNIFY)

		;野盗死すべし、慈悲はない
		SIF GET_COUNTRY_FROM_ID(COUNTRY_BANDIT) != -1 && UNIFY != GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
			CALL DESTROY_COUNTRY(GET_COUNTRY_FROM_ID(COUNTRY_BANDIT))

		;クリアフラグを立てる
		FLAG:28 = 1
	ENDIF

	;国家関係マップの作成
	CALL TMP_CREATE_RELATION_MAP

	;分割統治エンド
	IF CFLAG:MASTER:1 >= 1 && IS_NOENEMY(CFLAG:MASTER:1) && !FLAG:28
		;好感度が最も高いキャラとその好感度を求める
		LOCAL:1 = 0
		LOCAL:2 = -1
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):2 > LOCAL:1 && LOCAL:0 != MASTER && CFLAG:(LOCAL:0):12 == 0
				LOCAL:1 = CFLAG:(LOCAL:0):2
				LOCAL:2 = LOCAL:0
			ENDIF
		NEXT

		;野盗死すべし、ハイクを詠め、慈悲はない
		SIF GET_COUNTRY_FROM_ID(COUNTRY_BANDIT) != -1 && CFLAG:MASTER:1 != GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
			CALL DESTROY_COUNTRY(GET_COUNTRY_FROM_ID(COUNTRY_BANDIT))

		LOCAL:3 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
		PRINTL 
		SETCOLOR COLOR("注意")
		PRINTFORMW もはや争うべき勢力はこの幻想郷に存在しません
		PRINTFORMW 我が勢力は戦乱の時代を生き延びました！
		RESETCOLOR
		PRINTL 
		CUSTOMDRAWLINE *-
		WAIT
		PRINTFORML 幻想郷を焼き尽くさんばかりに燃え広がった戦乱の炎は、
		PRINTFORMW やがて君主同士が手を取り合い共存することで収まった。
		FOR LOCAL:0, 1, MAX_COUNTRY
			IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:1
				PRINTFORMW %NAME_FORMAL(GET_COUNTRY_BOSS(LOCAL:0))%。
			ENDIF
		NEXT
		IF NO:(LOCAL:3) == 0
			PRINTFORMW そしてもう一人――……
			PRINTW 
		ELSE
			PRINTFORMW そして%NAME_FORMAL(LOCAL:3)%。
		ENDIF
		PRINTFORMW 力が支配する弱肉強食の世となった幻想郷。
		PRINTFORMW そんな世にあっては類を見ない各勢力の理性的な対応は、後の世代に大きな影響を与えることになるだろう。
		PRINTFORMW ここに戦乱は終焉を迎え、幻想郷にしばしの平穏の時代が訪れたのであった……
		CUSTOMDRAWLINE *-
		PRINTL 
		PRINTW ……………………
		PRINTL 
		;好感度5000以上のキャラがいる場合
		IF LOCAL:1 >= 5000
			;口上の専用エンディングがあれば呼び出す
			RESULT = 0
			CALL KOJO_SINGLE_ENDING(LOCAL:2)
			IF !RESULT
				PRINTFORMW ひっそりと祝賀の宴から抜け出した%CALLNAME:MASTER%と%ANAME(LOCAL:2)%は、共に星空を見上げていた。
				PRINTFORMW ここに来るまで、二人で様々な苦難を乗り越えてきたが、今思えばあっという間だったような気もする。
				PRINTFORMW 一つの時代が、一つの物語が、今、終わりを告げたのだ。
				PRINTFORMW 不意に、%ANAME(LOCAL:2)%が%CALLNAME:MASTER%の手を、ぎゅっと、握った。
				PRINTFORMW その手を%CALLNAME:MASTER%も握り返す。
				PRINTFORMW 決して離れぬように、決して離さぬように……
				IF TALENT:MASTER:3 || TALENT:(LOCAL:2):3
					PRINTFORMW 優しい月の光に照らされながら――
					PRINTFORMW 二人はお互いの確かな存在を感じ合っていた。
				ELSE
					PRINTFORMW 二人は、お互いの存在を確かめ合うように、抱きしめ合い――
					PRINTFORMW そっと唇を重ねた。
					;キスの共通処理
					CALL KISS_COMMON(MASTER, @"%ANAME(LOCAL:2)%の唇")
					CALL KISS_COMMON(LOCAL:2, @"%ANAME(MASTER)%の唇")
					EXP:MASTER:キス経験 += 1
					EXP:(LOCAL:2):キス経験 += 1
				ENDIF
			ENDIF
		;好感度5000以上のキャラがいない場合
		ELSE
			PRINTFORMW ひっそりと祝賀の宴から抜け出した%CALLNAME:MASTER%は、一人天を見上げていた。
			PRINTFORMW 天下が定まったこと、それ自体は喜ばしいことに違いない。
			PRINTFORMW しかし、ずっと戦乱の中で居場所を見つけてきた%CALLNAME:MASTER%は、心の何処かで虚しさを感じていた。
			PRINTFORMW これからの平穏な世の中に、自分のいるべき場所はあるのだろうか……？
			PRINTL 
			;好感度50以上のキャラがいる場合
			IF LOCAL:1 >= 50
				PRINTFORMW 誰かが%CALLNAME:MASTER%を呼ぶ声がする。
				PRINTFORMW どうやら%ANAME(LOCAL:2)%が%CALLNAME:MASTER%を探しに来たようだ。
				PRINTFORMW 新しい居場所……そんなものはこれから探していけばいいのだろう。
				PRINTFORMW 戦いは終わった。時間はいくらでもある。
				PRINTFORMW %CALLNAME:MASTER%は%ANAME(LOCAL:2)%に返事をすると、酔い潰れる覚悟で宴の席へ戻っていった。
			ELSE
				PRINTFORMW 宴はまだ続いているようだ。
				PRINTFORMW ……こんな目出度い日に、辛気臭いことを考えても仕方ない。
				PRINTFORMW %CALLNAME:MASTER%は酔い潰れる覚悟を決めると、宴の席へ戻っていった。
			ENDIF
		ENDIF
		PRINTL 
		ALIGNMENT CENTER
		PRINTL ――――― つづく ―――――
		ALIGNMENT LEFT
		PRINTL 
		WAIT

		FOR LOCAL:0, 0, 20
			PRINTL 
		NEXT
		FOR LOCAL:0, 1, MAX_COUNTRY
			IF IS_COUNTRY(LOCAL:0)
				;部隊を解散
				CALL CLEAR_ALL_UNIT(LOCAL:0)
			ENDIF
		NEXT
		;守将を解除
		VARSET CITY_COMMANDER, 0

		;野盗の捕虜を解放
		IF CFLAG:(MASTER):1 != GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
			FOR LOCAL:0, 0, CHARANUM
				IF CFLAG:(LOCAL:0):9 == GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
					CFLAG:(LOCAL:0):9 = 0
				ENDIF
			NEXT
		ENDIF
		
		;残党の一斉検挙
		CALL CAPTURE_WANDERER(CFLAG:MASTER:1)

		;クリアフラグを立てる
		FLAG:28 = 1
	ENDIF
ENDIF

;-------------------------------------------------
;天下を統一した勢力があればその勢力番号を返す関数
;-------------------------------------------------
@GET_UNIFY_COUNTRY(ARG:0)
#FUNCTION
LOCAL:2 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "無名"
		IF CITY_OWNER:(LOCAL:0) != 0
			IF LOCAL:2 == 0
				LOCAL:2 = CITY_OWNER:(LOCAL:0)
			ELSEIF CITY_OWNER:(LOCAL:0) != LOCAL:2
				RETURNF 0
			ENDIF
		ENDIF
	ENDIF
NEXT
RETURNF LOCAL:2

;-------------------------------------------------
;ARG:0勢力が同盟エンドの条件を満たしたかどうかを判定する関数
;※使用前に TMP_CREATE_RELATION_MAP 関数を呼び出しておくこと！
;-------------------------------------------------
@IS_NOENEMY(ARG:0)
#FUNCTION

;全ての都市が無所属もしくは自勢力・永久同盟勢力の所有なら1を返す
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "無名"
		IF CITY_OWNER:(LOCAL:0) != 0 && TMP_COUNTRY_RELATION:(CFLAG:MASTER:1):(CITY_OWNER:(LOCAL:0)) < 4
			RETURNF 0
		ENDIF
	ENDIF
NEXT
RETURNF 1

;-------------------------------------------------
;ARG:0勢力による統一時に呼び出す。もし無所属の都市が存在すれば統一した国家に統合し、メッセージを表示する
;-------------------------------------------------
@UNIFY_FREE_CITY(ARG:0)
LOCAL:2 = 1
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "無名" && CITY_OWNER:(LOCAL:0) == 0
		;統一したのが主人公勢力ならメッセージを表示
		IF ARG:0 == CFLAG:MASTER:1
			IF LOCAL:2
				LOCAL:2 = 0
				PRINTL 
				SETCOLOR COLOR("注意")
				PRINTFORMW もはや大勢が決したため、無所属の都市は全て我が国に降伏しました
				RESETCOLOR
			ENDIF
			PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
		ENDIF
		CITY_OWNER:(LOCAL:0) = ARG:0
	ENDIF
NEXT
IF !LOCAL:2
	WAIT
ENDIF

;-------------------------------------------------
;統一後に行う残党の一斉検挙 ARG:0に併合先の国家番号を設定
;-------------------------------------------------
@CAPTURE_WANDERER(ARG:0)
;主人公勢力が検挙する場合メッセージと選択肢を表示
IF ARG:0 == CFLAG:MASTER:1
	LOCAL:9 = 0
	FOR LOCAL:0, 0, CHARANUM
		;放浪中の士官がいるか調べる
		IF CFLAG:(LOCAL:0):12 == 1
			IF !LOCAL:9
				LOCAL:9 = 1
				SETCOLOR COLOR("注意")
				PRINTW 残党および放浪中の士官を一斉に検挙しました
				RESETCOLOR
			ENDIF
			PRINTFORML %NAME:(LOCAL:0)%を捕らえました
		ENDIF
	NEXT
	IF LOCAL:9
		PRINTL 
		PRINTL 検挙した人間の処遇を決めて下さい
		PRINTL [0] 全員を登用
		PRINTL [1] 全員を投獄
		PRINTL [2] 個別に指示

		REDRAW 0

		$INPUT_LOOP_KENKYO
		INPUT

		IF RESULT == 0
			PRINTW 全員を我が国の士官として迎えました
			FOR LOCAL:0, 0, CHARANUM
				IF CFLAG:(LOCAL:0):12 == 1
					CFLAG:(LOCAL:0):1 = CFLAG:MASTER:1
					CFLAG:(LOCAL:0):12 = 0
					CFLAG:(LOCAL:0):13 = 0
					CFLAG:(LOCAL:0):14 = 0
				ENDIF
			NEXT
		ELSEIF RESULT == 1
			PRINTW 全員を捕虜として投獄しました
			FOR LOCAL:0, 0, CHARANUM
				IF CFLAG:(LOCAL:0):12 == 1
					CFLAG:(LOCAL:0):1 = 0
					CFLAG:(LOCAL:0):9 = CFLAG:MASTER:1
					CFLAG:(LOCAL:0):12 = 0
					CFLAG:(LOCAL:0):13 = 0
					CFLAG:(LOCAL:0):14 = 0
				ENDIF
			NEXT
		ELSEIF RESULT == 2
			CALL CAPTURE_WANDERER_EACH
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP_KENKYO
		ENDIF

		REDRAW 1
	ENDIF

;外来勢力が検挙する場合は全員を捕虜にする(外来勢力のIDは299)
ELSEIF COUNTRY_EVENT_ID:(ARG:0) == COUNTRY_GAIRAI || COUNTRY_EVENT_ID:(ARG:0) == 300
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):12 == 1
			CFLAG:(LOCAL:0):9 = ARG:0
			CFLAG:(LOCAL:0):12 = 0
			CFLAG:(LOCAL:0):13 = 0
			CFLAG:(LOCAL:0):14 = 0
		ENDIF
	NEXT

;主人公勢力以外が検挙するなら自動で全員を登用
ELSE
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):12 == 1
			CFLAG:(LOCAL:0):1 = ARG:0
			CFLAG:(LOCAL:0):12 = 0
			CFLAG:(LOCAL:0):13 = 0
			CFLAG:(LOCAL:0):14 = 0
		ENDIF
	NEXT
ENDIF

;-------------------------------------------------
;統一後の残党検挙で個別に扱いを設定
;-------------------------------------------------
@CAPTURE_WANDERER_EACH
;キャラの扱い設定用変数
#DIM CHARA_TREATMENT, 1000

VARSET CHARA_TREATMENT, 0

;●放浪士官の人数をカウント
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):12 == 1
		LOCAL:5 ++
	ENDIF
NEXT

;放浪士官が一人もいなければ戻る
IF LOCAL:5 == 0
	RETURN
ENDIF

;●表示部分
LOCAL:10 = (LOCAL:5 - 1) / 22 + 1
LOCAL:11 = 1

$SHOW_LOOP

DRAWLINE
PRINTFORML 各士官の処遇を決めて下さい
DRAWLINE

LOCAL:7 = 0
LOCAL:8 = 0
LOCAL:12 = (LOCAL:11 - 1) * 22
LOCAL:13 = LOCAL:11 * 22
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):12 == 1
		IF LOCAL:8 >= LOCAL:12 && LOCAL:8 < LOCAL:13
			PRINTFORM %ANAME(LOCAL:0), 10, RIGHT% …… 
			IF CHARA_TREATMENT:(LOCAL:0) == 0
				SETCOLOR COLOR("選択中")
				PRINTPLAIN [登用]
				RESETCOLOR
				PRINTBUTTON "[投獄]", LOCAL:0 * 2 + 101
			ELSE
				PRINTBUTTON "[登用]", LOCAL:0 * 2 + 100
				SETCOLOR COLOR("選択中")
				PRINTPLAIN [投獄]
				RESETCOLOR
			ENDIF
			PRINTL 
			LOCAL:7 ++
		ENDIF
		LOCAL:8 ++
	ENDIF
NEXT
DRAWLINE

IF LOCAL:10 >= 2
	LOCAL:7 += 2

	LOCALS:0 = 
	IF LOCAL:11 >= 2
		LOCALS:0 = [8] 前のページ
	ENDIF
	PRINTFORM %LOCALS:0, 25, LEFT%

	LOCALS:0 = page{LOCAL:11}/{LOCAL:10}
	PRINTPLAINFORM %LOCALS:0, 25, LEFT%

	LOCALS:0 = 
	IF LOCAL:11 < LOCAL:10
		LOCALS:0 = [9] 次のページ
	ENDIF
	PRINTFORM %LOCALS:0, 25, LEFT%
	PRINTL 
	DRAWLINE
ENDIF
PRINT [1] 全員を登用に設定
PRINTPLAIN     
PRINT [2] 全員を投獄に設定
PRINTL 
DRAWLINE
PRINTL [0] 決定

REDRAW 2

$INPUT_LOOP
INPUT

IF RESULT == 0
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):12 == 1
			CFLAG:(LOCAL:0):12 = 0
			CFLAG:(LOCAL:0):13 = 0
			CFLAG:(LOCAL:0):14 = 0
			IF CHARA_TREATMENT:(LOCAL:0) == 0
				CFLAG:(LOCAL:0):1 = CFLAG:MASTER:1
			ELSE
				CFLAG:(LOCAL:0):1 = 0
				CFLAG:(LOCAL:0):9 = CFLAG:MASTER:1
			ENDIF
		ENDIF
	NEXT
	REDRAW 1
	RETURN
ELSEIF RESULT == 1
	VARSET CHARA_TREATMENT, 0
ELSEIF RESULT == 2
	VARSET CHARA_TREATMENT, 1
ELSEIF RESULT == 8 && LOCAL:11 >= 2
	LOCAL:11 --
ELSEIF RESULT == 9 && LOCAL:11 < LOCAL:10
	LOCAL:11 ++
ELSEIF RESULT >= 100
	LOCAL:5 = (RESULT - 100) / 2
	LOCAL:6 = (RESULT - 100) % 2
	IF LOCAL:5 < CHARANUM && CFLAG:(LOCAL:5):12 == 1
		IF CHARA_TREATMENT:(LOCAL:5) != LOCAL:6
			CHARA_TREATMENT:(LOCAL:5) = LOCAL:6
		ENDIF
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CLEARLINE LOCAL:7 + 8
GOTO SHOW_LOOP

;-------------------------------------------------
;統一後の周回処理
;-------------------------------------------------
@GO_NEXT_LOOP
#DIM LCOUNT, 2

VARSET LOCAL, 0

DRAWLINE
PRINTL 今の状況を引き継いで新たに幻想郷に嵐を巻き起こすことができます
PRINTL 引き継ぎの設定をしてください
DRAWLINE

$SHOW_LOOP_SET
PRINTL ●固定キャラの引き継ぎ設定(主人公が『あなた』以外なら、この選択が反映されます）
PRINTL ※<借り物>のように、引き継がれない素質もあります※
CALL PRINT_TOGGLE_BUTTON("[体力など]", 100, GETBIT(LOCAL:20, 0))
CALL PRINT_TOGGLE_BUTTON("[好感度と陥落]", 101, GETBIT(LOCAL:20, 1))
CALL PRINT_TOGGLE_BUTTON("[ウフフ能力等]", 102, GETBIT(LOCAL:20, 2))
CALL PRINT_TOGGLE_BUTTON("[SLG能力等]", 103, GETBIT(LOCAL:20, 3))
CALL PRINT_TOGGLE_BUTTON("[刺青、ピアス、陰毛]", 104, GETBIT(LOCAL:20, 4))
CALL PRINT_TOGGLE_BUTTON("[性癖と嗜好]", 105, GETBIT(LOCAL:20, 5))
CALL PRINT_TOGGLE_BUTTON("[締まり]", 106, GETBIT(LOCAL:20, 6))

PRINTL 
PRINTL 
PRINTL ●資金・アイテム
CALL PRINT_SELECT_BUTTON("[引き継ぐ]", 110, LOCAL:21 == 0)
CALL PRINT_SELECT_BUTTON("[初期化]", 111, LOCAL:21 == 1)
PRINTL 

IF CSTR:MASTER:99 != "あなた"
PRINTL
PRINTL ●主人公の能力
CALL PRINT_TOGGLE_BUTTON("[体力など]", 120, GETBIT(LOCAL:22, 0))
CALL PRINT_TOGGLE_BUTTON("[好感度と陥落]", 121, GETBIT(LOCAL:22, 1))
CALL PRINT_TOGGLE_BUTTON("[ウフフ能力等]", 122, GETBIT(LOCAL:22, 2))
CALL PRINT_TOGGLE_BUTTON("[SLG能力等]", 123, GETBIT(LOCAL:22, 3))
CALL PRINT_TOGGLE_BUTTON("[刺青、ピアス、陰毛]", 124, GETBIT(LOCAL:22, 4))
CALL PRINT_TOGGLE_BUTTON("[性癖と嗜好]", 125, GETBIT(LOCAL:22, 5))
CALL PRINT_TOGGLE_BUTTON("[締まり]", 126, GETBIT(LOCAL:22, 6))
PRINTL
ENDIF

DRAWLINE
PRINTL [0] この設定で次の周回へ
PRINTL [1] 戻る

REDRAW 0

$INPUT_LOOP_SET
INPUT

IF RESULT == 1
	REDRAW 1
	RETURN 0
ELSEIF GROUPMATCH(RESULT, 100, 101, 102, 103, 104, 105, 106)
	INVERTBIT LOCAL:20, RESULT - 100
	CLEARLINE 10
	GOTO SHOW_LOOP_SET
ELSEIF RESULT >= 110 && RESULT <= 111 && LOCAL:21 != RESULT - 110
	LOCAL:21 = RESULT - 110
	CLEARLINE 10
	GOTO SHOW_LOOP_SET
ELSEIF CSTR:MASTER:99 != "あなた" && GROUPMATCH(RESULT, 120, 121, 122, 123, 124, 125, 126)
	INVERTBIT LOCAL:22,  RESULT - 120
	CLEARLINE 10
	GOTO SHOW_LOOP_SET
ELSEIF RESULT != 0
	CLEARLINE 10
	GOTO INPUT_LOOP_SET
ENDIF

REDRAW 1

;設定に基づいた初期化処理
FOR LOCAL, 1, CHARANUM
	CALL PASS_CHARA(LOCAL, LOCAL:20)
NEXT

;MASTERが「あなた」でないパターン
IF CSTR:MASTER:99 != "あなた"
	CALL PASS_CHARA(MASTER, LOCAL:22)
ENDIF


;CSVでは設定できないキャラデータの初期設定
CALL ADDITIONAL_CHARA_SETTING

;アイテムの初期化
IF LOCAL:21 == 1
	MONEY = 0
	GAMBLE_MONEY = 0
	VARSET ITEMSALES, 1, 0, 200
	VARSET ITEMSALES, 0, 200, 300
	VARSET ITEM, 0
ENDIF


ITEMSALES:太平要術の書 = 1
ITEM:太平要術の書 = 0

FOR LOCAL:0, 0, CHARANUM
	CFLAG:(LOCAL:0):1 = 0
	CFLAG:(LOCAL:0):9 = 0
	CFLAG:(LOCAL:0):10 = 0
	CFLAG:(LOCAL:0):11 = 0
	CFLAG:(LOCAL:0):12 = 0
	CFLAG:(LOCAL:0):13 = 0
	CFLAG:(LOCAL:0):14 = 0
	CFLAG:(LOCAL:0):29 = 0
	CFLAG:(LOCAL:0):42 = 0
	CFLAG:(LOCAL:0):59 = 0
	SIF !TALENT:(LOCAL:0):薬物依存
		CFLAG:(LOCAL:0):68 = 0
	SIF !TALENT:(LOCAL:0):ＮＴＲ
		CFLAG:(LOCAL:0):75 = 0
NEXT

;借り物を削除
FOR LOCAL:0, 0, CHARANUM
	TALENT:(LOCAL:0):借り物 = 0
NEXT

;キャラの情報を一部リセット
FOR LOCAL:0, 0, CHARANUM
	TALENT:(LOCAL:0):虚ろ = 0
	TALENT:(LOCAL:0):崩壊 = 0
	PALAM:(LOCAL:0):怒主 = 0
	PALAM:(LOCAL:0):怒外 = 0
	PALAM:(LOCAL:0):哀主 = 0
	PALAM:(LOCAL:0):哀外 = 0
	PALAM:(LOCAL:0):怖主 = 0
	PALAM:(LOCAL:0):怖外 = 0
	CFLAG:(LOCAL:0):崩壊 = 0
	BASE:(LOCAL:0):精神力 = MAXBASE:(LOCAL:0):精神力
NEXT

;キャラ間の好感度・敵対度のクリア
FOR LOCAL:0, 0, CHARANUM
	FOR LOCAL:1, 0, 1000
		REL_LIKE:(LOCAL:0):(LOCAL:1) = 0
		REL_HATE:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT

;イベントキャラの削除
FOR LOCAL:0, CHARANUM - 1, -1, -1
	IF NO:(LOCAL:0) >= 2000
		CALL DELETE_CHARA(LOCAL:0)
	ENDIF
NEXT

;ランダムキャラの人数をカウント
LOCAL:2 = 0
FOR LOCAL:0, 0, CHARANUM
	IF NO:(LOCAL:0) >= 201 && NO:(LOCAL:0) < 2000  && LOCAL:0 != MASTER
		LOCAL:2 ++
	ENDIF
NEXT

;ランダムキャラが存在する場合
IF LOCAL:2 >= 1
	;ページ数の計算
	LOCAL:3 = (LOCAL:2 - 1) / 66 + 1
	LOCAL:4 = 1
	VARSET LOCAL, -1, 20, 30

	DRAWLINE
	PRINTFORML ランダムキャラおよび子供を5人まで引き継ぐことができます
	PRINTFORML 引き継ぐキャラを選択して下さい
	DRAWLINE

	$SHOW_LOOP_RCHARA

	LOCAL:5 = 0
	LOCAL:6 = (LOCAL:4 - 1) * 66
	LOCAL:7 = LOCAL:4 * 66
	LOCAL:8 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF NO:(LOCAL:0) >= 201 && NO:(LOCAL:0) < 2000 && LOCAL:0 != MASTER
			IF LOCAL:5 >= LOCAL:6 && LOCAL:5 < LOCAL:7
				IF LOCAL:8 % 5 == 0 && LOCAL:8 >= 1
					PRINTL 
				ENDIF
				FOR LOCAL:1, 20, 30
					IF LOCAL:(LOCAL:1) == LOCAL:0
						SETCOLOR COLOR("選択中")
						BREAK
					ENDIF
				NEXT
				LOCALS:0 = \{%ANAME(LOCAL:0), 12, LEFT%\}
				PRINTBUTTON @"%LOCALS:0, 16, LEFT%", NO:(LOCAL:0) + 100
				RESETCOLOR
				LOCAL:8 ++
			ENDIF
			LOCAL:5 ++
		ENDIF
	NEXT
	PRINTL 
	DRAWLINE

	;消去する行数を計算
	LOCAL:15 = (LOCAL:8 - 1) / 5 + 4

	IF LOCAL:3 >= 2
		LOCAL:15 += 2

		IF LOCAL:4 >= 2
			PRINT [  8] 前のページ            
		ELSE
			PRINT                             
		ENDIF
		PRINTPLAINFORM page {LOCAL:4}/{LOCAL:3}                    
		IF LOCAL:4 < LOCAL:3
			PRINT [  9] 次のページ
		ENDIF
		PRINTL 
		DRAWLINE
	ENDIF

	PRINTFORML [  0] 決定

	REDRAW 0

	$INPUT_LOOP_RCHARA
	INPUT

	IF RESULT == 8 && LOCAL:4 >= 2
		LOCAL:4 --
		CLEARLINE LOCAL:15
		GOTO SHOW_LOOP_RCHARA
	ELSEIF RESULT == 9 && LOCAL:4 < LOCAL:3
		LOCAL:4 ++
		CLEARLINE LOCAL:15
		GOTO SHOW_LOOP_RCHARA
	ELSEIF RESULT != 0
		LOCAL:10 = NO_TO_CHARA(RESULT - 100)

		IF LOCAL:10 >= 0 && NO:(LOCAL:10) >= 201 && NO:(LOCAL:10) < 2000 && LOCAL:10 != MASTER
			;選択されたキャラなら選択解除
			FOR LOCAL:0, 20, 25
				IF LOCAL:(LOCAL:0) == LOCAL:10
					LOCAL:(LOCAL:0) = -1
					CLEARLINE LOCAL:15
					GOTO SHOW_LOOP_RCHARA
				ENDIF
			NEXT

			;選択されていないキャラなら選択
			FOR LOCAL:0, 20, 25
				IF LOCAL:(LOCAL:0) < 0
					LOCAL:(LOCAL:0) = LOCAL:10
					CLEARLINE LOCAL:15
					GOTO SHOW_LOOP_RCHARA
				ENDIF
			NEXT

			PRINTFORMW これ以上選択できません
			CLEARLINE 2
			GOTO INPUT_LOOP_RCHARA
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP_RCHARA
		ENDIF
	ENDIF

	REDRAW 1

	;選択キャラのリストをIDに変更、選択されたキャラの数をカウント
	LOCAL:10 = 0
	FOR LOCAL:0, 20, 30
		IF LOCAL:(LOCAL:0) >= 0
			LOCAL:(LOCAL:0) = GET_ID(LOCAL:(LOCAL:0))
			LOCAL:10 ++
		ENDIF
	NEXT

	;選択されていないランダムキャラの削除
	LOCAL:11 = LOCAL:10 - 1
	LOCAL:12 = CHARANUM
	FOR LOCAL:0, 0, LOCAL:12
		;前から削除していくとキャラ番号が順次変わってしまうため後ろから走査
		LOCAL:5 = LOCAL:12 - LOCAL:0 - 1
		IF NO:(LOCAL:5) >= 201 && LOCAL:5 != MASTER
			LOCAL:6 = 1
			FOR LOCAL:1, 20, 30
				IF GET_ID(LOCAL:5) == LOCAL:(LOCAL:1)
					LOCAL:6 = 0
					BREAK
				ENDIF
			NEXT
			IF LOCAL:6
				CALL DELETE_CHARA(LOCAL:5)
			ENDIF
		ENDIF
	NEXT
ENDIF

;出産後のキャラがいる場合、状態をリセット
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):25 >= 1
		;出産後の各種初期化処理
		CALL RESET_MOTHER_STATE(LOCAL:0, 0)
	ENDIF
NEXT

LOCAL:10 = 0
FOR LOCAL:0, 0, CHARANUM
	IF TALENT:(LOCAL:0):研究用奴隷の烙印 || TALENT:(LOCAL:0):ゴブリンの性奴隷 || TALENT:(LOCAL:0):薬売りの奴隷 || TALENT:(LOCAL:0):野盗の肉便器
		LOCAL:10 = 1
	ENDIF
NEXT
IF LOCAL:10
	PRINTFORML <研究用奴隷の烙印><ゴブリンの性奴隷><薬売りの奴隷><野盗の肉便器><貴族の虜>を引き継ぎますか？(様々な面で不利になります)
	CALL ASK_YN("引き継ぐ", "消去")
	IF RESULT == 1
		FOR LOCAL:0, 0, CHARANUM
			TALENT:(LOCAL:0):研究用奴隷の烙印 = 0
			TALENT:(LOCAL:0):ゴブリンの性奴隷 = 0
			TALENT:(LOCAL:0):薬売りの奴隷 = 0
			TALENT:(LOCAL:0):野盗の肉便器 = 0
			TALENT:(LOCAL:0):貴族の虜 = 0
		NEXT
	ENDIF
ENDIF

CALL NEWLOOP(1)

RETURN 1



@MASTER_RESET
VARSET LOCAL
LOADGLOBAL
DRAWLINE
PRINTL ★★主人公を選択して下さい★★
DRAWLINE
PRINTBUTTON "  0[新規作成    ]", 0
LOCAL:1 = 0
FOR LOCAL:0, 0, 30
	;体力が正なら登録キャラが存在すると判定
	IF MASTER_MAXBASE:(LOCAL:0):0 > 0
		IF LOCAL:1 % 4 == 0  && LOCAL:1 != 0
			PRINTL 
			PRINT    
		ENDIF
		LOCALS:0 = {LOCAL:0 + 10, 3}[%MASTER_NAME:(LOCAL:0):0, 12, LEFT%]
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", LOCAL:0 + 10
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL 
PRINTBUTTON "  99[やめる    ]", 99

$INPUT_LOOP
INPUT

;新規作成
IF RESULT == 0
	;空いている登録キャラ番号の取得
	LOCAL:5 = -1
	FOR LOCAL:0, 0, 30
		IF MASTER_MAXBASE:(LOCAL:0):0 <= 0
			LOCAL:5 = LOCAL:0
			BREAK
		ENDIF
	NEXT
	IF LOCAL:5 >= 0
		CALL CREATE_MASTER(LOCAL:5)
	ELSE
		PRINTFORML 登録キャラの数が最大です
		PRINTFORMW 先に不要な登録キャラを削除して下さい
	ENDIF
	RESTART

;登録キャラを選択
ELSEIF RESULT >= 10 && RESULT <= 40 && MASTER_MAXBASE:(RESULT - 10):0 > 0
	LOCAL:8 = RESULT - 10
	CALL APPLY_MASTER_DATA(MASTER, LOCAL:8)
	LOCAL:6 = MASTER
ELSEIF RESULT == 99
	RETURN
ELSE
GOTO INPUT_LOOP
ENDIF

;一時的にコンフィグの外見詳細表示をONにする
LOCAL:0 = CONFIG:6
CONFIG:6 = 1

DRAWLINE
CALL SHOW_INFO(MASTER)
DRAWLINE

CONFIG:6 = LOCAL:0

PRINTBUTTON " 0[このキャラを主人公にして開始]", 0
IF LOCAL:8 >= 0
	PRINT    
	PRINTBUTTON " 1[修正]", 1
	PRINT    
	PRINTBUTTON " 2[削除]", 2
ENDIF
PRINTL 
PRINTBUTTON " 9[戻る]", 9
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 9
	IF LOCAL:7
		;マスターを元に戻す
		SWAPCHARA MASTER, LOCAL:6
		CALL NEWGAME_SWAP_RELATIONS(LOCAL:6)
	ELSE
		;能力設定済みフラグをＯＮにする
		FLAG:5 = 1
	ENDIF
	RESTART
ELSEIF RESULT == 1 && LOCAL:8 >= 0
	CALL CREATE_MASTER(LOCAL:8, 1)
	SAVEGLOBAL
	RESTART
ELSEIF RESULT == 2 && LOCAL:8 >= 0
	PRINTL 本当に削除してもよろしいですか？
	CALL ASK_YN()
	IF RESULT == 0
		CALL DELETE_MASTER(LOCAL:8)
		SAVEGLOBAL
	ENDIF
	RESTART
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP2


;-------------------------------
;キャラの引き継ぎ処理を行う関数
;-------------------------------
@PASS_CHARA(対象, ビット)
#DIM 対象
#DIM ビット
#DIM ABL保存, 100
#DIM TALENT保存, 350
#DIM EXP保存, 100
#DIM COM_EXP保存, VARSIZE("COM_EXP")
#DIM BASE保存, VARSIZE("BASE")
#DIMS TATOO保存, VARSIZE("TATOO")
#DIMS キス保存
#DIMS 童貞保存
#DIMS 処女保存

;念のため
VARSET BASE保存
VARSET ABL保存
VARSET TALENT保存
VARSET EXP保存
VARSET TATOO保存
VARSET COM_EXP保存
VARSET キス保存
VARSET 童貞保存
VARSET 処女保存

;BASE
IF GETBIT(ビット, 0)
	FOR LOCAL, 0, 3
		BASE保存:LOCAL = MAXBASE:対象:LOCAL
	NEXT
ENDIF

;陥落素質、好感度
IF GETBIT(ビット, 1)
	LOCAL:1 = CFLAG:対象:好感度
	LOCAL:2 = CFLAG:対象:依存度
	LOCAL:3 = CFLAG:対象:従属度
	LOCAL:4 = MARK:対象:既成事実
	FOR LOCAL, 150, 161
		SIF GROUPMATCH(LOCAL, GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "正妻"))
			TALENT保存:LOCAL = TALENT:対象:LOCAL
	NEXT
ENDIF

;ウフフ能力、素質、経験引き継ぎ
IF GETBIT(ビット, 2)
	キス保存 = %FIRSTKISS_PARTNER:対象:0%
	童貞保存 = %DOUTEI_PARTNER:対象:0%
	処女保存 = %VIRGIN_PARTNER:対象:0%
	FOR LOCAL, 0, 195
		SIF TALENTNAME:LOCAL != "" && !(GROUPMATCH(LOCAL, GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "正妻")))
			TALENT保存:LOCAL = TALENT:対象:LOCAL
	NEXT
	FOR LOCAL, 280, 300

	NEXT
	FOR LOCAL, 40, 69
		SIF EXPNAME:LOCAL != ""
			EXP保存:LOCAL = EXP:対象:LOCAL
	NEXT
	FOR LOCAL, 0, 40
		SIF ABLNAME:LOCAL != ""
			ABL保存:LOCAL = ABL:対象:LOCAL
	NEXT
	
	FOR LOCAL, 90, 93
		SIF ABLNAME:LOCAL != ""
			ABL保存:LOCAL = ABL:対象:LOCAL
	NEXT
	TALENT保存:(GETNUM(TALENT, "妊娠")) = 0
	TALENT保存:(GETNUM(TALENT, "育児中")) = 0
ENDIF
;SLG能力、素質
IF GETBIT(ビット, 3)
	FOR LOCAL, 50, 62
		SIF ABLNAME:LOCAL != ""
			ABL保存:LOCAL = ABL:対象:LOCAL
	NEXT
	FOR LOCAL, 200, 250
		SIF TALENTNAME:LOCAL != ""
			TALENT保存:LOCAL = TALENT:(対象):LOCAL
	NEXT
ENDIF

;陰毛、ピアス、タトゥ
IF GETBIT(ビット, 4)
	TALENT保存:GETNUM(TALENT, "ピアス") = TALENT:対象:ピアス
	TALENT保存:GETNUM(TALENT, "ピアス穴") = TALENT:対象:ピアス穴
	TALENT保存:GETNUM(TALENT, "陰毛現在値") = TALENT:対象:陰毛現在値
	TALENT保存:GETNUM(TALENT, "陰毛目標値") = TALENT:対象:陰毛目標値
	FOR LOCAL, 0, VARSIZE("TATOO")
		TATOO保存:LOCAL = %TATOO:対象:LOCAL%
	NEXT
ENDIF

;性的嗜好
IF GETBIT(ビット, 5)
	FOR LOCAL, 0, VARSIZE("COM_EXP")
		COM_EXP保存:LOCAL = COM_EXP:対象:LOCAL
	NEXT
	TALENT保存:GETNUM(TALENT, "性的嗜好") = TALENT:対象:性的嗜好
	TALENT保存:GETNUM(TALENT, "性的嗜好ロック") = TALENT:対象:性的嗜好ロック
ENDIF



;締まり
IF GETBIT(ビット, 6)
	TALENT保存:GETNUM(TALENT, "Ｖ締まり") = TALENT:対象:Ｖ締まり
	TALENT保存:GETNUM(TALENT, "Ａ締まり") = TALENT:対象:Ａ締まり
ENDIF

;初期化
CALL INITIALIZE_CHARA(対象)

;BASE
IF GETBIT(ビット, 0)
	FOR LOCAL, 0, 3
		MAXBASE:対象:LOCAL = BASE保存:LOCAL
	NEXT
ENDIF

;陥落素質復元
IF GETBIT(ビット, 1)
	CFLAG:(対象):好感度 = LOCAL:1
	CFLAG:(対象):依存度 = LOCAL:2
	CFLAG:(対象):従属度 = LOCAL:3
	MARK:対象:既成事実  = LOCAL:4
	FOR LOCAL, 150, 161
		SIF GROUPMATCH(LOCAL, GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "正妻"))
			TALENT:対象:LOCAL = TALENT保存:LOCAL
	NEXT
ENDIF

;ウフフ復元
IF GETBIT(ビット, 2)
	FIRSTKISS_PARTNER:対象:0 = %キス保存%
	DOUTEI_PARTNER:対象:0 = %童貞保存%
	VIRGIN_PARTNER:対象:0 = %処女保存%
	FOR LOCAL, 0, 195
		SIF TALENTNAME:LOCAL != "" && !(GROUPMATCH(LOCAL, GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "正妻")))
			TALENT:対象:LOCAL = TALENT保存:LOCAL
	NEXT
	FOR LOCAL, 280, 300
		SIF TALENTNAME:LOCAL != ""
			TALENT:対象:LOCAL = TALENT保存:LOCAL
	NEXT
	FOR LOCAL, 40, 69
		SIF EXPNAME:LOCAL != ""
			EXP:対象:LOCAL = EXP保存:LOCAL
	NEXT
	FOR LOCAL, 0, 40
		SIF ABLNAME:LOCAL != ""
			ABL:対象:LOCAL = ABL保存:LOCAL
	NEXT
	FOR LOCAL, 90, 93
		SIF ABLNAME:LOCAL != ""
			ABL:対象:LOCAL = ABL保存:LOCAL
	NEXT
ENDIF

;SLG復元
IF GETBIT(ビット, 3)
	FOR LOCAL, 50, 62
		SIF ABLNAME:LOCAL != ""
			ABL:対象:LOCAL = ABL保存:LOCAL
	NEXT
	FOR LOCAL, 200, 250
		SIF TALENTNAME:LOCAL != ""
			TALENT:対象:LOCAL = TALENT保存:LOCAL
	NEXT
ENDIF

;陰毛、ピアス、タトゥ復元
IF GETBIT(ビット, 4)
	TALENT:対象:ピアス = TALENT保存:GETNUM(TALENT, "ピアス")
	TALENT:対象:ピアス穴 = TALENT保存:GETNUM(TALENT, "ピアス穴")
	TALENT:対象:陰毛現在値 = TALENT保存:GETNUM(TALENT, "陰毛現在値")
	TALENT:対象:陰毛目標値 = TALENT保存:GETNUM(TALENT, "陰毛目標値")
	
	FOR LOCAL, 0, VARSIZE("TATOO")
		TATOO:対象:LOCAL = %TATOO保存:LOCAL%
	NEXT
ENDIF

;性的嗜好
IF GETBIT(ビット, 5)
	FOR LOCAL, 0, VARSIZE("COM_EXP")
		COM_EXP:対象:LOCAL = COM_EXP保存:LOCAL
	NEXT
	TALENT:対象:性的嗜好 = TALENT保存:GETNUM(TALENT, "性的嗜好")
	TALENT:対象:性的嗜好ロック = TALENT保存:GETNUM(TALENT, "性的嗜好ロック")
ENDIF

;締まり
IF GETBIT(LOCAL:20, 6)
	TALENT:対象:Ｖ締まり = TALENT保存:GETNUM(TALENT, "Ｖ締まり")
	TALENT:対象:Ａ締まり = TALENT保存:GETNUM(TALENT, "Ａ締まり")
ENDIF
