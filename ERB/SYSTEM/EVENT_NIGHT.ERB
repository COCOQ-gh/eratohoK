;夜這い関係のイベント

;-------------------------------------------------
;夜イベントの最低条件チェック
;-------------------------------------------------
@IS_EVENT_NIGHT(ARG:0)
#FUNCTION
;主人公は禁止
IF ARG:0 == MASTER
	RETURNF 0
ENDIF

;好感度か従属度が800以上、かつ依存度が100以上必要
IF !((CFLAG:(ARG:0):2 >= 800 || CFLAG:(ARG:0):4 >= 800) && CFLAG:(ARG:0):3 >= 100)
	RETURNF 0
ENDIF

;無所属または主人公と所属勢力が異なれば禁止
IF CFLAG:(ARG:0):1 == 0 || CFLAG:(ARG:0):1 != CFLAG:MASTER:1
	RETURNF 0
ENDIF

;主人公または対象が捕虜なら禁止
IF CFLAG:MASTER:捕虜先 != 0 || CFLAG:(ARG:0):捕虜先 != 0
	RETURNF 0
ENDIF

;行動不能フラグが立っていれば禁止
IF CFLAG:(ARG:0):行動不能状態
	RETURNF 0
ENDIF



;虚ろ・崩壊なら禁止
IF TALENT:(ARG:0):虚ろ || TALENT:(ARG:0):崩壊
	RETURNF 0
ENDIF

;触手妊娠中は禁止
IF TALENT:(ARG:0):触手妊娠 || TALENT:(ARG:0):Ａ触手妊娠
	RETURNF 0
ENDIF

;金貸しに貸し出しているなら禁止
IF ID_TO_CHARA(DVAR:金貸し娘) == ARG:0
	RETURNF 0
ENDIF

;動物なら禁止
IF IS_ANIMAL(ARG:0)
	RETURNF 0
ENDIF

RETURNF 1

;-------------------------------------------------
;夜に起こるイベント
;-------------------------------------------------
@EVENT_NIGHT
;コンフィグで抑制されているなら戻る
IF CONFIG:0 == 1
	RETURN
ENDIF

;●対象キャラの選択
LOCAL:2 = 0
LOCAL:4 = -1

FOR LOCAL:0, 0, CHARANUM
	;夜イベントの発生条件を満たしているか
	IF IS_EVENT_NIGHT(LOCAL:0)
		;満たしているならLOCAL:2に好感度と従属度の大きい方を加算
		LOCAL:2 += MAX(CFLAG:(LOCAL:0):2, CFLAG:(LOCAL:0):4)
	ENDIF
NEXT

IF LOCAL:2 > 0
	;好感度or従属度に比例した確率で対象キャラをランダムセレクト
	LOCAL:3 = RAND:(LOCAL:2 + 2000)

	FOR LOCAL:0, 0, CHARANUM
		;夜イベントの発生条件を満たしているか
		IF IS_EVENT_NIGHT(LOCAL:0)
			LOCAL:3 -= MAX(CFLAG:(LOCAL:0):2, CFLAG:(LOCAL:0):4)
			IF LOCAL:3 < 0
				;LOCAL:4に対象キャラの番号が代入される
				LOCAL:4 = LOCAL:0
				BREAK
			ENDIF
		ENDIF
	NEXT
ENDIF

;対象キャラが選択されていなければ戻る
IF LOCAL:4 < 0
	RETURN
ENDIF

;●イベントの選択
LOCAL:5 = 0

;[合意]あり ＆ 対象の性知識Lv2以上 ＆ 主人公の特殊状態フラグが立っていない
IF TALENT:(LOCAL:4):合意 && ABL:(LOCAL:4):性知識 >= 2 && !CFLAG:MASTER:行動不能状態
	RESULT = 1
	IF RAND:100 < MIN(ABL:(LOCAL:4):欲望 * 5 + 25, 75)
		;夜這い性交
		CALL EVENT_NIGHT_SEX(LOCAL:4)
		IF RESULT
			LOCAL:5 = 1
		ENDIF
	ENDIF
	IF !RESULT
		IF RAND:100 < MIN(ABL:(LOCAL:4):欲望 * 4 + 50, 70)
			IF RAND:1001 - 500 < ABL:(LOCAL:4):主導度Ｕ
				IF ABL:(LOCAL:4):奉仕 >= 1
					;夜這い奉仕
					CALL EVENT_NIGHT_SERVE(LOCAL:4)
					LOCAL:5 = 1
				ENDIF
			ELSE
				IF EXP:(LOCAL:4):絶頂経験 >= 5
					;夜這い愛撫
					CALL EVENT_NIGHT_TOUCH(LOCAL:4)
					LOCAL:5 = 1
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF !LOCAL:5
	;主人公が臨月
	IF CFLAG:MASTER:行動不能状態 == 1
		CALL EVENT_NIGHT_M_PREGNANT(LOCAL:4)
	;主人公が育児中
	ELSEIF CFLAG:MASTER:行動不能状態 == 2
		CALL EVENT_NIGHT_M_REARING(LOCAL:4)
	;主人公が怪我
	ELSEIF CFLAG:MASTER:行動不能状態 == 3
		CALL EVENT_NIGHT_M_WOUND(LOCAL:4)

	ELSEIF (TALENT:(LOCAL:4):幼稚 || TALENT:(LOCAL:4):幼児 || TALENT:(LOCAL:4):素直) && RAND:2 == 0
		;添い寝
		CALL EVENT_NIGHT_SLEEP(LOCAL:4)
	ELSEIF !TALENT:(LOCAL:4):幼稚 && !TALENT:(LOCAL:4):幼児 && !TALENT:MASTER:幼児 && RAND:100 <= 30 + (TALENT:(LOCAL:4):酒豪 != 0) * 40
		;晩酌
		CALL EVENT_NIGHT_DRINK(LOCAL:4)
	ELSE
		;談笑
		CALL EVENT_NIGHT_TALK(LOCAL:4)
	ENDIF
ENDIF

;-------------------------------------------------
;夜に起こるイベント(主人公が臨月のときのお見舞い)
;-------------------------------------------------
@EVENT_NIGHT_M_PREGNANT(ARG:0)
DRAWLINE
PRINTFORMW %ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;確定父親の場合
IF CFLAG:MASTER:子の父親 == GET_ID(ARG:0)
	PRINTFORMW %ANAME(ARG:0)%は出産を間近に控えた%ANAME(MASTER)%の様子を見に来たようだ

	;口上の表示
	CALL KOJO_EVENT(ARG:0, 230)

	PRINTFORML <%ANAME(ARG:0)%>
	CFLAG:(ARG:0):2 += 100
	CFLAG:(ARG:0):3 += 50
	PRINTFORML 好感度＋100({CFLAG:(ARG:0):2})
	PRINTFORML 依存度＋50({CFLAG:(ARG:0):3})
;それ以外
ELSE
	PRINTFORMW %ANAME(ARG:0)%は%ANAME(MASTER)%の体調を気遣ってお見舞いに来たようだ

	;口上の表示
	CALL KOJO_EVENT(ARG:0, 231)

	PRINTFORML <%ANAME(ARG:0)%>
	CFLAG:(ARG:0):2 += 50
	CFLAG:(ARG:0):3 += 10
	PRINTFORML 好感度＋50({CFLAG:(ARG:0):2})
	PRINTFORML 依存度＋10({CFLAG:(ARG:0):3})
ENDIF

;-------------------------------------------------
;夜に起こるイベント(主人公が育児中のときのお見舞い)
;-------------------------------------------------
@EVENT_NIGHT_M_REARING(ARG:0)
DRAWLINE
PRINTFORMW %ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;子供のキャラ番号を取得
LOCAL:0 = ID_TO_CHARA(CFLAG:MASTER:育児対象)

;父親のキャラ番号を取得
LOCAL:1 = ID_TO_CHARA(CFLAG:(LOCAL:0):24)

;確定父親の場合
IF LOCAL:1 == ARG:0
	PRINTFORMW %ANAME(ARG:0)%は仕事の合間を縫って%ANAME(LOCAL:0)%の様子を見に来たようだ

	;口上の表示
	CALL KOJO_EVENT(ARG:0, 232)

	PRINTFORML <%ANAME(ARG:0)%>
	CFLAG:(ARG:0):2 += 100
	CFLAG:(ARG:0):3 += 50
	PRINTFORML 好感度＋100({CFLAG:(ARG:0):2})
	PRINTFORML 依存度＋50({CFLAG:(ARG:0):3})
;それ以外
ELSE
	PRINTFORMW %ANAME(ARG:0)%は%ANAME(LOCAL:0)%の世話を手伝いに来てくれたようだ

	;口上の表示
	CALL KOJO_EVENT(ARG:0, 233)

	PRINTFORML <%ANAME(ARG:0)%>
	CFLAG:(ARG:0):2 += 50
	CFLAG:(ARG:0):3 += 10
	PRINTFORML 好感度＋50({CFLAG:(ARG:0):2})
	PRINTFORML 依存度＋10({CFLAG:(ARG:0):3})
ENDIF

;-------------------------------------------------
;夜に起こるイベント(主人公が重傷のときのお見舞い)
;-------------------------------------------------
@EVENT_NIGHT_M_WOUND(ARG:0)
DRAWLINE
PRINTFORMW %ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;口上の表示
CALL KOJO_EVENT(ARG:0, 234)

PRINTFORML <%ANAME(ARG:0)%>
CFLAG:(ARG:0):2 += 50
CFLAG:(ARG:0):3 += 10
PRINTFORML 好感度＋50({CFLAG:(ARG:0):2})
PRINTFORML 依存度＋10({CFLAG:(ARG:0):3})

PRINTL 
PRINTFORMW %ANAME(ARG:0)%は%ANAME(MASTER)%に大事にするように言ってから帰っていきました

;-------------------------------------------------
;夜に起こるイベント(添い寝)
;-------------------------------------------------
@EVENT_NIGHT_SLEEP(ARG:0)
DRAWLINE
PRINTFORMW 夜、%ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;口上の表示
CALL KOJO_EVENT(ARG:0, 200)

PRINTFORML <%ANAME(ARG:0)%>
CFLAG:(ARG:0):2 += 50
CFLAG:(ARG:0):3 += 20
PRINTFORML 好感度＋50({CFLAG:(ARG:0):2})
PRINTFORML 依存度＋20({CFLAG:(ARG:0):3})

PRINTL 
PRINTFORMW %ANAME(MASTER)%は%ANAME(ARG:0)%と一緒に寝ることにしました

;-------------------------------------------------
;夜に起こるイベント(晩酌)
;-------------------------------------------------
@EVENT_NIGHT_DRINK(ARG:0)
DRAWLINE
PRINTFORMW 夜、%ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;口上の表示
CALL KOJO_EVENT(ARG:0, 201)

PRINTFORML <%ANAME(ARG:0)%>
CFLAG:(ARG:0):2 += 60
CFLAG:(ARG:0):3 += 10
PRINTFORML 好感度＋60({CFLAG:(ARG:0):2})
PRINTFORML 依存度＋10({CFLAG:(ARG:0):3})

PRINTL 
PRINTFORMW %ANAME(MASTER)%は%ANAME(ARG:0)%と夜遅くまで酒を飲み交わしました

;-------------------------------------------------
;夜に起こるイベント(談笑)
;-------------------------------------------------
@EVENT_NIGHT_TALK(ARG:0)
DRAWLINE
PRINTFORMW 夜、%ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;口上の表示
CALL KOJO_EVENT(ARG:0, 202)

PRINTFORML <%ANAME(ARG:0)%>
CFLAG:(ARG:0):2 += 50
CFLAG:(ARG:0):3 += 10
PRINTFORML 好感度＋50({CFLAG:(ARG:0):2})
PRINTFORML 依存度＋10({CFLAG:(ARG:0):3})

PRINTL 
PRINTFORML %ANAME(MASTER)%は%ANAME(ARG:0)%と楽しく語り合いました

;-------------------------------------------------
;夜に起こるイベント(夜這い奉仕)
;-------------------------------------------------
@EVENT_NIGHT_SERVE(ARG:0)
VARSET LOCAL, 0

;●取得経験の基本値を計算
LOCAL:20 += GET_EVENT_ABL_POWER(ABL:(ARG:0):欲望) + 10
LOCAL:22 += GET_EVENT_ABL_POWER(ABL:(ARG:0):性技) * 3
LOCAL:20 += GET_EVENT_ABL_POWER(ABL:(ARG:0):奉仕)
LOCAL:21 = LOCAL:21 * LOCAL:20 / 150
LOCAL:22 = LOCAL:22 * LOCAL:20 / 1000
LOCAL:23 = LOCAL:21 / 10
LOCAL:25 = (LOCAL:20 - 10) / 8
LOCAL:20 = LOCAL:20 / 10
LOCAL:30 = (GET_EVENT_ABL_POWER(ABL:MASTER:Ｃ感) + 5) * LOCAL:22 / 20
LOCAL:31 = (GET_EVENT_ABL_POWER(ABL:MASTER:Ｖ感) + 5) * LOCAL:22 / 20
LOCAL:32 = (GET_EVENT_ABL_POWER(ABL:MASTER:Ａ感) + 5) * LOCAL:22 / 20
LOCAL:33 = (GET_EVENT_ABL_POWER(ABL:MASTER:Ｂ感) + 5) * LOCAL:22 / 20

;●共通メッセージ
DRAWLINE
PRINTFORMW 夜、%ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;●加虐
IF ABL:(ARG:0):倒錯度 >= 500 || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 211)
	CALL FUCK(MASTER, "欲望, Ｃ, Ｖ, Ａ, Ｂ, 射精")
	CALL FUCK(ARG:0, "欲望, 性技, 奉仕, 精愛")
	PRINTFORMW %ANAME(MASTER)%は夜が更けるまで%ANAME(ARG:0)%に徹底的に虐められました

;●通常
ELSE
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 210)
	CALL FUCK(MASTER, "欲望, Ｃ, Ｂ, 射精")
	CALL FUCK(ARG:0, "欲望, 性技, 奉仕, 精愛")
	IF PENIS(MASTER)
		PRINTFORMW %ANAME(MASTER)%は夜が更けるまで%ANAME(ARG:0)%に搾り取られました
	ELSE
		PRINTFORMW %ANAME(MASTER)%は夜が更けるまで%ANAME(ARG:0)%に可愛がられました
	ENDIF
ENDIF

;-------------------------------------------------
;夜に起こるイベント(夜這い愛撫)
;-------------------------------------------------
@EVENT_NIGHT_TOUCH(ARG:0)
VARSET LOCAL, 0

;●取得経験の基本値を計算
LOCAL:30 += GET_EVENT_ABL_POWER(ABL:(ARG:0):Ｃ感)
LOCAL:31 += GET_EVENT_ABL_POWER(ABL:(ARG:0):Ｖ感)
LOCAL:32 += GET_EVENT_ABL_POWER(ABL:(ARG:0):Ａ感)
LOCAL:33 += GET_EVENT_ABL_POWER(ABL:(ARG:0):Ｂ感)
LOCAL:20 += GET_EVENT_ABL_POWER(ABL:(ARG:0):欲望) + 10
LOCAL:21 += (LOCAL:30 + LOCAL:33) * LOCAL:20 / 300
LOCAL:22 += (LOCAL:30 + LOCAL:31 + LOCAL:32 + LOCAL:33) * LOCAL:20 / 300
LOCAL:25 = (LOCAL:20 - 10) / 8
LOCAL:30 = (LOCAL:30 + 5) * LOCAL:20 / 100
LOCAL:31 = (LOCAL:31 + 5) * LOCAL:20 / 100
LOCAL:32 = (LOCAL:32 + 5) * LOCAL:20 / 100
LOCAL:33 = (LOCAL:33 + 5) * LOCAL:20 / 100
LOCAL:20 = LOCAL:20 / 10

;●共通メッセージ
DRAWLINE
PRINTFORMW 夜、%ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;●被虐
IF ABL:(ARG:0):倒錯度 >= 500 || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ)
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 213)
	CALL FUCK(MASTER, "欲望, 性技, 奉仕")
	CALL FUCK(ARG:0, "欲望, Ｃ, Ｖ, Ａ, Ｂ, 射精, 苦痛快楽, 緊縛")


	PRINTFORMW %ANAME(MASTER)%は夜が更けるまで%ANAME(ARG:0)%を虐め倒しました

;●通常
ELSE
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 212)
	CALL FUCK(MASTER, "欲望, 性技, 奉仕")
	CALL FUCK(ARG:0, "欲望, Ｃ, Ｖ, Ｂ, 射精")
	PRINTFORMW %ANAME(MASTER)%は夜が更けるまで%ANAME(ARG:0)%の体を可愛がりました
ENDIF

;-------------------------------------------------
;夜に起こるイベント(夜這い性交) ※実行できた場合は1を、実行条件を満たさなかった場合0を返す
;-------------------------------------------------
@EVENT_NIGHT_SEX(ARG:0)
VARSET LOCAL, 0

;●イベント内容の分岐
;性別と経験に応じて可能なものからランダムに選択
IF (PENIS(MASTER) && !TALENT:MASTER:童貞) || ITEM:ペニスバンド
	IF VAGINA(ARG:0) && !TALENT:(ARG:0):処女
		LOCAL:10 = EXP:(ARG:0):♀♂性交回数
	ENDIF
	LOCAL:11 = EXP:(ARG:0):Ａ♂性交回数
ENDIF
IF (PENIS(ARG:0) && !TALENT:(ARG:0):童貞) || ITEM:ペニスバンド
	IF VAGINA(MASTER) && !TALENT:MASTER:処女
		LOCAL:12 = EXP:(ARG:0):♂♀性交回数
	ENDIF
	LOCAL:13 = EXP:(ARG:0):♂Ａ性交回数
ENDIF
IF !PENIS(MASTER) && !PENIS(ARG:0)
	LOCAL:14 = ABL:(ARG:0):レズ * ABL:(ARG:0):レズ * 5
ENDIF

LOCAL:15 = LOCAL:10 + LOCAL:11 + LOCAL:12 + LOCAL:13 + LOCAL:14 - 10
IF LOCAL:15 <= 0
	;選択可能な内容がなければ戻る
	RETURN 0
ENDIF

LOCAL:6 = RAND:(LOCAL:15)
FOR LOCAL:0, 0, 5
	LOCAL:6 -= LOCAL:(LOCAL:0 + 10) - 1
	IF LOCAL:6 < 0
		LOCAL:5 = LOCAL:0
		BREAK
	ENDIF
NEXT

;●取得経験の基本値を計算
LOCAL:20 = GET_EVENT_ABL_POWER(ABL:(ARG:0):欲望) + 10
LOCAL:22 = GET_EVENT_ABL_POWER(ABL:(ARG:0):性技) * 3 / 2

;●共通メッセージ
DRAWLINE
PRINTFORMW 夜、%ANAME(ARG:0)%が%ANAME(MASTER)%の元を訪ねてきた

;●イベント内容別の処理
;主人公のＰ→対象のＶ
IF LOCAL:5 == 0
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 220)
	CALL FUCK(MASTER, "欲望, 性技, 射精, Ｃ")
	CALL FUCK(ARG:0, "欲望, Ｖ, 性技, 奉仕, 性交, \@ PENIS(MASTER) ? 精愛 # \@", "\@ CONFIG:2 == 0 ? 膣内射精 # \@", GET_ID(MASTER))
	PRINTL 
	IF PENIS(MASTER)
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで何度も%ANAME(ARG:0)%と交わりました
	ELSE
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、張り型で何度も%ANAME(ARG:0)%を犯しました
	ENDIF

;主人公のＰ→対象のＡ
ELSEIF LOCAL:5 == 1
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 221)
	CALL FUCK(MASTER, "欲望, 性技, 射精, Ｃ")
	CALL FUCK(ARG:0, "欲望, Ａ, 性技, 奉仕, 性交, \@ PENIS(MASTER) ? 精愛 # \@")
	PRINTL
	IF PENIS(MASTER)
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、何度も%ANAME(ARG:0)%の尻穴を犯しました
	ELSE
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、張り型で何度も%ANAME(ARG:0)%の尻穴を犯しました
	ENDIF

;対象のＰ→主人公のＶ
ELSEIF LOCAL:5 == 2
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 222)
	CALL FUCK(ARG:0, "欲望, 性技, 射精, Ｃ")
	CALL FUCK(MASTER, "欲望, Ｖ, 性技, 奉仕, 性交, \@ PENIS(MASTER) ? 精愛 # \@", "\@ CONFIG:2 == 0 ? 膣内射精 # \@", GET_ID(ARG:0))
	PRINTL 
	IF PENIS(ARG:0)
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、その体で何度も%ANAME(ARG:0)%の欲望を受け止めました
	ELSE
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、何度も%ANAME(ARG:0)%に張り型で犯されました
	ENDIF

;対象のＰ→主人公のＡ
ELSEIF LOCAL:5 == 3
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 223)
	CALL FUCK(ARG:0, "欲望, 性技, 射精, Ｃ")
	CALL FUCK(MASTER, "欲望, Ａ, 性技, 奉仕, 性交, \@ PENIS(MASTER) ? 精愛 # \@")
	PRINTL 
	IF PENIS(ARG:0)
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、尻穴で何度も%ANAME(ARG:0)%の欲望を受け止めました
	ELSE
		PRINTFORML %ANAME(MASTER)%は夜が更けるまで、何度も%ANAME(ARG:0)%に張り型で尻穴を犯されました
	ENDIF

;レズセックス
ELSE
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 224)

	CALL FUCK(MASTER, "欲望, 性技, 奉仕, Ｃ, レズ")
	CALL FUCK(ARG:0, "欲望, 性技, 奉仕, Ｃ, レズ")

	PRINTL 
	PRINTFORML %ANAME(MASTER)%は夜が更けるまで、ねっとりと%ANAME(ARG:0)%と肌を重ね合いました
ENDIF

WAIT

RETURN 1

;-------------------------------------------------
;夜這い性交の同性経験追加処理(キャラARG:0にARG:2だけの経験値を加算)
;-------------------------------------------------
@EVENT_NIGHT_SEX_SAMESEX_EXP(ARG:0, ARG:1, ARG:2)
IF IS_SAMESEX(ARG:0, ARG:1)
	IF IS_MALE(ARG:0)
		CALL PRINT_ADD_EXP(ARG:0, "ＢＬ経験値", ARG:2)
	ELSE
		CALL PRINT_ADD_EXP(ARG:0, "レズ経験値", ARG:2)
	ENDIF
ENDIF

;-------------------------------------------------
;イベント用のABL影響力を返す関数(ARG:0=能力Lv)
;-------------------------------------------------
@GET_EVENT_ABL_POWER(ARG:0)
#FUNCTION
RETURNF MIN(ARG:0, 5) * 10 + SQRT(MAX(ARG:0 - 5, 0) * 100)
