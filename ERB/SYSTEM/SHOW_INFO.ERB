;情報表示に関わる関数

;-------------------------------------------------
;ARG:0番のキャラの性別情報を表示（右詰め）
;ARG:1が真なら一文字表示
;ARG:2は詰め方向(FALSE = 左, TRUE = 右)
;ARG:3は詰め字数
;-------------------------------------------------
@PRINT_SEX(ARG:0, ARG:1 = 0, ARG:2 = 0, ARG:3 = 0)
LOCAL:0 = GETCOLOR()

SELECTCASE TALENT:(ARG:0):性別
	CASE 1
		SETCOLOR COLOR("女")
		LOCALS:1 = \@(!ARG:1) ? 女性 # 女\@
	CASE 0
		SETCOLOR COLOR("男")
		LOCALS:1 = \@(!ARG:1) ? 男性 # 男\@
	CASE 2
		SETCOLOR COLOR("女双")
		LOCALS:1 = \@(!ARG:1) ? 女性(双) # 双\@
	CASE 3
		SETCOLOR COLOR("男双")
		LOCALS:1 = \@(!ARG:1) ? 男性(双) # 双\@
	CASE 4
		SETCOLOR COLOR("男の娘")
		LOCALS:1 = \@(!ARG:1) ? 男の娘 # 男\@
	CASE 5
		SETCOLOR COLOR("男の娘双")
		LOCALS:1 = \@(!ARG:1) ? 男娘(双) # 双\@
	CASEELSE
		SETCOLOR COLOR("性別不明")
		LOCALS:1 = \@(!ARG:1) ? 不明 # ？\@
ENDSELECT

CALL SETCOLORBYSEX(ARG:0)
IF !ARG:2
	PRINTFORM %LOCALS:1, ARG:3, LEFT%
ELSE
	PRINTFORM %LOCALS:1, ARG:3, RIGHT%
ENDIF

SETCOLOR LOCAL:0

;-------------------------------------------------
;ARG:0番のキャラの情報を表示
;-------------------------------------------------
@SHOW_INFO(ARG:0)
IF FLAG:11 == 1
	CALL SHOW_TENDENCY_SETTING(ARG:0)
ELSEIF FLAG:11 == 2
	CALL SHOW_ABL_DETAIL(ARG:0)
ELSEIF FLAG:11 == 3
	CALL SHOW_PREGNANT_RECORD(ARG:0)
ELSEIF FLAG:11 == 10
	CALL SHOW_RELATION_LIST(ARG:0)
ELSE
	CALL SHOW_NAMEINFO(ARG:0)

	LOCAL:2 = ID_TO_CHARA(CFLAG:(ARG:0):24)
	LOCAL:3 = ID_TO_CHARA(CFLAG:(ARG:0):21)
	LOCAL:4 = 0
	PRINT 父親:
	IF LOCAL:2 >= 0
		PRINTFORM %NAME:(LOCAL:2), 12, LEFT%
	;特殊キャラ
	ELSE
		PRINTFORM %ID_TO_SP_NAME(CFLAG:(ARG:0):24), 12, LEFT%
	ENDIF
	PRINT 母親:
	IF LOCAL:3 >= 0
		PRINTFORM %NAME:(LOCAL:3)%
		LOCAL:4 += STRLENS(NAME:(LOCAL:3))
	ELSE
		PRINTFORM %ID_TO_SP_NAME(CFLAG:(ARG:0):21)%
		LOCAL:4 += STRLENS(ID_TO_SP_NAME(CFLAG:(ARG:0):21))
	ENDIF

	LOCALS:0 = 
	PRINTFORM %LOCALS:0, 12 - LOCAL:4%

	CALL PRINT_SEX(ARG:0, 0, 0, 9)

	LOCALS:0 = 
	IF ARG:0 != MASTER
		IF NO:(ARG:0) < 201
			SETCOLOR COLOR("注意")
			LOCALS:0 = 固定
		ELSEIF NO:(ARG:0) < 401
			SETCOLOR COLOR("シアン")
			LOCALS:0 = 汎用
		ELSEIF NO:(ARG:0) < 1000
			SETCOLOR 0x00FF80
			LOCALS:0 = 子供
		ELSE
			SETCOLOR COLOR("オレンジ")
			LOCALS:0 = 特殊
		ENDIF
	ENDIF
	PRINTFORM %LOCALS:0, 10, LEFT%
	RESETCOLOR

	LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:(ARG:0):1)
	IF LOCAL:4 >= 0
		LOCALS:0 = %NAME:(LOCAL:4)%
	ELSE
		LOCALS:0 = ----
	ENDIF
	PRINTFORM 所属:%LOCALS:0%
	PRINTL 
	PRINT 評価:
	CALL TMP_PRINT_CHARA_STARS(ARG:0)
	PRINTL
	PRINTFORM 好感度:{CFLAG:(ARG:0):2, 10, LEFT}依存度:{CFLAG:(ARG:0):3, 10, LEFT}従属度:{CFLAG:(ARG:0):4, 10, LEFT}

	LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:(ARG:0):9)
	IF LOCAL:4 >= 0
		LOCALS:0 = %NAME:(LOCAL:4)%
	ELSE
		LOCALS:0 = ----
	ENDIF
	PRINTFORM   捕虜:%LOCALS:0%
	IF LOCAL:4 >= 0
		IF CFLAG:(ARG:0):29 == 0
			PRINT (監禁)
		ELSE
			PRINT (軟禁)
		ENDIF
	ENDIF
	PRINTL 
	;（精神力ゲージの表示。捕虜時限定）
	IF CFLAG:(ARG:0):9 == CFLAG:MASTER:1 && CFLAG:(ARG:0):29 == 0 || CFLAG:(ARG:0):59
		PRINT     精神力 
		CALL PRINT_SHORT_MIND(ARG:0)
		PRINTL 
	ENDIF

	PRINTL 

	PRINTL □ 経験 □
	CALL NEW_PRINT_EXP(ARG:0)
	PRINTL 
	PRINTL □ 能力 □
	PRINTFORML     体力最大値:{MAXBASE:(ARG:0):体力, 6, LEFT}  気力最大値:{MAXBASE:(ARG:0):気力, 6, LEFT}  精神力最大値:{MAXBASE:(ARG:0):精神力, 6, LEFT}
	CALL NEW_PRINT_ABL(ARG:0)
	PRINTL 
	PRINTL □ 素質 □
	CALL NEW_PRINT_TALENT(ARG:0)
	PRINTL 
	PRINTL □ ＳＬＧ □
	CALL NEW_PRINT_SLGTALENT(ARG:0)
	PRINTL 
	IF CONFIG:16 == 0
		PRINTL □ タトゥ □
		CALL PRINT_TATOO(ARG:0)
		PRINTL 
	ENDIF
	PRINTL □ その他 □
	IF IS_FEMALE(ARG:0)
		PRINTFORML Ｖ締まり:%TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり)%
	ENDIF
	PRINTFORML Ａ締まり:%TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり)%
	IF CONFIG:14 == 0
		PRINT   ピアス:
		CALL PRINT_PIERCE(ARG:0)
		PRINTL 
	ENDIF
	IF CONFIG:15 == 0
		PRINT     陰毛:
		CALL PRINT_INMOU(ARG:0)
		PRINTL
	ENDIF
	PRINTFORML 性的嗜好:%TOSTR_SEXUAL_PREFERENCE(TALENT:(ARG:0):性的嗜好)%\@ TALENT:(ARG:0):性的嗜好ロック ? (他の者に刻まれた) # \@
	PRINTL
	PRINTL □ 特殊な陥落 □
	CALL NEW_PRINT_SPECIAL_TALENT(ARG:0)
	PRINTL
ENDIF

;-------------------------------------------------
;キャラARG:0の性癖設定画面  表示処理のみ
;-------------------------------------------------
@SHOW_TENDENCY_SETTING(ARG:0)
;1ページに表示するコマンド数の設定
LOCAL:5 = 18

;定義されているコマンドの数を数える
LOCAL:12 = 0
FOR LOCAL:0, 0, 300
	IF TRAINNAME:(LOCAL:0) != ""
		LOCAL:12 ++
	ENDIF
NEXT

;ページ数の計算
FLAG:52 = MAX(LOCAL:12 - 1, 0) / LOCAL:5 + 1
FLAG:51 = LIMIT(FLAG:51, 1, FLAG:52)

PRINTFORML ◆%ANAME(ARG:0)%の性癖設定◆
DRAWLINE
PRINTL   各コマンドの実行回数の確認と好みLvの変更が可能です
PRINTL   好みLvが高いほど実行時に中毒充足のソースが多く追加され、おまかせ・逆調教時の実行確率が増加します
PRINTL   好みLvを上げるのに必要な実行回数は  Lv0→1:10回  Lv1→2:35回  Lv2→3:100回
DRAWLINE
PRINTL       < コマンド名称 >      < プレイヤー >      < ターゲット >      <   第三者   >
PRINT 　　　　　　  　　　　　　　　　
PRINTBUTTON "[+]", 3000
PRINTBUTTON "[-]", 4000
PRINT 　　　　　　　
PRINTBUTTON "[+]", 5000
PRINTBUTTON "[-]", 6000
PRINT 　　　　　　　
PRINTBUTTON "[+]", 7000
PRINTBUTTON "[-]", 8000	
PRINTL 
LOCAL:13 = (FLAG:51 - 1) * LOCAL:5
LOCAL:14 = FLAG:51 * LOCAL:5
LOCAL:15 = 0
LOCAL:16 = 0
FOR LOCAL:0, 0, 290
	IF TRAINNAME:(LOCAL:0) != ""
		IF LOCAL:15 >= LOCAL:13 && LOCAL:15 < LOCAL:14
			PRINT   
			IF COM_EXP:(ARG:0):(LOCAL:0) + COM_EXP:(ARG:0):(LOCAL:0 + 300) + COM_EXP:(ARG:0):(LOCAL:0 + 600) >= 1
				PRINTFORM %TRAINNAME:(LOCAL:0), 20, RIGHT%
				CALL PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, LOCAL:0)
				CALL PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, LOCAL:0 + 300)
				CALL PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, LOCAL:0 + 600)
			ELSE
				CALL CHECK_COM_KNOWLEDGE(ARG:0, LOCAL:0)
				IF RESULT
					SETCOLOR 0x808080
					PRINTFORM %TRAINNAME:(LOCAL:0), 20, RIGHT%        未経験    知識あり
				ELSE
					SETCOLOR 0x505050
					PRINTFORM %TRAINNAME:(LOCAL:0), 20, RIGHT%        未経験    知識なし
				ENDIF
				RESETCOLOR
			ENDIF
			PRINTL 
			LOCAL:16 ++
		ENDIF
		LOCAL:15 ++
	ENDIF
NEXT
FOR LOCAL:0, LOCAL:16, LOCAL:5
	PRINTL 
NEXT
DRAWLINE
PRINT   
IF FLAG:51 >= 2
	PRINTBUTTON "101[←前のページ]", 101
ELSE
	SETCOLOR COLOR("選択不可")
	PRINT 101[←前のページ]
	RESETCOLOR
ENDIF
PRINTFORM         page{FLAG:51, 2, RIGHT}/{FLAG:52, 2, LEFT}       
IF FLAG:51 < FLAG:52
	PRINTBUTTON "102[次のページ→]", 102
ELSE
	SETCOLOR COLOR("選択不可")
	PRINT 102[次のページ→]
	RESETCOLOR
ENDIF
PRINTL 

;-------------------------------------------------
;性癖設定画面における好みLv変更ボタンの描画 ARG:0=キャラ番号、ARG:1=配列番号
;-------------------------------------------------
@PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, ARG:1)
PRINTFORM {COM_EXP:(ARG:0):(ARG:1) / 10, 8, RIGHT}回 

SELECTCASE COM_TENDENCY:(ARG:0):(ARG:1)
	CASE 1
		SETCOLOR 0x00FF00
	CASE 2
		SETCOLOR COLOR("オレンジ")
	CASE 3
		SETCOLOR 0xFF00FF
ENDSELECT
PRINTFORM Lv{COM_TENDENCY:(ARG:0):(ARG:1)}
RESETCOLOR

LOCAL:0 = COM_TENDENCY:(ARG:0):(ARG:1)
LOCAL:1 = COM_EXP:(ARG:0):(ARG:1) / 10
IF (LOCAL:0 <= 0 && LOCAL:1 >= 10) || (LOCAL:0 <= 1 && LOCAL:1 >= 35) || (LOCAL:0 <= 2 && LOCAL:1 >= 100)
	PRINTBUTTON "[+]", ARG:1 + 1000
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [+]
	RESETCOLOR
ENDIF
IF LOCAL:0 >= 1
	PRINTBUTTON "[-]", ARG:1 + 2000
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [-]
	RESETCOLOR
ENDIF

;-------------------------------------------------
;ARG:0番のキャラの名前に関する情報を表示
;-------------------------------------------------
@SHOW_NAMEINFO(ARG:0)
;名前の表示
IF CSTR:(ARG:0):0 == "" && CSTR:(ARG:0):1 == "" && NAME:(ARG:0) == CALLNAME:(ARG:0)
	PRINTFORM %NAME:(ARG:0)%
ELSE
	IF CSTR:(ARG:0):0 == ""
	ELSE
		PRINT 姓:
		PRINTFORM %CSTR:(ARG:0):0%
		IF CSTR:(ARG:0):5 != ""
			PRINTFORM %TOHALF("(" + CSTR:(ARG:0):5 + ")")%
		ENDIF
	ENDIF
	;名字がない場合はスペースを入れない（ズレてしまうので）
	IF CSTR:(ARG:0):0 == ""
		PRINT 名:
	ELSE
		PRINT   名:
	ENDIF
	IF CSTR:(ARG:0):1 == ""
		PRINTFORM ------
	ELSE
		PRINTFORM %CSTR:(ARG:0):1%
		IF CSTR:(ARG:0):6 != ""
			PRINTFORM %TOHALF("(" + CSTR:(ARG:0):6 + ")")%
		ENDIF
	ENDIF
	IF CSTR:(ARG:0):2 == ""
	ELSE
		PRINT   字:
		PRINTFORM %CSTR:(ARG:0):2%
		IF CSTR:(ARG:0):7 != ""
			PRINTFORM (%CSTR:(ARG:0):7%)
		ENDIF
	ENDIF
ENDIF
PRINT   
IF CSTR:(ARG:0):8 != ""
	PRINTFORM 種族:%CSTR:(ARG:0):8%
ENDIF
PRINTL 

;-------------------------------------------------
;ARG:0番のキャラの経験を表示
;-------------------------------------------------
@NEW_PRINT_EXP(ARG:0)
LOCAL:1 = 0
FOR LOCAL:0, 0, 100
	;並べ直したEXPをLOCAL:2に代入
	LOCAL:2 = GET_SORTED_EXP(LOCAL:0)

	;対応するEXPがなければスキップ
	IF LOCAL:2 < 0
		CONTINUE
	ENDIF

	IF EXP:(ARG:0):(LOCAL:2) > 0
		;改行判定
		IF LOCAL:1 > 0 && LOCAL:1 % 4 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++

		CALL VALUE_FORM, EXP:(ARG:0):(LOCAL:2), "経験"
		PRINTFORM %EXPNAME:(LOCAL:2), 12%:%RESULTS, 6% 
	ENDIF
NEXT
IF LOCAL:1 > 0
	PRINTL 
ENDIF

	PRINTFORML   ファーストキス    : %SEXUAL_EXPERIENCE:(ARG:0):初体験_ファーストキス%

IF VAGINA(ARG:0)
	PRINTFORML   処女を捧げた相手  : %SEXUAL_EXPERIENCE:(ARG:0):初体験_処女%
ENDIF

IF PENIS(ARG:0)
	PRINTFORML   童貞を捧げた相手  : %SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞%
ENDIF

	PRINTFORML 尻穴処女を捧げた相手: %SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女%
;-------------------------------------------------
;ARG:0番のキャラの能力を表示
;-------------------------------------------------
@NEW_PRINT_ABL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_LIST, 100

VARSET SHOW_LIST, -1

SHOW_LIST:0 = 50
SHOW_LIST:1 = 51
SHOW_LIST:2 = 52
SHOW_LIST:3 = 53
SHOW_LIST:4 = 60
SHOW_LIST:5 = 61
SHOW_LIST:10 = 0
SHOW_LIST:11 = 1
SHOW_LIST:12 = 2
SHOW_LIST:13 = 3
SHOW_LIST:14 = 4
SHOW_LIST:15 = 12
SHOW_LIST:20 = 13
SHOW_LIST:21 = 14
SHOW_LIST:22 = 20
SHOW_LIST:23 = 21
SHOW_LIST:24 = 22
SHOW_LIST:25 = 23
SHOW_LIST:30 = 24
SHOW_LIST:31 = 25
SHOW_LIST:32 = 26
SHOW_LIST:33 = 27
SHOW_LIST:34 = 28
SHOW_LIST:35 = 29
SHOW_LIST:40 = 30
SHOW_LIST:41 = 31
SHOW_LIST:42 = 32

FOR LOCAL:0, 0, 100
	;対応するABLがなければスキップ
	IF SHOW_LIST:(LOCAL:0) >= 0
		;10番ごとに改行
		IF LOCAL:0 > 0 && LOCAL:0 % 10 == 0
			PRINTL 
		ENDIF

		LOCAL:2 = SHOW_LIST:(LOCAL:0)

		;3桁までは右詰め、それ以上は左詰めで表示
		IF ABL:(ARG:0):(LOCAL:2) <= 99
			PRINTFORM %ABLNAME:(LOCAL:2), 8%:{ABL:(ARG:0):(LOCAL:2), 3} 
		ELSE
			PRINTFORM %ABLNAME:(LOCAL:2), 8%:{ABL:(ARG:0):(LOCAL:2), 4, LEFT}
		ENDIF
		;ランクづけ
		IF LOCAL <= 2
			CALL PRINT_ALPHABET_RANK(0, ABL:(ARG:0):(LOCAL:2))
		ELSEIF LOCAL == 3
			CALL PRINT_ALPHABET_RANK(1, ABL:(ARG:0):(LOCAL:2))
		ELSEIF LOCAL <= 5
			CALL PRINT_ALPHABET_RANK(2, ABL:(ARG:0):(LOCAL:2))
		ELSE
			CALL PRINT_ALPHABET_RANK(3, ABL:(ARG:0):(LOCAL:2))
		ENDIF
	ENDIF
NEXT
PRINTL 

IF ARG:0 != MASTER
	PRINT   主導度( 日常 ) 
	CALL PRINT_SLIDER(-1000, 1000, ABL:(ARG:0):主導度Ｎ, 30, 0xC040C0, 0x40C040)
	PRINTFORML {ABL:(ARG:0):主導度Ｎ, 5}

	PRINT   主導度(ウフフ) 
	CALL PRINT_SLIDER(-1000, 1000, ABL:(ARG:0):主導度Ｕ, 30, 0xC040C0, 0x40C040)
	PRINTFORM {ABL:(ARG:0):主導度Ｕ, 5}  

	IF ABL:(ARG:0):主導度Ｎ <= -500
		PRINT 超弱気で
	ELSEIF ABL:(ARG:0):主導度Ｎ <= -300
		PRINT 弱気で
	ELSEIF ABL:(ARG:0):主導度Ｎ <= -100
		PRINT 落ち着いた
	ELSEIF ABL:(ARG:0):主導度Ｎ < 100
		PRINT 友達感覚で
	ELSEIF ABL:(ARG:0):主導度Ｎ < 300
		PRINT 元気のいい
	ELSEIF ABL:(ARG:0):主導度Ｎ < 500
		PRINT 強気で
	ELSE
		PRINT 超強気で
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		PRINT 変態的な
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		PRINT 倒錯的な
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		PRINT ちょっと刺激的な
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		PRINT 好奇心旺盛な
	ELSE
		PRINT 甘々な
	ENDIF

	IF ABL:(ARG:0):主導度Ｕ <= -500
		PRINT ドＭ
	ELSEIF ABL:(ARG:0):主導度Ｕ <= -300
		PRINT Ｍ
	ELSEIF ABL:(ARG:0):主導度Ｕ <= -100
		PRINT 微Ｍ
	ELSEIF ABL:(ARG:0):主導度Ｕ < 100
		PRINT リバ
	ELSEIF ABL:(ARG:0):主導度Ｕ < 300
		PRINT 微Ｓ
	ELSEIF ABL:(ARG:0):主導度Ｕ < 500
		PRINT Ｓ
	ELSE
		PRINT ドＳ
	ENDIF
	PRINTL 

	PRINT   倒錯度         
	CALL PRINT_SLIDER(0, 1000, ABL:(ARG:0):倒錯度, 30, 0x4040C0, 0x4040C0)
	PRINTFORML {ABL:(ARG:0):倒錯度, 5}
ENDIF

IF ARG:0 != MASTER
	;既成事実Lvの表示
	PRINTFORM 　既成事実Lv{MARK:(ARG:0):既成事実}    
	FOR LOCAL:0, 0, 3
		IF MARK:(ARG:0):既成事実 > LOCAL:0
			SETCOLOR 0xFF80FF
		ELSE
			SETCOLOR COLOR("選択不可")
		ENDIF
		SELECTCASE LOCAL:0
			CASE 0
				PRINT 唇
			CASE 1
				PRINT →告
			CASE 2
				PRINT →Ｈ
		ENDSELECT
	NEXT
	RESETCOLOR
	PRINTL 
ENDIF

;-------------------------------------------------
;数値をスライダとして表示する関数
;ARG:0=スライダの最小値、ARG:1=スライダの最大値、ARG:2=スライダの値、ARG:3=スライダの幅、ARG:4=バーの色(正)、ARG:5=バーの色(負)
;-------------------------------------------------
@PRINT_SLIDER(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5)
;スライダの位置を四捨五入で求める
LOCAL:2 = LIMIT((ARG:2 - ARG:0) * (ARG:3 * 2 + 1) / ((ARG:1 - ARG:0) * 2), 0, ARG:3 - 1)

FOR LOCAL:0, 0, ARG:3
	IF LOCAL:0 == LOCAL:2
		PRINTFORM %UNICODE(0x2588)%
	ELSE
		LOCAL:3 = ARG:0 + (ARG:1 - ARG:0) * (LOCAL:0 * 2 + 1) / (ARG:3 * 2)

		IF LOCAL:3 >= 0
			SETCOLOR ARG:4
		ELSE
			SETCOLOR ARG:5
		ENDIF

		FONTSTYLE 4
		IF LOCAL:0 == 0
			PRINTFORM %UNICODE(0x25E3)%
		ELSEIF LOCAL:0 == ARG:3 - 1
			PRINTFORM %UNICODE(0x25E2)%
		ELSE
			PRINTFORM  
		ENDIF
		FONTSTYLE 0
	ENDIF
	RESETCOLOR
NEXT

;-------------------------------------------------
;ARG:0番のキャラの素質を表示
;-------------------------------------------------
@NEW_PRINT_TALENT(ARG:0)
#DIMS TN

PRINT   
LOCAL:1 = 0
FOR LOCAL:0, 1, 300
	IF TALENT:(ARG:0):(LOCAL:0)
		TN = %TALENTNAME:(LOCAL:0)%
		;非表示素質のスキップ
		IF !VAGINA(ARG:0) && (TN == "処女" || TN == "淫壷" || TN == "Ｖ敏感" || TN == "Ｖ鈍感")
			CONTINUE
		ELSEIF !PENIS(ARG:0) && TN == "童貞"
			CONTINUE
		ELSEIF IS_MALE(ARG:0) && (TN == "絶壁" || TN == "貧乳" || TN == "巨乳" || TN == "爆乳")
			CONTINUE
		ELSEIF VAGINA(ARG:0) && TN == "孕み可能"
			CONTINUE
		ELSEIF TN == "処女" || TN == "アナル処女" || TN == "キス未経験"
			CONTINUE
		ENDIF

		IF TN == "正妻" && IS_HUSBAND(ARG:0)
			TN = 夫
		ELSEIF TN == "処女" && TALENT:(ARG:0):(LOCAL:0) == 2
			TN = 無自覚非処女
		ELSEIF TN == "童貞" && TALENT:(ARG:0):(LOCAL:0) == 2
			TN = 無自覚非童貞
		ENDIF

		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 78
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINT   
		ENDIF

		PRINTFORM <%TN%> 
	ENDIF
NEXT
PRINTL 
PRINTFORML   成長型: %TOSTR_GROWTYPE(TALENT:(ARG:0):300)%タイプ(%TOSTR_GROWTYPE_B(TALENT:(ARG:0):300)%に適正)
;PRINTFORML     性格: %TOSTR_CHARATYPE(TALENT:(ARG:0):301)%

;コンフィグ「外見に関する詳細の表示」がON
IF CONFIG:6 == 1
	PRINT   髪色: 
	IF TALENT:(ARG:0):髪色 == 0
		PRINT 不明      
	ELSE
		LOCAL:10 = RGB_TO_H(TALENT:(ARG:0):髪色)
		LOCAL:11 = RGB_TO_S(TALENT:(ARG:0):髪色)
		LOCAL:12 = RGB_TO_V(TALENT:(ARG:0):髪色) - 8
		IF LOCAL:11 < 12
			LOCAL:11 *= 2
		ELSE
			LOCAL:11 += 12
		ENDIF
		FOR LOCAL:0, 0, 10
			SETCOLOR HSV_TO_RGB(LOCAL:10, LIMIT(LOCAL:11, 0, 100), LIMIT(LOCAL:12, 0, 100))
			PRINTFORM %UNICODE(0x2587)%
			IF LOCAL:11 >= 24
				LOCAL:11 += 2
			ELSEIF LOCAL:11 >= 12
				LOCAL:11 += 1
			ENDIF
			LOCAL:12 -= 3
		NEXT
		RESETCOLOR
	ENDIF
	PRINTFORM    髪の長さ: %TOSTR_HAIR_LENGTH(TALENT:(ARG:0):髪の長さ)%
	PRINTL 
ENDIF

;-------------------------------------------------
;ARG:0番のキャラのＳＬＧ素質を表示
;-------------------------------------------------
@NEW_PRINT_SLGTALENT(ARG:0)
#DIMS STN
LOCAL:1 = 0
LOCAL:4 = 0
PRINT   
FOR LOCAL, 0, 63
	IF GETBIT(TALENT:(ARG:0):ＳＬＧ素質, LOCAL) && TOSTR_SLG_TALENT(LOCAL) != ""
		STN = %TOSTR_SLG_TALENT(LOCAL)%
		;改行判定
		LOCAL:3 = STRLENS(STN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 78
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINT   
		ENDIF
		PRINTFORM <%STN%> 
		LOCAL:4 = 1
	ENDIF
NEXT
SIF !LOCAL:4
	PRINT なし
PRINTL

;-------------------------------------------------
;ARG:0番の陥落・特殊勢力陥落・デイリー陥落素質を一覧表示
;-------------------------------------------------
@NEW_PRINT_SPECIAL_TALENT(ARG:0)
#DIM 表示
#DIMS TN
PRINT   
LOCAL:1 = 0
表示 = 0
FOR LOCAL:0, 0, 64
	IF GETBIT(TALENT:(ARG:0):肉体陥落系, LOCAL) && TOSTR_SEX_TALENT(LOCAL) != ""
		TN = %TOSTR_SEX_TALENT(LOCAL:0)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 78
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINT   
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
FOR LOCAL:0, 0, 64
	IF GETBIT(TALENT:(ARG:0):デイリー陥落系, LOCAL) && TOSTR_DAILY_TALENT(LOCAL) != ""
		TN = %TOSTR_DAILY_TALENT(LOCAL:0)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 78
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINT   
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
FOR LOCAL:0, 0, MAX_SP_COUNTRY
	IF GETBIT(TALENT:(ARG:0):特殊勢力陥落系, LOCAL) && TOSTR_FALLEN_TALENT(LOCAL) != ""
		TN = %TOSTR_FALLEN_TALENT(LOCAL:0)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 78
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINT   
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
IF !表示
	PRINT なし
ENDIF
;-------------------------------------------------
;数値表示変更
;-------------------------------------------------
@VALUE_FORM(ARG, ARGS)
VARSET LOCAL, 0
VARSET LOCALS, ""

IF ARGS == "珠"
	IF ARG <= 9999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSEIF ARGS == "経験"
	IF ARG <= 999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSE
	IF ARG <= 99999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ENDIF

LOCAL:0 = 10000000000000000
LOCAL:1 = 1000000000000
LOCAL:2 = 100000000
LOCAL:3 = 10000
LOCAL:4 = 1
LOCALS:0 = 京
LOCALS:1 = 兆
LOCALS:2 = 億
LOCALS:3 = 万

FOR LOCAL:98, 0, 4
	IF ARG >= LOCAL:(LOCAL:98)
		LOCAL:99 = LOCAL:98
		BREAK
	ENDIF
NEXT

LOCAL:10 = ARG / LOCAL:(LOCAL:99)
LOCAL:11 = (ARG % LOCAL:(LOCAL:99)) / LOCAL:(LOCAL:99 + 1)

IF ARGS == "珠" || ARGS == "経験"
	RESULTS = {LOCAL:10}%LOCALS:(LOCAL:99)%
ELSE
	RESULTS = {LOCAL:10}%LOCALS:(LOCAL:99)%{LOCAL:11}%LOCALS:(LOCAL:99 + 1)%
ENDIF

;-------------------------------------------------
;ARG:0番のキャラの能力成長状況を詳細表示
;-------------------------------------------------
@SHOW_ABL_DETAIL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_LIST, 100

VARSET SHOW_LIST, -1

SHOW_LIST:0 = 50
SHOW_LIST:1 = 51
SHOW_LIST:2 = 52
SHOW_LIST:3 = 53
SHOW_LIST:4 = 60
SHOW_LIST:5 = 61
SHOW_LIST:10 = 0
SHOW_LIST:11 = 1
SHOW_LIST:12 = 2
SHOW_LIST:13 = 3
SHOW_LIST:14 = 4

SHOW_LIST:20 = 12
SHOW_LIST:21 = 13
SHOW_LIST:22 = 20
SHOW_LIST:23 = 21
SHOW_LIST:24 = 22
SHOW_LIST:25 = 23
SHOW_LIST:30 = 24
SHOW_LIST:31 = 25
SHOW_LIST:32 = 26
SHOW_LIST:33 = 27
SHOW_LIST:34 = 28
SHOW_LIST:35 = 29
SHOW_LIST:40 = 30
SHOW_LIST:41 = 31
SHOW_LIST:42 = 32


PRINTFORML ◆%ANAME(ARG:0)%の能力成長状況◆
LOCAL:1 = 0
FOR LOCAL:0, 0, 100
	;対応するABLがなければスキップ
	IF SHOW_LIST:(LOCAL:0) >= 0
		;改行判定
		IF LOCAL:1 > 0 && LOCAL:1 % 2 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++

		LOCAL:2 = SHOW_LIST:(LOCAL:0)

		PRINTFORM %ABLNAME:(LOCAL:2), 8%:{ABL:(ARG:0):(LOCAL:2), 4}

		RESULT = 0
		;必要な経験値の計算
		TRYCALLFORM CALC_ABLUP_EXP{LOCAL:2}(ARG:0)
		IF RESULT >= 1 && LOCAL:2 < 9999
			LOCAL:5 = RESULT
			RESULTS = 
			;能力に対応する経験値の名称を取得
			TRYCALLFORM ABLUP_EXPNAME{LOCAL:2}
			LOCAL:6 = NAME_TO_NUM("EXP", RESULTS)
			IF LOCAL:6 >= 0
				LOCAL:7 = EXP:(ARG:0):(LOCAL:6) * 20 / LOCAL:5
				IF LOCAL:7 >= 17
					LOCAL:8 = 0x00FF00
				ELSEIF LOCAL:7 >= 12
					LOCAL:8 = COLOR("注意")
				ELSE
					LOCAL:8 = 0xC0C0C0
				ENDIF
				FONTSTYLE 4
				FOR LOCAL:4, 0, 20
					IF LOCAL:4 < LOCAL:7
						SETCOLOR LOCAL:8
					ELSE
						SETCOLOR 0x404040
					ENDIF
					PRINTFORM  
				NEXT
				FONTSTYLE 0
				RESETCOLOR
			ENDIF
		ELSE
			SETCOLOR 0x8080D0
			FONTSTYLE 4
			PRINT        
			FONTSTYLE 0
			PRINT  最大 
			FONTSTYLE 4
			PRINT        
			FONTSTYLE 0
			RESETCOLOR
		ENDIF
		PRINT   
	ENDIF
NEXT
PRINTL 

;-------------------------------------------------
;ARG:0番の他キャラに対する好感度・敵対値を一覧表示
;-------------------------------------------------
@SHOW_RELATION_LIST(ARG:0)
FOR LOCAL:0, 0, CHARANUM
	PRINTFORM %NAME:(LOCAL:0), 8, RIGHT%
	PRINTFORM   {REL_LIKE:(ARG:0):(LOCAL:0), 5, RIGHT}
	PRINTFORM   {REL_HATE:(ARG:0):(LOCAL:0), 5, RIGHT}
	PRINTL 
NEXT