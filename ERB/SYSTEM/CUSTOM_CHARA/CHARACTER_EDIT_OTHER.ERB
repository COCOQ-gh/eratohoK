@CHARACTER_EDIT_UI_OTHER(対象)
#DIM 対象

IF FLAG:周回数 > 0
    PRINTFORM 所属:
    IF CFLAG:対象:所属 == 0
        IF CFLAG:対象:特殊状態 == 特殊状態_死亡
            PRINTFORM 無所属(死亡)          
        ELSEIF CFLAG:対象:特殊状態 == 特殊状態_放浪
            PRINTFORM 無所属(放浪)          
        ELSE
            PRINTFORM 無所属(未登場)        
        ENDIF
    ELSE
        PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属)), MAX_CHARANAME_LENGTH + 4, LEFT%
    ENDIF
    IF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
        SETCOLOR カラー_選択不可
        PRINTPLAIN ※君主は変更不可※
        RESETCOLOR
    ELSE
        CALL PRINTBUTTON_EX("[変更] ", 0)
        CALL PRINTBUTTON_EX("[一覧] ", 1)
    ENDIF
    PRINTL

    PRINTFORM 捕虜:
    IF CFLAG:対象:捕虜先 == 0
        PRINTFORM %"なし", MAX_CHARANAME_LENGTH + 4, LEFT%
    ELSE
        PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:捕虜先)), MAX_CHARANAME_LENGTH + 4, LEFT%
    ENDIF
    IF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
        SETCOLOR カラー_選択不可
        PRINTPLAIN ※君主は変更不可※
        RESETCOLOR
    ELSE
        CALL PRINTBUTTON_EX("[変更] ", 10)
        CALL PRINTBUTTON_EX("[一覧] ", 11)
    ENDIF
    PRINTL
    CALL SINGLE_DRAWLINE
ENDIF
;特殊処理
PRINTBUTTON "[      性別]:", 100
PRINTFORML %TOSTR_SEX(TALENT:対象:GETNUM(TALENT, "性別"))%
SIF HAS_VAGINA(対象)
	PRINTFORM 乳房:%TOSTR_BUSTSIZE(GET_BUSTSIZE(対象)), 10, LEFT% 
SIF HAS_PENIS(対象)
	PRINTFORM 陰茎:%TOSTR_PENISSIZE(GET_PENISSIZE(対象)), 10, LEFT%
PRINTFORML 尻:%TOSTR_HIPSIZE(TALENT:対象:ヒップサイズ), 10, LEFT%

IF HAS_VAGINA(対象)
	PRINTFORM 膣締:%TOSTR_TIGHTNESS(TALENT:(対象):Ｖ締まり), 10, LEFT% 
ENDIF
PRINTFORM 尻締:%TOSTR_TIGHTNESS(TALENT:(対象):Ａ締まり), 10, LEFT% 
PRINTL
PRINT 髪色:
CALL PRINT_HAIR_COLOR(TALENT:対象:髪色) 
PRINTFORML  髪長:%TOSTR_HAIR_LENGTH(TALENT:対象:髪の長さ)%
CALL SHOW_BODYSIZE(対象)
PRINTBUTTON "[ボディ設定]", 101
PRINTBUTTON "[      陰毛]:", 105
PRINTFORML %GET_INMOU_NAME(TALENT:対象:陰毛現在値)%
PRINTBUTTON "[  タトゥー]:", 106
CALL PRINT_TATOO(対象)
PRINTL
PRINTBUTTON "[    ピアス]:", 107
CALL PRINT_PIERCE(対象)
PRINTL
PRINTFORM タグ:
FOR LOCAL, 0, VARSIZE("TAG_NAME")
    CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 1000 + LOCAL, HAS_TAG(対象, LOCAL))
NEXT
PRINTL


@CHARACTER_EDIT_INPUT_OTHER(対象, 入力値)
#DIM 対象
#DIM 入力値

SELECTCASE RESULT
    ;性別
    CASE 100
        TALENT:対象:性別 = ROUND_INCREMENT(TALENT:対象:性別, 0, 5)
    ;胸
    CASE 101
    	CALL BEAUTIFY(対象)
    ;陰毛
    CASE 105
        CALL ASK_MULTI("パイパン", "つるつる", "うっすら", "柔毛", "ふんわり", "標準", "多め", "ふさふさ", "もっさり", "剛毛", "尻まで", "♥")
        TALENT:対象:陰毛現在値 = RESULT -1
        TALENT:対象:陰毛目標値 = RESULT - 1
    CASE 106
        CALL TATOO_MENU(対象, 1)
    CASE 107
        CALL PIERCE_MENU(対象, 1)
    CASE 1000 TO 1000 + VARSIZE("TAG_NAME")
        CALL INVERT_TAG(対象, 入力値 - 1000)
ENDSELECT

IF FLAG:周回数 > 0
    SELECTCASE 入力値
        CASE 0
            SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
                RETURN
            IF CFLAG:対象:所属 == 0 && CFLAG:対象:特殊状態 == 0 && !CFLAG:対象:捕虜先
                CFLAG:対象:特殊状態 = 特殊状態_放浪
            ELSEIF CFLAG:対象:所属 == 0 && CFLAG:対象:特殊状態 == 特殊状態_放浪
                CFLAG:対象:特殊状態 = 特殊状態_死亡
            ELSE
                LOCAL:1 = 0
                FOR LOCAL, CFLAG:対象:所属 + 1, MAX_COUNTRY
                    SIF !IS_COUNTRY(LOCAL)
                        CONTINUE
                    CALL CHANGE_COUNTRY(対象, LOCAL)
                    LOCAL:1 = 1
                    BREAK
                NEXT
                IF LOCAL:1 == 0
                    CALL CHANGE_COUNTRY(対象, 0)
                    CFLAG:対象:特殊状態 = 0
                ENDIF
            ENDIF
        CASE 1
            SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
                RETURN
            FOR LOCAL, 0, MAX_COUNTRY
                SIF !IS_COUNTRY(LOCAL)
                    CONTINUE
                PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
            NEXT
            PRINTFORML [{MAX_COUNTRY}] 無所属(未登場)
            PRINTFORML [{MAX_COUNTRY + 1}] 無所属(放浪)
            PRINTFORML [{MAX_COUNTRY + 2}] 無所属(死亡)
            INPUT
            IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
                CALL CHANGE_COUNTRY(対象, RESULT, 0)
            ELSEIF RESULT == MAX_COUNTRY
                CALL CHANGE_COUNTRY(対象, 0, 1)
                CFLAG:対象:特殊状態 = 0
            ELSEIF RESULT == MAX_COUNTRY + 1
                CALL CHANGE_COUNTRY(対象, 0, 1)
            ELSEIF RESULT == MAX_COUNTRY + 2
                CALL CHANGE_COUNTRY(対象, 0, 1, 1)
            ENDIF
        CASE 10
            SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
                RETURN
            LOCAL:1 = 0
            FOR LOCAL:0, CFLAG:対象:捕虜先 + 1, MAX_COUNTRY
                IF IS_COUNTRY(LOCAL)
                    CALL CAPTURE(対象, LOCAL)
                    LOCAL:1 = 1
                    BREAK
                ENDIF
            NEXT
            SIF LOCAL:1 == 1
                CALL CAPTURE(対象, 0)
        CASE 11
            SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
                RETURN
            FOR LOCAL, 0, MAX_COUNTRY
                SIF !IS_COUNTRY(LOCAL)
                    CONTINUE
                PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
            NEXT
            PRINTFORML [{MAX_COUNTRY}] なし
            INPUT
            IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
                CALL CAPTURE(対象, RESULT)
            ELSEIF RESULT == MAX_COUNTRY
                CALL CAPTURE(対象, 0)
            ENDIF      
    ENDSELECT
ENDIF