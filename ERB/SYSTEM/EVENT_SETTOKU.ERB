;-------------------------------------------------
;説得イベント(@EVENT_PRISONERから流用)
;-------------------------------------------------
@SHOP_LIFE_SETTOKU(対象)
#DIM 説得者, 3
#DIM 説得者数
#DIM 対象
VARSET 説得者, -1
VARSET 説得者数
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:LOCAL:捕虜調教の助手
		説得者:説得者数 = LOCAL
		説得者数 ++
	ENDIF
NEXT

IF 説得者:0 == -1
	PRINTFORMW 先に説得者を設定してください
	RETURN 0
ENDIF

FOR LOCAL, 0, VARSIZE("説得者")
	SIF 説得者:LOCAL == -1
		BREAK
	CALL DO_SETTOKU(対象, 説得者:LOCAL)
	SIF RESULT == 2
		BREAK
NEXT

RETURN 1


;戦略
;判定値を以下を基準に算出
;1.互いの自身の君主への印象
;2.互いの相手の君主への印象
;3.互いの政治力
;4.互いの知略
;判定値が大きいほど有利
@DO_SETTOKU(対象, 説得者)
#DIM 説得者
#DIM 対象
#DIM 説得者君主
#DIM 対象君主
#DIM 判定基礎値,2
#DIM 判定値
#DIM 効果量, 2
VARSET 判定基礎値
説得者君主 = GET_COUNTRY_BOSS(CFLAG:説得者:所属)
対象君主 = GET_COUNTRY_BOSS(CFLAG:対象:所属)

;説得側の判定値
;君主の場合は忠誠最大として扱う

判定基礎値:0 = CALC_SETTOKU_EFFECT_TARGET(説得者, 対象)
判定基礎値:1 = CALC_SETTOKU_EFFECT_TARGET(対象, 説得者)

PRINTFORML -<%ANAME(説得者)%>の<%ANAME(対象)%>への説得-
判定値 = (判定基礎値:0 - 判定基礎値:1) * 2

CALL FIND_GENERAL_SKILL(説得者, "弁舌")
SIF RESULT
	判定値 += RAND(5, 15)
CALL FIND_GENERAL_SKILL(説得者, "風説流布")
SIF RESULT
	判定値 += RAND(5, 15)

SELECTCASE 判定値
	CASE IS <= -100
		PRINTFORMW %ANAME(説得者)%は逆に感化された
	CASE IS <= -60
		PRINTFORMW %ANAME(説得者)%は丸め込まれてしまった
	CASE IS < 0
		PRINTFORMW %ANAME(説得者)%は説得に失敗した
	CASE IS <= 30
		PRINTFORMW %ANAME(説得者)%は説得に成功した
	CASE IS <= 60
		PRINTFORMW %ANAME(説得者)%は順調に説得できた
	CASE IS <= 100
		PRINTFORMW %ANAME(説得者)%はかなり順調に説得できた
	CASE IS > 100
		PRINTFORMW %ANAME(説得者)%は%ANAME(対象)%を感化させたようだ
ENDSELECT

CFLAG:対象:行動済み = 1
CFLAG:説得者:行動済み = 1


効果量:0 = 判定値
効果量:1 = 判定値

IF 判定値 > 0
	SIF REL_LIKE:対象:説得者君主 < REL_HATE:対象:説得者君主
		TIMES 効果量:1, 1.5
	CALL CHANGE_RELATION_O_TO_C(対象, CFLAG:説得者:所属, 効果量:0 * 2 / 3, -効果量:1 * 2/ 3)
	CALL CHANGE_RELATION_O_TO_O(対象, 説得者, 効果量:0 / 2, -効果量:1 / 2)
	CALL CHANGE_RELATION_O_TO_C(説得者, CFLAG:説得者:所属, 効果量:0 / 3, -効果量:1 / 3)
ELSEIF 判定値 < 0
	CALL CHANGE_RELATION_O_TO_C(対象, CFLAG:説得者:所属, 効果量:0 / 2, -効果量:1 / 2)
	CALL CHANGE_RELATION_O_TO_O(対象, 説得者, RAND(5, 10), -RAND(5, 10))
ENDIF

IF SETTOKU_REL_EFFECT(対象, 説得者君主) >= 95 && 判定値 > 100 && ABL_POWER(ABL:説得者:政治, 説得者) + ABL_POWER(ABL:説得者:知略, 説得者) > ABL_POWER(ABL:対象:政治, 対象) + ABL_POWER(ABL:対象:知略, 対象)
	CALL COLORPRINT(@"説得の末、%ANAME(対象)%は移籍の意思を固めた……", カラー_注意, "W")
	CALL CHANGE_COUNTRY(対象, CFLAG:説得者:所属, 1)
	RETURN 2
ENDIF

RETURN 1


;-------------------------------------------------
;(ARG:0)のキャラから(ARG:1)のキャラに対する
;-------------------------------------------------
@SETTOKU_REL_EFFECT(ARG:0, ARG:1)
#FUNCTION
RETURNF MAX((REL_LIKE:(ARG:0):(ARG:1) - REL_HATE:(ARG:0):(ARG:1)) * 100 / 1500, 0)

;-------------------------------------------------
;(ARG:0)がARG:1を説得する場合の説得能力を算出
;-------------------------------------------------
@CALC_SETTOKU_EFFECT(ARG:0, ARG:1)
#FUNCTION
RETURNF SETTOKU_BASE_EFFECT(ARG:0) + SETTOKU_REL_EFFECT(ARG:0, ARG:1) / 2

;-------------------------------------------------
;(ARG:0)がARG:1に説得される場合の説得能力を算出
;-------------------------------------------------
@CALC_SETTOKU_EFFECT_TARGET(ARG:0, ARG:1)
#FUNCTION
#DIM 君主
君主 = GET_COUNTRY_BOSS(CFLAG:(ARG:1):所属)
RETURNF SETTOKU_BASE_EFFECT(ARG:0) - (君主 >= 0 ? SETTOKU_REL_EFFECT(ARG:1, 君主) # 0)


;-------------------------------------------------
;(ARG:0)のキャラの基礎説得能力を算出
;-------------------------------------------------
@SETTOKU_BASE_EFFECT(ARG:0)
#FUNCTION
#DIM 君主
君主 = GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属)
LOCAL = 0
IF ARG:0 != 君主 && 君主 >= 0
	LOCAL += SETTOKU_REL_EFFECT(ARG:0, 君主) / 2
ELSE
	LOCAL += 50
ENDIF
RETURNF LOCAL + ABL_POWER(ABL:(ARG:0):政治, ARG:0) / 1000 + ABL_POWER(ABL:(ARG:0):知略, ARG:0) / 1000
