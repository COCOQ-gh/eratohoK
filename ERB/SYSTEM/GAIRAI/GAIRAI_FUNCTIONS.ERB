;--------------------------------------
;外来勢力出現イベント
;出現できたら1、できなければ0をかえす
;都市 = 最初に奪う都市 指定しなければ野盗と同一の方式
;メッセージフラグ = 汎用メッセージ表示フラグ　指定しなければ表示
;--------------------------------------
@GAIRAI_RISE(都市 = -1, メッセージフラグ = 1)
#DIM 外来
#DIM メッセージフラグ
#DIM 都市

;外来勢力を作成
CALL GAIRAI_INIT()
外来 = RESULT
;作れなかったら戻す
SIF 外来 == -1
	RETURN 0

;引数で奪う都市が指定されていなければ、BANDIT_RISE_SELECT_CITY()を流用
IF 都市 < 0 || 都市 >= CITY_NUM
	FOR LOCAL, 0, GAIRAI_RANK
		CALL BANDIT_RISE_SELECT_CITY()
		都市 = RESULT
		;多分ないはずなんだけど、奪えなかったら諦め
		IF 都市 == -1
			CONTINUE
		ENDIF
		CITY_OWNER:(都市) = 外来
		CALL CLEAR_CITY_COMMANDER(都市, 0)
		CALL CLEAR_CITY_COMMANDER(都市, 1)
	NEXT
ELSE
	CITY_OWNER:(ARG:0) = 外来
	CALL CLEAR_CITY_COMMANDER(都市, 0)
	CALL CLEAR_CITY_COMMANDER(都市, 1)	
ENDIF


IF メッセージフラグ
	DRAWLINE
	SETCOLOR COLOR("警告")
	PRINTFORMW 結界の外から突如、外来人が雪崩のように攻めこんできました！
	PRINTFORMW 彼らの電撃的な作戦行動により、%CITY_NAME:(都市)%\@ GET_OWN_CITY(外来) > 1 ? など # \@が奪われました
	PRINTFORMW これに伴い、幻想郷の各勢力が異変解決の準備を始めたようです…
	RESETCOLOR
	DRAWLINE
ENDIF

RETURN 1

;--------------------------------------
;ターンエンド時の外来兵の処理
;--------------------------------------
@TURNEND_GAIRAI
#DIM 外来

SIF !DVAR:外来勢力出現済み
	RETURN -1
	
;外来勢力の番号を取得(ID:299)
外来 = GET_COUNTRY_FROM_ID(299)
IF 外来 == -1
	SETCOLOR COLOR("注意")
	PRINTFORML 壊滅的な被害を受けた外来人は、結界の外に撤退していきました
	PRINTFORML 幻想郷の勝利です！
	RESETCOLOR
	;外来人を削除する
	LOCAL:1 = CHARANUM
	FOR LOCAL:0, 0, LOCAL:1
		LOCAL:2 = LOCAL:1 - LOCAL:0 - 1
		IF TALENT:(LOCAL:2):外来人
			CALL DELETE_CHARA(LOCAL:2)
		ENDIF
	NEXT
	RETURN -1
ENDIF

;兵力の増える処理
IF DAY % 3 == 0
	LOCAL:0 = MAX(RAND:3000, 1000)
	PRINTFORML 外来人につけば「実験」のおこぼれにあずかれると聞いた野良妖怪たちが、次々と寝返っているようです……
	PRINTFORML 外来人軍の兵力が{LOCAL:0}増加した
	COUNTRY_SOLDIER:(外来) += LOCAL:0
ENDIF

;調教処理
CALL TRAIN_BY_GAIRAI

;--------------------------------------
;外来勢力の初期化処理
;--------------------------------------
@GAIRAI_INIT(ARG:0)
#DIM LCOUNT
VARSET LOCAL

LOCAL:4 = GET_NEW_COUNTRY()
;作れなかったらなしで
IF LOCAL:4 == -1
	RETURN -1
ENDIF
COUNTRY_COLOR:(LOCAL:4) = 0x483D8B
COUNTRY_SOLDIER:(LOCAL:4) = 20000 + GAIRAI_RANK * 1000
COUNTRY_AI_TYPE:(LOCAL:4) = AI_通常
COUNTRY_EVENT_ID:(LOCAL:4) = 299

FOR LCOUNT, 0, 15
	CALL ADD_VOID_CHARA()
	LOCAL = RESULT
	NAME:LOCAL = 外来人
	CALLNAME:LOCAL = 外来人
	MAXBASE:LOCAL:体力 = 3000
	MAXBASE:LOCAL:気力 = 3000
	MAXBASE:LOCAL:精神力 = 1500
	BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力
	BASE:LOCAL:精神力 = MAXBASE:LOCAL:精神力
	ABL:LOCAL:Ｃ感 = 3
	ABL:LOCAL:欲望 = 8
	ABL:LOCAL:性技 = 3
	ABL:LOCAL:性知識 = 5
	ABL:LOCAL:奉仕 = 1
	ABL:LOCAL:性交 = 10
	ABL:LOCAL:射精 = 5
	EXP:LOCAL:絶頂経験 = 500
	ABL:LOCAL:武闘 = 40 + RAND:20 + 5 * GAIRAI_RANK
	ABL:LOCAL:知略 = 40 + RAND:20 + 5 * GAIRAI_RANK
	ABL:LOCAL:政治 = RAND(30, 60)
	ABL:LOCAL:歌唱 = 10
	ABL:LOCAL:料理 = 10
	TALENT:LOCAL:両刀 = 1
	TALENT:LOCAL:サド = 1
	TALENT:LOCAL:絶倫 = 1
	TALENT:LOCAL:現代兵術 = 1
	TALENT:LOCAL:外来人 = 1
	;所属を野盗勢力にする
	CFLAG:LOCAL:1 = LOCAL:4
	CALL NEWCHARA_INIT(LOCAL)
	IF !LOCAL:3
		LOCAL:3 = LOCAL
		COUNTRY_BOSS:(LOCAL:4) = GET_ID(LOCAL:3)
	ENDIF
NEXT
DVAR:外来勢力出現済み = 1

RETURN LOCAL:4

;--------------------------------------
;外来人の侵攻イベントを実行するSLGイベント。
;判定通ったら素のGAIRAI_RISEを呼ぶだけ。
;25期以降、50期までに徐々に確率が高まっていく。既に発生していたらなし。
;--------------------------------------
@SLG_GAIRAI_RISE()
SIF DVAR:外来勢力出現済み || !GAIRAI_RANK || !((DAY >= 25 && DAY - 25 > RAND:100) || DAY > 50)
	RETURN -1

CALL GAIRAI_RISE()