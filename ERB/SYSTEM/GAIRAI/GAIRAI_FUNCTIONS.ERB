;--------------------------------------
;外来勢力出現イベント
;出現できたら1、できなければ0をかえす
;都市 = 最初に奪う都市 指定しなければ野盗と同一の方式
;メッセージフラグ = 汎用メッセージ表示フラグ　指定しなければ表示
;--------------------------------------
@GAIRAI_RISE(都市 = -1, メッセージフラグ = 1)
#DIM 外来
#DIM メッセージフラグ
#DIM 都市

;外来勢力を作成
CALL GAIRAI_INIT()
外来 = RESULT
;作れなかったら戻す
SIF 外来 == -1
	RETURN 0

;引数で奪う都市が指定されていなければ、BANDIT_RISE_SELECT_CITY()を流用
IF 都市 < 0 || 都市 >= CITY_NUM
	CALL BANDIT_RISE_SELECT_CITY()
ELSE
	都市 = ARG:0
ENDIF
;多分ないはずなんだけど、奪えなかったらもどす
IF 都市 == -1
	RETURN 0
ENDIF

CITY_OWNER(都市) = 外来
CALL CLEAR_CITY_COMMANDER(都市, 0)
CALL CLEAR_CITY_COMMANDER(都市, 1)

IF メッセージフラグ
	DRAWLINE
	PRINTL 
	SETCOLOR COLOR("警告")
	PRINTFORMW 結界の外から突如、外来人が雪崩のように攻めこんできました！
	PRINTFORMW 彼らの電撃的な作戦行動により、%CITY_NAME(都市)%が奪われました
	PRINTFORMW これに伴い、幻想郷の各勢力が、異変解決の準備を始めたようです…
	RESETCOLOR
	DRAWLINE
ENDIF

RETURN 1

;--------------------------------------
;ターンエンド時の外来兵の処理
;外来勢力がなかったら問答無用で撤退イベントが発生するので注意。
;出現済みフラグを準備して、出現済みフラグが立たない限りは呼ばせないこと。
;撤退イベント発生で-1を返すので、シナリオ側かなにかでそれを拾い、終了フラグをたてて呼ばせなくすること。
;--------------------------------------
@TURNEND_GAIRAI
#DIM 外来

;外来勢力の番号を取得(ID:299)
外来 = GET_COUNTRY_FROM_ID(299)
IF 外来 == -1
	SETCOLOR COLOR("注意")
	PRINTFORML 壊滅的な被害を受けた外来人は、結界の外に撤退していきました
	PRINTFORML 幻想郷の勝利です！
	RESETCOLOR
	;外来人を削除する
	LOCAL:1 = CHARANUM
	FOR LOCAL:0, 0, LOCAL:1
		LOCAL:2 = LOCAL:1 - LOCAL:0 - 1
		IF TALENT:(LOCAL:2):外来人
			CALL DELETE_CHARA(LOCAL:2)
		ENDIF
	NEXT
	RETURN -1
ENDIF

;兵力の増える処理
IF DAY % 3 == 0
	LOCAL:0 = MAX(RAND:3000, 1000)
	PRINTFORML 外来人につけば「実験」のおこぼれにあずかれると聞いた野良妖怪たちが、次々と寝返っているようです……
	PRINTFORML 外来人軍の兵力が{LOCAL:0}増加した
	COUNTRY_SOLDIER:(外来) += LOCAL:0
ENDIF

;調教処理
CALL TRAIN_BY_GAIRAI
