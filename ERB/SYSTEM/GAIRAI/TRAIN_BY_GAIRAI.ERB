;-------------------------------------------------
;外来勢力による調教
;-------------------------------------------------
@TRAIN_BY_GAIRAI
#DIM 外来
#DIM 対象
;IDから外来勢力の番号を取得(外来勢力のIDは299)
外来 = GET_COUNTRY_FROM_ID(COUNTRY_GAIRAI)
LOCAL:9 = 1

SIF 外来 == -1
	RETURN -1

FOR 対象, 0, CHARANUM
	;外来勢力に寝返った士官／外来勢力の捕虜となった士官の扱い
	IF (CFLAG:対象:所属 == 外来 && !CFLAG:対象:捕虜先 || CFLAG:対象:9 == 外来) && NO:対象 < 2000
		;調教の様子を表示
		IF NO:対象 < 2000 && TRAIN_IS_SHOW_TEXT(対象) && (!TALENT:対象:研究用奴隷の烙印 || (TALENT:対象:研究用奴隷の烙印 && CFLAG:対象:所属 != 外来) || (TALENT:対象:研究用奴隷の烙印 && !RAND:3))
			IF LOCAL:9
				LOCAL:9 = 0
				DRAWLINE
				PRINTW 外来人に捕らえられた士官が「研究」されているようです…
			ENDIF
			PRINTL 
			CALL GAIRAI_TRAIN_MSG(対象)
			
			;性処理と同等の経験を取得させる
			LOCAL:1 = 0
			LOCAL:1 += MAX(ABL:対象:Ｃ感 - 1, 0)
			LOCAL:1 += MAX(ABL:対象:Ｖ感 - 1, 0) * 3
			LOCAL:1 += MAX(ABL:対象:Ａ感 - 1, 0) * 3
			LOCAL:1 += MAX(ABL:対象:Ｂ感 - 1, 0)
			LOCAL:1 += MAX(ABL:対象:Ｍ感 - 1, 0) / 2
			LOCAL:1 = LOCAL:1 * (ABL:対象:欲望 + 2) / 2
			IF PENIS(対象)
				EXP:対象:Ｃ感経験値 += ABL:対象:Ｃ感 + 1 + RAND:3
				EXP:対象:Ａ感経験値 += ABL:対象:Ａ感 * 30 + 20 + RAND:16
			ELSE
				EXP:対象:Ｃ感経験値 += ABL:対象:Ｃ感 * 5 + 5 + RAND:11
				EXP:対象:Ｖ感経験値 += ABL:対象:Ｖ感 * 20 + 15 + RAND:11
				EXP:対象:Ａ感経験値 += ABL:対象:Ａ感 * 20 + 15 + RAND:11
			ENDIF
			EXP:対象:Ｂ感経験値 += ABL:対象:Ｂ感 * 4 + 4 + RAND:9
			EXP:対象:Ｍ感経験値 += ABL:対象:Ｍ感 * 4 + 4 + RAND:9
			EXP:対象:欲望経験値 += ABL:対象:欲望 * 3 + 2 + LOCAL:1 + RAND:7
			EXP:対象:奉仕経験値 += ABL:対象:奉仕 * 2 + 5 + RAND:11
			EXP:対象:性交経験値 += ABL:対象:性交 * 5 + 15 + RAND:11
			EXP:対象:性技経験値 += ABL:対象:性技 + 2 + RAND:4
			IF IS_MALE(対象) && GAIRAI_SEX_VS(対象) != 1
				EXP:対象:ＢＬ経験値 += 25 + RAND:11
			ENDIF
			EXP:対象:精愛経験値 += ABL:対象:精愛 * 10 + 30 + RAND:21
			IF IS_FEMALE(対象) && GAIRAI_SEX_VS(対象) == 1
				EXP:(対象):レズ経験値 += 25 + RAND:11
			ENDIF
			IF PENIS(対象)
				EXP:対象:射精経験値 += ABL:対象:射精 * 10 + 30 + RAND:21
			ENDIF
			EXP:対象:キス経験 += 2 + RAND:8
			EXP:対象:口淫経験 += 3 + RAND:10
			EXP:対象:絶頂経験 += LOCAL:1 * (60 + RAND:81) / 100
			EXP:対象:輪姦経験 += 3 + RAND(2, MAX(3, MIN(8, ABL:対象:欲望)))
			;中出しと妊娠判定
			IF CONFIG:1 == 0 && (VAGINA(対象) || TALENT:(対象):孕み可能) && GAIRAI_SEX_VS(対象) != 1
				FOR LOCAL:1, 440, 480
					TCVAR:(対象):(LOCAL:1) = 0
				NEXT
				CALL RECORD_INJECTION(対象, -11, 4)
				CALL CHECK_PREGNANT(対象)
			ENDIF

			;調教カウンタを加算
			CFLAG:対象:調教カウンタ ++
			
			;能力上昇メッセを表示
			CALL TRAIN_AUTO_ABLUP(対象, 0)

			;「解放カウンタが一定以上で、堕ちていない」か「ゴブリン所属でなく、堕ちている」で寝返る
			IF (CFLAG:対象:調教カウンタ >= TRAIN_FALLEN_TERM:3 && !TALENT:対象:研究用奴隷の烙印) || (CFLAG:対象:所属 != 外来 && TALENT:対象:研究用奴隷の烙印)
				;調教の様子を表示
				IF TRAIN_IS_SHOW_TEXT(対象)
					PRINTFORML ・
					PRINTFORML ・
					PRINTFORML ・
					IF TALENT:(対象):研究用奴隷の烙印
						PRINTFORML 最初こそ抵抗していた%ANAME(対象)%だったが、既に外来人に屈したその体は与えられる快楽に敏感に反応してしまう
						PRINTFORML 触れられるたび、犯されるたび、己が研究用の奴隷に過ぎないことを、%ANAME(対象)%は思い出していく
						PRINTFORML 絶え間なく続けられる陵辱に、ほどなくして%ANAME(対象)%は快感を受け入れ、幸せそうな表情で自ら奉仕を始めた…
					ELSE
						;MASTERが外来所属でないなら、裏切り
						IF CFLAG:MASTER:所属 != 外来
							PRINTFORMW 外来人たちの度重なる研究により、浅ましく快楽を求める雌へとなりはてた%ANAME(対象)%は、\@IS_LOVER(対象) ? 愛して「いた」%ANAME(MASTER)% # 仲間 \@を裏切った
							PRINTFORMW そして今後も研究してもらうために、研究用の奴隷となることに同意してしまった……
							IF TALENT:(対象):烙印 && CFLAG:MASTER:所属 != 外来
								PRINTFORML %ANAME(対象)%が%ANAME(MASTER)%専用であることを示す烙印の上から、
								PRINTFORMW 新たに%ANAME(対象)%が外来人の研究用奴隷となったことを示す烙印が焼き入れられた
								TALENT:(対象):烙印 = 0
							ELSE
								PRINTFORMW %ANAME(対象)%の体に、外来人の研究用奴隷の証である烙印が焼き入れられた
							ENDIF
							SIF IS_LOVER(対象)
								PRINTFORMW 烙印の熱が冷めるころには%ANAME(対象)%の頭の中から、かつて愛していた%ANAME(MASTER)%のことが、綺麗さっぱり消え失せていた……
						;MASTERが外来所属なら、仲間に加わる
						ELSE
							PRINTFORMW %ANAME(MASTER)%や外来人たちの度重なる研究により、浅ましく快楽を求める雌へとなりはてた%ANAME(対象)%は、己の本分を思い知った
							PRINTFORMW そして今後も研究してもらうために、研究用の奴隷となることに同意した……
						ENDIF
					ENDIF
				ENDIF
				CALL KOJO_SPECIAL_FALLEN_EVENT(対象, COUNTRY_GAIRAI)
				SETCOLOR COLOR("警告")
				PRINTFORMW %ANAME(対象)%は外来勢力に寝返った…
				RESETCOLOR
				CFLAG:対象:1 = 外来
				CFLAG:対象:9 = 0
				TALENT:対象:研究用奴隷の烙印 = 1
				CALL SET_PIERCE_RANKED(対象, 0, ピアス_ラビア, ピアス_ペニス, ピアス_クリトリス, ピアス_乳首, ピアス_へそ, ピアス_耳, ピアス_舌, ピアス_鼻)
				SIF RESULT != -1
					CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_PIERCE_NAME(RESULT)%に、研究対象であることを示すピアスが取り付けられた……", COLOR("桃"), "W")
				SIF CFLAG:MASTER:所属 != 外来
					CALL LOSE_RELATION_TALENT(対象)
			ENDIF
			WAIT
		ENDIF
		

	ENDIF
NEXT

