@REV_TRAIN_GOBLIN
#DIM 候補, 3000
#DIM 候補数
#DIM 参加者, 8
#DIM 参加者数
#DIM ゴブリン
#DIM ループ数
VARSET 候補, -1
VARSET 候補数
VARSET 参加者, -1
VARSET 参加者数

ゴブリン = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_ホフゴブリン))

;四回ごとに誘われる
IF FLAG:逆調教回数 > 0 && FLAG:逆調教回数 % 4 == 0
	PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように調教部屋で拘束され、
	PRINTFORMW あとはホフゴブリン達に汚されるのを待つのみとなった
	PRINTFORML やってきたホフゴブリンは、どうだ、そろそろ俺達の奴隷になる気になったか、と尋ねた
	PRINTFORMW 奴隷になることを認めれば、ある程度の行動の自由はくれてやるぞ、と……
	PRINTFORML このまま拒否し続けても、こいつらは自分を汚し続けるだろう
	PRINTFORML 首を縦に振れば、いくらかは楽になれるのかもしれない
	PRINTFORMW どうするべきだろうか……
	CALL ASK_YN("諦めて受け入れる（奴隷化)", "こんなやつらの奴隷なんてごめんだ")
	IF RESULT == 0
		PRINTFORML %ANAME(MASTER)%が頷くと、ホフゴブリンはニヤリと笑った
		IF HAS_PENIS(MASTER)
			CALL SET_PIERCE(MASTER, ピアス_ペニス, 0)
			IF RESULT
				CALL COLORPRINT(@"そしてそのペニスに、痛々しいピアスを取り付けた……", カラー_ピンク, "W")
			ELSE
				CALL COLORPRINT(@"そしてそのペニスに、二度と消えない焼き印を刻んだ……", カラー_ピンク, "W")
			ENDIF
		ELSE
			CALL SET_PIERCE(MASTER, ピアス_乳首, 0)
			IF RESULT
				CALL COLORPRINT(@"そしてその乳首に、痛々しいピアスを取り付けた……", カラー_ピンク, "W")
			ELSE
				CALL COLORPRINT(@"そしてその乳房に、二度と消えない焼き印を刻んだ……", カラー_ピンク, "W")
			ENDIF
		ENDIF
		PRINTFORMW さらにその首に、家畜用の首輪が嵌められた
		SETCOLOR カラー_注意
		PRINTFORMW %ANAME(MASTER)%はホフゴブリンの性奴隷となりました
		RESETCOLOR
		SETBIT TALENT:MASTER:特殊勢力陥落系, 特殊勢力_ホフゴブリン
		CALL CHANGE_COUNTRY(MASTER, ゴブリン, 1)
		CALL PLAYER_FALLEN_TO_SP_COUNTRY(ゴブリン)
		RETURN 1
	ELSE
		PRINTFORML そうかよと、%ANAME(MASTER)%の拒否を特に気にした風でもなくホフゴブリンは受け止める
		PRINTFORML ならもっと犯して、自分の立場ってのを教えてやらなくちゃなぁと、その顔は下卑た笑みを浮かべた……
		PRINTFORMW 彼らはそのまま、%ANAME(MASTER)%に群がっていく……
	ENDIF
ENDIF
;全員の調教参加フラグを解除
CVARSET CFLAG, 6, 0

;1/3の確率でゴブリンに寝返った顔見知りのキャラが調教者になる　ただしマスターが男なら確実に来る
IF IS_MALE(MASTER) || RAND:3 
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):面識 && !IS_SP_CHARA(LOCAL) && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:捕虜先 && GETBIT(TALENT:(LOCAL:0):特殊勢力陥落系, 特殊勢力_ホフゴブリン) && !RAND:2
			候補:候補数 = LOCAL:0
			候補数 ++
		ENDIF
	NEXT
	IF 候補数
		CALL FISHER_YATES_SHAFFLE(候補数)
		FOR LOCAL, 0, MIN(候補数, 4)
			参加者:参加者数 = 候補:(SHAFFLE_ARRAY:(LOCAL:0))
			CFLAG:(参加者:参加者数):調教参加フラグ = 1
			参加者数 ++
		NEXT
		IF 参加者数
			LOCALS:0 = 
			FOR LOCAL:0, 0, 参加者数
				IF LOCALS:0 == ""
					LOCALS:0 = %ANAME(参加者:LOCAL)%
				ELSE
					LOCALS:0 = %LOCALS:0%、%ANAME(参加者:LOCAL)%
				ENDIF
			NEXT
			CALL SINGLE_DRAWLINE
			PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように調教部屋で拘束され、
			PRINTFORMW あとは男達に汚されるのを待つのみとなった
			IF 参加者数
				PRINTFORMW やってきたホフゴブリンは、%ANAME(MASTER)%が良く見知った相手を連れてきていた
				PRINTFORMW %LOCALS:0%…
				PRINTFORMW 首には家畜用の首輪がかけられ、乳房には痛々しいピアス、下腹に「奴隷」の文字とハートの焼き印が押されている
				PRINTFORMW 惨めな姿なのだろうが、幸せそうな表情を浮かべている。その瞳がどこか虚ろなことさえのぞけば
				PRINTFORMW 事前にたっぷりと犯されてきたのだろう、全身精液まみれで、あらゆる穴からは大量の濁液が滴り落ちている
				IF HAS_PENIS(MASTER)
					PRINTFORMW 性欲に満ちた視線は、%ANAME(MASTER)%ではなく、%ANAME(MASTER)%のいきり立った男根に注がれている
					PRINTFORMW ……だが、「今の主人」のモノと比べたのだろう、どこか物足りなげでもあった……
				ELSE
					PRINTFORMW 性欲に満ちた視線は、%ANAME(MASTER)%というより、%ANAME(MASTER)%のカラダに対して注がれていた…
				ENDIF
				PRINTFORMW ホフゴブリンはニヤニヤとしながら、俺たちの奴隷をお前にも使わせてやるよと言った
				PRINTFORMW かつて%ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@が%ANAME(MASTER)%と知り合い「だった」ことを、掴んでいるようだ……
				PRINTFORMW 使ってやるから股をひらけ、ホフゴブリンがそのように言うと、%ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@は従順に従った……
			ENDIF
			FLAG:特殊勢力逆調教知り合いフラグ = 1
		ENDIF
	ENDIF
ENDIF
IF 参加者数
	ループ数 = MIN(4, VARSIZE("参加者") - 参加者数)
ELSE
	ループ数 = 4
ENDIF

FOR LOCAL:0, 0, ループ数
	;確率で犬豚が対象に
	IF !RAND:15
		FOR LOCAL:1, 0, CHARANUM
			IF CFLAG:(LOCAL:1):所属 == ゴブリン && IS_ANIMAL(LOCAL:1) && !CFLAG:(LOCAL:1):調教参加フラグ
				参加者:参加者数 =  LOCAL:1
				参加者数 ++
				CFLAG:(LOCAL:1):調教参加フラグ = 1
				BREAK
			ENDIF
		NEXT
		CALL ADD_ANIMAL(@"\@ RAND:2 ? 犬 # 豚 \@", LOCAL:6)
		CFLAG:(CHARANUM -1):調教参加フラグ = 1
		参加者:参加者数 =  CHARANUM - 1
		参加者数 ++
	;普通にゴブリンを選出
	ELSE
		FOR LOCAL:1, 0, CHARANUM
			IF TALENT:(LOCAL:1):特殊勢力素質 == 特殊勢力_ホフゴブリン && !CFLAG:(LOCAL:1):調教参加フラグ && CFLAG:(LOCAL:1):所属 == ゴブリン
				参加者:参加者数 =  LOCAL:1
				参加者数 ++
				CFLAG:(LOCAL:1):調教参加フラグ = 1
				CFLAG:(LOCAL:1):TEXT_LAST非表示 = 1
				CFLAG:(LOCAL:1):能力固定フラグ = 1
				BREAK
			ENDIF
		NEXT
	ENDIF
NEXT

FOR LOCAL:0, 0, 参加者数
	CFLAG:(参加者:(LOCAL:0)):調教参加フラグ = 1
NEXT
;逆調教モード
FLAG:調教モード = 4
;ウフフモード
FLAG:ウフフフラグ = 1
;アイテムを一通り追加
ITEM:A_ローター = 1
ITEM:A_バイブ = 1
ITEM:A_アナルバイブ = 1
ITEM:A_ペニスバンド = 1
ITEM:A_オナホール = 1
ITEM:A_クリキャップ = 1
ITEM:A_ニプルキャップ = 1
ITEM:A_針 = 1
ITEM:A_鞭 = 1
ITEM:A_目隠し = 1
ITEM:A_鼻フック = 1
ITEM:A_口枷 = 1
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
;回数をカウント
FLAG:逆調教回数 ++
;解放カウンタをリセット
CFLAG:MASTER:解放カウンタ = 0

