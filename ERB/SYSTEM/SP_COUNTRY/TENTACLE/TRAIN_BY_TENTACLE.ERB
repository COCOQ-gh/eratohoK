;-------------------------------------------------
;触手による調教
;-------------------------------------------------
@TRAIN_BY_TENTACLE
#DIM 触手
#DIM 対象

触手 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_触手))

SIF 触手 == -1 || CONFIG:200
	RETURN -1

FOR 対象, 0, CHARANUM
	SIF !IS_TRAIN_BY_SP_COUNTRY(対象, 触手)
		CONTINUE
	IF LOCAL:1
		LOCAL:1 = 0
		DRAWLINE
		PRINTFORMW 触手に囚われた士官が、彼らの繁殖のため使われているようです……
		PRINTFORML 
	ENDIF
	
	CALL TENTACLE_TRAIN_MSG(対象)
	CALL FUCK_GANGBANG_SP(対象, GET_SPERM_ID("触手"), @"触手", @"触手", 触手)
	CFLAG:対象:調教カウンタ++

	;あまり長く捕らえられていると寝返る
	IF IS_BETRAY_TO_SP_COUNTRY(対象, 触手)
		SETCOLOR COLOR("警告")
		IF !GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_触手)
			PRINTFORMW 触手たちの陵辱で、%ANAME(対象)%は心も体も折れてしまった
			PRINTFORMW 頃合いを見計らって、触手たちは%ANAME(対象)%の肉体に寄生した……
			IF TALENT:対象:烙印
				PRINTFORML %ANAME(対象)%が%ANAME(MASTER)%専用であることを示す烙印を、
				PRINTFORMW 寄生した触手は超常的な力でかき消した
				TALENT:対象:烙印 = 0
			ELSE
				PRINTFORMW 触手は%ANAME(対象)%の身体に巣食い始めた
			ENDIF
			SIF IS_LOVER(対象)
				PRINTFORMW かつて愛していた%ANAME(MASTER)%のことが、%ANAME(対象)%の頭の中から削除された……
		ELSE
			PRINTFORMW 触手たちは再び%ANAME(対象)%に寄生し、彼女の意識に自らが触手の苗床に過ぎないという刷り込みを行った
			PRINTFORMW 洗脳された%ANAME(対象)%は、触手を増やすという唯一の目的のため、その身体を捧げることにした……
		ENDIF
		RESETCOLOR
		
		CALL KOJO_SPECIAL_FALLEN_EVENT(対象, SP_COUNTRY_ID:(特殊勢力_触手))

		CFLAG:対象:所属 = 触手
		CFLAG:対象:捕虜先 = 0
		SETBIT TALENT:対象:特殊勢力陥落系, 特殊勢力_触手
		SIF SP_TRAIN_IS_DECREASE_CFLAG(対象, 触手)
			CALL LOSE_RELATION_TALENT(対象)
		CALL COLORPRINTFORM(@"触手たちは、早速%ANAME(対象)%を苗床として利用することにした……", COLOR("桃"), "W")
		FOR LOCAL, 0, 5
			CALL FUCK_GANGBANG_SP(対象, GET_SPERM_ID("触手"), @"触手", @"触手", 触手)
		NEXT
	ENDIF
	WAIT
NEXT