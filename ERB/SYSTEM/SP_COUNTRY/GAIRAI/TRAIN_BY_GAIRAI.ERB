;-------------------------------------------------
;外来勢力による調教
;-------------------------------------------------
@TRAIN_BY_GAIRAI
#DIM 外来
#DIM 対象
;IDから外来勢力の番号を取得(外来勢力のIDは299)
外来 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_外来人))
LOCAL:9 = 1

SIF 外来 == -1 || CONFIG:200
	RETURN -1

FOR 対象, 0, CHARANUM
	;外来勢力に寝返った士官／外来勢力の捕虜となった士官の扱い
	IF (CFLAG:対象:所属 == 外来 && !CFLAG:対象:捕虜先 || CFLAG:対象:捕虜先 == 外来) && NO:対象 < 2000
		;個人的な嗜好による改造
		IF !TALENT:対象:GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人)
			;軟禁中はスキップ
			SIF CFLAG:対象:捕虜先 == 外来 && CFLAG:対象:29
				CONTINUE
			;MASTER以外の身内はスキップ
			SIF CFLAG:対象:所属 == 外来 && 対象 != MASTER
				CONTINUE
		ENDIF
		
		;調教の様子を表示
		IF !TALENT:対象:特殊勢力素質 && TRAIN_IS_SHOW_TEXT(対象) && (!GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人) || (GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人) && CFLAG:対象:所属 != 外来) || (GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人) && !RAND:3))
			IF LOCAL:9
				LOCAL:9 = 0
				DRAWLINE
				PRINTW 外来人に捕らえられた士官が「研究」されているようです…
			ENDIF
			PRINTL 
			
			CALL GAIRAI_TRAIN_MSG(対象)
			IF IS_FEMALE(対象) && GAIRAI_SEX_VS(対象) == 1
				CALL FORCE_FUCK(対象, SP_NAME_TO_ID("外来人"), @"\@ RAND:2 ? # 外来人の唇 ? 外来人の秘唇\@", @"外来人", 2, SP_TRAIN_IS_DECREASE_CFLAG(対象, SP_COUNTRY_ID:(特殊勢力_外来人)), 0, 1)
			ELSE
				CALL FORCE_FUCK(対象, SP_NAME_TO_ID("外来人"), @"\@ RAND:2 ? # 外来人の唇 ? 外来人のペニス\@", @"外来人", 2, SP_TRAIN_IS_DECREASE_CFLAG(対象, SP_COUNTRY_ID:(特殊勢力_外来人)))
			ENDIF
			
			;調教カウンタを加算
			CFLAG:対象:調教カウンタ ++

			;「解放カウンタが一定以上で、堕ちていない」か「ゴブリン所属でなく、堕ちている」で寝返る
			IF (CFLAG:対象:調教カウンタ >= TRAIN_FALLEN_TERM:3 && !GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人)) || (CFLAG:対象:所属 != 外来 && GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人))
				;調教の様子を表示
				IF TRAIN_IS_SHOW_TEXT(対象)
					PRINTFORML ・
					PRINTFORML ・
					PRINTFORML ・
					IF GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人)
						PRINTFORML 最初こそ抵抗していた%ANAME(対象)%だったが、既に外来人に屈したその体は与えられる快楽に敏感に反応してしまう
						PRINTFORML 触れられるたび、犯されるたび、己が研究用の奴隷に過ぎないことを、%ANAME(対象)%は思い出していく
						PRINTFORML 絶え間なく続けられる陵辱に、ほどなくして%ANAME(対象)%は快感を受け入れ、幸せそうな表情で自ら奉仕を始めた…
					ELSE
						;MASTERが外来所属でないなら、裏切り
						IF CFLAG:MASTER:所属 != 外来
							PRINTFORMW 外来人たちの度重なる研究により、浅ましく快楽を求める雌へとなりはてた%ANAME(対象)%は、\@IS_LOVER(対象) ? 愛して「いた」%ANAME(MASTER)% # 仲間 \@を裏切った
							PRINTFORMW そして今後も研究してもらうために、研究用の奴隷となることに同意してしまった……
							IF TALENT:(対象):烙印 && CFLAG:MASTER:所属 != 外来
								PRINTFORML %ANAME(対象)%が%ANAME(MASTER)%専用であることを示す烙印の上から、
								PRINTFORMW 新たに%ANAME(対象)%が外来人の研究用奴隷となったことを示す烙印が焼き入れられた
								TALENT:(対象):烙印 = 0
							ELSE
								PRINTFORMW %ANAME(対象)%の体に、外来人の研究用奴隷の証である烙印が焼き入れられた
							ENDIF
							SIF IS_LOVER(対象)
								PRINTFORMW 烙印の熱が冷めるころには%ANAME(対象)%の頭の中から、かつて愛していた%ANAME(MASTER)%のことが、綺麗さっぱり消え失せていた……
						;MASTERが外来所属なら、仲間に加わる
						ELSE
							PRINTFORMW %ANAME(MASTER)%や外来人たちの度重なる研究により、浅ましく快楽を求める雌へとなりはてた%ANAME(対象)%は、己の本分を思い知った
							PRINTFORMW そして今後も研究してもらうために、研究用の奴隷となることに同意した……
						ENDIF
					ENDIF
				ENDIF
				CALL KOJO_SPECIAL_FALLEN_EVENT(対象, SP_COUNTRY_ID:(特殊勢力_外来人))
				SETCOLOR COLOR("警告")
				PRINTFORMW %ANAME(対象)%は外来勢力に寝返った…
				RESETCOLOR
				CFLAG:対象:1 = 外来
				CFLAG:対象:9 = 0
				SETBIT TALENT:対象:特殊勢力陥落系, 特殊勢力_外来人
				SELECTCASE RAND:10
					CASE IS < 3
						CALL SET_PIERCE_RANDOM(対象, 0)
						SIF RESULT != -1
							CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_PIERCE_NAME(RESULT)%に、研究対象であることを示すピアスが取り付けられた……", COLOR("桃"), "W")
					CASE IS < 7
						CALL SET_TATOO_RANDOM(対象, "研究用奴隷", 0)
						SIF RESULT != -1
							CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_TATOO_NAME(RESULT)%に、「%TATOO:対象:RESULT%」とタトゥーが刻まれた……", COLOR("桃"), "W")
					CASEELSE
						IF GET_INMOU(対象) >= 陰毛_標準
						CALL COLORPRINTFORM(@"服従の証として、%ANAME(対象)%の陰毛はハート型に整えられた……", COLOR("桃"), "W")
						TALENT:対象:陰毛目標値 = 陰毛_ハート型
						TALENT:対象:陰毛現在値 = 陰毛_ハート型
					ELSE
						CALL COLORPRINTFORM(@"服従の証として、%ANAME(対象)%の陰毛は永久脱毛された……", COLOR("桃"), "W")
						TALENT:対象:陰毛目標値 = 陰毛_パイパン
						TALENT:対象:陰毛現在値 = 陰毛_パイパン
					ENDIF
				ENDSELECT
				SIF CFLAG:MASTER:所属 != 外来
					CALL LOSE_RELATION_TALENT(対象)
			ENDIF
			WAIT
		ENDIF
		

	ENDIF
NEXT

