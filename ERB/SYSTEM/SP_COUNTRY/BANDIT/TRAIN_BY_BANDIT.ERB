;-------------------------------------------------
;調教メッセージ
;-------------------------------------------------
@TRAIN_BY_BANDIT()
#DIM 野盗
#DIM 対象

野盗 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_野盗))

SIF 野盗 == -1 || CONFIG:200
	RETURN -1


FOR LOCAL:0, 0, CHARANUM
	対象 = LOCAL:0
	SIF !IS_TRAIN_BY_SP_COUNTRY(対象, 野盗)
		CONTINUE
	IF !LOCAL:1
		DRAWLINE
		PRINTFORMW 野盗に囚われた各国の仕官が陵辱されているようです……
		PRINTFORML
		LOCAL:1 = 1
	ENDIF

	;メッセ表示
	CALL BANDIT_TRAIN_MSG(対象)
	CALL FORCE_FUCK(対象, SP_NAME_TO_ID("野盗"), @"野盗の\@ RAND:2 ? ペニス # 唇\@", "野盗", 2, SP_TRAIN_IS_DECREASE_CFLAG(対象, 野盗))
	CFLAG:(LOCAL:0):調教カウンタ ++
	
	;あまり長く捕らえられていると寝返る
	IF IS_BETRAY_TO_SP_COUNTRY(対象, 野盗)
		SETCOLOR COLOR("警告")
		IF !GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_野盗)
			PRINTFORMW ……長きにわたる調教生活により、%ANAME(対象)%の心は折れてしまったようだ
			SIF IS_LOVER(対象)
				PRINTFORML %ANAME(MASTER)%への愛情よりも、野盗どもに使われる快楽が上回ったのだ
			PRINTFORMW %ANAME(対象)%は野盗の肉便器となり、寝返った
		ELSE
			PRINTFORMW ……野盗の調教を受け、%ANAME(対象)%は彼らの肉便器に過ぎない己の身分を思い出した
			PRINTFORMW %ANAME(対象)%は野盗の肉便器に復職した
		ENDIF
		RESETCOLOR
		
		CALL KOJO_SPECIAL_FALLEN_EVENT(対象, SP_COUNTRY_ID:(特殊勢力_野盗))

		CFLAG:対象:所属 = 野盗
		CFLAG:対象:捕虜先 = 0
		SIF !SP_TRAIN_IS_DECREASE_CFLAG(対象, 野盗)
			CALL LOSE_RELATION_TALENT(対象)
		SETBIT TALENT:対象:特殊勢力陥落系, 特殊勢力_野盗

		$HUKUJU_LOOP
		SELECTCASE RAND:10
			CASE IS < 4
				CALL SET_PIERCE_RANDOM(対象, 0)
				SIF RESULT != -1
					CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_PIERCE_NAME(RESULT)%に、服従の証のピアスが取り付けられた……", COLOR("桃"), "W")
			CASE IS < 7
				SIF IS_MALE(対象)
					GOTO HUKUJU_LOOP
				CALL SET_TATOO_RANDOM(対象, STR_FOR_TATOO(-1), 0)
				SIF RESULT != -1
					CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_TATOO_NAME(RESULT)%に、「%TATOO:対象:RESULT%」とタトゥーが刻まれた……", COLOR("桃"), "W")
			CASEELSE
				IF GET_INMOU(対象) >= 陰毛_標準
				CALL COLORPRINTFORM(@"服従の証として、%ANAME(対象)%の陰毛はハート型に整えられた……", COLOR("桃"), "W")
				TALENT:対象:陰毛目標値 = 陰毛_ハート型
				TALENT:対象:陰毛現在値 = 陰毛_ハート型
			ELSE
				CALL COLORPRINTFORM(@"服従の証として、%ANAME(対象)%の陰毛は永久脱毛された……", COLOR("桃"), "W")
				TALENT:対象:陰毛目標値 = 陰毛_パイパン
				TALENT:対象:陰毛現在値 = 陰毛_パイパン
			ENDIF
		ENDSELECT
	ENDIF
	WAIT
NEXT