;--------------------------------------
;サキュバス出現イベント
;出現できたら1、できなければ0をかえす
;--------------------------------------
@SUCCUBUS_RISE()
#DIM サキュバス

;サキュバス勢力を作成
CALL SUCCUBUS_INIT()
サキュバス = RESULT
;作れなかったら戻す
SIF サキュバス == -1
	RETURN 0

CALL SP_COUNTRY_RISE(サキュバス)

SIF !RESULT
	RETURN 0

SP_COUNTRY_APPEARED:特殊勢力_サキュバス = 1
RETURN 1


;--------------------------------------
;蜂起イベントが発生するかの判定
;--------------------------------------
@SUCCUBUS_RISE_RATE()
;既に出現しているか、勢力なし設定なら発動しない
SIF SP_COUNTRY_APPEARED:特殊勢力_サキュバス || !SP_COUNTRY_RANK:(特殊勢力_サキュバス)
	RETURN 0

;25期以降、確率判定をクリアしていること
SIF !(DAY >= 25 && DAY - 15 > RAND:100)
	RETURN 0
RETURN 1

;--------------------------------------
;蜂起イベント用メッセージ関数
;--------------------------------------
@SUCCUBUS_RISE_MSG(勢力, 対象)
#DIM 勢力
#DIM 対象

DRAWLINE
SETCOLOR COLOR("警告")
PRINTFORMW 夢の世界の住人であるサキュバス達が、突如として幻想郷に流れ込んできました！
PRINTFORMW 彼女らは幻想郷を淫らな世界に造り替えるべく、%CITY_NAME:(対象)%\@ GET_OWN_CITY(勢力) > 1 ? など # \@を占拠したようです
PRINTFORMW これに伴い、幻想郷の各勢力が異変解決の準備を始めたようです…
RESETCOLOR


;--------------------------------------
;ターンエンド時のサキュバスの処理
;--------------------------------------
@TURNEND_SUCCUBUS
;6ターンに1回、ドレイン
CALL SUCCUBUS_DRAIN(DAY % 6 == 0)


;--------------------------------------
;滅亡時の処理
;--------------------------------------
@SUCCUBUS_DESTROY_MSG
DRAWLINE
SETCOLOR COLOR("注意")
PRINTFORML
PRINTFORML
PRINTFORML
PRINTFORMW 壊滅的な被害を受けたサキュバスは、夢の世界に撤退していきました
PRINTFORMW ドレミー・スイートが夢の世界への道をしっかり施錠したので、当面は大丈夫でしょう……
PRINTFORML
PRINTFORML
PRINTFORML
RESETCOLOR

;--------------------------------------
;サキュバスの初期化処理
;--------------------------------------
@SUCCUBUS_INIT(ARG:0)
#DIM LCOUNT
VARSET LOCAL

LOCAL:4 = GET_NEW_COUNTRY()
;作れなかったらなしで
IF LOCAL:4 == -1
	RETURN -1
ENDIF
COUNTRY_COLOR:(LOCAL:4) = 0x8B008B
COUNTRY_SOLDIER:(LOCAL:4) = 20000 + SP_COUNTRY_RANK:(特殊勢力_サキュバス) * 2000
COUNTRY_AI_TYPE:(LOCAL:4) = AI_好戦
COUNTRY_EVENT_ID:(LOCAL:4) = SP_COUNTRY_ID:(特殊勢力_サキュバス)

CALL ADD_VOID_CHARA()
LOCAL = RESULT
SELECTCASE SP_COUNTRY_BOSSNAME:(特殊勢力_サキュバス)
	CASE 0
		NAME:LOCAL = サキュバス
		CALLNAME:LOCAL = サキュバス
		CSTR:LOCAL:0 = サキュバス
		CSTR:LOCAL:1 = サキュバス
		CSTR:LOCAL:3 = サキュバス
		CSTR:LOCAL:4 = サキュバス
		CSTR:LOCAL:6 = サキュバス
		CSTR:LOCAL:7 = サキュバス
		CSTR:LOCAL:8 = サキュバス
	CASE 1
		NAME:LOCAL = サキュバスクイーン
		CALLNAME:LOCAL = サキュバスクイーン
		CSTR:LOCAL:0 = サキュバスクイーン
		CSTR:LOCAL:1 = サキュバスクイーン
		CSTR:LOCAL:3 = サキュバスクイーン
		CSTR:LOCAL:4 = サキュバスクイーン
		CSTR:LOCAL:6 = サキュバスクイーン
		CSTR:LOCAL:7 = サキュバスクイーン
		CSTR:LOCAL:8 = サキュバスクイーン
	CASE 2
		TARGET = LOCAL
		CALL SET_RANDOM_NAME_K(1)
		NAME:LOCAL = %CSTR:LOCAL:1%
		CALLNAME:LOCAL = %CSTR:LOCAL:1%
ENDSELECT
MAXBASE:LOCAL:体力 = 3000
MAXBASE:LOCAL:気力 = 3000
MAXBASE:LOCAL:精神力 = 1500
BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
BASE:LOCAL:気力 = MAXBASE:LOCAL:気力
BASE:LOCAL:精神力 = MAXBASE:LOCAL:精神力
ABL:LOCAL:Ｃ感 = 3
ABL:LOCAL:Ｂ感 = 3
ABL:LOCAL:Ｖ感 = 3
ABL:LOCAL:Ａ感 = 3
ABL:LOCAL:Ｍ感 = 3
ABL:LOCAL:欲望 = 8
ABL:LOCAL:性技 = 4
ABL:LOCAL:性知識 = 5
ABL:LOCAL:奉仕 = 3
ABL:LOCAL:性交 = 3
ABL:LOCAL:精愛 = 3
ABL:LOCAL:レズ = 3
ABL:LOCAL:肝臓 = 3
EXP:LOCAL:絶頂経験 = 500
ABL:LOCAL:武闘 = 50 + RAND(5, 15) + 3 * SP_COUNTRY_RANK:(特殊勢力_サキュバス)
ABL:LOCAL:知略 = 50 + RAND(5, 15) + 5 * SP_COUNTRY_RANK:(特殊勢力_サキュバス)
ABL:LOCAL:政治 = 50 + RAND(5, 15) + 5 * SP_COUNTRY_RANK:(特殊勢力_サキュバス)
ABL:LOCAL:歌唱 = 30
ABL:LOCAL:料理 = 30
TALENT:LOCAL:性別 = 1
TALENT:LOCAL:絶倫 = 1
TALENT:LOCAL:巨乳 = 1
TALENT:LOCAL:美脚 = 1
TALENT:LOCAL:美尻 = 1
TALENT:LOCAL:特殊勢力素質 = 特殊勢力_サキュバス
TALENT:LOCAL:陰毛現在値 = 陰毛_標準
TALENT:LOCAL:陰毛目標値 = 陰毛_標準
TALENT:LOCAL:Ｖ締まり = GET_DEFAULT_TIGHTNESS("名器")
TALENT:LOCAL:Ａ締まり = GET_DEFAULT_TIGHTNESS("名器")
;所属をサキュバスにする
CFLAG:LOCAL:1 = LOCAL:4
CALL NEWCHARA_INIT(LOCAL)
COUNTRY_BOSS:(LOCAL:4) = GET_ID(LOCAL)


FOR LCOUNT, 0, 14
	CALL ADD_VOID_CHARA()
	LOCAL = RESULT
	NAME:LOCAL = サキュバス%UNICODE(0xFF21 + LCOUNT)%
	CALLNAME:LOCAL = サキュバス%UNICODE(0xFF21 + LCOUNT)%
	MAXBASE:LOCAL:体力 = 3000
	MAXBASE:LOCAL:気力 = 3000
	MAXBASE:LOCAL:精神力 = 1500
	BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力
	BASE:LOCAL:精神力 = MAXBASE:LOCAL:精神力
	ABL:LOCAL:Ｃ感 = 3
	ABL:LOCAL:Ｂ感 = 3
	ABL:LOCAL:Ｖ感 = 3
	ABL:LOCAL:Ａ感 = 3
	ABL:LOCAL:Ｍ感 = 3
	ABL:LOCAL:欲望 = 8
	ABL:LOCAL:性技 = 4
	ABL:LOCAL:性知識 = 5
	ABL:LOCAL:奉仕 = 3
	ABL:LOCAL:性交 = 3
	ABL:LOCAL:精愛 = 3
	ABL:LOCAL:レズ = 3
	ABL:LOCAL:肝臓 = 3
	EXP:LOCAL:絶頂経験 = 500
	ABL:LOCAL:武闘 = 40 + RAND(5, 10) + 3 * SP_COUNTRY_RANK:(特殊勢力_サキュバス)
	ABL:LOCAL:知略 = 40 + RAND(5, 10) + 5 * SP_COUNTRY_RANK:(特殊勢力_サキュバス)
	ABL:LOCAL:政治 = 40 + RAND(5, 10) + 5 * SP_COUNTRY_RANK:(特殊勢力_サキュバス)
	ABL:LOCAL:歌唱 = 30
	ABL:LOCAL:料理 = 30
	TALENT:LOCAL:性別 = 1
	TALENT:LOCAL:絶倫 = 1
	TALENT:LOCAL:美脚 = 1
	TALENT:LOCAL:美尻 = 1
	TARGET = LOCAL
	CALL RANDOM_CHARA_BUSTSIZE
	TALENT:LOCAL:特殊勢力素質 = 特殊勢力_サキュバス
	TALENT:LOCAL:陰毛現在値 = 陰毛_標準
	TALENT:LOCAL:陰毛目標値 = 陰毛_標準
	TALENT:LOCAL:Ｖ締まり = GET_DEFAULT_TIGHTNESS("名器")
	TALENT:LOCAL:Ａ締まり = GET_DEFAULT_TIGHTNESS("名器")
	;所属をサキュバスにする
	CFLAG:LOCAL:1 = LOCAL:4
	CALL NEWCHARA_INIT(LOCAL)
NEXT
RETURN LOCAL:4

;-----------------------------
;サキュバスの定期イベント
;-----------------------------
@SUCCUBUS_DRAIN(条件)
#DIM サキュバス
#DIM 条件
#DIM 候補, 1000
#DIM 候補数
#DIM 対象, 2
#DIM メッセージ
#DIM 吸収候補, 5
#DIM 吸収対象
#DIM 吸収値
VARSET 候補, -1
VARSET 候補数

サキュバス = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_サキュバス))

;サキュバス勢力があり、条件を満たしている
SIF サキュバス == -1 || !条件
RETURN -1

FOR LOCAL, 0, CHARANUM
	;サキュバス所属で、捕虜でなく、陥落済み　念のためSPキャラを除外
	IF CFLAG:LOCAL:所属 == サキュバス && !CFLAG:LOCAL:9 && IS_FEMALE(LOCAL) && NO < 2000 && GETBIT(TALENT:LOCAL:特殊勢力陥落系, 特殊勢力_サキュバス)
		候補:候補数 = LOCAL
		候補数 ++
	ENDIF
NEXT

SIF 候補数 < 1
	RETURN -1

対象 = 候補:(RAND:候補数)

SIF 対象 == -1 || CONFIG:200
	RETURN -1

吸収候補:0 = (RAND:(ABL:対象:武闘))
吸収候補:1 = (RAND:(ABL:対象:知略))
吸収候補:2 = (RAND:(ABL:対象:政治))
吸収候補:3 = (RAND:(ABL:対象:歌唱))
吸収候補:4 = (RAND:(ABL:対象:料理))

吸収対象 = FINDELEMENT(吸収候補, MAXARRAY(吸収候補))
SELECTCASE 吸収対象
	CASE 0
		吸収対象 = GETNUM(ABL, "武闘")
	CASE 1
		吸収対象 = GETNUM(ABL, "知略")
	CASE 2
		吸収対象 = GETNUM(ABL, "政治")
	CASE 3
		吸収対象 = GETNUM(ABL, "歌唱")
	CASE 4
		吸収対象 = GETNUM(ABL, "料理")
ENDSELECT

SIF ABL:対象:吸収対象 < 10
	RETURN -1

吸収値 = LIMIT(ABL:対象:吸収対象 / 10, 1, 3)

VARSET 候補, -1
VARSET 候補数, 0

FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:所属 == サキュバス && !CFLAG:LOCAL:9 && TALENT:LOCAL:特殊勢力素質 == 特殊勢力_サキュバス
		候補:候補数 = LOCAL
		候補数 ++
	ENDIF
NEXT

SIF 候補数 < 1
	RETURN -1

対象:1 = 候補:(RAND:候補数)

SIF 対象 == -1
	RETURN -1

DRAWLINE
SETCOLOR COLOR("桃")
PRINTFORMW サキュバスは%ANAME(対象)%から精気を吸収している……
PRINTFORML 
SETCOLOR COLOR("警告")
PRINTFORMW サキュバスは%ANAME(対象)%の能力を奪い取った！
PRINTFORMW %ANAME(対象)%の%ABLNAME:吸収対象%が{吸収値}下がった！
PRINTFORMW %ANAME(対象:1)%の%ABLNAME:吸収対象%が{吸収値}上がった！
PRINTFORML 
ABL:(対象:0):吸収対象 -= 吸収値
ABL:(対象:1):吸収対象 += 吸収値


;-----------------------------
;サキュバスに捕らえられたときのイベント
;-----------------------------
@MASTER_CAPTURED_SUCCUBUS(ARG:0)
PRINTFORMW 捕らえられた%ANAME(MASTER)%はサキュバスの前に引きずり出された…
IF IS_MALE(MASTER)
	PRINTFORMW サキュバスは%ANAME(MASTER)%を見ると、いい男ねと呟く
	PRINTFORMW 私たちの眷属になって、たくさんイイコトをしない？　と誘ってきた
	PRINTFORMW それはつまり夢魔になるということだが、拒否するより待遇はマシになるようだ……
	PRINTFORMW 受け入れますか？
	CALL ASK_YN("受け入れる", "拒否する")
	IF RESULT == 0
		PRINTFORMW 投獄されるよりはマシだろう。%ANAME(MASTER)%が頷くと、サキュバスはにっこりと笑う
		PRINTFORMW そのまま彼女らは%ANAME(MASTER)%を取り囲み、その身体を愛撫し始めた
		PRINTFORMW 気が遠くなるほどの快楽の中で、%ANAME(MASTER)%の意識は夢に蕩けていった……
		SETCOLOR COLOR("注意")
		PRINTFORMW %ANAME(MASTER)%は淫魔になりました
		RESETCOLOR
	ELSE
		PRINTFORMW お前達になど手を貸すか。%ANAME(MASTER)%は毅然として言い放つ	
		PRINTFORMW 彼女らは苦笑しながらも、%ANAME(MASTER)%を投獄した……
	ENDIF
ELSEIF GETBIT(TALENT:MASTER:特殊勢力陥落系, 特殊勢力_サキュバス)
	PRINTFORMW また来てくれたのねと、彼女らはは%ANAME(MASTER)%の身体に手を這わせながらささやく
	PRINTFORMW それだけで%ANAME(MASTER)%の身体は、彼らに刻み込まれた快楽を思い出して濡れてしまう
	PRINTFORMW %ANAME(MASTER)%の声が甘く蕩け始めた頃、また私たちとイイコトをする？　と彼女らは尋ねる
	PRINTFORMW 受け入れますか？
	CALL ASK_YN("受け入れる", "拒否する")
	IF RESULT == 0
		PRINTFORMW 彼らによって育て上げられた女の本性が、%ANAME(MASTER)%を頷かせた
		PRINTFORMW おかえりという言葉を%ANAME(MASTER)%にかけると、サキュバスは%ANAME(MASTER)%を愛撫しはじめた……
		SETCOLOR COLOR("注意")
		PRINTFORMW %ANAME(MASTER)%は再び、夢の世界の住人となった
		RESETCOLOR
	ELSE
		PRINTFORMW 拒否されるということは考えになかったらしく、サキュバス達はしばし意外そうにしていた
		PRINTFORMW それならそれで、夢の世界の素晴らしさを思い出させてあげると、不敵に笑う
		PRINTFORMW サキュバス達は%ANAME(MASTER)%を投獄した……
	ENDIF
ELSE
	PRINTFORMW サキュバスは%ANAME(MASTER)%の体をじっくりと眺める。
	PRINTFORMW 私たちの眷属になって、たくさんイイコトをしない？　と誘ってきた
	PRINTFORMW とんでもない提案だが、拒否しようと、受け入れるように仕向けてくるのだろう
	PRINTFORMW 受け入れますか？
	CALL ASK_YN("受け入れる", "拒否する")
	IF RESULT == 0
		PRINTFORMW どうせされることは変わらないのだ。%ANAME(MASTER)%は力なく頷く。
		PRINTFORMW そうこなくちゃなと、サキュバスは淫蕩な笑みを浮かべた
		PRINTFORMW %ANAME(MASTER)%は、夢魔として生まれ変わっていく……
		SETCOLOR COLOR("注意")
		PRINTFORMW %ANAME(MASTER)%はサキュバスになりました
		RESETCOLOR
	ELSE
		PRINTFORMW そんなことができるか。%ANAME(MASTER)%がきっぱりと拒否すると、サキュバスは驚いた顔をする
		PRINTFORMW どうして？　こんなに気持ちいいのにという彼女らとは、価値観を共有できそうもない
		PRINTFORMW サキュバス達は%ANAME(MASTER)%を投獄した……
	ENDIF
ENDIF
IF RESULT == 0
	CFLAG:MASTER:1 = ARG:0
	SETBIT TALENT:MASTER:特殊勢力陥落系, 特殊勢力_サキュバス
	FOR LOCAL:0, 0, CHARANUM
		IF TALENT:(LOCAL:0):特殊勢力素質 == 特殊勢力_サキュバス
			TALENT:(LOCAL:0):合意 = 1
			CFLAG:(LOCAL:0):2 = MAX(CFLAG:(LOCAL:0):2, 500)
			CFLAG:(LOCAL:0):3 = MAX(CFLAG:(LOCAL:0):3, 500)
		ENDIF
	NEXT
	RETURN 1
ELSE
	CFLAG:MASTER:9 = ARG:0
	RETURN 2
ENDIF
