;-------------------------------------------------
;サキュバスによる調教
;-------------------------------------------------
@TRAIN_BY_SUCCUBUS
#DIM サキュバス
#DIM 対象

サキュバス = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_サキュバス))

SIF サキュバス == -1 || CONFIG:200
	RETURN -1

LOCAL:1 = 0

FOR 対象, 0, CHARANUM
	SIF !IS_TRAIN_BY_SP_COUNTRY(対象, サキュバス)
		CONTINUE
	IF !LOCAL:1
		LOCAL:1 = 1
		DRAWLINE
		PRINTFORMW サキュバスに捕らえられた士官が陵辱されているようです…
		PRINTFORML 
	ENDIF
	
	CALL SUCCUBUSS_TRAIN_MSG(対象)
	CFLAG:対象:調教カウンタ++

	;あまり長く捕らえられていると寝返る
	IF IS_BETRAY_TO_SP_COUNTRY(対象, サキュバス)
		PRINTL
		SETCOLOR COLOR("警告")
		IF !GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_サキュバス)
			PRINTFORMW サキュバスたちに散々に弄ばれ、%ANAME(対象)%の心は夢の世界に蕩けていった
			PRINTFORMW 気づいた頃には、%ANAME(対象)%は一匹の夢魔に生まれ変わっていた……
			IF TALENT:対象:烙印
				PRINTFORML %ANAME(対象)%が%ANAME(MASTER)%専用であることを示す烙印は、
				PRINTFORMW いつの間にか消えていた
				TALENT:対象:烙印 = 0
			ENDIF
			SIF IS_LOVER(対象)
				PRINTFORMW %ANAME(対象)%の頭の中から、かつて愛していた%ANAME(MASTER)%のことが、綺麗さっぱり消え失せた……
		ELSE
			PRINTFORMW サキュバスたちに散々に弄ばれ、%ANAME(対象)%の意識は再び夢の世界へと還っていった
			PRINTFORMW %ANAME(対象)%は再び、夢魔として生まれ変わった……
		ENDIF
		RESETCOLOR
		
		CALL KOJO_SPECIAL_FALLEN_EVENT(対象, SP_COUNTRY_ID:(特殊勢力_サキュバス))

		CFLAG:対象:所属 = サキュバス
		CFLAG:対象:捕虜先 = 0
		SETBIT TALENT:対象:特殊勢力陥落系, 特殊勢力_サキュバス
		SIF SP_TRAIN_IS_DECREASE_CFLAG(対象, サキュバス)
			CALL LOSE_RELATION_TALENT(対象)

		SELECTCASE RAND:10
			CASE IS < 4
				CALL SET_PIERCE_RANDOM(対象, 0)
				SIF RESULT != -1
					CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_PIERCE_NAME(RESULT)%には、淫魔であることを示すピアスが取り付けられていた……", COLOR("桃"), "W")
			CASEELSE
				IF GET_INMOU(対象) != 陰毛_パイパン
					CALL COLORPRINTFORM(@"%ANAME(対象)%の陰毛は、淫らな本性を示すようなハート型に整えられていた……", COLOR("桃"), "W")
					TALENT:対象:陰毛目標値 = 陰毛_ハート型
					TALENT:対象:陰毛現在値 = 陰毛_ハート型
				ENDIF
		ENDSELECT
	ENDIF
	PRINTL
	WAIT
NEXT
