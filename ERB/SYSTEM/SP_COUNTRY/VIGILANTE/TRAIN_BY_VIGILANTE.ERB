;-------------------------------------------------
;自警団による調教（？）
;-------------------------------------------------
@TRAIN_BY_VIGILANTE
#DIM 自警団
#DIM 対象
;IDから自警団勢力の番号を取得
自警団 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_自警団))

SIF 自警団 == -1 || CONFIG:200
	RETURN -1

FOR 対象, 0, CHARANUM
	SIF !IS_TRAIN_BY_SP_COUNTRY(対象, 自警団)
		CONTINUE
	IF LOCAL:1
		LOCAL:1 = 0
		DRAWLINE
		PRINTFORMW 自警団に捕らえられた士官が熱心な説得を受けているようです…
		PRINTFORML 
	ENDIF
	
	PRINTFORML %ANAME(対象)%はアツい説得を受けている……
	CFLAG:対象:調教カウンタ++

	IF INRANGE(CFLAG:対象:調教カウンタ, TRAIN_FALLEN_TERM:2, TRAIN_FALLEN_TERM:3) && !GETBIT(TALENT:対象:特殊勢力陥落系, 特殊勢力_自警団)
		SETCOLOR COLOR("警告")
		PRINTFORML アツい説得で、%ANAME(対象)%は自警団への参加の意欲を固めている……
		PRINTFORML このまま助けがなければ、本当に自警団に参加するだろう
		RESETCOLOR
	ENDIF
	;あまり長く捕らえられていると寝返る
	IF IS_BETRAY_TO_SP_COUNTRY(対象, 自警団)
		SETCOLOR COLOR("警告")
		PRINTFORMW 自警団員らの熱心で誠実な説得で、%ANAME(対象)%は自警団への参加の意思を固めた
		PRINTFORMW 支給された制服に、%ANAME(対象)%は袖を通した……
		RESETCOLOR
		CALL KOJO_SPECIAL_FALLEN_EVENT(対象, SP_COUNTRY_ID:(特殊勢力_自警団))
		CFLAG:対象:所属 = 自警団
		CFLAG:対象:捕虜先 = 0
	ENDIF
	PRINTL
	WAIT
NEXT