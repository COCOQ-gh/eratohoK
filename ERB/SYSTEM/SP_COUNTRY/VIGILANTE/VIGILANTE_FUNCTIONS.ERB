;--------------------------------------
;ターンエンド時の自警団の処理
;--------------------------------------
@TURNEND_VIGILANTE
#DIM 自警団
自警団 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_自警団)

;兵力の増える処理
IF DAY % 3 == 0
	CALL SINGLE_DRAWLINE
	LOCAL:0 = MAX(RAND:4000, 1000)
	LOCAL:0 += RAND:500 * SP_COUNTRY_RANK:(特殊勢力_自警団)
	PRINTFORML 自警団が徴兵を行っているようです……
	PRINTFORML 自警団の兵力が{LOCAL:0}増加した
	COUNTRY_SOLDIER:(自警団) += LOCAL:0
ENDIF
;4ターンに1回、特別な徴兵
CALL VIGILANTE_SPECIAL_REQRUITMENT(DAY % 4 == 0)

;--------------------------------------
;滅亡時の処理
;--------------------------------------
@VIGILANTE_DESTROY_MSG
CALL SINGLE_DRAWLINE
SETCOLOR カラー_注意
PRINTFORML
PRINTFORML
PRINTFORML
PRINTFORMW 壊滅的な被害を受けた自警団は、活動を継続することができなくなったようです……
PRINTFORMW 彼らは解散してしまいました
PRINTFORM
PRINTFORML
PRINTFORML
RESETCOLOR

;--------------------------------------
;自警団の初期化処理
;--------------------------------------
@VIGILANTE_INIT(ARG:0)
#DIM LCOUNT
VARSET LOCAL

SIF GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_自警団)) != -1
	RETURN 0

SIF SP_COUNTRY_RANK:特殊勢力_自警団 == 0
	RETURN 0

LOCAL:4 = GET_NEW_COUNTRY()

;作れなかったらなしで
IF LOCAL:4 == -1
	RETURN 0
ENDIF

COUNTRY_COLOR:(LOCAL:4) = 0x33CC66
COUNTRY_SOLDIER:(LOCAL:4) = 20000 + SP_COUNTRY_RANK:(特殊勢力_自警団) * 2000
COUNTRY_AI_TYPE:(LOCAL:4) = AI_防衛
COUNTRY_EVENT_ID:(LOCAL:4) = SP_COUNTRY_ID:(特殊勢力_自警団)

CALL CREATE_RANDOM_CHARA
FLAG:汎用武将カウント --
LOCAL = RESULT
IF LOCAL == -1
	COUNTRY_COLOR:(LOCAL:4) = 0
	COUNTRY_SOLDIER:(LOCAL:4) = 0
	COUNTRY_AI_TYPE:(LOCAL:4) = 0
	COUNTRY_EVENT_ID:(LOCAL:4) = 0
	RETURN -1
ENDIF

NO:LOCAL = GET_EMPTY_NO()
SELECTCASE SP_COUNTRY_BOSSNAME:(特殊勢力_自警団)
	CASE 0
		NAME:LOCAL = 自警団員
		CALLNAME:LOCAL = 自警団員
		CSTR:LOCAL:1 = 自警団員
		CSTR:LOCAL:3 = ジケイダンイン
		CSTR:LOCAL:4 = ジケイダンイン
		CSTR:LOCAL:6 = ジケイダンイン
	CASE 1
		NAME:LOCAL = 自警団長
		CALLNAME:LOCAL = 自警団長
		CSTR:LOCAL:1 = 自警団長
		CSTR:LOCAL:3 = ジケイダンチョウ
		CSTR:LOCAL:4 = ジケイダンチョウ
		CSTR:LOCAL:6 = ジケイダンチョウ
	CASE 2
		TARGET = LOCAL
		CALL SET_RANDOM_NAME_K(LOCAL)
		NAME:LOCAL = %CSTR:LOCAL:1%
		CALLNAME:LOCAL = %CSTR:LOCAL:1%
	CASE 3
		NAME:LOCAL = %SP_COUNTRY_BOSSNAME_FIRST_NAME:特殊勢力_自警団%
		CALLNAME:LOCAL = %SP_COUNTRY_BOSSNAME_FIRST_NAME:特殊勢力_自警団%
		CSTR:LOCAL:0 = %SP_COUNTRY_BOSSNAME_LAST_NAME:特殊勢力_自警団%
		CSTR:LOCAL:1 = %SP_COUNTRY_BOSSNAME_FIRST_NAME:特殊勢力_自警団%
ENDSELECT
ABL:LOCAL:武闘 = 40 + 5 * SP_COUNTRY_ABL_MODIFIER(特殊勢力_自警団) + RAND:10
ABL:LOCAL:防衛 = 50 + 5 * SP_COUNTRY_ABL_MODIFIER(特殊勢力_自警団) + RAND:10
ABL:LOCAL:知略 = 40 + 5 * SP_COUNTRY_ABL_MODIFIER(特殊勢力_自警団) + RAND:10
ABL:LOCAL:政治 = 40 + 5 * SP_COUNTRY_ABL_MODIFIER(特殊勢力_自警団) + RAND:10
ABL:LOCAL:歌唱 = 20 + 3 * SP_COUNTRY_ABL_MODIFIER(特殊勢力_自警団)
ABL:LOCAL:料理 = 20 + 3 * SP_COUNTRY_ABL_MODIFIER(特殊勢力_自警団)
TALENT:LOCAL:特殊勢力素質 = 特殊勢力_自警団
TALENT:LOCAL:陰毛現在値 = 陰毛_標準
TALENT:LOCAL:陰毛目標値 = 陰毛_標準
TALENT:LOCAL:Ｖ締まり = GET_DEFAULT_TIGHTNESS("普通")
TALENT:LOCAL:Ａ締まり = GET_DEFAULT_TIGHTNESS("普通")
;所属を自警団にする
CFLAG:LOCAL:1 = LOCAL:4
CALL INIT_NEWCHARA(LOCAL)
COUNTRY_BOSS:(LOCAL:4) = GET_ID(LOCAL)


FOR LCOUNT, 0, SP_COUNTRY_RANK:特殊勢力_自警団 * 2
	CALL CREATE_RANDOM_CHARA
	FLAG:汎用武将カウント --
	LOCAL = RESULT
	NAME:LOCAL = 自警団員%UNICODE(0xFF21 + LCOUNT)%
	CALLNAME:LOCAL = 自警団員%UNICODE(0xFF21 + LCOUNT)%
	NO:LOCAL = GET_EMPTY_NO()
	TALENT:LOCAL:特殊勢力素質 = 特殊勢力_自警団
	;所属を自警団にする
	CFLAG:LOCAL:1 = LOCAL:4
	CALL INIT_NEWCHARA(LOCAL)
NEXT
SP_COUNTRY_APPEARED:特殊勢力_自警団 = 1

RETURN LOCAL:4

;--------------------------------------
;自警団の定期イベント。
;陥落済みキャラを使って兵数を増やす
;--------------------------------------
@VIGILANTE_SPECIAL_REQRUITMENT(条件 = 1)
#DIM 自警団
#DIM 条件
#DIM 候補, 3000
#DIM 候補数
#DIM 対象
#DIM メッセージ
VARSET 候補, -1
VARSET 候補数

自警団 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_自警団))

;自警団勢力があり、条件を満たしている
SIF 自警団 == -1 || !条件
RETURN -1

LOCAL = MAX(RAND:3000, 1000)
CALL SINGLE_DRAWLINE
SETCOLOR カラー_注意
PRINTFORMW 自警団は演説を行っている……
PRINTFORMW その内容に賛同した人妖{LOCAL}人が自警団に参加しました
RESETCOLOR

COUNTRY_SOLDIER:自警団 += LOCAL

RETURN 1

;-----------------------------
;自警団に捕らえられたときのイベント
;-----------------------------
@MASTER_CAPTURED_VIGILANTE(ARG:0)
PRINTFORMW 捕らえられた%ANAME(MASTER)%は自警団の前に引きずり出された…
PRINTFORMW 自警団は%ANAME(MASTER)%に熱心な説得をする
PRINTFORMW 自分たちは幻想郷の未来のために戦っている、君も一緒にどうだ……
PRINTFORMW 理想が先行しがちだが、参加すれば投獄されずに済むようだ
PRINTFORMW 受け入れますか？
CALL ASK_YN("受け入れる", "拒否する")
IF RESULT == 0
	PRINTFORMW 彼らの理想は道理が通っている。協力するのにやぶさかではない
	PRINTFORMW そう伝えると団長は笑顔を浮かべ、そうこなくちゃなといった
	PRINTFORMW 渡された制服に、%ANAME(MASTER)%は袖を通した……
	CALL COLORPRINT(@"%ANAME(MASTER)%は自警団の一員となりました", カラー_注意, "W")
ELSE
	PRINTFORMW 協力はできない。%ANAME(MASTER)%がきっぱりと拒否すると、団長は残念そうな顔を浮かべる
	PRINTFORMW 自警団員たちは%ANAME(MASTER)%を牢屋に牢獄した……
ENDIF
IF RESULT == 0
	CALL CHANGE_COUNTRY(MASTER, ARG:0, 1)
	FOR LOCAL:0, 0, CHARANUM
		IF TALENT:(LOCAL:0):特殊勢力素質 == 特殊勢力_自警団
			CFLAG:(LOCAL:0):2 = MAX(CFLAG:(LOCAL:0):2, 500)
			CFLAG:(LOCAL:0):3 = MAX(CFLAG:(LOCAL:0):3, 500)
		ENDIF
	NEXT
	RETURN 0
ELSE
	CALL CAPTURE(MASTER, ARG:0)
	RETURN 2
ENDIF
