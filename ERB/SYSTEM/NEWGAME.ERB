;ゲーム開始時やロード時の処理

;-------------------------
;タイトル画面
;-------------------------
@SYSTEM_TITLE
RESETDATA
DRAWLINE
ALIGNMENT CENTER
PRINTFORML 　　,,,,,,,,　 ,,, ,,,,,　 　,,,,,,,,,　,,,lll,,,,,, 　　,,,,,,,, 　　llll ,,,,,,, 　　　,,,,,,,,　　 
PRINTFORML 　 l|'　 'lll,　lllll''''' l''  ''lllll　　lll'　　 　lll''''''''lll, lllll'' ''llll　lll''''''''lll, 
PRINTFORML 　lll'''''''''　llll 　　　ll''''''llll　　lll 　　　llll　　　　llll llll　　　llll llll　　　　llll 
PRINTFORML 　 l|,,,,,lll'　lll　　　 |ll,,,,,,llll　　lll,,,,,,, lll,,,,,,,,lll' llll　　　llll　lll,,,,,,,,lll' 
PRINTFORML 　　 '''''''　　''''　　 　'''''''' '''' 　''''''''' 　　''''''''　　 ''''　　　'''' 　　''''''''　　 
PRINTFORML
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　 ,ll'''''''ll　　,.|''''''''ll　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　,ll'　　　lll,l'''　　　,,,l''　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　 　,ll'　　　ll''　　　,,,ll''　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　,ll'　　　　　　,,,ll''　　　　　　　　 　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　ll　　　　　 ,,ll'''　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　 　l'　　　　　　ll,　　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　l'　　　, 　　　ll　　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　 ll 　　　lll,　　　ll　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　ll 　　　ll' l, 　 　'll　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　 ll　　　 ,l'　　ll　　 　ll　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　 ,ll　　 　,l' 　　'll　　　　ll　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　 '''''''''　　　　　　'''''''''''　　　　　　　　　　　　　　　　　 
PRINTFORML 
PRINTFORML %GAMEBASE_AUTHOR%
PRINTFORML Ver{GAMEBASE_VERSION / 1000}.{GAMEBASE_VERSION % 1000}
PRINTFORML %GAMEBASE_INFO%
DRAWLINE
PRINTBUTTON " 0[最初から]", 0
PRINTL 
PRINTBUTTON " 1[続きから]", 1
PRINTL 
PRINTBUTTON " 2[カスタムキャラ]", 2
PRINTL
[IF_DEBUG]
PRINTBUTTON "99[   DEBUG]", 99
PRINTL 
[ENDIF]
ALIGNMENT LEFT
$INPUT_LOOP
INPUT

IF RESULT == 0
	BEGIN FIRST
ELSEIF RESULT == 1
	LOADGAME
	RESTART
ELSEIF RESULT == 2
	CALL CUSTOM_CHARA_MENU
	RESTART
ELSEIF RESULT == 99
	[IF_DEBUG]
	CALL DEBUGMENU()
	RESTART
	[ENDIF]
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ニューゲーム
;-------------------------------------------------
@EVENTFIRST
;初期化処理
CALL INITIALIZE

CALL NEWLOOP

SIF RESULT
	BEGIN SHOP
BEGIN TITLE

;-------------------------------------------------
;ロード時の処理(※複数定義可能)
;-------------------------------------------------
@EVENTLOAD
;セーブデータのアップデート
CALL UPDATE

;マップを準備
CALL MAPSETUP

;ロード時の初期化処理
CALL INITIALIZE_COMMON

;-------------------------------------------------
;ロード時とニューゲーム時の共通初期化処理
;-------------------------------------------------
@INITIALIZE_COMMON
;グローバル変数の読み込み
LOADGLOBAL

;乱数初期化
INITRAND

;ターゲットの初期化
CALL CLEAR_MTAR

;PALAMLVの設定
PALAMLV:0 = 0
PALAMLV:1 = 100
PALAMLV:2 = 300
PALAMLV:3 = 500
PALAMLV:4 = 1500
PALAMLV:5 = 3000
PALAMLV:6 = 6000
PALAMLV:7 = 10000
PALAMLV:8 = 30000
PALAMLV:9 = 50000
PALAMLV:10 = 100000
PALAMLV:11 = 250000
PALAMLV:12 = 500000
PALAMLV:13 = 1000000
PALAMLV:14 = 2500000
PALAMLV:15 = 5000000
PALAMLV:16 = 10000000
PALAMLV:17 = 25000000
PALAMLV:18 = 50000000
PALAMLV:19 = 100000000
PALAMLV:20 = 250000000
PALAMLV:21 = 500000000
PALAMLV:22 = 1000000000
PALAMLV:23 = 2500000000
PALAMLV:24 = 5000000000
PALAMLV:25 = 9999999999

;COSの値を事前に計算してリストに記録する
FOR LOCAL:0, 0, 360
	COS_LIST:(LOCAL:0) = CALC_COS(LOCAL:0)
NEXT

;アイテムの開発期間を設定
CALL SET_ITEMTERM

;都市の文字色リセット
CALL RESET_CITY_COLOR

;ランダムキャラの名前候補リストを作成
CALL CREATE_NAME_LIST

;タトゥーのランダム入力内容リストを作成
CALL CREATE_TATOO_LIST

;精液マップを作成
CALL CREATE_SPERM_MAP

;デイリーマップを作製
CALL CREATE_DAILY_MAP

;ランク閾値の生成
CALL INITIALIZE_RANK

;能力値の最小・最大を設定
FOR LOCAL:0, 0, 50
	ABL_MIN:(LOCAL:0) = 0
	ABL_MAX:(LOCAL:0) = 9999
NEXT
FOR LOCAL:0, 50, 70
	ABL_MIN:(LOCAL:0) = 0
	ABL_MAX:(LOCAL:0) = 300
NEXT
ABL_MAX:14 = 5
ABL_MIN:60 = -99
ABL_MIN:61 = -99
ABL_MIN:90 = -1000
ABL_MAX:90 = 1000
ABL_MIN:91 = -1000
ABL_MAX:91 = 1000
ABL_MIN:92 = 0
ABL_MAX:92 = 1000

;AI用の各種変数を設定
CALL AI_BASE_SETTING

CALL TMP_CREATE_UNIT_MAP

;-------------------------------------------------
;ランク閾値の初期化
;-------------------------------------------------
@INITIALIZE_RANK
ランク閾値:ランク_ＳＬＧ:ランク_S = 100
ランク閾値:ランク_ＳＬＧ:ランク_A = 80
ランク閾値:ランク_ＳＬＧ:ランク_B = 70
ランク閾値:ランク_ＳＬＧ:ランク_C = 60
ランク閾値:ランク_ＳＬＧ:ランク_D = 50
ランク閾値:ランク_ＳＬＧ:ランク_E = 40
ランク閾値:ランク_ＳＬＧ:ランク_F = 30
ランク閾値:ランク_ＳＬＧ:ランク_G = 0
ランク閾値:ランク_ＳＬＧ:ランク_無 = 0

ランク閾値:ランク_その他:ランク_S = 10
ランク閾値:ランク_その他:ランク_A = 8
ランク閾値:ランク_その他:ランク_B = 6
ランク閾値:ランク_その他:ランク_C = 4
ランク閾値:ランク_その他:ランク_D = 3
ランク閾値:ランク_その他:ランク_E = 2
ランク閾値:ランク_その他:ランク_F = 1
ランク閾値:ランク_その他:ランク_G = 0
ランク閾値:ランク_その他:ランク_無 = 0

ランク閾値:ランク_部隊:ランク_S = 150000
ランク閾値:ランク_部隊:ランク_A = 80000
ランク閾値:ランク_部隊:ランク_B = 60000
ランク閾値:ランク_部隊:ランク_C = 50000
ランク閾値:ランク_部隊:ランク_D = 40000
ランク閾値:ランク_部隊:ランク_E = 20000
ランク閾値:ランク_部隊:ランク_F = 10000
ランク閾値:ランク_部隊:ランク_G = 0
ランク閾値:ランク_部隊:ランク_無 = 0


;-------------------------------------------------
;ニューゲーム時の初期化処理
;-------------------------------------------------
@INITIALIZE
;念のため全データ初期化
RESETDATA
;乱数ダンプ
DUMPRAND
;コンフィグの初期設定
CALL INIT_CONFIG

;全ての固定キャラと「あなた」を作成する
FOR LOCAL:0, 0, MAX_FIXED_CHARA
	;CSVファイルの存在判定
	SIF EXISTCSV(LOCAL:0)
		CALL ADD_CHARA_FROM_CSV(LOCAL:0)
NEXT

;CSVでは設定できないキャラデータの初期設定
CALL ADDITIONAL_CHARA_SETTING

;ロード時とニューゲーム時の共通初期化処理
CALL INITIALIZE_COMMON

;0～199番のアイテムを売りに出す
VARSET ITEMSALES, 1, 0, 200

;周回を0に設定
FLAG:周回数 = 0

;-------------------------------------------------
;CSVでは設定できないキャラデータの初期設定
;-------------------------------------------------
@ADDITIONAL_CHARA_SETTING
CFLAG:(NAME_TO_CHARA("アリス")):母親 = GET_ID(NAME_TO_CHARA("神綺"))


;-------------------------------------------------
;ARG:0 = 1 で周回モード
;-------------------------------------------------
@NEWLOOP(ARG:0=0)

CALL NEWLOOP_RESET_VARS

;周回数が1増える
FLAG:周回数++

;シナリオ側で特殊キャラを生成しており、かつ一週目、「戻る」したときのため、特殊キャラを全て削除する
LOCAL:2 = CHARANUM
FOR LOCAL:0, 0, LOCAL:2
	LOCAL:1 = LOCAL:2 - LOCAL:0 - 1
	IF CFLAG:(LOCAL:1):所属 == ARG:0 && IS_SP_CHARA(LOCAL:1)
		CALL DELETE_CHARA(LOCAL:1)
	ENDIF
NEXT


;シナリオ選択
CALL SELECT_SCENARIO
IF RESULT == -1
	RETURN 0
ENDIF

TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS '= RESULTS
PRINTFORML シナリオ 「%LOCALS%」 で開始します

;シナリオマップ選択処理
TRYCALLFORM SCENARIO_MAPSELECT_%SCENARIOID%

;マップ初期化
CALL MAPSETUP(1)
CALL MAP_INIT

;シナリオイントロ
TRYCALLFORM SCENARIO_INTRO_%SCENARIOID%

PRINTFORML このシナリオで遊びますか？
CALL ASK_YN
IF RESULT == 1
	FLAG:周回数 --
	RESTART
ENDIF

;シナリオ初期勢力配置
TRYCCALLFORM SCENARIO_PLACEMENT_%SCENARIOID%
CATCH
	CALL SCENARIO_PLACEMENT_DEFAULT
ENDCATCH

;勢力選択
CALL SELECT_COUNTRY

;戻った場合
IF RESULT == -1
	FLAG:周回数 = 0
	RESTART
ENDIF


;特殊勢力君主スタートならなにもしない
IF MATCH(SP_COUNTRY_START, 特殊勢力スタート_君主)
;観戦モードの場合、前の週でキャラを変更したとき用に、「あなた」と主人公を入れ替えて戻す処理だけは必要
ELSEIF FLAG:観戦モード
	SWAPCHARA NAME_TO_CHARA("あなた"), MASTER
;どちらでもなければ、設定する
ELSEIF !FLAG:観戦モード
	CALL SELECT_MASTER
ENDIF

;旗揚げスタートなら、勢力を設定
IF FLAG:OP選択勢力 == -4
	CALL START_WITH_NEW_COUNTRY
ENDIF

;カスタムキャラの扱いの設定
CALL CUSTOM_SETTING

;登録キャラの扱いを設定
;FOR LOCAL:0, 0, MAX_MASTER
;	;体力が正なら登録キャラが存在すると判定（主人公は除外）
;	IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
;		LOCAL:1 ++
;	ENDIF
;NEXT

;いたら扱いを選択
;SIF LOCAL:1 > 0
;	CALL SET_登録キャラ

;LOCAL:1 = 0

;ランダムキャラを使用するかどうか
CALL SELECT_RCHARA

;特殊勢力の設定
CALL SP_COUNTRY_SETTING

CALL SP_COUNTRY_START_INITIALIZE

TRYCALLFORM SCENARIO_%SCENARIOID%_AFTERSELECT

;引き継ぎキャラの所属を主人公勢力にする
FOR LOCAL:0, 0, CHARANUM
	IF IS_CHILD(LOCAL) || IS_RANDOM_CHARA(LOCAL)
		CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属, 1)
	ENDIF
NEXT

;開始時の国庫を設定
;シナリオに応じて
FOR LOCAL:0, 1, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	MONEY:(LOCAL:0) += GET_SUM_ECONOMY(LOCAL:0) * 100 / (100 * 100)
NEXT


;初期ボーナス
IF !FLAG:観戦モード && FLAG:あなた新規作成 && MASTER == NAME_TO_CHARA("あなた")
	CALL NEWLOOP_ANATA_BONUS
	FLAG:あなた新規作成 = 0
ENDIF

RETURN 1



;-------------------------------------
;新しく周回する時用、変数のリセット処理
;-------------------------------------
@NEWLOOP_RESET_VARS
;日付を1日目に戻す
DAY = 1

;触手部屋の管理者を初期化
FLAG:触手部屋管理者 = 0

;フェイズを拠点フェイズにする
TIME = 0

;シナリオ専用変数をリセット
VARSET SCVAR, 0

;デイリーイベント変数をリセット
VARSET DVAR, 0
VARSET DSTR, 
VARSET SDVAR, 0
VARSET SDSTR, 
;口上用デイリーイベント変数をリセット
FOR LOCAL, 0, VARSIZE("KDVAR")
	CVARSET KDVAR, LOCAL, 0
	CVARSET KDSTR, LOCAL, 
NEXT

;特殊勢力関係の変数をリセット
VARSET SP_COUNTRY_START
VARSET SP_COUNTRY_RANK
VARSET SP_COUNTRY_APPEARED
VARSET SP_COUNTRY_BOSSNAME
VARSET SP_COUNTRY_BOSSNAME_LAST_NAME
VARSET SP_COUNTRY_BOSSNAME_FIRST_NAME
VARSET SP_COUNTRY_INITIAL_RISE
VARSET BANDIT_HAS_INITIAL_CITIES

FOR LOCAL, 0, VARSIZE("SP_TRAIN_COUNT")
	CVARSET SP_TRAIN_COUNT, LOCAL, 0
NEXT





;捕虜について削除
VARSET PRISONER_TARGET, -1

;スカウト回数をリセット
VARSET SCOUT_COUNT, 0

;特別訓練回数をリセット
VARSET SPECIAL_TRAIN_COUNT, 0

;変数の初期化
TARGET = -1
ASSI = -1

;クリアフラグを解除
FLAG:クリアフラグ = 0

SHOP_TIME = 0

;料理長をリセット（料理長がランダムキャラや出産キャラで、かつそのキャラを引き継がなかったときのため）
CALL SET_COOK(-1)

;観戦モードフラグを0
FLAG:観戦モード = 0

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0


;SLG関連の変数をクリア
STATS_TIME = 0
VARSET STATS_BOSS_ID_LIST, 0
VARSET STATS_GDP, 0
VARSET STATS_ARMY, 0
VARSET CITY_ROOT, 0
VARSET CITY_TYPE, 0
VARSET CITY_ECONOMY, 0
VARSET CITY_SOLDIER, 0
VARSET CITY_GUARD, 0
VARSET CITY_OWNER, 0
VARSET CITY_COMMANDER, 0
VARSET COUNTRY_BOSS, 0
VARSET COUNTRY_COLOR, 0
VARSET COUNTRY_SOLDIER, 0
VARSET COUNTRY_AI_TYPE, 0
VARSET COUNTRY_NOTARGET_TERM, 0
VARSET COUNTRY_IS_CLOSED, 0
VARSET COUNTRY_EVENT_ID, 0
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, 10
		UNIT_SOLDIER:(LOCAL:0):(LOCAL:1) = 0
		UNIT_POSITION:(LOCAL:0):(LOCAL:1) = 0
		UNIT_TARGET:(LOCAL:0):(LOCAL:1) = 0
		UNIT_COMMANDER:(LOCAL:0):(LOCAL:1) = 0
		UNIT_BREAK_TERM:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
VARSET TREATY_A_TERM, 0
VARSET TREATY_U_TERM, 0
VARSET TREATY_U_TARGET, 0
VARSET TREATY_C_TERM, 0
VARSET DIPLOMACY_HATE, 0
VARSET COUNTRY_NOTDIPLOMACY_TERM, 0
FOR LOCAL:0, 0, MAX_TREATY_A
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
FOR LOCAL:0, 0, MAX_TREATY_U
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_U_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
FOR LOCAL:0, 0, MAX_TREATY_C
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT

;マップIDをリセット
MAPID '= "DEFAULT"

;各勢力の国庫を空に（プレイヤーの懐はそのまま）
VARSET MONEY, 0, 1

;-------------------------------------------------
;シナリオ選択
;-------------------------------------------------
@SELECT_SCENARIO
#DIM IN
CALL CREATE_SCENARIO_INDEX
PRINTL ★★シナリオを選択して下さい★★
DRAWLINE
FOR LOCAL:0, 0, SCENARIO_NUMBER
	SIF SCENARIO_SELECT:(LOCAL:0) == -1
		BREAK
		
		LOCALS:0  '= ""
		LOCALS:0  += "[●"
		RESULTS:0 '= ""
		TRYCALLFORM SCENARIO_NAME_%SCENARIO_ID:(LOCAL:0)%
		LOCALS:0 = %LOCALS:0%%RESULTS:0, 26, LEFT%
		LOCALS:0 += "]"
		LOCALS:0 = {SCENARIO_SELECT:(LOCAL:0),3 , RIGHT}%LOCALS:0%
		
		PRINTBUTTON LOCALS:0, SCENARIO_SELECT:(LOCAL:0)
		PRINTL
NEXT

IF FLAG:周回数 == 1
	DRAWLINE
	PRINTBUTTON "99[タイトルに戻る]", 99
ENDIF

$INPUT_LOOP
INPUT
IN = RESULT
FOR LOCAL:0 , 0, SCENARIO_NUMBER
	IF IN == 99 && FLAG:周回数 == 1
		RETURN -1
	ENDIF
	IF IN == SCENARIO_SELECT:(LOCAL:0)
		SCENARIOID '= SCENARIO_ID:LOCAL
		RETURN 1
	ENDIF
NEXT
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;勢力選択
;-------------------------------------------------
@SELECT_COUNTRY
#DIM FIRST_LINE
#DIM 選択勢力
#DIM ボス
#DIM 選択不可

CALL TMP_PREPARE_COUNTRY_STARS

選択勢力 = 0
FIRST_LINE = LINECOUNT
$SHOW_LOOP

DRAWLINE
PRINT ★★所属勢力選択★★
PRINTPLAIN   
PRINTBUTTON "97[オプション]", 97
PRINTPLAIN   
PRINTBUTTON "98[観戦]", 98
PRINTPLAIN   
PRINTBUTTON "99[放浪]", 99
IF GET_NEW_COUNTRY() != -1
	PRINTPLAIN   
	PRINTBUTTON "100[特殊]", 100
	PRINTPLAIN   
	PRINTBUTTON "101[旗揚げ]", 101
ENDIF
IF FLAG:周回数 == 1
	PRINTPLAIN   
	PRINTBUTTON "999[戻る]", 999
ENDIF
PRINTL 
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

ボス = ID_TO_CHARA(COUNTRY_BOSS:選択勢力)

IF ボス < 0
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL 
ELSE
	PRINT この都市は 
	CALL COLORPRINT(@"%NAME_FORMAL(ボス)%", COUNTRY_COLOR:選択勢力)
	PRINTL  の勢力下にあります
	;星表示
	DRAWLINE
	PRINTFORM 難易度:
	CALL TMP_PRINT_COUNTRY_DIFFICULTY(選択勢力)
	PRINT    あまりあてにならないので注意！
	PRINTL
	;所属キャラ一覧表示
	DRAWLINE
	PRINTFORM 都市数: {GET_OWN_CITY(選択勢力), 3, LEFT}
	PRINTFORM 経済力: {GET_SUM_ECONOMY(選択勢力) / 100, 7, LEFT}
	PRINTFORM 防衛力: {GET_SUM_GUARD(選択勢力), 7, LEFT}
	PRINTL
	PRINTFORM 所属キャラ:

	;ボスだけ特殊表示
	CALL COLORPRINT(@"%ANAME(ボス)%   ", COUNTRY_COLOR:選択勢力)
	LOCAL:1 = STRLENS(LOCALS:0)

	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == ボス || CFLAG:LOCAL:所属 != 選択勢力
			CONTINUE
		PRINTFORM %ANAME(LOCAL)%   
	NEXT
	PRINTL

	;その勢力がプレイ可能か調べる。未定義なら全勢力プレイ可能とする
	TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
		IF !RESULT
			CALL COLORPRINT(@"このシナリオでは、その勢力でプレイすることはできません", COLOR("注意"))
			選択不可 = 1
		ELSE
			PRINTBUTTON " 0[この勢力で開始]", 0
			PRINTL
		ENDIF
	CATCH
		PRINTBUTTON " 0[この勢力で開始]", 0
		PRINTL
	ENDCATCH
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	IF ボス >= 0
		TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
			SIF RESULT
				GOTO SELECTABLE
			PRINTFORMW このシナリオでは、その勢力でプレイすることはできません
		CATCH
			$SELECTABLE
			FLAG:OP選択勢力 = 選択勢力
			PRINTFORMW %NAME:(ボス)%勢力で開始します
			RETURN
		ENDCATCH
	ENDIF
ELSEIF RESULT == 97
	;オプション設定
	CALL NEWGAME_SETTING
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 98
	FLAG:OP選択勢力 = -1
	FLAG:観戦モード = 1
	TIME = 1
	PRINTFORMW 観戦モードで開始します
	RETURN
ELSEIF RESULT == 99
	FLAG:OP選択勢力 = 0
	TIME = 1
	PRINTFORMW 放浪状態から開始します
	RETURN
ELSEIF RESULT == 100 && GET_NEW_COUNTRY() != -1
	FLAG:OP選択勢力 = -3
	CALL SP_COUNTRY_START_SELECT()
	RETURN
ELSEIF RESULT == 101 && GET_NEW_COUNTRY() != -1
	FLAG:OP選択勢力 = -4
	PRINTFORMW 旗揚げして開始します
	RETURN
ELSEIF RESULT == 999 && FLAG:周回数 == 1
	FLAG:周回数 --
	CVARSET CFLAG, 1, 0
	CVARSET CFLAG, 9, 0
	CVARSET CFLAG, 12, 0
	FOR LOCAL, 0, CHARANUM
		FOR LOCAL, 1, CHARANUM
			REL_LIKE:(LOCAL:0):(LOCAL:1) = 0
			REL_HATE:(LOCAL:0):(LOCAL:1) = 0
		NEXT
	NEXT
	RETURN -1
;都市をクリック
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		選択勢力 = LOCAL:4
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		選択勢力 = 0
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;特殊勢力の選択
;-------------------------------------------------
@SP_COUNTRY_START_SELECT()
#DIM FIRST_LINE
PRINTFORMW 特殊な勢力で開始します
PRINTFORMW どれで開始しますか？

FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF SP_COUNTRY_NAME:LOCAL != ""
		PRINTFORML [{LOCAL}] %SP_COUNTRY_NAME:LOCAL%
NEXT

$INPUT_LOOP2
INPUT

LOCAL = RESULT

SIF SP_COUNTRY_NAME:LOCAL == ""
	GOTO INPUT_LOOP2

PRINTFORML どういう境遇でスタートしますか？
CALL ASK_MULTI("士官(調教を受けない)", "奴隷(最初から合意もち)", "捕虜(無所属・捕虜からスタート)", "君主")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 士官としてスタートします
	CASE 1
		PRINTFORMW 奴隷としてスタートします
	CASE 2
		PRINTFORMW 捕虜としてスタートします
	CASE 3
		PRINTFORMW 君主としてスタートします
ENDSELECT

SP_COUNTRY_START:LOCAL = RESULT + 1

;この時点で設定していない（シナリオの初期設定時点で設定していない）なら決めておく
IF SP_COUNTRY_RANK:LOCAL == 0
	FIRST_LINE = LINECOUNT
	$SHOW_LOOP
	CALL SP_COUNTRY_START_SETTING(LOCAL)
ENDIF

LOCAL:1 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:LOCAL)

;君主スタート時の設定
IF SP_COUNTRY_START:LOCAL == 特殊勢力スタート_君主
	LOCAL:2 = NAME_TO_CHARA("あなた")
	LOCAL:3 = GET_COUNTRY_BOSS(LOCAL:1)
	;前の週の主人公と「あなた」を入れ替え、「あなた」と君主を入れ替え、君主のNOとIDを「あなた」に設定、「あなた」のNOをINT_MAXにして削除
	SWAPCHARA MASTER, LOCAL:2
	SWAPCHARA MASTER, LOCAL:3
	CFLAG:MASTER:キャラＩＤ = CFLAG:(LOCAL:3):キャラＩＤ
	COUNTRY_BOSS:(LOCAL:1) = GET_ID(MASTER)
	TALENT:MASTER:特殊勢力素質 = 0
	NO:MASTER = NO:(LOCAL:3)
	CSTR:MASTER:99 = あなた
	NO:(LOCAL:3) = __INT_MAX__
	CALL DELETE_CHARA(LOCAL:3)
ENDIF


	



;-------------------------------------------------
;主人公選択
;-------------------------------------------------
@SELECT_MASTER
#DIM 旧MASTERID
#DIM 選択MASTERID
#DIM 引継ぎ使用可
#DIM FIRSTLINE
VARSET LOCAL
LOADGLOBAL

;星評価を用意
CALL TMP_PREPARE_CHARA_STARS()

VARSET 旧MASTERID
VARSET 選択MASTERID

IF FLAG:周回数 > 1
	旧MASTERID = GET_ID(MASTER)
	引継ぎ使用可 = CFLAG:MASTER:所属 == FLAG:OP選択勢力 || GET_COUNTRY_BOSS(CFLAG:MASTER:所属) != MASTER
ELSE
	旧MASTERID = 0
	引継ぎ使用可 = 1
ENDIF

FIRSTLINE = LINECOUNT
$SHOW_LOOP

DRAWLINE
PRINTL ★★主人公を選択して下さい★★

SIF FLAG:OP選択勢力 <= 0
	PRINTL ※特殊なスタートを選択しているので、既存キャラ一覧に君主以外の全キャラが表示されます
;二週目以降で選択勢力と異なる勢力の君主の場合、前週と同じ選択をさせるとまずいので選択不可とする
SIF 旧MASTERID > 0 && !引継ぎ使用可
	CALL COLORPRINT("※前周の主人公が選択した勢力以外の勢力の君主です。「前週と同じ」は利用できません", COLOR("注意"), "L")
DRAWLINE

;ここから登録キャラの表示
PRINTBUTTON "[あなたを新規作成]", 0
PRINTFORM ※ 現在の「あなた」のデータは削除されます
;二週目以降で引継ぎが使用できるなら
IF 旧MASTERID > 0 && !引継ぎ使用可
	PRINTL
	PRINTBUTTON "[前週の主人公を利用]", 98
ENDIF

;ここから固定キャラの表示
PRINTL 
DRAWLINE
PRINTL ●既存のキャラ
PRINT    
LOCAL:1 = 0
;選択勢力と同一勢力所属のキャラを選べる
;放浪や旗揚げ、特殊スタートの場合、君主以外を選べる
FOR LOCAL:0, 1, CHARANUM
	SIF !SELECT_MASTER_CAN_SELECT(LOCAL:0)
		CONTINUE
	IF LOCAL:1 % 4 == 0 && LOCAL:1 >= 1
		PRINTL 
	ENDIF
	LOCALS:0 = [%ANAME(LOCAL:0), 12, LEFT%]
	PRINTBUTTON @"%LOCALS:0, 19, LEFT%", NO:(LOCAL:0) + 100
	LOCAL:1 ++
NEXT
PRINTL 

$INPUT_LOOP1
INPUT

;新規作成
IF RESULT == 0
	CALL INITIALIZE_CHARA(NAME_TO_CHARA("あなた"))
	CALL MASTER_EDIT(NAME_TO_CHARA("あなた"))
	選択MASTERID = GET_ID(RESULT)
	FLAG:あなた新規作成 = 1
;前週と同じ
ELSEIF RESULT == 98 && 旧MASTERID > 0
	IF !引継ぎ使用可
		PRINTFORMW 前周の主人公が選択した勢力以外の勢力の君主です。「前週と同じ」は利用できません
		CLEARLINE LINECOUNT - FIRSTLINE
		GOTO SHOW_LOOP
	ENDIF
	選択MASTERID = GET_ID(MASTER)
;既存のキャラを選択
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 100)
	IF SELECT_MASTER_CAN_SELECT(LOCAL:2)
		選択MASTERID = GET_ID(LOCAL:2)
	ELSE
		GOTO INPUT_LOOP1
	ENDIF
ENDIF

;一時的にコンフィグの外見詳細表示をONにする
LOCAL:0 = CONFIG:6
CONFIG:6 = 1

CALL TMP_PREPARE_CHARA_STARS()

DRAWLINE
CALL SHOW_INFO(ID_TO_CHARA(選択MASTERID))
DRAWLINE

CONFIG:6 = LOCAL:0

PRINTBUTTON " [このキャラを主人公にして開始]", 0
PRINTL 
PRINTBUTTON " [戻る]", 9
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

;
SELECTCASE RESULT
	;このキャラを主人公にする
	CASE 0
		REDRAW 1
		;二週目以降なら、MASTERと「あなた」を入れ替えて元に戻す
		;MASTERが「あなた」以外だと、並びがぐちゃぐちゃになってしまうので（IDの仕組みがあるし問題ないと思うけど）
		IF 旧MASTERID > 0
			SWAPCHARA MASTER, NAME_TO_CHARA("あなた")
			CALL NEWGAME_SWAP_RELATIONS(ID_TO_CHARA(旧MASTERID))
		ENDIF
		;MASTERと選択したMASTERを入れ替える
		SWAPCHARA MASTER, ID_TO_CHARA(選択MASTERID)
		CALL NEWGAME_SWAP_RELATIONS(ID_TO_CHARA(選択MASTERID))

		;主人公の所属を選択した勢力にする
		;既存勢力以外のスタートを選んでいる場合はとりあえず0にしておく
		CALL CHANGE_COUNTRY(MASTER, MAX(FLAG:OP選択勢力, 0), 1)
		;放浪する可能性があるのでへし折る
		CFLAG:MASTER:特殊状態 = 0

		;体力気力精神力の現在値を最大値に変更
		FOR LOCAL:2, 0, VARSIZE("BASE")
			BASE:MASTER:(LOCAL:2) = MAXBASE:MASTER:(LOCAL:2)
		NEXT

		;性知識の設定
		IF ABL:MASTER:性知識 < 5
			PRINTFORML 現在、%ANAME(MASTER)%の性知識は{ABL:MASTER:性知識}です
			PRINTFORML Lv5(最大)未満の場合、一部のウフフコマンドの実行に制限が掛かります
			PRINTFORML 後から上げられますが手間なので、ここで最大にしておきますか？　
			CALL ASK_YN()
			IF RESULT == 0
				ABL:MASTER:性知識 = 5
				PRINTFORMW %ANAME(MASTER)%の性知識をLv5にしました
			ENDIF
		ENDIF
		RETURN
	;戻る
	CASE 9
		CLEARLINE LINECOUNT - FIRSTLINE
		GOTO SHOW_LOOP
ENDSELECT
CLEARLINE 1
GOTO INPUT_LOOP2


;-------------------------------------------------
;主人公選択時、そのキャラが選択できるか
;-------------------------------------------------
@SELECT_MASTER_CAN_SELECT(対象)
#FUNCTION
#DIM 対象
;固定キャラでないなら弾く
SIF !IS_FIXED_CHARA(対象)
	RETURNF 0
;どこかの勢力を選択した場合は、そこ所属のキャラ以外は弾く。
SIF FLAG:OP選択勢力 > 0 && CFLAG:対象:所属 != FLAG:OP選択勢力
	RETURNF 0
;特殊なスタートを選択した場合、君主は弾く。
SIF FLAG:OP選択勢力 <= 0 && GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
	RETURNF 0

RETURNF 1

;-------------------------------------------------
;君主である固定キャラ使用時に、他勢力との関係をMASTERに移しておく関数
;SWAP_CHARAと併用する
;ARG:0 = 交換対象
;-------------------------------------------------
@NEWGAME_SWAP_RELATIONS(ARG:0)
#DIM REL_LIKE_COPY,1
#DIM REL_HATE_COPY,1
FOR LOCAL, 0, 1000
	REL_LIKE_COPY = REL_LIKE:(ARG:0):LOCAL
	REL_HATE_COPY = REL_HATE:(ARG:0):LOCAL
	REL_LIKE:(ARG:0):LOCAL = REL_LIKE:MASTER:LOCAL
	REL_HATE:(ARG:0):LOCAL = REL_HATE:MASTER:LOCAL
	REL_LIKE:MASTER:LOCAL = REL_LIKE_COPY
	REL_HATE:MASTER:LOCAL = REL_HATE_COPY
NEXT
FOR LOCAL, 0, CHARANUM
	REL_LIKE_COPY = REL_LIKE:(LOCAL):MASTER
	REL_HATE_COPY = REL_HATE:(LOCAL):MASTER
	REL_LIKE:(LOCAL):MASTER = REL_LIKE:(LOCAL):(ARG:0)
	REL_HATE:(LOCAL):MASTER = REL_HATE:(LOCAL):(ARG:0)
	REL_LIKE:(LOCAL):(ARG:0) = REL_LIKE_COPY
	REL_HATE:(LOCAL):(ARG:0) = REL_HATE_COPY
NEXT


;-------------------------------------------------
;旗揚げスタート時用、勢力の設定
;-------------------------------------------------
@START_WITH_NEW_COUNTRY
#DIM FIRST_LINE
#DIM 勢力
#DIM 都市
#DIM 対象
#DIM 消去行
#DIM 士官, 3000
勢力 = GET_NEW_COUNTRY()

DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE
PRINTL 旗揚げする都市を選択してください
PRINTL 無所属な（白っぽい色の）都市のみ指定可能です
DRAWLINE
PRINTL 


$INPUT_LOOP_CITY
INPUT

IF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
	LOCAL:5 = RESULT - 1000
	;指定された都市が存在し、無所属である場合
	IF GET_RELAYNAME(LOCAL:5) != "無名" && CITY_TYPE:(LOCAL:5) == 0 && CITY_OWNER:(LOCAL:5) == 0
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(LOCAL:5)%で旗揚げします
		都市 = LOCAL:5
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF

DRAWLINE
PRINTL 自勢力の士官として加えるキャラを4名まで選択できます
PRINTL なお、君主はリストから除外されています
DRAWLINE
REDRAW 0

FIRST_LINE = LINECOUNT
$SHOW_LOOP_CHARA
VARSET LOCAL
消去行 = 0
FOR LOCAL, 1, CHARANUM
	;君主以外の固定キャラ
	IF GET_COUNTRY_BOSS(CFLAG:LOCAL:所属) != LOCAL && IS_FIXED_CHARA(LOCAL) && LOCAL != NAME_TO_CHARA("あなた")
		IF LOCAL:1 % 4 == 0 && LOCAL:1 >= 1
			PRINTL 
			消去行 ++
		ENDIF
		LOCALS:0 = {NO:(LOCAL:0) + 100}[%ANAME(LOCAL:0), 12, LEFT%]
		SIF 士官:LOCAL
			SETCOLOR COLOR("選択中")
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", NO:(LOCAL:0) + 100
		RESETCOLOR
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL
DRAWLINE
PRINTBUTTON "[これでよし]", 0
PRINTL
消去行 += 4
$INPUT_LOOP
INPUT
IF RESULT == 0
	FLAG:OP選択勢力 = 勢力
	CITY_OWNER:都市 = 勢力
	COUNTRY_BOSS:勢力 = GET_ID(MASTER)
	COUNTRY_COLOR:勢力 = TALENT:MASTER:髪色 == 0 ? 0x404040 # TALENT:MASTER:髪色
	;経済規模に対する最大兵数の半分を得る
	COUNTRY_SOLDIER:勢力 = GET_SUM_ECONOMY(勢力) / 20
	CITY_SOLDIER:都市 = 500
	CALL CHANGE_COUNTRY(MASTER, 勢力, 1)
	PRINTFORML %ANAME(MASTER)%
	FOR LOCAL, 0, CHARANUM
		IF 士官:LOCAL == 1
			CALL CHANGE_COUNTRY(LOCAL, 勢力, 1)
		ENDIF
	NEXT
	REDRAW 1
	RETURN
ELSE
	対象 = NO_TO_CHARA(RESULT - 100)
	IF 対象 >= 0 && GET_COUNTRY_BOSS(CFLAG:対象:所属) != 対象 && IS_FIXED_CHARA(対象) && 対象 != NAME_TO_CHARA("あなた")
		;指定人数4人以下または指定済みならインバート
		IF MATCH(士官, 1) < 4 || 士官:対象
			士官:対象 = !士官:対象
		ELSE
			PRINTFORML 4人まで指定できます
		ENDIF
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP_CHARA
ENDIF

;-------------------------------------------------
;「あなた」を新規に作成した場合のボーナス
;-------------------------------------------------
@NEWLOOP_ANATA_BONUS
#DIM FIRST_LINE
#DIM 選択ボーナス
FIRST_LINE = LINECOUNT
CALL COLORPRINT("あなた新規作成ボーナス！", COLOR("注意"), "L")
PRINTFORML 「あなた」を新規に作成して主人公にしたので、ボーナスがもらえます
PRINTFORML どれにしますか？(どれも後から手に入ります)

CALL ASK_MULTI("素質「酒豪」", "素質「天の御遣い」", "素質「妖術知識」と妖術ステータス10", "スキル「超成長力」", "金200000", "いらない")
選択ボーナス = RESULT
SELECTCASE 選択ボーナス
	CASE 0
		PRINTFORML 酒に非常に強くなる素質「酒豪」にしますか？
		PRINTFORML (ステータス「肝臓」が低い、または酒を飲まないプレイスタイルだと微妙です)
		PRINTFORML (通常取得には大量の飲酒が必要なため、かかる手間はボーナス中でも随一です)
	CASE 1
		PRINTFORML 好感度を得やすくなる素質「天の御遣い」にしますか？
		PRINTFORML (上昇好感度が低い場合にしか効果がありません)
		PRINTFORML (屈服させるルートでキャラとかかわる場合無意味です)
	CASE 2
		PRINTFORML 触手が扱えるようになる素質「妖術知識」と、ＳＬＧステータス「妖術」10のセットにしますか？
		PRINTFORML (「妖術知識」の取得は安価です)
		PRINTFORML (妖術はとても鍛えにくいのですが、10程度だと効果のほどは微妙です)
	CASE 3
		PRINTFORML ＳＬＧステータスの成長が速くなり、毎ターン経験値が入るスキル「超成長力」にしますか？
		PRINTFORML (育成は楽になりますが、このゲームは能力値が高いほどステータスを1伸ばす価値が低くなります)
		PRINTFORML (十分に鍛えられたキャラだと、育成の手間とリターン自体が微妙です)
	CASE 4
		PRINTFORML 金は力。金200000にしますか？
		PRINTFORML (序盤は非常に助かりますが、終盤は小銭となるでしょう)
	CASE 5
		PRINTFORML マジで？
		PRINTFORML (実はあとから別のボーナス！　とかマジでないです)
		PRINTFORML (縛りプレイ用。きみはもの好きなのか？)
ENDSELECT

CALL ASK_YN

IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

SELECTCASE 選択ボーナス
	CASE 0
		TALENT:MASTER:酒豪 = 1
	CASE 1
		TALENT:MASTER:天の御遣い = 1
	CASE 2
		TALENT:MASTER:妖術知識 = 1
		ABL:MASTER:妖術 = 10
	CASE 3
		CALL LEARN_SKILL_GENERAL_BY_NAME(MASTER, "超成長力")
	CASE 4
		MONEY += 200000
	CASE 5
ENDSELECT

;-------------------------------------------------
;登録キャラの扱いを設定する関数
;-------------------------------------------------
@SET_登録キャラ
;勢力の数を数える
LOCAL:2 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:2 ++
	ENDIF
NEXT

DRAWLINE
PRINTL 選ばなかった登録キャラを登場させるかどうかを設定できます（現在は0人 or 全員）
PRINTL [0] 登場させない
PRINTL [1] 主人公勢力に組み込む
IF LOCAL:2 >= 2
	PRINTL [2] 主人公以外のランダムな勢力に組み込む
	PRINTL [3] 選択した勢力に組み込む
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [2] 主人公以外のランダムな勢力に組み込む
	PRINTL 
	PRINTPLAIN [3] 選択した勢力に組み込む
	PRINTL 
	RESETCOLOR
ENDIF

$INPUT_LOOP_登録キャラ
INPUT

IF RESULT == 0
	PRINTW 主人公以外の登録キャラは登場しません
ELSEIF RESULT == 1
	PRINTW 登録キャラを主人公勢力のキャラとして登場させます
	;登録キャラの扱いを設定
	FOR LOCAL:0, 0, 30
		;体力が正なら登録キャラが存在すると判定（主人公は除外）
		IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
			CALL ADD_VOID_CHARA()
			LOCAL:1 = RESULT
			;NOの設定
			NO:(LOCAL:1) = FLAG:汎用武将カウント + MIN_NO_RANDOM_CHARA
			FLAG:汎用武将カウント ++
			CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
			CALL NEWCHARA_INIT(LOCAL:1)
			CALL CHANGE_COUNTRY(LOCAL:1, CFLAG:MASTER:所属, 1)
			FOR LOCAL:2, 0, VARSIZE("BASE")
				BASE:(LOCAL:1):(LOCAL:2) = MAXBASE:(LOCAL:1):(LOCAL:2)
			NEXT
		ENDIF
	NEXT

ELSEIF RESULT == 2 && LOCAL:2 >= 2
	PRINTW 登録キャラをランダムな勢力のキャラとして登場させます
	;ランダムに勢力を選ぶ
	;登録キャラの扱いを設定
	FOR LOCAL:0, 0, 30
		;体力が正なら登録キャラが存在すると判定（主人公は除外）
		IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
			CALL ADD_VOID_CHARA()
			LOCAL:1 = RESULT
			;NOの設定
			NO:(LOCAL:1) = FLAG:汎用武将カウント + MIN_NO_RANDOM_CHARA
			FLAG:汎用武将カウント ++
			CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
			CALL NEWCHARA_INIT(LOCAL:1)
			;ループ用にしか使わないし直後に固定値代入しているので使い回し許してください（何でもしません）
			FOR LOCAL:2, 0, VARSIZE("BASE")
				BASE:(LOCAL:1):(LOCAL:2) = MAXBASE:(LOCAL:1):(LOCAL:2)
			NEXT
			$COUNTRY_LOOP
			LOCAL:3 = RAND:MAX_COUNTRY
			SIF !IS_COUNTRY(LOCAL:3) || LOCAL:3 == CFLAG:MASTER:所属
				GOTO COUNTRY_LOOP
			CALL CHANGE_COUNTRY(LOCAL:1, LOCAL:3, 1)
		ENDIF
	NEXT
ELSEIF RESULT == 3 && LOCAL:2 >= 2
	CALL SELECT_COUNTRY_登録キャラ
ELSE
	GOTO INPUT_LOOP_登録キャラ
ENDIF

;-------------------------------------------------
;登録キャラを追加する勢力の選択
;-------------------------------------------------
@SELECT_COUNTRY_登録キャラ
#DIM FIRST_LINE
DRAWLINE
PRINTL ★★登録キャラの所属勢力を選択して下さい★★
DRAWLINE
FOR LOCAL:0, 1, MAX_COUNTRY
	IF COUNTRY_BOSS:(LOCAL:0) >= 1
		LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))
		PRINTFORM [{LOCAL:0 + 100, 3, RIGHT}] %NAME:(LOCAL:2)%
		IF NAME:(LOCAL:2) != CALLNAME:(LOCAL:2)
			PRINTFORM (%CALLNAME:(LOCAL:2)%)
		ENDIF
		PRINTL 
	ENDIF
NEXT
DRAWLINE
PRINTL [200] マップを見る

$INPUT_LOOP
INPUT

IF RESULT == 200
	DRAWLINE
	CALL SET_CITY_COLOR_COUNTRY
	CALL DRAWMAP
	CALL RESET_CITY_COLOR
	DRAWLINE
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL [200] 戻る

	REDRAW 0
	FIRST_LINE = LINECOUNT
	$INPUT_LOOP_MAP
	INPUT

	IF RESULT == 200
		REDRAW 1
		RESTART
	ELSEIF RESULT >= 1001 && RESULT < 1000 + MAX_CITY
		LOCAL:2 = CITY_OWNER:(RESULT - 1000)
		LOCAL:3 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:2))
		IF LOCAL:2 > 0
			CLEARLINE LINECOUNT - FIRST_LINE
			CALL SET_CITY_COLOR_COUNTRY
			CALL DRAWMAP
			CALL RESET_CITY_COLOR
			DRAWLINE
			PRINTPLAIN この都市は　
			PRINTFORM [{LOCAL:2 + 100, 3, RIGHT}] %NAME:(LOCAL:3)%
			PRINTPLAIN 　の勢力下にあります
			PRINTL 
			PRINTL [200] 戻る
			GOTO INPUT_LOOP_MAP
		ENDIF
	ELSEIF RESULT >= 100 && RESULT < 200
		LOCAL:6 = RESULT - 100
		LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
		IF LOCAL:5 > 0
			;登録キャラの扱いを設定
			FOR LOCAL:0, 0, 30
				;体力が正なら登録キャラが存在すると判定（主人公は除外）
				IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
					CALL ADD_VOID_CHARA()
					LOCAL:1 = RESULT
					CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
					CALL NEWCHARA_INIT(LOCAL:1)
					CALL CHANGE_COUNTRY(LOCAL:1, CFLAG:(LOCAL:5):所属, 1)
					FOR LOCAL:2, 0, VARSIZE("BASE")
						BASE:(LOCAL:1):(LOCAL:2) = MAXBASE:(LOCAL:1):(LOCAL:2)
					NEXT
				ENDIF
			NEXT
			PRINTFORMW 登録キャラを%NAME:(LOCAL:5)%勢力の士官として登場させます
			REDRAW 1
			RETURN
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP_MAP

ELSEIF RESULT >= 100 && RESULT < 200
	LOCAL:6 = RESULT - 100
	LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
	IF LOCAL:5 > 0
		;登録キャラの扱いを設定
		FOR LOCAL:0, 0, 30
			;体力が正なら登録キャラが存在すると判定（主人公は除外）
			IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
				CALL ADD_VOID_CHARA()
				LOCAL:1 = RESULT
				CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
				CALL CHANGE_COUNTRY(LOCAL:1, CFLAG:(LOCAL:5):所属, 1)
				CALL NEWCHARA_INIT(LOCAL:1)
			ENDIF
		NEXT
		PRINTFORMW 登録キャラを%NAME:(LOCAL:5)%勢力の士官として登場させます
		RETURN
	ENDIF
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ランダムキャラを使用するかどうか選択
;-------------------------------------------------
@SELECT_RCHARA
IF FLAG:OPランダムキャラ使用 == -1
	FLAG:ランダムキャラ使用 = 0
ELSEIF FLAG:OPランダムキャラ使用 == 1
	FLAG:ランダムキャラ使用 = 1
ELSE
	DRAWLINE
	PRINTFORML このシナリオではランダムキャラを使用できます
	PRINTFORML ランダムキャラを使用しますか？
	CALL ASK_YN
	IF RESULT == 0
		FLAG:ランダムキャラ使用 = 1
	ELSE
		FLAG:ランダムキャラ使用 = 0
	ENDIF
ENDIF
;-------------------------------------------------
;セーブ時の処理
;-------------------------------------------------
@SAVEINFO
IF TIME == 0
	LOCALS:0 = 拠
ELSE
	LOCALS:0 = 戦
ENDIF
LOCAL:0 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
IF LOCAL:0 >= 0
	LOCALS:1 = %NAME:(LOCAL:0)%
ELSE
	LOCALS:1 = ？
ENDIF
TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS:2 '= RESULTS
PUTFORM  %LOCALS:2, 26, LEFT% {MIN(99999, DAY), 5}期<%LOCALS:0%> %LOCALS:1, 6%勢力　%CALLNAME:MASTER%

;-------------------------------------------------
;オートセーブ時の処理
;-------------------------------------------------
@SYSTEM_AUTOSAVE
IF TIME == 0
	LOCALS:0 = 拠
ELSE
	LOCALS:0 = 戦
ENDIF
LOCAL:0 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
IF LOCAL:0 >= 0
	LOCALS:1 = %NAME:(LOCAL:0)%
ELSE
	LOCALS:1 = ？
ENDIF
TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS:2 '= RESULTS
DUMPRAND
SAVEDATA 99, @"%LOCALS:2, 26, LEFT% {MIN(99999, DAY), 5}期<%LOCALS:0%> %LOCALS:1, 6%勢力　%CALLNAME:MASTER%"

