;ゲーム開始時やロード時の処理

;-------------------------
;タイトル画面
;-------------------------
@SYSTEM_TITLE
DRAWLINE
ALIGNMENT CENTER
PRINTFORML 　　,,,,,,,,　 ,,, ,,,,,　 　,,,,,,,,,　,,,lll,,,,,, 　　,,,,,,,, 　　llll ,,,,,,, 　　　,,,,,,,,　　 
PRINTFORML 　 l|'　 'lll,　lllll''''' l''  ''lllll　　lll'　　 　lll''''''''lll, lllll'' ''llll　lll''''''''lll, 
PRINTFORML 　lll'''''''''　llll 　　　ll''''''llll　　lll 　　　llll　　　　llll llll　　　llll llll　　　　llll 
PRINTFORML 　 l|,,,,,lll'　lll　　　 |ll,,,,,,llll　　lll,,,,,,, lll,,,,,,,,lll' llll　　　llll　lll,,,,,,,,lll' 
PRINTFORML 　　 '''''''　　''''　　 　'''''''' '''' 　''''''''' 　　''''''''　　 ''''　　　'''' 　　''''''''　　 
PRINTFORML
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　 ,ll'''''''ll　　,.|''''''''ll　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　,ll'　　　lll,l'''　　　,,,l''　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　 　,ll'　　　ll''　　　,,,ll''　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　,ll'　　　　　　,,,ll''　　　　　　　　 　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　ll　　　　　 ,,ll'''　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　 　l'　　　　　　ll,　　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　l'　　　, 　　　ll　　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　 ll 　　　lll,　　　ll　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　ll 　　　ll' l, 　 　'll　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　 ll　　　 ,l'　　ll　　 　ll　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　 ,ll　　 　,l' 　　'll　　　　ll　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　 '''''''''　　　　　　'''''''''''　　　　　　　　　　　　　　　　　 
PRINTFORML 
PRINTFORML %GAMEBASE_AUTHOR%
PRINTFORML Ver{GAMEBASE_VERSION / 1000}.{GAMEBASE_VERSION % 1000}
PRINTFORML %GAMEBASE_INFO%
DRAWLINE
PRINTBUTTON " 0[最初から]", 0
PRINTL 
PRINTBUTTON " 1[続きから]", 1
PRINTL 
[IF_DEBUG]
PRINTBUTTON "99[   DEBUG]", 99
PRINTL 
[ENDIF]
ALIGNMENT LEFT
$INPUT_LOOP
INPUT

IF RESULT == 0
	BEGIN FIRST
ELSEIF RESULT == 1
	LOADGAME
	RESTART
ELSEIF RESULT == 99
	[IF_DEBUG]
	CALL DEBUGMENU()
	RESTART
	[ENDIF]
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ニューゲーム
;-------------------------------------------------
@EVENTFIRST
;初期化処理
CALL INITIALIZE

CALL NEWLOOP

SIF RESULT
BEGIN SHOP
BEGIN TITLE

;-------------------------------------------------
;ロード時の処理(※複数定義可能)
;-------------------------------------------------
@EVENTLOAD
;セーブデータのアップデート
CALL UPDATE

;マップを準備
CALL MAPSETUP

;ロード時の初期化処理
CALL INITIALIZE_COMMON

;-------------------------------------------------
;ロード時とニューゲーム時の共通初期化処理
;-------------------------------------------------
@INITIALIZE_COMMON
;グローバル変数の読み込み
LOADGLOBAL

;乱数初期化
INITRAND

;ターゲットの初期化
CALL CLEAR_MTAR

;PALAMLVの設定
PALAMLV:0 = 0
PALAMLV:1 = 100
PALAMLV:2 = 300
PALAMLV:3 = 500
PALAMLV:4 = 1500
PALAMLV:5 = 3000
PALAMLV:6 = 6000
PALAMLV:7 = 10000
PALAMLV:8 = 30000
PALAMLV:9 = 50000
PALAMLV:10 = 100000
PALAMLV:11 = 250000
PALAMLV:12 = 500000
PALAMLV:13 = 1000000
PALAMLV:14 = 2500000
PALAMLV:15 = 5000000
PALAMLV:16 = 10000000
PALAMLV:17 = 25000000
PALAMLV:18 = 50000000
PALAMLV:19 = 100000000
PALAMLV:20 = 250000000
PALAMLV:21 = 500000000
PALAMLV:22 = 1000000000
PALAMLV:23 = 2500000000
PALAMLV:24 = 5000000000
PALAMLV:25 = 9999999999

ランク閾値:ランク_三能力:ランク_S = 100
ランク閾値:ランク_三能力:ランク_A = 80
ランク閾値:ランク_三能力:ランク_B = 70
ランク閾値:ランク_三能力:ランク_C = 60
ランク閾値:ランク_三能力:ランク_D = 50
ランク閾値:ランク_三能力:ランク_E = 40
ランク閾値:ランク_三能力:ランク_F = 30
ランク閾値:ランク_三能力:ランク_G = 0
ランク閾値:ランク_三能力:ランク_無 = 0

ランク閾値:ランク_妖術:ランク_S = 80
ランク閾値:ランク_妖術:ランク_A = 60
ランク閾値:ランク_妖術:ランク_B = 50
ランク閾値:ランク_妖術:ランク_C = 40
ランク閾値:ランク_妖術:ランク_D = 30
ランク閾値:ランク_妖術:ランク_E = 20
ランク閾値:ランク_妖術:ランク_F = 10
ランク閾値:ランク_妖術:ランク_G = 0
ランク閾値:ランク_妖術:ランク_無 = 0

ランク閾値:ランク_料理歌唱:ランク_S = 80
ランク閾値:ランク_料理歌唱:ランク_A = 70
ランク閾値:ランク_料理歌唱:ランク_B = 60
ランク閾値:ランク_料理歌唱:ランク_C = 50
ランク閾値:ランク_料理歌唱:ランク_D = 40
ランク閾値:ランク_料理歌唱:ランク_E = 30
ランク閾値:ランク_料理歌唱:ランク_F = 20
ランク閾値:ランク_料理歌唱:ランク_G = 10
ランク閾値:ランク_料理歌唱:ランク_無 = 0

ランク閾値:ランク_その他:ランク_S = 10
ランク閾値:ランク_その他:ランク_A = 8
ランク閾値:ランク_その他:ランク_B = 6
ランク閾値:ランク_その他:ランク_C = 4
ランク閾値:ランク_その他:ランク_D = 3
ランク閾値:ランク_その他:ランク_E = 2
ランク閾値:ランク_その他:ランク_F = 1
ランク閾値:ランク_その他:ランク_G = 0
ランク閾値:ランク_その他:ランク_無 = 0

;COSの値を事前に計算してリストに記録する
FOR LOCAL:0, 0, 360
	COS_LIST:(LOCAL:0) = CALC_COS(LOCAL:0)
NEXT

;アイテムの開発期間を設定
CALL SET_ITEMTERM

;都市の文字色リセット
CALL RESET_CITY_COLOR

;ランダムキャラの名前候補リストを作成
CALL CREATE_NAME_LIST

;タトゥーのランダム入力内容リストを作成
CALL CREATE_TATOO_LIST

;精液マップを作成
CALL CREATE_SPERM_MAP

;能力値の最小・最大を設定
FOR LOCAL:0, 0, 50
	ABL_MIN:(LOCAL:0) = 0
	ABL_MAX:(LOCAL:0) = 9999
NEXT
FOR LOCAL:0, 50, 70
	ABL_MIN:(LOCAL:0) = 0
	ABL_MAX:(LOCAL:0) = 300
NEXT
ABL_MAX:14 = 5
ABL_MIN:60 = -99
ABL_MIN:61 = -99
ABL_MIN:90 = -1000
ABL_MAX:90 = 1000
ABL_MIN:91 = -1000
ABL_MAX:91 = 1000
ABL_MIN:92 = 0
ABL_MAX:92 = 1000

;AI用の各種変数を設定
CALL AI_BASE_SETTING

CALL TMP_CREATE_UNIT_MAP

;-------------------------------------------------
;ニューゲーム時の初期化処理
;-------------------------------------------------
@INITIALIZE
;念のため全データ初期化
RESETDATA
;乱数ダンプ
DUMPRAND
;コンフィグの初期設定
CALL INIT_CONFIG

;全ての固定キャラと「あなた」を作成する
FOR LOCAL:0, 0, 200 + 1
	;CSVファイルの存在判定
	SIF EXISTCSV(LOCAL:0)
		CALL ADD_CHARA_FROM_CSV(LOCAL:0)
NEXT

;CSVでは設定できないキャラデータの初期設定
CALL ADDITIONAL_CHARA_SETTING

;ロード時とニューゲーム時の共通初期化処理
CALL INITIALIZE_COMMON

;0～199番のアイテムを売りに出す
VARSET ITEMSALES, 1, 0, 200

;周回を0に設定
FLAG:周回数 = 0

;-------------------------------------------------
;CSVでは設定できないキャラデータの初期設定
;-------------------------------------------------
@ADDITIONAL_CHARA_SETTING
CFLAG:(NAME_TO_CHARA("アリス")):母親 = GET_ID(NAME_TO_CHARA("神綺"))


;-------------------------------------------------
;ARG:0 = 1 で周回モード
;-------------------------------------------------
@NEWLOOP(ARG:0=0)
;日付を1日目に戻す
DAY = 1

;触手部屋の管理者を初期化
FLAG:触手部屋管理者 = -1

;フェイズを拠点フェイズにする
TIME = 0

;シナリオ専用変数をリセット
VARSET SCVAR, 0

;デイリーイベント変数をリセット
VARSET DVAR, 0
VARSET DSTR, 
VARSET SDVAR, 0
VARSET SDSTR, 
;口上用デイリーイベント変数をリセット
FOR LOCAL, 0, VARSIZE("KDVAR")
	CVARSET KDVAR, LOCAL, 0
	CVARSET KDSTR, LOCAL, 
NEXT

;特殊勢力スタート変数をリセット
VARSET SP_COUNTRY_START

;特殊勢力の出現フラグをリセット
VARSET SP_COUNTRY_APPEARED

;捕虜について削除
VARSET PRISONER_TARGET, -1

;スカウト回数をリセット
VARSET SCOUT_COUNT, 0

;変数の初期化
TARGET = -1
ASSI = -1

;周回数が1増える
FLAG:周回数++

;クリアフラグを解除
FLAG:クリアフラグ = 0

SHOP_TIME = 0

;料理長をリセット（料理長がランダムキャラや出産キャラで、かつそのキャラを引き継がなかったときのため）
CALL SET_COOK(-1)

;観戦モードフラグを0
FLAG:観戦モード = 0

;SLG関連の変数をクリア
STATS_TIME = 0
VARSET STATS_BOSS_ID_LIST, 0
VARSET STATS_GDP, 0
VARSET STATS_ARMY, 0
VARSET CITY_ROOT, 0
VARSET CITY_TYPE, 0
VARSET CITY_ECONOMY, 0
VARSET CITY_SOLDIER, 0
VARSET CITY_GUARD, 0
VARSET CITY_OWNER, 0
VARSET CITY_COMMANDER, 0
VARSET COUNTRY_BOSS, 0
VARSET COUNTRY_COLOR, 0
VARSET COUNTRY_SOLDIER, 0
VARSET COUNTRY_AI_TYPE, 0
VARSET COUNTRY_NOTARGET_TERM, 0
VARSET COUNTRY_IS_CLOSED, 0
VARSET COUNTRY_EVENT_ID, 0
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, 10
		UNIT_SOLDIER:(LOCAL:0):(LOCAL:1) = 0
		UNIT_POSITION:(LOCAL:0):(LOCAL:1) = 0
		UNIT_TARGET:(LOCAL:0):(LOCAL:1) = 0
		UNIT_COMMANDER:(LOCAL:0):(LOCAL:1) = 0
		UNIT_BREAK_TERM:(ARG:0):(LOCAL:1) = 0
	NEXT
NEXT
VARSET TREATY_A_TERM, 0
VARSET TREATY_U_TERM, 0
VARSET TREATY_U_TARGET, 0
VARSET TREATY_C_TERM, 0
VARSET DIPLOMACY_HATE, 0
FOR LOCAL:0, 0, MAX_TREATY_A
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
FOR LOCAL:0, 0, MAX_TREATY_U
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_U_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
FOR LOCAL:0, 0, MAX_TREATY_C
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT

;各勢力の国庫を空に（プレイヤーの懐はそのまま）
VARSET MONEY, 0, 1

;マップIDをリセット
MAPID '= "DEFAULT"

;シナリオ選択
CALL SELECT_SCENARIO
LOCAL:10 = RESULT

IF LOCAL:10 == 0
	BEGIN TITLE
	RETURN 0
ELSEIF LOCAL:10 == 1

	TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
	LOCALS '= RESULTS
	PRINTFORML シナリオ 「%LOCALS%」 で開始します

	;シナリオマップ選択処理
	TRYCALLFORM SCENARIO_MAPSELECT_%SCENARIOID%

	;マップ初期化
	CALL MAPSETUP(1)
	CALL MAP_INIT

	;シナリオイントロ
	TRYCALLFORM SCENARIO_INTRO_%SCENARIOID%

	;シナリオ初期勢力配置
	TRYCCALLFORM SCENARIO_PLACEMENT_%SCENARIOID%
	CATCH
		CALL SCENARIO_PLACEMENT_DEFAULT
	ENDCATCH
	

	$SELECT_SCENARIO

	;勢力選択
	CALL SELECT_COUNTRY

	IF RESULT == -1
		FLAG:周回数 = 0
		RESTART
	ENDIF
	
	;観戦モードでないときだけ、設定を弄る
	IF !FLAG:観戦モード
		CALL SELECT_MASTER
	ELSE
		;観戦モードの場合、前の週でキャラを変更したとき用に、「あなた」と主人公を入れ替えて戻す処理だけは必要
		SWAPCHARA NAME_TO_CHARA("あなた"), MASTER
	ENDIF

	IF FLAG:3 == -1
		CALL START_WITH_NEW_COUNTRY
	ENDIF
	CFLAG:MASTER:1 = FLAG:3
	
	;登録キャラの扱いを設定
	FOR LOCAL:0, 0, 30
		;体力が正なら登録キャラが存在すると判定（主人公は除外）
		IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
			LOCAL:1 ++
		ENDIF
	NEXT
	
	SIF LOCAL:1 > 0
		CALL SET_登録キャラ

	LOCAL:1 = 0
	
	;霖之助の扱いを設定
	;SIF NAME_TO_CHARA("霖之助") != MASTER
	;	CALL SET_霖之助

	;引き継ぎキャラの所属を主人公勢力にする
	FOR LOCAL:0, 0, CHARANUM
		IF INRANGE(NO:(LOCAL:0), 201, 2000 - 1)
			CFLAG:(LOCAL:0):1 = CFLAG:MASTER:1
			CFLAG:(LOCAL:0):特殊状態フラグ = 0 
		ENDIF
	NEXT

	;開始時の国庫を設定
	;シナリオに応じて
	FOR LOCAL:0, 1, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL:0)
			CONTINUE
		MONEY:(LOCAL:0) += GET_SUM_ECONOMY(LOCAL:0) * 100 / (100 * 100)
	NEXT


	;ランダムキャラを使用するかどうか
	CALL SELECT_RCHARA
	
	;特殊勢力の設定
	CALL SELECT_SP_COUNTRY
	
	TRYCALLFORM SCENARIO_%SCENARIOID%_AFTERSELECT
	
	CALL SELECT_SLG_SETTING
	
	;一週目のみ、金1000をもらえる
	IF FLAG:周回数 == 1
		CALL COLORPRINTFORM("一週目ボーナス！", COLOR("注意"), "W")
		PRINTFORMW 金1000が支給されます
		MONEY += 1000
	ENDIF
	
	RETURN 1
ENDIF

;-------------------------------------------------
;シナリオ選択
;-------------------------------------------------
@SELECT_SCENARIO
#DIM IN
CALL CREATE_SCENARIO_INDEX
PRINTL ★★シナリオを選択して下さい★★
DRAWLINE
FOR LOCAL:0, 0, SCENARIO_NUMBER
	SIF SCENARIO_SELECT:(LOCAL:0) == -1
		BREAK
		
		LOCALS:0  '= ""
		LOCALS:0  += "[●"
		RESULTS:0 '= ""
		TRYCALLFORM SCENARIO_NAME_%SCENARIO_ID:(LOCAL:0)%
		LOCALS:0 = %LOCALS:0%%RESULTS:0, 26, LEFT%
		LOCALS:0 += "]"
		LOCALS:0 = {SCENARIO_SELECT:(LOCAL:0),3 , RIGHT}%LOCALS:0%
		
		PRINTBUTTON LOCALS:0, SCENARIO_SELECT:(LOCAL:0)
		PRINTL
NEXT

IF FLAG:周回数 == 1
	DRAWLINE
	PRINTBUTTON "99[タイトルに戻る]", 99
ENDIF

$INPUT_LOOP
INPUT
IN = RESULT
FOR LOCAL:0 , 0, SCENARIO_NUMBER
	IF IN == 99 && FLAG:周回数 == 1
		RETURN 0
	ENDIF
	IF IN == SCENARIO_SELECT:(LOCAL:0)
		SCENARIOID '= SCENARIO_ID:LOCAL
		RETURN 1
	ENDIF
NEXT
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;勢力選択
;-------------------------------------------------
@SELECT_COUNTRY
#DIM FIRST_LINE
#DIM 選択勢力
#DIM ボス
#DIM 選択不可

CALL TMP_PREPARE_COUNTRY_STARS

選択勢力 = 0
FIRST_LINE = LINECOUNT
$SHOW_LOOP

DRAWLINE
PRINT ★★所属勢力選択★★
PRINTPLAIN   
PRINTBUTTON "97[オプション]", 97
PRINTPLAIN   
PRINTBUTTON "98[観戦]", 98
PRINTPLAIN   
PRINTBUTTON "99[放浪]", 99
PRINTPLAIN   
PRINTBUTTON "100[特殊]", 100
IF GET_NEW_COUNTRY() != -1
	PRINTPLAIN   
	PRINTBUTTON "101[旗揚げ]", 101
ENDIF
IF FLAG:周回数 == 1
	PRINTPLAIN   
	PRINTBUTTON "999[戻る]", 999
ENDIF
PRINTL 
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

ボス = ID_TO_CHARA(COUNTRY_BOSS:選択勢力)

IF ボス < 0
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL 
ELSE
	PRINT この都市は 
	CALL COLORPRINTFORM(@"%NAME_FORMAL(ボス)%", COUNTRY_COLOR:選択勢力)
	PRINTL  の勢力下にあります
	;星表示
	DRAWLINE
	PRINTFORM 難易度:
	CALL TMP_PRINT_COUNTRY_DIFFICULTY(選択勢力)
	PRINT    あまりあてにならないので注意！
	PRINTL
	;所属キャラ一覧表示
	DRAWLINE
	PRINTFORM 都市数: {GET_OWN_CITY(選択勢力), 3, LEFT}
	PRINTFORM 経済力: {GET_SUM_ECONOMY(選択勢力) / 100, 7, LEFT}
	PRINTFORM 防衛力: {GET_SUM_GUARD(選択勢力), 7, LEFT}
	PRINTL
	PRINTFORM 所属キャラ:

	;ボスだけ特殊表示
	CALL COLORPRINTFORM(@"%ANAME(ボス)%   ", COUNTRY_COLOR:選択勢力)
	LOCAL:1 = STRLENS(LOCALS:0)

	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == ボス || CFLAG:LOCAL:所属 != 選択勢力
			CONTINUE
		PRINTFORM %ANAME(LOCAL)%   
	NEXT
	PRINTL

	;その勢力がプレイ可能か調べる。未定義なら全勢力プレイ可能とする
	TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
		IF !RESULT
			CALL COLORPRINTFORM(@"このシナリオでは、その勢力でプレイすることはできません", COLOR("注意"))
			選択不可 = 1
		ELSE
			PRINTBUTTON " 0[この勢力で開始]", 0
			PRINTL
		ENDIF
	CATCH
		PRINTBUTTON " 0[この勢力で開始]", 0
		PRINTL
	ENDCATCH
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	IF ボス >= 0
		TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
			SIF RESULT
				GOTO SELECTABLE
			PRINTFORMW このシナリオでは、その勢力でプレイすることはできません
		CATCH
			$SELECTABLE
			FLAG:3 = 選択勢力
			PRINTFORMW %NAME:(ボス)%勢力で開始します
			RETURN
		ENDCATCH
	ENDIF
ELSEIF RESULT == 97
	;オプション設定
	CALL NEWGAME_SETTING
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 98
	FLAG:3 = 0
	FLAG:観戦モード = 1
	TIME = 1
	PRINTFORMW 観戦モードで開始します
	RETURN
ELSEIF RESULT == 99
	FLAG:3 = 0
	TIME = 1
	PRINTFORMW 放浪状態から開始します
	RETURN
ELSEIF RESULT == 100
	FLAG:3 = 0
	CALL SP_COUNTRY_SELECT
	RETURN
ELSEIF RESULT == 101 && GET_NEW_COUNTRY() != -1
	FLAG:3 = -1
	PRINTFORMW 旗揚げしてスタートします
	RETURN
ELSEIF RESULT == 999 && FLAG:周回数 == 1
	FLAG:周回数 --
	CVARSET CFLAG, 1, 0
	CVARSET CFLAG, 9, 0
	CVARSET CFLAG, 12, 0
	FOR LOCAL, 0, CHARANUM
		FOR LOCAL, 1, CHARANUM
			REL_LIKE:(LOCAL:0):(LOCAL:1) = 0
			REL_HATE:(LOCAL:0):(LOCAL:1) = 0
		NEXT
	NEXT
	RETURN -1
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		選択勢力 = LOCAL:4
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		選択勢力 = 0
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP


;-------------------------------------------------
;特殊勢力の選択
;-------------------------------------------------
@SP_COUNTRY_SELECT
PRINTFORMW 特殊な勢力で開始します
PRINTFORMW どれで開始しますか？
FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF SP_COUNTRY_NAME:LOCAL != ""
		PRINTFORML [{LOCAL}] %SP_COUNTRY_NAME:LOCAL%
NEXT
$INPUT_LOOP2
INPUT
LOCAL = RESULT
SIF SP_COUNTRY_NAME:LOCAL == ""
	GOTO INPUT_LOOP2
PRINTFORML どういう境遇でスタートしますか？
CALL ASK_MULTI("士官(調教を受けない)", "奴隷(最初から合意もち)", "捕虜(無所属・捕虜からスタート)")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 士官としてスタートします
	CASE 1
		PRINTFORMW 奴隷としてスタートします
	CASE 2
		PRINTFORMW 捕虜としてスタートします
ENDSELECT
SP_COUNTRY_START:LOCAL = RESULT + 1

PRINTFORML %SP_COUNTRY_NAME:LOCAL%の強さはどうしますか？
CALL ASK_MULTI("よわい", "ややよわ", "ふつう", "ややつよ", "つよい")
PRINTFORMW 強さは{RESULT}で設定します
SP_COUNTRY_RANK:LOCAL = RESULT

PRINTFORML %SP_COUNTRY_NAME:LOCAL%のボスの名前はどうしますか？
CALL ASK_MULTI(@"勢力名", "固有名", "ランダム名")
PRINTFORMW 名前を設定しました
SP_COUNTRY_BOSSNAME:LOCAL = RESULT

CALLFORM %SP_COUNTRY_NAME_ENG:LOCAL%_RISE()


;-------------------------------------------------
;主人公選択
;-------------------------------------------------
@SELECT_MASTER
#DIM MASTERそのまま
VARSET LOCAL
LOADGLOBAL

;あなたの所属を無所属にする
CFLAG:MASTER:1 = 0

DRAWLINE
PRINTL ★★主人公を選択して下さい★★
PRINTL ※通常はプレイヤーの分身となる主人公を新規作成し、作成したキャラを選択します
SIF FLAG:3 <= 0
	PRINTL ※特殊なスタートを選択しているので、既存キャラ一覧に君主以外の全キャラが表示されます
DRAWLINE
PRINTL ●登録キャラ
PRINT    
PRINTBUTTON "  0[新規作成    ]", 0
;二週目以降なら
IF FLAG:周回数 > 1
	PRINT    
	PRINTBUTTON "98[前週と同じで]", 98
ENDIF
LOCAL:1 = 0
FOR LOCAL:0, 0, 30
	;体力が正なら登録キャラが存在すると判定
	IF MASTER_MAXBASE:(LOCAL:0):0 > 0
		IF LOCAL:1 % 4 == 0
			PRINTL 
			PRINT    
		ENDIF
		LOCALS:0 = {LOCAL:0 + 10, 3}[%MASTER_NAME:(LOCAL:0):0, 12, LEFT%]
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", LOCAL:0 + 10
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE
PRINTL ●既存のキャラ
;PRINT    
;PRINTBUTTON " 99[霖之助      ]", 99
;PRINTL 
PRINT    
LOCAL:1 = 0
FOR LOCAL:0, 1, CHARANUM
	;選択勢力と同一勢力所属のキャラを選べる
	;ただし旗揚げスタート時と放浪スタート時と特殊盗スタート時は、君主以外であれば誰でも選択できる（利便性）
	IF ((FLAG:3 <= 0 && GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):所属) != LOCAL:0 ) || (FLAG:3 > 0 && CFLAG:(LOCAL:0):所属 == FLAG:3)) && NO:(LOCAL:0) < 2000 && !TALENT:(LOCAL:2):特殊勢力素質
		IF LOCAL:1 % 4 == 0 && LOCAL:1 >= 1
			PRINTL 
			PRINT    
		ENDIF
		LOCALS:0 = {NO:(LOCAL:0) + 100}[%ANAME(LOCAL:0), 12, LEFT%]
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", NO:(LOCAL:0) + 100
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL 

$INPUT_LOOP1
INPUT

;新規作成
IF RESULT == 0
	;空いている登録キャラ番号の取得
	LOCAL:5 = -1
	FOR LOCAL:0, 0, 30
		IF MASTER_MAXBASE:(LOCAL:0):0 <= 0
			LOCAL:5 = LOCAL:0
			BREAK
		ENDIF
	NEXT
	IF LOCAL:5 >= 0
		CALL CREATE_MASTER(LOCAL:5)
	ELSE
		PRINTFORML 登録キャラの数が最大です
		PRINTFORMW 先に不要な登録キャラを削除して下さい
	ENDIF
	RESTART
;前週と同じ
ELSEIF RESULT == 98
	IF FLAG:周回数 > 1
		MASTERそのまま = 1
		LOCAL:6 = MASTER
	ELSE
		GOTO INPUT_LOOP1
	ENDIF
;登録キャラを選択
ELSEIF RESULT >= 10 && RESULT <= 40 && MASTER_MAXBASE:(RESULT - 10):0 > 0
	MASTER_ID = RESULT - 10
	ADDCHARA 0
	LOCAL:6 = CHARANUM -1
	CALL APPLY_MASTER_DATA(LOCAL:6, MASTER_ID)
	LOCAL:7 = 1
;既存のキャラを選択
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 100)
	IF LOCAL:2 >= 0 && ((FLAG:3 <= 0 && GET_COUNTRY_BOSS(CFLAG:(LOCAL:2):所属) != LOCAL:0 ) || (FLAG:3 > 0 && CFLAG:(LOCAL:2):所属 == FLAG:3)) && NO:(LOCAL:2) < 2000 && !TALENT:(LOCAL:2):特殊勢力素質
		LOCAL:6 = LOCAL:2
		MASTER_ID = -1
	ELSE
		GOTO INPUT_LOOP1
	ENDIF
ENDIF

IF !MASTERそのまま
	;前の週でキャラを変更したとき用に、「あなた」と主人公を入れ替えて戻す
	SWAPCHARA NAME_TO_CHARA("あなた"), MASTER
ENDIF

;一時的にコンフィグの外見詳細表示をONにする
LOCAL:0 = CONFIG:6
CONFIG:6 = 1

DRAWLINE
CALL SHOW_INFO(LOCAL:6)
DRAWLINE

CONFIG:6 = LOCAL:0

PRINTBUTTON " 0[このキャラを主人公にして開始]", 0
IF MASTER_ID >= 0
	PRINT    
	PRINTBUTTON " 1[修正]", 1
	PRINT    
	PRINTBUTTON " 2[削除]", 2
ENDIF
PRINTL 
PRINTBUTTON " 9[戻る]", 9
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 0
	REDRAW 1
	SWAPCHARA MASTER, LOCAL:6
	CALL NEWGAME_SWAP_RELATIONS(LOCAL:6)
	IF LOCAL:7
		CFLAG:MASTER:キャラＩＤ = CFLAG:(LOCAL:6):キャラＩＤ
		NO:(LOCAL:6) = __INT_MAX__
		CALL DELETE_CHARA(LOCAL:6)
	ENDIF
	;主人公の所属を選択した勢力に
	CFLAG:MASTER:1 = FLAG:3
	;体力気力精神力の現在値を最大値に変更
	FOR LOCAL:2, 0, VARSIZE("BASE")
		BASE:MASTER:(LOCAL:2) = MAXBASE:MASTER:(LOCAL:2)
	NEXT
	;主人公の「放浪」は通常の放浪フラグで処理されるモノではないので、放浪フラグをへし折っておく
	CFLAG:MASTER:特殊状態フラグ = 0
	IF ABL:MASTER:性知識 < 5
		PRINTFORML 主人公の性知識Lvを最大(Lv5)にしますか？
		PRINTFORML 　※Lv5未満の場合、一部のウフフコマンドの実行に制限が掛かります
		PRINTFORML 　※性知識は特定のコマンドやアイテムなどで上げることが可能です
		CALL ASK_YN()
		IF RESULT == 0
			ABL:MASTER:性知識 = 5
			PRINTFORMW %ANAME(MASTER)%の性知識をLv5にしました
		ENDIF
	ENDIF
	RETURN

ELSEIF RESULT == 9
	IF LOCAL:7
		NO:(LOCAL:6) = __INT_MAX__
		CALL DELETE_CHARA(LOCAL:6)
	ENDIF
	RESTART
ELSEIF RESULT == 1 && MASTER_ID >= 0
	CALL CREATE_MASTER(MASTER_ID, 1)
	SAVEGLOBAL
	RESTART

ELSEIF RESULT == 2 && LOCAL:8 >= 0
	PRINTL 本当に削除してもよろしいですか？
	CALL ASK_YN()
	IF RESULT == 0
		CALL DELETE_MASTER(MASTER_ID)
		SAVEGLOBAL
	ENDIF
	RESTART
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP2

;-------------------------------------------------
;君主である固定キャラ使用時に、他勢力との関係をMASTERに移しておく関数
;SWAP_CHARAと併用する
;ARG:0 = 交換対象
;-------------------------------------------------
@NEWGAME_SWAP_RELATIONS(ARG:0)
#DIM REL_LIKE_COPY,1
#DIM REL_HATE_COPY,1
FOR LOCAL, 0, 1000
	REL_LIKE_COPY = REL_LIKE:(ARG:0):LOCAL
	REL_HATE_COPY = REL_HATE:(ARG:0):LOCAL
	REL_LIKE:(ARG:0):LOCAL = REL_LIKE:MASTER:LOCAL
	REL_HATE:(ARG:0):LOCAL = REL_HATE:MASTER:LOCAL
	REL_LIKE:MASTER:LOCAL = REL_LIKE_COPY
	REL_HATE:MASTER:LOCAL = REL_HATE_COPY
NEXT
FOR LOCAL, 0, CHARANUM
	REL_LIKE_COPY = REL_LIKE:(LOCAL):MASTER
	REL_HATE_COPY = REL_HATE:(LOCAL):MASTER
	REL_LIKE:(LOCAL):MASTER = REL_LIKE:(LOCAL):(ARG:0)
	REL_HATE:(LOCAL):MASTER = REL_HATE:(LOCAL):(ARG:0)
	REL_LIKE:(LOCAL):(ARG:0) = REL_LIKE_COPY
	REL_HATE:(LOCAL):(ARG:0) = REL_HATE_COPY
NEXT

;-------------------------------------------------
;登録キャラの扱いを設定する関数
;-------------------------------------------------
@SET_登録キャラ
;勢力の数を数える
LOCAL:2 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:2 ++
	ENDIF
NEXT

DRAWLINE
PRINTL 選ばなかった登録キャラを登場させるかどうかを設定できます（現在は0人 or 全員）
PRINTL [0] 登場させない
PRINTL [1] 主人公勢力に組み込む
IF LOCAL:2 >= 2
	PRINTL [2] 主人公以外のランダムな勢力に組み込む
	PRINTL [3] 選択した勢力に組み込む
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [2] 主人公以外のランダムな勢力に組み込む
	PRINTL 
	PRINTPLAIN [3] 選択した勢力に組み込む
	PRINTL 
	RESETCOLOR
ENDIF

$INPUT_LOOP_登録キャラ
INPUT

IF RESULT == 0
	PRINTW 主人公以外の登録キャラは登場しません
ELSEIF RESULT == 1
	PRINTW 登録キャラを主人公勢力のキャラとして登場させます
	;登録キャラの扱いを設定
	FOR LOCAL:0, 0, 30
		;体力が正なら登録キャラが存在すると判定（主人公は除外）
		IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
			CALL ADD_VOID_CHARA()
			LOCAL:1 = RESULT
			CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
			CALL NEWCHARA_INIT(LOCAL:1)
			CFLAG:(LOCAL:1):1 = CFLAG:MASTER:1
			SIF CFLAG:MASTER:1 == 0
				CFLAG:(LOCAL:1):特殊状態フラグ = 1
			FOR LOCAL:2, 0, VARSIZE("BASE")
				BASE:(LOCAL:1):(LOCAL:2) = MAXBASE:(LOCAL:1):(LOCAL:2)
			NEXT
		ENDIF
	NEXT

ELSEIF RESULT == 2 && LOCAL:2 >= 2
	PRINTW 登録キャラをランダムな勢力のキャラとして登場させます
	;ランダムに勢力を選ぶ
	;登録キャラの扱いを設定
	FOR LOCAL:0, 0, 30
		;体力が正なら登録キャラが存在すると判定（主人公は除外）
		IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
			CALL ADD_VOID_CHARA()
			LOCAL:1 = RESULT
			CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
			CALL NEWCHARA_INIT(LOCAL:1)
			;ループ用にしか使わないし直後に固定値代入しているので使い回し許してください（何でもしません）
			FOR LOCAL:2, 0, VARSIZE("BASE")
				BASE:(LOCAL:1):(LOCAL:2) = MAXBASE:(LOCAL:1):(LOCAL:2)
			NEXT
			$COUNTRY_LOOP
			LOCAL:3 = RAND:MAX_COUNTRY
			SIF !IS_COUNTRY(LOCAL:3) || LOCAL:3 == CFLAG:MASTER:1
				GOTO COUNTRY_LOOP
			CFLAG:(LOCAL:1):1 = LOCAL:3
		ENDIF
	NEXT
ELSEIF RESULT == 3 && LOCAL:2 >= 2
	CALL SELECT_COUNTRY_登録キャラ
ELSE
	GOTO INPUT_LOOP_登録キャラ
ENDIF

;-------------------------------------------------
;登録キャラを追加する勢力の選択
;-------------------------------------------------
@SELECT_COUNTRY_登録キャラ
#DIM FIRST_LINE
DRAWLINE
PRINTL ★★登録キャラの所属勢力を選択して下さい★★
DRAWLINE
FOR LOCAL:0, 1, MAX_COUNTRY
	IF COUNTRY_BOSS:(LOCAL:0) >= 1
		LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))
		PRINTFORM [{LOCAL:0 + 100, 3, RIGHT}] %NAME:(LOCAL:2)%
		IF NAME:(LOCAL:2) != CALLNAME:(LOCAL:2)
			PRINTFORM (%CALLNAME:(LOCAL:2)%)
		ENDIF
		PRINTL 
	ENDIF
NEXT
DRAWLINE
PRINTL [200] マップを見る

$INPUT_LOOP
INPUT

IF RESULT == 200
	DRAWLINE
	CALL SET_CITY_COLOR_COUNTRY
	CALL DRAWMAP
	CALL RESET_CITY_COLOR
	DRAWLINE
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL [200] 戻る

	REDRAW 0
	FIRST_LINE = LINECOUNT
	$INPUT_LOOP_MAP
	INPUT

	IF RESULT == 200
		REDRAW 1
		RESTART
	ELSEIF RESULT >= 1001 && RESULT < 1000 + MAX_CITY
		LOCAL:2 = CITY_OWNER:(RESULT - 1000)
		LOCAL:3 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:2))
		IF LOCAL:2 > 0
			CLEARLINE LINECOUNT - FIRST_LINE
			CALL SET_CITY_COLOR_COUNTRY
			CALL DRAWMAP
			CALL RESET_CITY_COLOR
			DRAWLINE
			PRINTPLAIN この都市は　
			PRINTFORM [{LOCAL:2 + 100, 3, RIGHT}] %NAME:(LOCAL:3)%
			PRINTPLAIN 　の勢力下にあります
			PRINTL 
			PRINTL [200] 戻る
			GOTO INPUT_LOOP_MAP
		ENDIF
	ELSEIF RESULT >= 100 && RESULT < 200
		LOCAL:6 = RESULT - 100
		LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
		IF LOCAL:5 > 0
			;登録キャラの扱いを設定
			FOR LOCAL:0, 0, 30
				;体力が正なら登録キャラが存在すると判定（主人公は除外）
				IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
					CALL ADD_VOID_CHARA()
					LOCAL:1 = RESULT
					CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
					CALL NEWCHARA_INIT(LOCAL:1)
					CFLAG:(LOCAL:1):1 = CFLAG:(LOCAL:5):1
					FOR LOCAL:2, 0, VARSIZE("BASE")
						BASE:(LOCAL:1):(LOCAL:2) = MAXBASE:(LOCAL:1):(LOCAL:2)
					NEXT
				ENDIF
			NEXT
			PRINTFORMW 登録キャラを%NAME:(LOCAL:5)%勢力の士官として登場させます
			REDRAW 1
			RETURN
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP_MAP

ELSEIF RESULT >= 100 && RESULT < 200
	LOCAL:6 = RESULT - 100
	LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
	IF LOCAL:5 > 0
		;登録キャラの扱いを設定
		FOR LOCAL:0, 0, 30
			;体力が正なら登録キャラが存在すると判定（主人公は除外）
			IF MASTER_MAXBASE:(LOCAL:0):0 > 0 && MASTER_ID != LOCAL:0
				CALL ADD_VOID_CHARA()
				LOCAL:1 = RESULT
				CALL APPLY_MASTER_DATA(LOCAL:1, LOCAL:0)
				CFLAG:(LOCAL:1):1 = CFLAG:(LOCAL:5):1
				CALL NEWCHARA_INIT(LOCAL:1)
			ENDIF
		NEXT
		PRINTFORMW 登録キャラを%NAME:(LOCAL:5)%勢力の士官として登場させます
		RETURN
	ENDIF
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;霖之助の扱いを設定する関数(もう使われてません）
;-------------------------------------------------
@SET_霖之助
;勢力の数を数える
LOCAL:2 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:2 ++
	ENDIF
NEXT

DRAWLINE
PRINTL 森近霖之助を登場させるかどうかを設定できます
PRINTL [0] 登場させない
PRINTL [1] 主人公勢力に組み込む
IF LOCAL:2 >= 2
	PRINTL [2] 主人公以外のランダムな勢力に組み込む
	PRINTL [3] 選択した勢力に組み込む
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [2] 主人公以外のランダムな勢力に組み込む
	PRINTL 
	PRINTPLAIN [3] 選択した勢力に組み込む
	PRINTL 
	RESETCOLOR
ENDIF

$INPUT_LOOP_霖之助
INPUT

IF RESULT == 0
	PRINTW 霖之助は登場しません
ELSEIF RESULT == 1
	PRINTW 霖之助を主人公勢力のキャラとして登場させます
	CFLAG:NAME_TO_CHARA("霖之助"):1 = CFLAG:MASTER:1
	SIF CFLAG:MASTER:1 == 0
		CFLAG:NAME_TO_CHARA("霖之助"):特殊状態フラグ = 1

ELSEIF RESULT == 2 && LOCAL:2 >= 2
	PRINTW 霖之助をランダムな勢力のキャラとして登場させます
	;ランダムに勢力を選ぶ
	LOCAL:3 = RAND:(LOCAL:2 - 1)
	LOCAL:2 = 0
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:1
			IF LOCAL:2 == LOCAL:3
				;選んだ勢力に霖之助を追加
				CFLAG:NAME_TO_CHARA("霖之助"):1 = LOCAL:0
				BREAK
			ENDIF
			LOCAL:2 ++
		ENDIF
	NEXT

ELSEIF RESULT == 3 && LOCAL:2 >= 2
	CALL SELECT_COUNTRY_霖之助

ELSE
	GOTO INPUT_LOOP_霖之助
ENDIF

;-------------------------------------------------
;霖之助を追加する勢力の選択
;-------------------------------------------------
@SELECT_COUNTRY_霖之助
#DIM FIRST_LINE
DRAWLINE
PRINTL ★★霖之助の所属勢力を選択して下さい★★
DRAWLINE
FOR LOCAL:0, 1, MAX_COUNTRY
	IF COUNTRY_BOSS:(LOCAL:0) >= 1
		LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))
		PRINTFORM [{LOCAL:0 + 100, 3, RIGHT}] %NAME:(LOCAL:2)%
		IF NAME:(LOCAL:2) != CALLNAME:(LOCAL:2)
			PRINTFORM (%CALLNAME:(LOCAL:2)%)
		ENDIF
		PRINTL 
	ENDIF
NEXT
DRAWLINE
PRINTL [200] マップを見る

$INPUT_LOOP
INPUT

IF RESULT == 200
	DRAWLINE
	CALL SET_CITY_COLOR_COUNTRY
	CALL DRAWMAP
	CALL RESET_CITY_COLOR
	DRAWLINE
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL [200] 戻る

	REDRAW 0
	FIRST_LINE = LINECOUNT
	$INPUT_LOOP_MAP
	INPUT

	IF RESULT == 200
		REDRAW 1
		RESTART
	ELSEIF RESULT >= 1001 && RESULT < 1000 + MAX_CITY
		LOCAL:2 = CITY_OWNER:(RESULT - 1000)
		LOCAL:3 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:2))
		IF LOCAL:2 > 0
			CLEARLINE LINECOUNT - FIRST_LINE
			CALL SET_CITY_COLOR_COUNTRY
			CALL DRAWMAP
			CALL RESET_CITY_COLOR
			DRAWLINE
			PRINTPLAIN この都市は　
			PRINTFORM [{LOCAL:2 + 100, 3, RIGHT}] %NAME:(LOCAL:3)%
			PRINTPLAIN 　の勢力下にあります
			PRINTL 
			PRINTL [200] 戻る
			GOTO INPUT_LOOP_MAP
		ENDIF
	ELSEIF RESULT >= 100 && RESULT < 200
		LOCAL:6 = RESULT - 100
		LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
		IF LOCAL:5 > 0
			;選んだ勢力に霖之助を追加
			CFLAG:NAME_TO_CHARA("霖之助"):1 = LOCAL:6
			PRINTFORMW 霖之助を%NAME:(LOCAL:5)%勢力の士官として登場させます
			REDRAW 1
			RETURN
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP_MAP

ELSEIF RESULT >= 100 && RESULT < 200
	LOCAL:6 = RESULT - 100
	LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
	IF LOCAL:5 > 0
		;選んだ勢力に霖之助を追加
		CFLAG:NAME_TO_CHARA("霖之助"):1 = LOCAL:6
		PRINTFORMW 霖之助を%NAME:(LOCAL:5)%勢力の士官として登場させます
		RETURN
	ENDIF
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ランダムキャラを使用するかどうか選択
;-------------------------------------------------
@SELECT_RCHARA
IF FLAG:4 == -1
	FLAG:44 = 0
ELSEIF FLAG:4 == 1
	FLAG:44 = 1
ELSE
	DRAWLINE
	PRINTFORML このシナリオではランダムキャラを使用できます
	PRINTFORML ランダムキャラを使用しますか？
	CALL ASK_YN
	IF RESULT == 0
		FLAG:44 = 1
	ELSE
		FLAG:44 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;特殊な勢力についての諸々を設定
;-------------------------------------------------
@SELECT_SP_COUNTRY
#DIM 野盗都市
#DIM FIRST_LINE
#DIM 勢力定数割り当て
#DIM 勢力
VARSET 勢力定数割り当て
;特殊勢力スタートを選択している場合は、先んじてSP_COUNTRY_RANK:(特殊勢力_野盗)をとりあえず1にしておく
FOR LOCAL, 0, MAX_SP_COUNTRY
	IF SP_COUNTRY_START:LOCAL
		勢力定数割り当て = LOCAL
		勢力 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:勢力定数割り当て)
	ENDIF
NEXT
DRAWLINE
PRINTFORML シナリオに登場する「特殊な」勢力の強さを選んで下さい
PRINTFORML 各勢力、小さい数字ほど弱く、0なら出現しません。大きい数字ほど強くなります
PRINTFORML （一定条件を満たして初めて登場する勢力も存在します）
PRINTFORML

FIRST_LINE = LINECOUNT
$SHOW_LOOP

IF 勢力定数割り当て != 特殊勢力_野盗
	PRINTFORML 野盗\@ GET_NEW_COUNTRY() == -1 ? （勢力が一杯であるため、出現できません） # \@
	CALL PRINTBUTTON_EX("{0}", 10, 勢力定数割り当て != 特殊勢力_野盗, SP_COUNTRY_RANK:(特殊勢力_野盗) == 0)
	CALL PRINTBUTTON_EX("{1}", 11, 勢力定数割り当て != 特殊勢力_野盗, SP_COUNTRY_RANK:(特殊勢力_野盗) == 1)
	CALL PRINTBUTTON_EX("{2}", 12, 勢力定数割り当て != 特殊勢力_野盗, SP_COUNTRY_RANK:(特殊勢力_野盗) == 2)
	CALL PRINTBUTTON_EX("{3}", 13, 勢力定数割り当て != 特殊勢力_野盗, SP_COUNTRY_RANK:(特殊勢力_野盗) == 3)
	CALL PRINTBUTTON_EX("{4}", 14, 勢力定数割り当て != 特殊勢力_野盗, SP_COUNTRY_RANK:(特殊勢力_野盗) == 4)
	CALL PRINTBUTTON_EX("{5}", 15, 勢力定数割り当て != 特殊勢力_野盗, SP_COUNTRY_RANK:(特殊勢力_野盗) == 5)
	PRINTL
ENDIF
FOR LOCAL, 2, MAX_SP_COUNTRY
	LOCALS = %SP_COUNTRY_NAME:LOCAL%
	SIF LOCALS == "" || LOCAL == 勢力定数割り当て
		CONTINUE
	PRINTFORML %LOCALS%
	CALL PRINTBUTTON_EX("{0}", 10 * LOCAL,     勢力定数割り当て != LOCAL, SP_COUNTRY_RANK:LOCAL == 0)
	CALL PRINTBUTTON_EX("{1}", 10 * LOCAL + 1, 勢力定数割り当て != LOCAL, SP_COUNTRY_RANK:LOCAL == 1)
	CALL PRINTBUTTON_EX("{2}", 10 * LOCAL + 2, 勢力定数割り当て != LOCAL, SP_COUNTRY_RANK:LOCAL == 2)
	CALL PRINTBUTTON_EX("{3}", 10 * LOCAL + 3, 勢力定数割り当て != LOCAL, SP_COUNTRY_RANK:LOCAL == 3)
	CALL PRINTBUTTON_EX("{4}", 10 * LOCAL + 4, 勢力定数割り当て != LOCAL, SP_COUNTRY_RANK:LOCAL == 4)
	CALL PRINTBUTTON_EX("{5}", 10 * LOCAL + 5, 勢力定数割り当て != LOCAL, SP_COUNTRY_RANK:LOCAL == 5)
	PRINTL
NEXT

PRINTL
PRINTFORML ボスの名前（ランダム名は男性名です）

FOR LOCAL, 0, MAX_SP_COUNTRY
	LOCALS = %SP_COUNTRY_NAME:LOCAL%
	SIF LOCALS == "" || LOCAL == 勢力定数割り当て
		CONTINUE
	PRINTFORML %LOCALS%
	CALL PRINTBUTTON_EX(@"[勢力名]", 10 * LOCAL + 100, SP_COUNTRY_RANK:LOCAL, SP_COUNTRY_BOSSNAME:LOCAL == 0)
	CALL PRINTBUTTON_EX(@"[固有名]",   10 * LOCAL + 101, SP_COUNTRY_RANK:LOCAL, SP_COUNTRY_BOSSNAME:LOCAL == 1)
	CALL PRINTBUTTON_EX(@"[ランダム]", 10 * LOCAL + 102, LOCAL != 勢力定数割り当て && SP_COUNTRY_RANK:LOCAL, SP_COUNTRY_BOSSNAME:LOCAL == 2)
	PRINTL
NEXT
PRINTL
IF 勢力定数割り当て != 特殊勢力_野盗
	PRINTL 野盗の初期都市（ランダム）
	CALL PRINTBUTTON_EX("{なし}" , 200, SP_COUNTRY_RANK:(特殊勢力_野盗), 野盗都市 == 0)
	CALL PRINTBUTTON_EX("{あり}" , 201, SP_COUNTRY_RANK:(特殊勢力_野盗), 野盗都市 == 1)
	PRINTL
ENDIF
PRINTL
PRINTBUTTON "[これで決定]", 999
$INPUT_LOOP
INPUT
IF RESULT == 999
	;野盗あり設定ならここで勢力作成
	SIF SP_COUNTRY_RANK:(特殊勢力_野盗) && GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_野盗) == -1
		CALL BANDIT_INIT
	SIF 野盗都市
		CALL BANDIT_RISE()
	;特殊勢力スタートならここで所属やら何やら書き換える
	IF 勢力定数割り当て
		SELECTCASE SP_COUNTRY_START:勢力定数割り当て
			CASE 特殊勢力スタート_士官
				CFLAG:MASTER:所属 = 勢力
			CASE 特殊勢力スタート_奴隷
				CFLAG:MASTER:所属 = 勢力
				FOR LOCAL, 0, CHARANUM
					IF TALENT:LOCAL:特殊勢力素質 == 勢力定数割り当て
						TALENT:LOCAL:合意 = 1
						CFLAG:LOCAL:好感度 = 800
						CFLAG:LOCAL:依存度 = 300
						CFLAG:LOCAL:面識 = 1
					ENDIF
				NEXT
			CASE 特殊勢力スタート_捕虜
				CFLAG:MASTER:捕虜先 = 勢力
		ENDSELECT
	ENDIF
	RETURN
ENDIF

IF INRANGE(RESULT, 0, 99)
	;選択肢
	LOCAL = RESULT % 10
	;勢力
	LOCAL:1 = RESULT / 10

	IF 勢力定数割り当て == LOCAL:1
		PRINTFORML %SP_COUNTRY_NAME:勢力定数割り当て%でのスタートを選択しているため、ここでは強さを変更できません
	ELSEIF SP_COUNTRY_NAME:(LOCAL:1) != "" && INRANGE(LOCAL, 0, 5)
		SP_COUNTRY_RANK:(LOCAL:1) = LOCAL
	ENDIF
ENDIF

IF INRANGE(RESULT, 100, 199)
	RESULT -= 100
	;選択肢
	LOCAL = RESULT % 10
	;勢力
	LOCAL:1 = RESULT / 10
	IF 勢力定数割り当て == LOCAL:1
		PRINTFORML スタートに指定した勢力の君主は指定時にしか変えられません
	ELSEIF SP_COUNTRY_NAME:(LOCAL:1) != "" && INRANGE(LOCAL, 0, 2) && SP_COUNTRY_RANK:(LOCAL:1) 
		SP_COUNTRY_BOSSNAME:(LOCAL:1) = LOCAL
	ENDIF
ENDIF

IF 勢力定数割り当て != 特殊勢力_野盗 && GROUPMATCH(RESULT, 200, 201)
	野盗都市 = RESULT - 200
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP
;-------------------------------------------------
;設定セットを選択
;-------------------------------------------------
@SELECT_SLG_SETTING
DRAWLINE
PRINTFORML SLG用設定を選択してください（下に行くほどSLG側の難易度が上がります）
PRINTFORML 一部例外を除き、これらの設定はゲーム中でも変更可能です
CALL COLORPRINTFORM("デフォルト設定で遊ぶのを強くオススメします", COLOR("注意"), "L")
CALL COLORPRINTFORM("その他形式でのバランステストは行っていません", COLOR("注意"), "L")
PRINTL 設定 | 難易度 | 好感度補正 | 補正方式 | 兵力補正 | 継戦補正 | 敵対補正 |
PRINTL [0]  |  簡単  |    高い    |   通常   | がっつり |   なし   |   EASY   | 初めて・嫁の活躍を見たい方向け
PRINTL [1]  |  簡単  |    普通    |   通常   | がっつり | 武将：低 |   EASY   | 上の設定で少し物足りなくなった人に
PRINTL [2]  |  簡単  |    普通    |   通常   |   強め   | 武将：通 |  NORMAL  | それぞれの設定を一通り試したい人向け
PRINTL [3]  |  普通  |    低い    |   通常   |   弱め   | 回数：低 |  NORMAL  | キャラの能力に重点を置きたい人向け
PRINTL [99] |  普通  |    無し    |   －－   |   弱め   | 回数：低 |  NORMAL  | デフォルト：恋と戦は別腹です
PRINTL [4]  |  普通  |    無し    |   －－   | がっつり | 回数：通 |   HARD   | 愛の力のない純粋な戦略を楽しみたい方に
PRINT [5]  |  困難  |    高い    |  倍率型  |   強め   | 武将：高 | LUNATIC  | 
SETCOLOR COLOR("注意")
PRINTL 周回前提：準備はできていますか？

RESETCOLOR
PRINT [6]  |  
SETCOLOR COLOR("警告")
PRINT 虐め  
PRINTD |    普通    |  倍率型  |   弱め   | 武将：高 |  EXTRA   | 
PRINTL 周回前提：まぁがんばれ
RESETCOLOR

PRINT [7]  |  
SETCOLOR COLOR("警告")
PRINT 虐め  
PRINTD |    低い    |  倍率型  |   無し   | 回数：高 |  
PRINT EXTRA+  
PRINTD | 
SETCOLOR COLOR("灰色")
PRINTL ようこそ、悪夢へ
RESETCOLOR



PRINTL [99] デフォルトのままであそぶ（強く推奨）
$INPUT_LOOP_SLG
INPUT

SELECTCASE RESULT
CASE 0
	CONFIG:300 = 0
	CONFIG:309 = 1
	CONFIG:310 = 0
	CONFIG:312 = 3
	CONFIG:313 = 0
	CONFIG:314 = 0
CASE 1
	CONFIG:300 = 0
	CONFIG:309 = 0
	CONFIG:310 = 0
	CONFIG:312 = 3
	CONFIG:313 = 4
	CONFIG:314 = 0
CASE 2
	CONFIG:300 = 0
	CONFIG:309 = 0
	CONFIG:310 = 0
	CONFIG:312 = 2
	CONFIG:313 = 5
	CONFIG:314 = 1
CASE 3
	CONFIG:300 = 1
	CONFIG:309 = 3
	CONFIG:310 = 0
	CONFIG:312 = 1
	CONFIG:313 = 1
	CONFIG:314 = 1
CASE 4
	CONFIG:300 = 1
	CONFIG:309 = 2
	CONFIG:310 = 0
	CONFIG:312 = 3
	CONFIG:313 = 2
	CONFIG:314 = 2
CASE 5
	CONFIG:300 = 2
	CONFIG:309 = 1
	CONFIG:310 = 2
	CONFIG:312 = 2
	CONFIG:313 = 6
	CONFIG:314 = 3
CASE 6
	CONFIG:300 = 3
	CONFIG:309 = 0
	CONFIG:310 = 2
	CONFIG:312 = 1
	CONFIG:313 = 6
	CONFIG:314 = 4
CASE 7
	CONFIG:300 = 3
	CONFIG:309 = 3
	CONFIG:310 = 2
	CONFIG:312 = 0
	CONFIG:313 = 3
	CONFIG:314 = 5
CASE 99
CASEELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_SLG
ENDSELECT

IF INRANGE(RESULT, 0, 7)
	PRINTL 設定が変更されました
	SIF RESULT == 6
		PRINTL なお、難易度はゲーム中変更できません。
	SIF RESULT == 7
		PRINTL なお、難易度及び敵対補正がロックされました。
ELSE
	PRINTW 設定を変更せずに開始します。
ENDIF
DRAWLINE
WAIT

RETURN

;-------------------------------------------------
;セーブ時の処理
;-------------------------------------------------
@SAVEINFO
IF TIME == 0
	LOCALS:0 = 拠
ELSE
	LOCALS:0 = 戦
ENDIF
LOCAL:0 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
IF LOCAL:0 >= 0
	LOCALS:1 = %NAME:(LOCAL:0)%
ELSE
	LOCALS:1 = ？
ENDIF
TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS:2 '= RESULTS
PUTFORM  %LOCALS:2, 26, LEFT% {MIN(99999, DAY), 5}期<%LOCALS:0%> %LOCALS:1, 6%勢力　%CALLNAME:MASTER%

;-------------------------------------------------
;オートセーブ時の処理
;-------------------------------------------------
@SYSTEM_AUTOSAVE
IF TIME == 0
	LOCALS:0 = 拠
ELSE
	LOCALS:0 = 戦
ENDIF
LOCAL:0 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
IF LOCAL:0 >= 0
	LOCALS:1 = %NAME:(LOCAL:0)%
ELSE
	LOCALS:1 = ？
ENDIF
TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS:2 '= RESULTS
DUMPRAND
SAVEDATA 99, @"%LOCALS:2, 26, LEFT% {MIN(99999, DAY), 5}期<%LOCALS:0%> %LOCALS:1, 6%勢力　%CALLNAME:MASTER%"


;-------------------------------------------------
;旗揚げスタート
;-------------------------------------------------
@START_WITH_NEW_COUNTRY
#DIM FIRST_LINE
#DIM 勢力
#DIM 都市
#DIM 対象
#DIM 消去行
#DIM 士官, 1000
勢力 = GET_NEW_COUNTRY()

DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE
PRINTL 旗揚げする都市を選択してください
PRINTL 無所属な（白っぽい色の）都市のみ指定可能です
DRAWLINE
PRINTL 


$INPUT_LOOP_CITY
INPUT

IF RESULT >= 1001 && RESULT < 1100
	LOCAL:5 = RESULT - 1000
	;指定された都市が存在し、無所属である場合
	IF GET_RELAYNAME(LOCAL:5) != "無名" && CITY_TYPE:(LOCAL:5) == 0 && CITY_OWNER:(LOCAL:5) == 0
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(LOCAL:5)%で旗揚げします
		都市 = LOCAL:5
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF

DRAWLINE
PRINTL 自勢力の士官として加えるキャラを選択してください
PRINTL 君主はリストから除外されています
DRAWLINE
REDRAW 0

FIRST_LINE = LINECOUNT
$SHOW_LOOP_CHARA
VARSET LOCAL
消去行 = 0
FOR LOCAL, 1, CHARANUM
	;君主以外
	IF GET_COUNTRY_BOSS(CFLAG:LOCAL:所属) != LOCAL
		IF LOCAL:1 % 4 == 0 && LOCAL:1 >= 1
			PRINTL 
			消去行 ++
		ENDIF
		LOCALS:0 = {NO:(LOCAL:0) + 100}[%ANAME(LOCAL:0), 12, LEFT%]
		SIF 士官:LOCAL
			SETCOLOR COLOR("選択中")
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", NO:(LOCAL:0) + 100
		RESETCOLOR
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL
DRAWLINE
PRINTBUTTON "[これでよし]", 0
PRINTL
消去行 += 4
$INPUT_LOOP
INPUT
IF RESULT == 0
	FLAG:3 = 勢力
	CFLAG:MASTER:所属 = 勢力
	CITY_OWNER:都市 = 勢力
	COUNTRY_BOSS:勢力 = GET_ID(MASTER)
	COUNTRY_COLOR:勢力 = TALENT:MASTER:髪色 == 0 ? 0x404040 # TALENT:MASTER:髪色
	;経済規模に対する最大兵数の半分を得る
	COUNTRY_SOLDIER:勢力 = GET_SUM_ECONOMY(勢力) / 20
	CITY_SOLDIER:都市 = 500
	FOR LOCAL, 0, CHARANUM
		IF 士官:LOCAL == 1
			CFLAG:LOCAL:所属 = 勢力
			CFLAG:LOCAL:特殊状態フラグ = 0
		ENDIF
	NEXT
	REDRAW 1
	RETURN
ELSE
	対象 = NO_TO_CHARA(RESULT - 100)
	SIF 対象 >= 0 && GET_COUNTRY_BOSS(CFLAG:対象:所属) != 対象
		士官:対象 = !士官:対象
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP_CHARA
ENDIF