;ゲーム開始時やロード時の処理

;-------------------------
;タイトル画面
;-------------------------
@SYSTEM_TITLE
DRAWLINE
ALIGNMENT CENTER
PRINTFORML 　　,,,,,,,,　 ,,, ,,,,,　 　,,,,,,,,,　,,,lll,,,,,, 　　,,,,,,,, 　　llll ,,,,,,, 　　　,,,,,,,,　　 
PRINTFORML 　 l|'　 'lll,　lllll''''' l''  ''lllll　　lll'　　 　lll''''''''lll, lllll'' ''llll　lll''''''''lll, 
PRINTFORML 　lll'''''''''　llll 　　　ll''''''llll　　lll 　　　llll　　　　llll llll　　　llll llll　　　　llll 
PRINTFORML 　 l|,,,,,lll'　lll　　　 |ll,,,,,,llll　　lll,,,,,,, lll,,,,,,,,lll' llll　　　llll　lll,,,,,,,,lll' 
PRINTFORML 　　 '''''''　　''''　　 　'''''''' '''' 　''''''''' 　　''''''''　　 ''''　　　'''' 　　''''''''　　 
PRINTFORML
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　 ,ll'''''''ll　　,.|''''''''ll　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　,ll'　　　lll,l'''　　　,,,l''　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　 　,ll'　　　ll''　　　,,,ll''　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　,ll'　　　　　　,,,ll''　　　　　　　　 　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　　ll　　　　　 ,,ll'''　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　 　l'　　　　　　ll,　　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　　l'　　　, 　　　ll　　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　 ll 　　　lll,　　　ll　　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　　ll 　　　ll' l, 　 　'll　　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　　 ll　　　 ,l'　　ll　　 　ll　　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　 ,ll　　 　,l' 　　'll　　　　ll　　　　　　　　　　　　　　　　　　
PRINTFORML 　　　　　　　　　　　　　　　　　 '''''''''　　　　　　'''''''''''　　　　　　　　　　　　　　　　　 
PRINTFORML 
PRINTFORML %GAMEBASE_AUTHOR%
PRINTFORML Ver{GAMEBASE_VERSION / 1000}.{GAMEBASE_VERSION % 1000}
PRINTFORML %GAMEBASE_INFO%

DRAWLINE
PRINTBUTTON " 0[最初から]", 0
PRINTL 
PRINTBUTTON " 1[続きから]", 1
PRINTL 
ALIGNMENT LEFT
$INPUT_LOOP
INPUT

IF RESULT == 0
	BEGIN FIRST
ELSEIF RESULT == 1
	LOADGAME
	RESTART
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ニューゲーム
;-------------------------------------------------
@EVENTFIRST
;初期化処理
CALL INITIALIZE

CALL NEWLOOP

SIF RESULT
BEGIN SHOP
BEGIN TITLE

;-------------------------------------------------
;ロード時の処理(※複数定義可能)
;-------------------------------------------------
@EVENTLOAD
;セーブデータのアップデート
CALL UPDATE

;マップを準備
CALL MAPSETUP

;ロード時の初期化処理
CALL INITIALIZE_COMMON

;-------------------------------------------------
;ロード時とニューゲーム時の共通初期化処理
;-------------------------------------------------
@INITIALIZE_COMMON
;グローバル変数の読み込み
LOADGLOBAL

;ターゲットの初期化
CALL CLEAR_MTAR

;PALAMLVの設定
PALAMLV:0 = 0
PALAMLV:1 = 100
PALAMLV:2 = 300
PALAMLV:3 = 500
PALAMLV:4 = 1500
PALAMLV:5 = 3000
PALAMLV:6 = 6000
PALAMLV:7 = 10000
PALAMLV:8 = 30000
PALAMLV:9 = 50000
PALAMLV:10 = 100000
PALAMLV:11 = 250000
PALAMLV:12 = 500000
PALAMLV:13 = 1000000
PALAMLV:14 = 2500000
PALAMLV:15 = 5000000
PALAMLV:16 = 10000000
PALAMLV:17 = 25000000
PALAMLV:18 = 50000000
PALAMLV:19 = 100000000
PALAMLV:20 = 250000000
PALAMLV:21 = 500000000
PALAMLV:22 = 1000000000
PALAMLV:23 = 2500000000
PALAMLV:24 = 5000000000
PALAMLV:25 = 9999999999

;COSの値を事前に計算してリストに記録する
FOR LOCAL:0, 0, 360
	COS_LIST:(LOCAL:0) = CALC_COS(LOCAL:0)
NEXT

;アイテムの開発期間を設定
CALL SET_ITEMTERM

;都市の文字色リセット
CALL RESET_CITY_COLOR

;ランダムキャラの名前候補リストを作成
CALL CREATE_NAME_LIST

;能力値の最小・最大を設定
FOR LOCAL:0, 0, 50
	ABL_MIN:(LOCAL:0) = 0
	ABL_MAX:(LOCAL:0) = 9999
NEXT
FOR LOCAL:0, 50, 70
	ABL_MIN:(LOCAL:0) = 0
	ABL_MAX:(LOCAL:0) = 300
NEXT
ABL_MAX:14 = 5
ABL_MIN:60 = -99
ABL_MIN:61 = -99
ABL_MIN:90 = -1000
ABL_MAX:90 = 1000
ABL_MIN:91 = -1000
ABL_MAX:91 = 1000
ABL_MIN:92 = 0
ABL_MAX:92 = 1000

;AI用の各種変数を設定
CALL AI_BASE_SETTING

;勢力の強要ペナルティのリセット
VARSET DIPLOMACY_HATE:0:0, 0
VARSET DIPLOMACY_REQUEST_SUCCESS_COUNT, 0

;タチ役に恋慕　等をCONFIG:88で設定用
SIF CONFIG:88 == 0
	CONFIG:88 = NAME_TO_CHARA("藍")

;-------------------------------------------------
;ニューゲーム時の初期化処理
;-------------------------------------------------
@INITIALIZE
;念のため全データ初期化
RESETDATA

;コンフィグの初期設定
CALL INIT_CONFIG

;全ての固定キャラと「あなた」を作成する
FOR LOCAL:0, 0, 200 + 1
	;CSVファイルの存在判定
	SIF EXISTCSV(LOCAL:0)
		CALL ADD_CHARA_FROM_CSV(LOCAL:0)
NEXT

;CSVでは設定できないキャラデータの初期設定
CALL ADDITIONAL_CHARA_SETTING

;ロード時とニューゲーム時の共通初期化処理
CALL INITIALIZE_COMMON

;0～199番のアイテムを売りに出す
VARSET ITEMSALES, 1, 0, 200

;周回を0に設定
FLAG:26 = 0

;-------------------------------------------------
;CSVでは設定できないキャラデータの初期設定
;-------------------------------------------------
@ADDITIONAL_CHARA_SETTING
CFLAG:(NAME_TO_CHARA("アリス")):21 = GET_ID(NAME_TO_CHARA("神綺"))


;-------------------------------------------------
;ARG:0 = 1 で周回モード
;-------------------------------------------------
@NEWLOOP(ARG:0=0)
;日付を1日目に戻す
DAY = 1

;触手部屋の管理者を初期化
FLAG:35 = -1

;フェイズを拠点フェイズにする
TIME = 0

;シナリオ専用変数をリセット
VARSET SCVAR, 0

;デイリーイベント変数をリセット
VARSET DVAR, 0

;野盗スタート変数をリセット
VARSET BANDIT_START

;変数の初期化
TARGET = -1
ASSI = -1

;周回数が1増える
FLAG:26++

;クリアフラグを解除
FLAG:28 = 0

SHOP_TIME = 0

;料理長をリセット（料理長がランダムキャラや出産キャラで、かつそのキャラを引き継がなかったときのため）
CALL SET_COOK(-1)

;SLG関連の変数をクリア
STATS_TIME = 0
VARSET STATS_BOSS_ID_LIST, 0
VARSET STATS_GDP, 0
VARSET STATS_ARMY, 0
VARSET CITY_ROOT, 0
VARSET CITY_TYPE, 0
VARSET CITY_ECONOMY, 0
VARSET CITY_SOLDIER, 0
VARSET CITY_GUARD, 0
VARSET CITY_OWNER, 0
VARSET CITY_COMMANDER, 0
VARSET COUNTRY_BOSS, 0
VARSET COUNTRY_COLOR, 0
VARSET COUNTRY_SOLDIER, 0
VARSET COUNTRY_AI_TYPE, 0
VARSET COUNTRY_NOTARGET_TERM, 0
VARSET COUNTRY_IS_CLOSED, 0
VARSET COUNTRY_EVENT_ID, 0
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, 10
		UNIT_SOLDIER:(LOCAL:0):(LOCAL:1) = 0
		UNIT_POSITION:(LOCAL:0):(LOCAL:1) = 0
		UNIT_TARGET:(LOCAL:0):(LOCAL:1) = 0
		UNIT_COMMANDER:(LOCAL:0):(LOCAL:1) = 0
		UNIT_BREAK_TERM:(ARG:0):(LOCAL:1) = 0
	NEXT
NEXT
VARSET TREATY_A_TERM, 0
VARSET TREATY_U_TERM, 0
VARSET TREATY_U_TARGET, 0
VARSET TREATY_C_TERM, 0
FOR LOCAL:0, 0, MAX_TREATY_A
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
FOR LOCAL:0, 0, MAX_TREATY_U
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_U_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT
FOR LOCAL:0, 0, MAX_TREATY_C
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) = 0
	NEXT
NEXT

;各勢力の国庫を空に（プレイヤーの懐はそのまま）
VARSET MONEY, 0, 1

;マップIDをリセット
MAPID '= "DEFAULT"

;シナリオ選択
CALL SELECT_SCENARIO
LOCAL:10 = RESULT

IF LOCAL:10 == 0
	BEGIN TITLE
	RETURN 0
ELSEIF LOCAL:10 == 1

	TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
	LOCALS '= RESULTS
	PRINTFORML シナリオ 「%LOCALS%」 で開始します

	;シナリオマップ選択処理
	TRYCALLFORM SCENARIO_MAPSELECT_%SCENARIOID%

	;マップ初期化
	CALL MAPSETUP(1)
	CALL MAP_INIT

	;シナリオ初期処理
	TRYCALLFORM SCENARIO_%SCENARIOID%

	$SELECT_SCENARIO

	;2週目以降で、プレイヤーが頭首になっていた場合、勢力選択はできない
	IF FLAG:26 >= 2 && GET_COUNTRY_BOSS(CFLAG:MASTER:1) == MASTER
		PRINTFORMW このシナリオでは%ANAME(MASTER)%は頭首なので、勢力選択はスキップされます。
		CALL NEWGAME_SETTING
	ELSE
		;勢力選択
		CALL SELECT_COUNTRY
		;その勢力がプレイ可能か調べる。未定義なら全勢力プレイ可能とする
		TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%
		IF !RESULT
			SETCOLOR COLOR("警告")
			PRINTFORML !!!このシナリオでは、その勢力でプレイすることはできません!!!
			PRINTFORMW 勢力選択画面に戻ります
			RESETCOLOR
			GOTO SELECT_SCENARIO
		ENDIF
		CATCH
			
		ENDCATCH
		;観戦モードでなく、かつ1週目のときだけ、あなたを弄る
		IF !FLAG:1 && FLAG:26 == 1
			CALL SELECT_MASTER
		ENDIF
		CFLAG:MASTER:1 = FLAG:3
	ENDIF
	IF NAME_TO_CHARA("霖之助") != MASTER
		;霖之助の扱いを設定
		CALL SET_霖之助
	ENDIF

	;引き継ぎキャラの所属を主人公勢力にする
	FOR LOCAL:0, 0, CHARANUM
		IF INRANGE(NO:(LOCAL:0), 201, 2000 - 1)
			CFLAG:(LOCAL:0):1 = CFLAG:MASTER:1
			CFLAG:(LOCAL:0):12 = 0 
		ENDIF
	NEXT

	;開始時の国庫を設定
	;シナリオに応じて
	FOR LOCAL:0, 1, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL:0)
			CONTINUE
		MONEY:(LOCAL:0) += GET_SUM_ECONOMY(LOCAL:0) * 10 / (100 * 100)
	NEXT


	;ランダムキャラを使用するかどうか
	CALL SELECT_RCHARA
	
	;野盗を使用するかどうか
	CALL SELECT_BANDIT
	SIF BANDIT_RANK
		CALL BANDIT_INIT

	CALL SELECT_GAIRAI
	CALL SELECT_GOBLIN
	CALL SELECT_SLG_SETTING

	RETURN 1
ENDIF

;-------------------------------------------------
;シナリオ選択
;-------------------------------------------------
@SELECT_SCENARIO
#DIM IN
CALL CREATE_SCENARIO_INDEX
PRINTL ★★シナリオを選択して下さい★★
DRAWLINE
FOR LOCAL:0, 0, SCENARIO_NUMBER
	SIF SCENARIO_SELECT:(LOCAL:0) == -1
		BREAK
		
		LOCALS:0  '= ""
		LOCALS:0  += "[●"
		RESULTS:0 '= ""
		TRYCALLFORM SCENARIO_NAME_%SCENARIO_ID:(LOCAL:0)%
		LOCALS:0 = %LOCALS:0%%RESULTS:0, 26, LEFT%
		LOCALS:0 += "]"
		LOCALS:0 = {SCENARIO_SELECT:(LOCAL:0),3 , RIGHT}%LOCALS:0%
		
		PRINTBUTTON LOCALS:0, SCENARIO_SELECT:(LOCAL:0)
		PRINTL
NEXT

IF FLAG:26 == 1
	DRAWLINE
	PRINTBUTTON "99[タイトルに戻る]", 99
ENDIF

$INPUT_LOOP
INPUT
IN = RESULT
FOR LOCAL:0 , 0, SCENARIO_NUMBER
	IF IN == 99 && FLAG:26 == 1
		RETURN 0
	ENDIF
	IF IN == SCENARIO_SELECT:(LOCAL:0)
		SCENARIOID '= SCENARIO_ID:LOCAL
		RETURN 1
	ENDIF
NEXT
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;勢力選択
;-------------------------------------------------
@SELECT_COUNTRY
LOCAL:10 = 0

$SHOW_LOOP

DRAWLINE
PRINT ★★所属勢力選択★★
PRINTPLAIN   
PRINTBUTTON "97[オプション設定]", 97
PRINTPLAIN   
PRINTBUTTON "98[放浪スタート]", 98
IF FLAG:26 == 1
	PRINTPLAIN   
	PRINTBUTTON "99[観戦モード]", 99
ENDIF
PRINTPLAIN   
PRINTBUTTON "100[野盗で開始]", 100
PRINTL 
DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE

LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:10))

IF LOCAL:2 < 0
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL 
ELSE
	PRINT この都市は 
	SETCOLOR COUNTRY_COLOR:(LOCAL:10)
	PRINTFORM %NAME_FORMAL(LOCAL:2)%
	RESETCOLOR
	PRINTL  の勢力下にあります
	PRINTBUTTON " 0[この勢力で開始]", 0
	PRINTL 
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	IF LOCAL:2 >= 0
		FLAG:3 = LOCAL:10
		PRINTFORMW %NAME:(LOCAL:2)%勢力で開始します
		RETURN
	ENDIF

ELSEIF RESULT == 97
	;オプション設定
	CALL NEWGAME_SETTING
	GOTO SHOW_LOOP
ELSEIF RESULT == 98
	FLAG:3 = 0
	TIME = 1
	PRINTFORMW 放浪状態から開始します
	RETURN

ELSEIF RESULT == 99 && FLAG:26 == 1
	FLAG:3 = 0
	FLAG:1 = 1
	TIME = 1
	PRINTFORMW 観戦モードで開始します
	RETURN

ELSEIF RESULT == 100
	FLAG:3 = 0
	PRINTFORMW 野盗勢力で開始します
	PRINTFORMW 野盗の士官、あるいは野盗の奴隷、または野盗の捕虜
	PRINTFORMW どれで開始しますか？
	CALL ASK_MULTI("野盗の士官(戦略フェイズ時、調教を受けない)", "野盗の奴隷(野盗が最初から合意もち)", "野盗の捕虜(無所属・野盗の捕虜からスタート)")
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW 野盗の士官としてスタートします
		CASE 1
			PRINTFORMW 野盗の奴隷としてスタートします
		CASE 2
			PRINTFORMW 野盗の捕虜としてスタートします
	ENDSELECT
	BANDIT_START = RESULT + 1
	RETURN

ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		LOCAL:10 = LOCAL:4
		CLEARLINE 34
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		LOCAL:10 = 0
		CLEARLINE 34
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;主人公選択
;-------------------------------------------------
@SELECT_MASTER
LOADGLOBAL

;あなたの所属を無所属にする
CFLAG:MASTER:1 = 0

DRAWLINE
PRINTL ★★主人公を選択して下さい★★
PRINTL ※通常はプレイヤーの分身となる主人公を新規作成し、作成したキャラを選択します
DRAWLINE
PRINTL ●登録キャラ
PRINT    
PRINTBUTTON "  0[新規作成    ]", 0
LOCAL:1 = 0
FOR LOCAL:0, 0, 30
	;体力が正なら登録キャラが存在すると判定
	IF MASTER_MAXBASE:(LOCAL:0):0 > 0
		IF LOCAL:1 % 4 == 0
			PRINTL 
			PRINT    
		ENDIF
		LOCALS:0 = {LOCAL:0 + 10, 3}[%MASTER_NAME:(LOCAL:0):0, 12, LEFT%]
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", LOCAL:0 + 10
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE
PRINTL ●既存のキャラ
PRINT    
PRINTBUTTON " 99[霖之助        ]", 99
PRINTL 
PRINT    
LOCAL:1 = 0
FOR LOCAL:0, 1, CHARANUM
	IF CFLAG:(LOCAL:0):1 == FLAG:3 && NO:(LOCAL:0) != 69 && NO:(LOCAL:0) < 2000
		IF LOCAL:1 % 4 == 0 && LOCAL:1 >= 1
			PRINTL 
			PRINT    
		ENDIF
		LOCALS:0 = {NO:(LOCAL:0) + 100}[%ANAME(LOCAL:0), 12, LEFT%]
		PRINTBUTTON @"%LOCALS:0, 19, LEFT%", NO:(LOCAL:0) + 100
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL 

$INPUT_LOOP1
INPUT

;新規作成
IF RESULT == 0
	;空いている登録キャラ番号の取得
	LOCAL:5 = -1
	FOR LOCAL:0, 0, 30
		IF MASTER_MAXBASE:(LOCAL:0):0 <= 0
			LOCAL:5 = LOCAL:0
			BREAK
		ENDIF
	NEXT
	IF LOCAL:5 >= 0
		CALL CREATE_MASTER(LOCAL:5)
	ELSE
		PRINTFORML 登録キャラの数が最大です
		PRINTFORMW 先に不要な登録キャラを削除して下さい
	ENDIF
	RESTART

;登録キャラを選択
ELSEIF RESULT >= 10 && RESULT <= 40 && MASTER_MAXBASE:(RESULT - 10):0 > 0
	LOCAL:8 = RESULT - 10
	CALL APPLY_MASTER_DATA(MASTER, LOCAL:8)
	LOCAL:6 = MASTER

;霖之助を選択
ELSEIF RESULT == 99
	LOCAL:6 = NAME_TO_CHARA("霖之助")
	LOCAL:8 = -1

;既存のキャラを選択
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 100)
	IF LOCAL:2 >= 0 && CFLAG:(LOCAL:2):1 == FLAG:3
		LOCAL:6 = LOCAL:2
		LOCAL:8 = -1
	ELSE
		GOTO INPUT_LOOP1
	ENDIF
ENDIF

LOCAL:7 = 0
;主人公に既存のキャラを選んだ場合
IF LOCAL:6 != MASTER
	;選択したキャラをマスターに変更
	SWAPCHARA MASTER, LOCAL:6
	CALL NEWGAME_SWAP_RELATIONS(LOCAL:6)
	LOCAL:7 = 1
ENDIF

;一時的にコンフィグの外見詳細表示をONにする
LOCAL:0 = CONFIG:6
CONFIG:6 = 1

DRAWLINE
CALL SHOW_INFO(MASTER)
DRAWLINE

CONFIG:6 = LOCAL:0

PRINTBUTTON " 0[このキャラを主人公にして開始]", 0
IF LOCAL:8 >= 0
	PRINT    
	PRINTBUTTON " 1[修正]", 1
	PRINT    
	PRINTBUTTON " 2[削除]", 2
ENDIF
PRINTL 
PRINTBUTTON " 9[戻る]", 9
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 0
	REDRAW 1
	;主人公の所属を選択した勢力に
	CFLAG:MASTER:1 = FLAG:3

	IF ABL:MASTER:性知識 < 5
		PRINTFORML 主人公の性知識Lvを最大(Lv5)にしますか？
		PRINTFORML 　※Lv5未満の場合、一部のウフフコマンドの実行に制限が掛かります
		PRINTFORML 　※性知識は特定のコマンドやアイテムなどで上げることが可能です
		CALL ASK_YN()
		IF RESULT == 0
			ABL:MASTER:性知識 = 5
			PRINTFORMW %ANAME(MASTER)%の性知識をLv5にしました
		ENDIF
	ENDIF
	RETURN

ELSEIF RESULT == 9
	IF LOCAL:7
		;マスターを元に戻す
		SWAPCHARA MASTER, LOCAL:6
		CALL NEWGAME_SWAP_RELATIONS(LOCAL:6)
	ELSE
		;能力設定済みフラグをＯＮにする
		FLAG:5 = 1
	ENDIF
	RESTART

ELSEIF RESULT == 1 && LOCAL:8 >= 0
	CALL CREATE_MASTER(LOCAL:8, 1)
	SAVEGLOBAL
	RESTART

ELSEIF RESULT == 2 && LOCAL:8 >= 0
	PRINTL 本当に削除してもよろしいですか？
	CALL ASK_YN()
	IF RESULT == 0
		CALL DELETE_MASTER(LOCAL:8)
		SAVEGLOBAL
	ENDIF
	RESTART
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP2

;-------------------------------------------------
;キャラARG:0にARG:1番目の主人公データを適用
;-------------------------------------------------
@APPLY_MASTER_DATA(ARG:0, ARG:1)
;基本名(NAME)
NAME:(ARG:0) = %MASTER_NAME:(ARG:1):0%
;呼び名(CALLNAME)（旧真名）
CALLNAME:(ARG:0) = %MASTER_NAME:(ARG:1):1%
;姓
CSTR:(ARG:0):0 = %MASTER_NAME:(ARG:1):2%
;名
CSTR:(ARG:0):1 = %MASTER_NAME:(ARG:1):3%
;字
CSTR:(ARG:0):2 = %MASTER_NAME:(ARG:1):4%
;基本名(NAME)のフリガナ
CSTR:(ARG:0):3 = %MASTER_NAME:(ARG:1):5%
;呼び名(CALLNAME)のフリガナ
CSTR:(ARG:0):4 = %MASTER_NAME:(ARG:1):6%
;姓のフリガナ
CSTR:(ARG:0):5 = %MASTER_NAME:(ARG:1):7%
;名のフリガナ
CSTR:(ARG:0):6 = %MASTER_NAME:(ARG:1):8%
;字のフリガナ
CSTR:(ARG:0):7 = %MASTER_NAME:(ARG:1):9%
;愛称
CSTR:(ARG:0):98 = %MASTER_NAME:(ARG:1):10%
FOR LOCAL:0, 0, 100
	MAXBASE:(ARG:0):(LOCAL:0) = MASTER_MAXBASE:(ARG:1):(LOCAL:0)
	ABL:(ARG:0):(LOCAL:0) = MASTER_ABL:(ARG:1):(LOCAL:0)
	EXP:(ARG:0):(LOCAL:0) = MASTER_EXP:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 1000
	TALENT:(ARG:0):(LOCAL:0) = MASTER_TALENT:(ARG:1):(LOCAL:0)
NEXT

;-------------------------------------------------
;キャラARG:0番目の主人公データを削除
;-------------------------------------------------
@DELETE_MASTER(ARG:0)
;主人公データを前に１つ詰める
FOR LOCAL:1, ARG:0, 29
	FOR LOCAL:0, 0, 100
		MASTER_MAXBASE:(LOCAL:1):(LOCAL:0) = MASTER_MAXBASE:(LOCAL:1 + 1):(LOCAL:0)
		MASTER_ABL:(LOCAL:1):(LOCAL:0) = MASTER_ABL:(LOCAL:1 + 1):(LOCAL:0)
		MASTER_EXP:(LOCAL:1):(LOCAL:0) = MASTER_EXP:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, 1000
		MASTER_TALENT:(LOCAL:1):(LOCAL:0) = MASTER_TALENT:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, 10
		MASTER_NAME:(LOCAL:1):(LOCAL:0) = %MASTER_NAME:(LOCAL:1 + 1):(LOCAL:0)%
	NEXT
	MASTER_NAMETYPE:(LOCAL:1) = MASTER_NAMETYPE:(LOCAL:1 + 1)
NEXT

FOR LOCAL:0, 0, 100
	MASTER_MAXBASE:29:(LOCAL:0) = 0
	MASTER_ABL:29:(LOCAL:0) = 0
	MASTER_EXP:29:(LOCAL:0) = 0
NEXT
FOR LOCAL:0, 0, 1000
	MASTER_TALENT:29:(LOCAL:0) = 0
NEXT
FOR LOCAL:0, 0, 10
	MASTER_NAME:29:(LOCAL:0) = 
NEXT
MASTER_NAMETYPE:29 = 0

;-------------------------------------------------
;配列のARG:0番目に主人公を新規作成 ARG:1に1を設定すると修正モード
;-------------------------------------------------
@CREATE_MASTER(ARG:0, ARG:1 = 0)
;名前の一時設定
#DIMS TMP_NAME, 30

VARSET TMP_NAME, ""

LOCAL:20 = 0

;修正の場合
IF ARG:1
	IF MASTER_NAMETYPE:(ARG:0) == 0
		TMP_NAME:0 = %MASTER_NAME:(ARG:0):0%
		TMP_NAME:1 = %MASTER_NAME:(ARG:0):10%
	ELSEIF MASTER_NAMETYPE:(ARG:0) == 1
		TMP_NAME:10 = %MASTER_NAME:(ARG:0):1%
		TMP_NAME:11 = %MASTER_NAME:(ARG:0):2%
		TMP_NAME:12 = %MASTER_NAME:(ARG:0):3%
		TMP_NAME:13 = %MASTER_NAME:(ARG:0):4%
		TMP_NAME:14 = %MASTER_NAME:(ARG:0):6%
		TMP_NAME:15 = %MASTER_NAME:(ARG:0):7%
		TMP_NAME:16 = %MASTER_NAME:(ARG:0):8%
		TMP_NAME:17 = %MASTER_NAME:(ARG:0):9%
		TMP_NAME:18 = %MASTER_NAME:(ARG:0):10%
	ELSE
		TMP_NAME:20 = %MASTER_NAME:(ARG:0):2%
		TMP_NAME:21 = %MASTER_NAME:(ARG:0):3%
		TMP_NAME:22 = %MASTER_NAME:(ARG:0):7%
		TMP_NAME:23 = %MASTER_NAME:(ARG:0):8%
		TMP_NAME:24 = %MASTER_NAME:(ARG:0):10%
	ENDIF
	LOCAL:20 = MASTER_NAMETYPE:(ARG:0)

;新規作成の場合
ELSE
	;デフォルト能力の設定
	MASTER_MAXBASE:(ARG:0):0 = 2000
	MASTER_MAXBASE:(ARG:0):1 = 1500
	MASTER_MAXBASE:(ARG:0):2 = 2500

	MASTER_TALENT:(ARG:0):0 = 0
	MASTER_TALENT:(ARG:0):1 = 1
	MASTER_TALENT:(ARG:0):2 = 1
	MASTER_TALENT:(ARG:0):3 = 1
	MASTER_TALENT:(ARG:0):200 = 1
	MASTER_TALENT:(ARG:0):300 = 0

	MASTER_ABL:(ARG:0):14 = 5
	MASTER_ABL:(ARG:0):50 = 50
	MASTER_ABL:(ARG:0):51 = 50
	MASTER_ABL:(ARG:0):52 = 50
	MASTER_ABL:(ARG:0):60 = 15
	MASTER_ABL:(ARG:0):61 = 15

	TMP_NAME:0 = あなた
ENDIF

$SHOW_LOOP1

DRAWLINE
PRINTFORML ★★主人公キャラの素質・能力を設定して下さい★★
DRAWLINE

$SHOW_LOOP2

;妖術が1以上なら<妖術知識>を付与
MASTER_TALENT:(ARG:0):201 = MASTER_ABL:(ARG:0):53 >= 1

PRINT   名前の入力方式:
CALL PRINT_SELECT_BUTTON(@" 0\{カナ名・特殊名\}", 0, LOCAL:20 == 0)
CALL PRINT_SELECT_BUTTON(@" 1\{中国名\}", 1, LOCAL:20 == 1)
CALL PRINT_SELECT_BUTTON(@" 2\{日本名\}", 2, LOCAL:20 == 2)
PRINTL 

IF LOCAL:20 == 0
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTFORM     呼称 -- %TMP_NAME:0, 14, LEFT%
	PRINTBUTTON " 10[入力]", 10
	PRINTBUTTON " 11[カナ名ランダム]", 11
	PRINTBUTTON " 100[消去]", 100
	PRINTL 
	PRINTFORM     愛称 -- %TMP_NAME:1, 14, LEFT%
	PRINTBUTTON " 12[入力]", 12
	PRINTBUTTON " 101[消去]", 101
	PRINTL 
ELSEIF LOCAL:20 == 1
	PRINTL 
	PRINTFORM       姓 -- %TMP_NAME:11, 10, LEFT%%TMP_NAME:15, 14, LEFT%
	PRINTBUTTON " 12[入力]", 12
	PRINTBUTTON " 13[ランダム]", 13
	PRINTBUTTON " 101[消去]", 101
	PRINTL 
	PRINTFORM       名 -- %TMP_NAME:12, 10, LEFT%%TMP_NAME:16, 14, LEFT%
	PRINTBUTTON " 14[入力]", 14
	PRINTBUTTON " 15[ランダム]", 15
	PRINTBUTTON " 102[消去]", 102
	PRINTL 
	PRINTFORM       字 -- %TMP_NAME:13, 10, LEFT%%TMP_NAME:17, 14, LEFT%
	PRINTBUTTON " 16[入力]", 16
	PRINTBUTTON " 17[ランダム]", 17
	PRINTBUTTON " 103[消去]", 103
	PRINTL 
	PRINTFORM     愛称 -- %TMP_NAME:18, 14, LEFT%
	PRINTBUTTON " 18[入力]", 18 
	PRINTBUTTON " 104[消去]", 104
	PRINTL
ELSEIF LOCAL:20 == 2
	PRINTL 
	PRINTL 
	PRINTFORM       姓 -- %TMP_NAME:20, 10, LEFT%%TMP_NAME:22, 14, LEFT%
	PRINTBUTTON " 10[入力]", 10
	PRINTBUTTON " 100[消去]", 100
	PRINTL 
	PRINTFORM       名 -- %TMP_NAME:21, 10, LEFT%%TMP_NAME:23, 14, LEFT%
	PRINTBUTTON " 12[入力]", 12
	PRINTBUTTON " 101[消去]", 101
	PRINTL 
	PRINTFORM     愛称 -- %TMP_NAME:24, 14, LEFT%
	PRINTBUTTON " 13[入力]", 13
	PRINTBUTTON " 102[消去]", 102
	PRINTL
ENDIF
DRAWLINE

PRINT     性別 -- 
CALL PRINTBUTTON_EX("20{  男性} ", 20, 1, MASTER_TALENT:(ARG:0):0 == 0)
CALL PRINTBUTTON_EX("21{  女性} ", 21, 1, MASTER_TALENT:(ARG:0):0 == 1)
CALL PRINTBUTTON_EX("22{  女双} ", 22, 1, MASTER_TALENT:(ARG:0):0 == 2)
CALL PRINTBUTTON_EX("23{  男双} ", 23, 1, MASTER_TALENT:(ARG:0):0 == 3)
CALL PRINTBUTTON_EX("24{男の娘} ", 24, 1, MASTER_TALENT:(ARG:0):0 == 4)
CALL PRINTBUTTON_EX("25{男の娘双} ", 25, 1, MASTER_TALENT:(ARG:0):0 == 5)
PRINTL 

PRINT キス経験 -- 
CALL PRINTBUTTON_EX("30{  なし} ", 30, 1, MASTER_TALENT:(ARG:0):3)
CALL PRINTBUTTON_EX("31{  あり} ", 31, 1, !MASTER_TALENT:(ARG:0):3)
PRINTL 

PRINT 性経験♂ -- 
CALL PRINTBUTTON_EX("40{  童貞} ", 40, 1, MASTER_TALENT:(ARG:0):2)
CALL PRINTBUTTON_EX("41{非童貞} ", 41, 1, !MASTER_TALENT:(ARG:0):2)
PRINTL 

PRINT 性経験♀ -- 
CALL PRINTBUTTON_EX("50{  処女} ", 50, 1, MASTER_TALENT:(ARG:0):1)
CALL PRINTBUTTON_EX("51{非処女} ", 51, 1, !MASTER_TALENT:(ARG:0):1)
PRINTL 

PRINT   バスト -- 
LOCAL:0 = MASTER_TALENT:(ARG:0):140 || MASTER_TALENT:(ARG:0):141 || MASTER_TALENT:(ARG:0):142 || MASTER_TALENT:(ARG:0):143
CALL PRINTBUTTON_EX("60{  絶壁} ", 60, 1, MASTER_TALENT:(ARG:0):140)
CALL PRINTBUTTON_EX("61{  貧乳} ", 61, 1, MASTER_TALENT:(ARG:0):141)
CALL PRINTBUTTON_EX("62{  並乳} ", 62, 1, !LOCAL:0)
CALL PRINTBUTTON_EX("63{  巨乳} ", 63, 1, MASTER_TALENT:(ARG:0):142)
CALL PRINTBUTTON_EX("64{  爆乳} ", 64, 1, MASTER_TALENT:(ARG:0):143)
PRINTL 

PRINT 天の御遣い- 
CALL PRINTBUTTON_EX("70{  あり} ", 70, 1, MASTER_TALENT:(ARG:0):200)
CALL PRINTBUTTON_EX("71{  なし} ", 71, 1, !MASTER_TALENT:(ARG:0):200)
PRINTL 

PRINTL   ※童貞・処女・バストは性別によって非表示。特殊なアイテムで性別変更した場合などに反映

DRAWLINE
PRINTFORML ※武闘・知略・政治はそれぞれ52、歌唱と料理は27程度が全キャラの平均です
PRINTFORM 武闘:{MASTER_ABL:(ARG:0):50, 3} -- 
PRINTBUTTON "[ +1]", 200
PRINTBUTTON "[ -1]", 201
PRINTBUTTON "[+10]", 202
PRINTBUTTON "[-10]", 203
PRINT   
PRINTBUTTON "80[直接入力]", 80
PRINTL 
PRINTFORM 知略:{MASTER_ABL:(ARG:0):51, 3} -- 
PRINTBUTTON "[ +1]", 210
PRINTBUTTON "[ -1]", 211
PRINTBUTTON "[+10]", 212
PRINTBUTTON "[-10]", 213
PRINT   
PRINTBUTTON "81[直接入力]", 81
PRINTL 
PRINTFORM 政治:{MASTER_ABL:(ARG:0):52, 3} -- 
PRINTBUTTON "[ +1]", 220
PRINTBUTTON "[ -1]", 221
PRINTBUTTON "[+10]", 222
PRINTBUTTON "[-10]", 223
PRINT   
PRINTBUTTON "82[直接入力]", 82
PRINTL 
PRINTFORM 妖術:{MASTER_ABL:(ARG:0):53, 3} -- 
PRINTBUTTON "[ +1]", 230
PRINTBUTTON "[ -1]", 231
PRINTBUTTON "[+10]", 232
PRINTBUTTON "[-10]", 233
PRINT   
PRINTBUTTON "83[直接入力]", 83
PRINT  ※1以上に設定すると素質<妖術知識>が付与
PRINTL 
PRINTFORM 歌唱:{MASTER_ABL:(ARG:0):60, 3} -- 
PRINTBUTTON "[ +1]", 240
PRINTBUTTON "[ -1]", 241
PRINTBUTTON "[+10]", 242
PRINTBUTTON "[-10]", 243
PRINT   
PRINTBUTTON "84[直接入力]", 84
PRINTL 
PRINTFORM 料理:{MASTER_ABL:(ARG:0):61, 3} -- 
PRINTBUTTON "[ +1]", 250
PRINTBUTTON "[ -1]", 251
PRINTBUTTON "[+10]", 252
PRINTBUTTON "[-10]", 253
PRINT   
PRINTBUTTON "85[直接入力]", 85
PRINTL 
DRAWLINE
PRINTBUTTON "  90[詳細設定]", 90
PRINTL 
DRAWLINE
PRINTBUTTON "  91[情報確認]", 91
PRINTL 
PRINTBUTTON "  99[    登録]", 99
PRINT                     
PRINTBUTTON "  95[キャンセル]", 95

;名前の調整
FOR LOCAL:0, 0, 10
	MASTER_NAME:(ARG:0):(LOCAL:0) = 
NEXT
IF LOCAL:20 == 0
	MASTER_NAME:(ARG:0):0 = %TMP_NAME:0%
	MASTER_NAME:(ARG:0):1 = %TMP_NAME:0%
	MASTER_NAME:(ARG:0):10 = %TMP_NAME:1%
ELSEIF LOCAL:20 == 1
	MASTER_NAME:(ARG:0):0 = %TMP_NAME:11%%TMP_NAME:12%
	;真名が無くなったので暫定的にCALLNAMEは「字」を使います
	MASTER_NAME:(ARG:0):1 = %TMP_NAME:13%
	MASTER_NAME:(ARG:0):2 = %TMP_NAME:11%
	MASTER_NAME:(ARG:0):3 = %TMP_NAME:12%
	MASTER_NAME:(ARG:0):4 = %TMP_NAME:13%
	MASTER_NAME:(ARG:0):5 = %TMP_NAME:15%%TMP_NAME:16%
	;真名が無くなったので暫定的にCALLNAMEは「字」を使います（こちらはフリガナ）
	MASTER_NAME:(ARG:0):6 = %TMP_NAME:17%
	MASTER_NAME:(ARG:0):7 = %TMP_NAME:15%
	MASTER_NAME:(ARG:0):8 = %TMP_NAME:16%
	MASTER_NAME:(ARG:0):9 = %TMP_NAME:17%
	MASTER_NAME:(ARG:0):10 = %TMP_NAME:18%
ELSEIF LOCAL:20 == 2
	;日本人名のNAMEはNPCに合わせて「名」を使います
	MASTER_NAME:(ARG:0):0 = %TMP_NAME:21%
	;日本人名のCALLNAMEはNPCに合わせて「名」を使います
	MASTER_NAME:(ARG:0):1 = %TMP_NAME:21%
	MASTER_NAME:(ARG:0):2 = %TMP_NAME:20%
	MASTER_NAME:(ARG:0):3 = %TMP_NAME:21%
	;日本人名のNAMEはNPCに合わせて「名」を使います（こちらはフリガナ）
	MASTER_NAME:(ARG:0):5 = %TMP_NAME:23%
	;日本人名のCALLNAMEはNPCに合わせて「名」を使います（こちらはフリガナ）
	MASTER_NAME:(ARG:0):6 = %TMP_NAME:23%
	MASTER_NAME:(ARG:0):7 = %TMP_NAME:22%
	MASTER_NAME:(ARG:0):8 = %TMP_NAME:23%
	MASTER_NAME:(ARG:0):10 = %TMP_NAME:24%
ENDIF
MASTER_NAMETYPE:(ARG:0) = LOCAL:20
REDRAW 0

$INPUT_LOOP2
INPUT

;登録
IF RESULT == 99
	SAVEGLOBAL
	RETURN

;戻る
ELSEIF RESULT == 95
	PRINTL 登録せずに戻ります。よろしいですか？
	CALL ASK_YN()
	IF RESULT == 0
		RETURN
	ELSE
		GOTO SHOW_LOOP1
	ENDIF

;情報表示
ELSEIF RESULT == 91
	ADDCHARA 0
	CALL APPLY_MASTER_DATA(CHARANUM - 1, ARG:0)
	SWAPCHARA MASTER, CHARANUM - 1

	;一時的にコンフィグの外見詳細表示をONにする
	LOCAL:0 = CONFIG:6
	CONFIG:6 = 1

	DRAWLINE
	CALL SHOW_INFO(MASTER)
	WAIT
	SWAPCHARA MASTER, CHARANUM - 1

	CONFIG:6 = 0

	;特例(通常は危険なのでDELCHARA禁止)
	DELCHARA CHARANUM - 1
	GOTO SHOW_LOOP1

;詳細設定
ELSEIF RESULT == 90
	CALL CREATE_MASTER_DETAIL(ARG:0)
	GOTO SHOW_LOOP1

ELSEIF RESULT >= 80 && RESULT <= 85
	LOCAL:0 = RESULT
	IF LOCAL:0 == 80
		PRINT 武闘
	ELSEIF LOCAL:0 == 81
		PRINT 知略
	ELSEIF LOCAL:0 == 82
		PRINT 政治
	ELSEIF LOCAL:0 == 83
		PRINT 妖術
	ELSEIF LOCAL:0 == 84
		PRINT 歌唱
	ELSEIF LOCAL:0 == 85
		PRINT 料理
	ENDIF
	PRINTL の値を入力して下さい(0～120)
	INPUT
	IF LOCAL:0 == 80
		MASTER_ABL:(ARG:0):50 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 81
		MASTER_ABL:(ARG:0):51 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 82
		MASTER_ABL:(ARG:0):52 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 83
		MASTER_ABL:(ARG:0):53 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 84
		MASTER_ABL:(ARG:0):60 = LIMIT(RESULT, 0, 120)
	ELSEIF LOCAL:0 == 85
		MASTER_ABL:(ARG:0):61 = LIMIT(RESULT, 0, 120)
	ENDIF
	GOTO SHOW_LOOP1
ELSE
	IF RESULT >= 10 && RESULT <= 19
		IF LOCAL:20 == 0 && RESULT == 10
			PRINTFORML 主人公の呼称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:0 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 0 && RESULT == 12
			PRINTFORML 主人公の愛称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:1 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 12
			PRINTFORML 主人公の姓を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:11 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:15 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 14
			PRINTFORML 主人公の名を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:12 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:16 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 16
			PRINTFORML 主人公の字を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:13 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:17 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 1 && RESULT == 18
			PRINTFORML 主人公の愛称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:18 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 2 && RESULT == 10
			PRINTFORML 主人公の姓を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:20 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:22 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 2 && RESULT == 12
			PRINTFORML 主人公の名を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:21 = %RESULTS%
				PRINTFORML (必要な場合は)読みをカタカナで入力して下さい
				INPUTS
				IF RESULTS != ""
					TMP_NAME:23 = %RESULTS%
				ENDIF
			ENDIF
			GOTO SHOW_LOOP1
		ELSEIF LOCAL:20 == 2 && RESULT == 13
			PRINTFORML 主人公の愛称を入力して下さい
			INPUTS
			IF RESULTS != ""
				TMP_NAME:24 = %RESULTS%
			ENDIF
			GOTO SHOW_LOOP1
		ENDIF
	ENDIF

	CLEARLINE 28
	IF RESULT >= 0 && RESULT <= 3
		LOCAL:20 = RESULT
	ELSEIF LOCAL:20 == 0 && RESULT == 11
		IF GROUPMATCH(MASTER_TALENT:(ARG:0):0, 0, 3, 4, 5)
			CALL GENERATE_FIRST_NAME_KM
		ELSE
			CALL GENERATE_FIRST_NAME_KF
		ENDIF
		TMP_NAME:0 = %RESULTS:0%
	ELSEIF LOCAL:20 == 1 && RESULT == 11
		IF GROUPMATCH(MASTER_TALENT:(ARG:0):0, 0, 3, 4, 5)
			CALL GENERATE_TRUE_NAME_M
		ELSE
			CALL GENERATE_TRUE_NAME_F
		ENDIF
		TMP_NAME:10 = %RESULTS:0%
		TMP_NAME:14 = %RESULTS:1%
	ELSEIF LOCAL:20 == 1 && RESULT == 13
		CALL GENERATE_FAMILY_NAME
		TMP_NAME:11 = %RESULTS:0%
		TMP_NAME:15 = %RESULTS:1%
	ELSEIF LOCAL:20 == 1 && RESULT == 15
		CALL GENERATE_FIRST_NAME
		TMP_NAME:12 = %RESULTS:0%
		TMP_NAME:16 = %RESULTS:1%
	ELSEIF LOCAL:20 == 1 && RESULT == 17
		CALL GENERATE_MIDDLE_NAME
		TMP_NAME:13 = %RESULTS:0%
		TMP_NAME:17 = %RESULTS:1%
	ELSEIF RESULT >= 20 && RESULT <= 25
		MASTER_TALENT:(ARG:0):0 = RESULT - 20
	ELSEIF RESULT >= 30 && RESULT <= 31
		MASTER_TALENT:(ARG:0):3 = 31 - RESULT
	ELSEIF RESULT >= 40 && RESULT <= 41
		MASTER_TALENT:(ARG:0):2 = 41 - RESULT
	ELSEIF RESULT >= 50 && RESULT <= 51
		MASTER_TALENT:(ARG:0):1 = 51 - RESULT
	ELSEIF RESULT >= 60 && RESULT <= 64
		MASTER_TALENT:(ARG:0):140 = 0
		MASTER_TALENT:(ARG:0):141 = 0
		MASTER_TALENT:(ARG:0):142 = 0
		MASTER_TALENT:(ARG:0):143 = 0
		SELECTCASE RESULT - 60
			CASE 0
				MASTER_TALENT:(ARG:0):140 = 1
			CASE 1
				MASTER_TALENT:(ARG:0):141 = 1
			CASE 3
				MASTER_TALENT:(ARG:0):142 = 1
			CASE 4
				MASTER_TALENT:(ARG:0):143 = 1
		ENDSELECT
	ELSEIF RESULT >= 70 && RESULT <= 71
		MASTER_TALENT:(ARG:0):200 = 71 - RESULT
	ELSEIF RESULT >= 100 && RESULT <= 103
		IF LOCAL:20 == 0 && RESULT == 100
			TMP_NAME:0 = 
		ELSEIF LOCAL:20 == 0 && RESULT == 101
			TMP_NAME:1 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 100
			TMP_NAME:10 = 
			TMP_NAME:14 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 101
			TMP_NAME:11 = 
			TMP_NAME:15 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 102
			TMP_NAME:12 = 
			TMP_NAME:16 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 103
			TMP_NAME:13 = 
			TMP_NAME:17 = 
		ELSEIF LOCAL:20 == 1 && RESULT == 104
			TMP_NAME:18 = 
		ELSEIF LOCAL:20 == 2 && RESULT == 100
			TMP_NAME:20 = 
			TMP_NAME:22 = 
		ELSEIF LOCAL:20 == 2 && RESULT == 101
			TMP_NAME:21 = 
			TMP_NAME:23 = 
		ELSEIF LOCAL:20 == 2 && RESULT == 102
			TMP_NAME:24 = 
		ENDIF
	ELSEIF RESULT >= 200 && RESULT < 300
		LOCAL:4 = (RESULT - 200) / 10
		LOCAL:5 = RESULT % 10
		IF LOCAL:4 >= 0 && LOCAL:4 <= 5
			IF LOCAL:4 < 4
				LOCAL:4 += 50
			ELSE
				LOCAL:4 += 56
			ENDIF
			SELECTCASE LOCAL:5
				CASE 0
					MASTER_ABL:(ARG:0):(LOCAL:4) += 1
				CASE 1
					MASTER_ABL:(ARG:0):(LOCAL:4) -= 1
				CASE 2
					MASTER_ABL:(ARG:0):(LOCAL:4) += 10
				CASE 3
					MASTER_ABL:(ARG:0):(LOCAL:4) -= 10
			ENDSELECT
			MASTER_ABL:(ARG:0):(LOCAL:4) = LIMIT(MASTER_ABL:(ARG:0):(LOCAL:4), 0, 120)
		ENDIF
	ENDIF
ENDIF
GOTO SHOW_LOOP2

;-------------------------------------------------
;主人公の詳細設定
;-------------------------------------------------
@CREATE_MASTER_DETAIL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_ABL_LIST, 100

;髪のHSVカラー
#DIM HAIR_HSV, 3

;TALENTNAMEの一時保存用
#DIMS TN

#DIM PAGE = 1
#DIM MAX_PAGE = 0

VARSET SHOW_ABL_LIST, -1

SHOW_ABL_LIST:0 = 0
SHOW_ABL_LIST:1 = 1
SHOW_ABL_LIST:2 = 2
SHOW_ABL_LIST:3 = 3
SHOW_ABL_LIST:4 = 4
SHOW_ABL_LIST:10 = 13
SHOW_ABL_LIST:11 = 14
SHOW_ABL_LIST:12 = 27
SHOW_ABL_LIST:13 = 28

IF MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:0 = RGB_TO_H(MASTER_TALENT:(ARG:0):302)
	HAIR_HSV:1 = RGB_TO_S(MASTER_TALENT:(ARG:0):302)
	HAIR_HSV:2 = RGB_TO_V(MASTER_TALENT:(ARG:0):302)
ELSE
	VARSET HAIR_HSV, 0
ENDIF

$SHOW_LOOP1

DRAWLINE
PRINTL 主人公の詳細設定
DRAWLINE

$SHOW_LOOP2

IF HAIR_HSV:2 > 0
	MASTER_TALENT:(ARG:0):302 = HSV_TO_RGB(HAIR_HSV:0, HAIR_HSV:1, HAIR_HSV:2)
ENDIF

PRINT □ 能力 □
FOR LOCAL:0, 0, 3
	PRINTL 
	PRINTFORM %BASENAME:(LOCAL:0), 8%最大値:{MASTER_MAXBASE:(ARG:0):(LOCAL:0), 4} -- 
	PRINTBUTTON "[+100]", LOCAL:0 + 2000
	PRINTBUTTON "[-100]", LOCAL:0 + 2100
	PRINT  
	PRINTBUTTON @"{LOCAL:0 + 700}[入力]", LOCAL:0 + 700
NEXT
LOCAL:3 = 0
FOR LOCAL:0, 0, 20
	LOCAL:2 = SHOW_ABL_LIST:(LOCAL:0)
	IF LOCAL:2 >= 0
		IF LOCAL:0 % 10 == 0 || LOCAL:3 == 5
			LOCAL:3 = 0
			PRINTL 
			PRINT     
		ELSE
			PRINT  
		ENDIF
		PRINTFORM %ABLNAME:(LOCAL:2), 6, RIGHT%:{MASTER_ABL:(ARG:0):(LOCAL:2), 2}
		PRINTBUTTON "[+]", LOCAL:0 + 1000
		PRINTBUTTON "[-]", LOCAL:0 + 1100
		LOCAL:3 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE

PRINT □ 素質 □
LOCAL:3 = 0
LOCAL:4 = 0
FOR LOCAL:0, 1, 300
	TN = %TALENTNAME:(LOCAL:0)%
	IF TN != ""
		;非表示素質のスキップ
		IF GROUPMATCH(TN, "絶壁", "貧乳", "巨乳", "爆乳")
			CONTINUE
		ELSEIF LOCAL:0 >= 150 && !GROUPMATCH(TN, "天の御遣い", "技師", "超成長力")
			CONTINUE
		ENDIF

		IF LOCAL:3 >= (PAGE - 1) * 44 && LOCAL:3 < PAGE * 44
			;改行判定
			IF LOCAL:3 % 4 == 0
				PRINTL 
				PRINT   
				LOCAL:4 ++
			ENDIF
			IF MASTER_TALENT:(ARG:0):(LOCAL:0)
				SETCOLOR COLOR("シアン")
			ENDIF
			PRINTBUTTON @"{LOCAL:0, 3}\{%TN, 14%\}", LOCAL:0
			RESETCOLOR
			PRINT    
		ENDIF
		LOCAL:3 ++
	ENDIF
NEXT
FOR LOCAL:0, 0, 11 - LOCAL:4
	PRINTL 
NEXT
;最大ページ数が設定されていない場合、ここで初めて設定
IF MAX_PAGE <= 0
	MAX_PAGE = (LOCAL:3 - 1) / 44 + 1
ENDIF

PRINTL 
PRINT   
PRINTBUTTON "901[前のページ]", 901
PRINT   
PRINTBUTTON "902[次のページ]", 902
PRINTL 
DRAWLINE
LOCAL:0 = MASTER_TALENT:(ARG:0):300
LOCALS:0 = (%TOSTR_GROWTYPE_B(LOCAL:0)%に適性)
PRINTFORM    成長型:%TOSTR_GROWTYPE(LOCAL:0)%%LOCALS:0, 25, LEFT%
PRINTBUTTON "300[変更]", 300
PRINTL 
PRINTFORM      髪色:
CALL PRINT_HAIR_COLOR(MASTER_TALENT:(ARG:0):302)
PRINTFORM %TOSTR_SPACE(19)%
PRINTBUTTON "310[ランダム]", 310
IF MASTER_TALENT:(ARG:0):302 != 0
	PRINT  
	PRINTBUTTON "319[不明にする]", 319
ENDIF
PRINTL 
IF MASTER_TALENT:(ARG:0):302 != 0
	PRINT           
	PRINTBUTTON "311[色相+10]", 311
	PRINT  
	PRINTBUTTON "312[色相-10]", 312
	PRINT  
	PRINTBUTTON "313[彩度+5]", 313
	PRINT  
	PRINTBUTTON "314[彩度-5]", 314
	PRINT  
	PRINTBUTTON "315[明度+5]", 315
	PRINT  
	PRINTBUTTON "316[明度-5]", 316
	PRINT  
ENDIF
PRINTL 
LOCALS:0 = %TOSTR_HAIR_LENGTH(MASTER_TALENT:(ARG:0):303)%({MASTER_TALENT:(ARG:0):303})
PRINTFORM  髪の長さ:%LOCALS:0, 29, LEFT%
PRINTBUTTON "320[ランダム]", 320
PRINT  
PRINTBUTTON "321[直接入力]", 321
PRINTL 
PRINTL  ※髪色・髪の長さはコンフィグの「外見に関する詳細の表示」がONのときのみ表示
DRAWLINE

PRINTBUTTON "999[決定]", 999

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW 1
	RETURN
ELSEIF RESULT == 901
	PAGE --
	IF PAGE < 1
		PAGE = MAX_PAGE
	ENDIF
ELSEIF RESULT == 902
	PAGE ++
	IF PAGE > MAX_PAGE
		PAGE = 1
	ENDIF
ELSEIF RESULT >= 700 && RESULT <= 702
	LOCAL:0 = RESULT - 700
	PRINTFORML %BASENAME:(LOCAL:0)%の最大値を入力して下さい(500～5000)
	INPUT
	IF RESULT != 0
		MASTER_MAXBASE:(ARG:0):(LOCAL:0) = LIMIT(RESULT, 500, 5000)
	ENDIF
	GOTO SHOW_LOOP1
ELSEIF RESULT >= 1000 && RESULT <= 1020 && SHOW_ABL_LIST:(RESULT - 1000) >= 0
	LOCAL:0 = SHOW_ABL_LIST:(RESULT - 1000)
	MASTER_ABL:(ARG:0):(LOCAL:0) = LIMIT(MASTER_ABL:(ARG:0):(LOCAL:0) + 1, 0, MAX(ABL_MAX:(LOCAL:0), 15))
ELSEIF RESULT >= 1100 && RESULT <= 1120 && SHOW_ABL_LIST:(RESULT - 1100) >= 0
	LOCAL:0 = SHOW_ABL_LIST:(RESULT - 1100)
	MASTER_ABL:(ARG:0):(LOCAL:0) = LIMIT(MASTER_ABL:(ARG:0):(LOCAL:0) - 1, 0, MAX(ABL_MAX:(LOCAL:0), 15))
ELSEIF RESULT >= 2000 && RESULT <= 2002
	MASTER_MAXBASE:(ARG:0):(RESULT - 2000) = LIMIT(MASTER_MAXBASE:(ARG:0):(RESULT - 2000) + 100, 500, 5000)
ELSEIF RESULT >= 2100 && RESULT <= 2102
	MASTER_MAXBASE:(ARG:0):(RESULT - 2100) = LIMIT(MASTER_MAXBASE:(ARG:0):(RESULT - 2100) - 100, 500, 5000)
ELSEIF RESULT == 300
	MASTER_TALENT:(ARG:0):300 ++
	IF MASTER_TALENT:(ARG:0):300 >= 9
		MASTER_TALENT:(ARG:0):300 = 0
	ENDIF
ELSEIF RESULT == 310
	HAIR_HSV:0 = RAND:360
	HAIR_HSV:1 = RAND:93
	HAIR_HSV:2 = RAND:68 + 30
ELSEIF RESULT == 319 && MASTER_TALENT:(ARG:0):302 != 0
	MASTER_TALENT:(ARG:0):302 = 0
	VARSET HAIR_HSV, 0
ELSEIF RESULT == 311 && MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:0 += 10
	IF HAIR_HSV:0 >= 360
		HAIR_HSV:0 -= 360
	ENDIF
ELSEIF RESULT == 312 && MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:0 -= 10
	IF HAIR_HSV:0 < 0
		HAIR_HSV:0 += 360
	ENDIF
ELSEIF RESULT == 313 && MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:1 = LIMIT(HAIR_HSV:1 + 5, 0, 95)
ELSEIF RESULT == 314 && MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:1 = LIMIT(HAIR_HSV:1 - 5, 0, 95)
ELSEIF RESULT == 315 && MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:2 = LIMIT(HAIR_HSV:2 + 5, 30, 100)
ELSEIF RESULT == 316 && MASTER_TALENT:(ARG:0):302 != 0
	HAIR_HSV:2 = LIMIT(HAIR_HSV:2 - 5, 30, 100)
ELSEIF RESULT == 320
	MASTER_TALENT:(ARG:0):303 = RAND:990 + 10
ELSEIF RESULT == 321
	DRAWLINE
	PRINTL 髪の長さを数値で入力して下さい
	PRINTL ------長さの表記------
	PRINTL   2～ 24 スキンヘッド
	PRINTL  25～174 超ショート
	PRINTL 175～274 ショート
	PRINTL 275～474 ミドル
	PRINTL 475～674 ロング
	PRINTL 675～999 超ロング
	INPUT
	MASTER_TALENT:(ARG:0):303 = LIMIT(RESULT, 2, 999)
ELSEIF RESULT >= 1 && RESULT < 300 && TALENTNAME:RESULT != ""
	IF !GROUPMATCH(TALENTNAME:RESULT, "絶壁", "貧乳", "巨乳", "爆乳")
		IF RESULT < 150 || GROUPMATCH(TALENTNAME:RESULT, "天の御遣い", "技師", "超成長力")
			MASTER_TALENT:(ARG:0):RESULT = !MASTER_TALENT:(ARG:0):RESULT
		ENDIF
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CLEARLINE 29
GOTO SHOW_LOOP2

;-------------------------------------------------
;君主である固定キャラ使用時に、他勢力との関係をMASTERに移しておく関数
;SWAP_CHARAと併用する
;ARG:0 = 交換対象
;-------------------------------------------------
@NEWGAME_SWAP_RELATIONS(ARG:0)
#DIM REL_LIKE_COPY,1
#DIM REL_HATE_COPY,1
FOR LOCAL, 0, 1000
	REL_LIKE_COPY = REL_LIKE:(ARG:0):LOCAL
	REL_HATE_COPY = REL_HATE:(ARG:0):LOCAL
	REL_LIKE:(ARG:0):LOCAL = REL_LIKE:MASTER:LOCAL
	REL_HATE:(ARG:0):LOCAL = REL_HATE:MASTER:LOCAL
	REL_LIKE:MASTER:LOCAL = REL_LIKE_COPY
	REL_HATE:MASTER:LOCAL = REL_HATE_COPY
NEXT
FOR LOCAL, 0, CHARANUM
	REL_LIKE_COPY = REL_LIKE:(LOCAL):MASTER
	REL_HATE_COPY = REL_HATE:(LOCAL):MASTER
	REL_LIKE:(LOCAL):MASTER = REL_LIKE:(LOCAL):(ARG:0)
	REL_HATE:(LOCAL):MASTER = REL_HATE:(LOCAL):(ARG:0)
	REL_LIKE:(LOCAL):(ARG:0) = REL_LIKE_COPY
	REL_HATE:(LOCAL):(ARG:0) = REL_HATE_COPY
NEXT
;-------------------------------------------------
;霖之助の扱いを設定する関数
;-------------------------------------------------
@SET_霖之助
;勢力の数を数える
LOCAL:2 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:2 ++
	ENDIF
NEXT

DRAWLINE
PRINTL 森近霖之助を登場させるかどうかを設定できます
PRINTL [0] 登場させない
PRINTL [1] 主人公勢力に組み込む
IF LOCAL:2 >= 2
	PRINTL [2] 主人公以外のランダムな勢力に組み込む
	PRINTL [3] 選択した勢力に組み込む
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAIN [2] 主人公以外のランダムな勢力に組み込む
	PRINTL 
	PRINTPLAIN [3] 選択した勢力に組み込む
	PRINTL 
	RESETCOLOR
ENDIF

$INPUT_LOOP_霖之助
INPUT

IF RESULT == 0
	PRINTW 霖之助は登場しません
ELSEIF RESULT == 1
	PRINTW 霖之助を主人公勢力のキャラとして登場させます
	CFLAG:NAME_TO_CHARA("霖之助"):1 = CFLAG:MASTER:1

ELSEIF RESULT == 2 && LOCAL:2 >= 2
	PRINTW 霖之助をランダムな勢力のキャラとして登場させます
	;ランダムに勢力を選ぶ
	LOCAL:3 = RAND:(LOCAL:2 - 1)
	LOCAL:2 = 0
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:1
			IF LOCAL:2 == LOCAL:3
				;選んだ勢力に霖之助を追加
				CFLAG:NAME_TO_CHARA("霖之助"):1 = LOCAL:0
				BREAK
			ENDIF
			LOCAL:2 ++
		ENDIF
	NEXT

ELSEIF RESULT == 3 && LOCAL:2 >= 2
	CALL SELECT_COUNTRY_霖之助

ELSE
	GOTO INPUT_LOOP_霖之助
ENDIF

;-------------------------------------------------
;霖之助を追加する勢力の選択
;-------------------------------------------------
@SELECT_COUNTRY_霖之助
DRAWLINE
PRINTL ★★霖之助の所属勢力を選択して下さい★★
DRAWLINE
FOR LOCAL:0, 1, MAX_COUNTRY
	IF COUNTRY_BOSS:(LOCAL:0) >= 1
		LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))
		PRINTFORM [{LOCAL:0 + 100, 3, RIGHT}] %NAME:(LOCAL:2)%
		IF NAME:(LOCAL:2) != CALLNAME:(LOCAL:2)
			PRINTFORM (%CALLNAME:(LOCAL:2)%)
		ENDIF
		PRINTL 
	ENDIF
NEXT
DRAWLINE
PRINTL [200] マップを見る

$INPUT_LOOP
INPUT

IF RESULT == 200
	DRAWLINE
	CALL SET_CITY_COLOR_COUNTRY
	CALL DRAWMAP
	CALL RESET_CITY_COLOR
	DRAWLINE
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL [200] 戻る

	REDRAW 0

	$INPUT_LOOP_MAP
	INPUT

	IF RESULT == 200
		REDRAW 1
		RESTART
	ELSEIF RESULT >= 1001 && RESULT < 1000 + MAX_CITY
		LOCAL:2 = CITY_OWNER:(RESULT - 1000)
		LOCAL:3 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:2))
		IF LOCAL:2 > 0
			CLEARLINE 31
			CALL SET_CITY_COLOR_COUNTRY
			CALL DRAWMAP
			CALL RESET_CITY_COLOR
			DRAWLINE
			PRINTPLAIN この都市は　
			PRINTFORM [{LOCAL:2 + 100, 3, RIGHT}] %NAME:(LOCAL:3)%
			PRINTPLAIN 　の勢力下にあります
			PRINTL 
			PRINTL [200] 戻る
			GOTO INPUT_LOOP_MAP
		ENDIF
	ELSEIF RESULT >= 100 && RESULT < 200
		LOCAL:6 = RESULT - 100
		LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
		IF LOCAL:5 > 0
			;選んだ勢力に霖之助を追加
			CFLAG:NAME_TO_CHARA("霖之助"):1 = LOCAL:6
			PRINTFORMW 霖之助を%NAME:(LOCAL:5)%勢力の士官として登場させます
			REDRAW 1
			RETURN
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP_MAP

ELSEIF RESULT >= 100 && RESULT < 200
	LOCAL:6 = RESULT - 100
	LOCAL:5 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:6))
	IF LOCAL:5 > 0
		;選んだ勢力に霖之助を追加
		CFLAG:NAME_TO_CHARA("霖之助"):1 = LOCAL:6
		PRINTFORMW 霖之助を%NAME:(LOCAL:5)%勢力の士官として登場させます
		RETURN
	ENDIF
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;野盗の追加
;-------------------------------------------------
@BANDIT_INIT()
#DIM LCOUNT
VARSET LOCAL
IF GET_COUNTRY_FROM_ID(COUNTRY_BANDIT) == -1
	LOCAL:4 = GET_NEW_COUNTRY()
	COUNTRY_COLOR:(LOCAL:4) = 0x800000
	COUNTRY_SOLDIER:(LOCAL:4) = 1000 * POWER(2, BANDIT_RANK)
	COUNTRY_AI_TYPE:(LOCAL:4) = AI_通常
	COUNTRY_EVENT_ID:(LOCAL:4) = COUNTRY_BANDIT
	COUNTRY_IS_CLOSED:(LOCAL:4) = 1	
ELSE
	LOCAL:4 = GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)
ENDIF
FOR LCOUNT, 0, 15
	CALL ADD_VOID_CHARA()
	LOCAL = RESULT
	NAME:LOCAL = 野盗
	CALLNAME:LOCAL = 野盗
	MAXBASE:LOCAL:体力 = 3000
	MAXBASE:LOCAL:気力 = 3000
	MAXBASE:LOCAL:精神力 = 1500
	BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力
	BASE:LOCAL:精神力 = MAXBASE:LOCAL:精神力
	ABL:LOCAL:Ｃ感 = 3
	ABL:LOCAL:欲望 = 8
	ABL:LOCAL:性技 = 3
	ABL:LOCAL:性知識 = 5
	ABL:LOCAL:奉仕 = 1
	ABL:LOCAL:性交 = 10
	ABL:LOCAL:射精 = 5
	EXP:LOCAL:絶頂経験 = 500
	;ある程度性能にばらつきを入れる
	;乱数の幅を大きくしておくことで振れ幅を大きくし、複数の乱数に分けることである程度中心値に収束するように
	;初期設定でランクを上げるとランク×4ずつ能力値が上がる
	SELECTCASE LCOUNT
	CASE 0 TO 3
		;一般兵:35-60(収束期待値：45)/35-60(収束期待値：45)
		ABL:LOCAL:武闘 = 30 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:16
		ABL:LOCAL:知略 = 30 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:16
	CASE 4 TO 7
		;武闘兵:35-75(収束期待値：50)/25-55(収束期待値：35)
		ABL:LOCAL:武闘 = 30 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:11 + RAND:21
		ABL:LOCAL:知略 = 20 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:11 + RAND:11
	CASE 8 TO 11
		;知能犯:25-55(収束期待値：35)/35-75(収束期待値：50)
		ABL:LOCAL:武闘 = 20 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:11 + RAND:11
		ABL:LOCAL:知略 = 30 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:11 + RAND:21
	CASE 12 TO 14
		;エリート:合計値は120、45-75(収束期待値:60)/45-75(収束期待値:60)
		ABL:LOCAL:武闘 =  40 + 4 * BANDIT_RANK + RAND:16 + RAND:16
		ABL:LOCAL:知略 = 120 + 4 * BANDIT_RANK - ABL:LOCAL:武闘
	CASEELSE
		;例外、一般兵と同値
		ABL:LOCAL:武闘 = 30 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:6 + RAND:11
		ABL:LOCAL:知略 = 30 + 4 * BANDIT_RANK + MAX(RAND:21 - 10, 0) + RAND:6 + RAND:11
	ENDSELECT
	ABL:LOCAL:政治 = 5
	ABL:LOCAL:歌唱 = 10
	ABL:LOCAL:料理 = 10
	TALENT:LOCAL:両刀 = 1
	TALENT:LOCAL:サド = 1
	TALENT:LOCAL:絶倫 = 1
	TALENT:LOCAL:野盗 = 1
	;奴隷スタートなら好感度と合意と会ったことあるフラグを編集
	IF BANDIT_START == 2
		TALENT:LOCAL:合意 = 1
		CFLAG:LOCAL:好感度 = RAND(300, 800)
		CFLAG:LOCAL:17 = 1
	ENDIF
	;所属を野盗勢力にする
	CFLAG:LOCAL:1 = LOCAL:4
	CALL NEWCHARA_INIT(LOCAL)
	IF !LOCAL:3
		LOCAL:3 = LOCAL
		COUNTRY_BOSS:(LOCAL:4) = GET_ID(LOCAL:3)
	ENDIF
NEXT

IF BANDIT_START == 1 || BANDIT_START == 2
	CFLAG:MASTER:所属 = LOCAL:4
ELSEIF BANDIT_START == 3
	CFLAG:MASTER:捕虜先 = LOCAL:4
ENDIF

SIF BANDIT_START == 1
	TALENT:MASTER:野盗 = 1

PRINTFORMW 野盗は最初から都市を……
CALL ASK_MULTI("所有している(ランダムです)", "所有していない")
IF RESULT == 0
	CALL BANDIT_RISE()
ELSE
	PRINTFORMW 野盗は土地ナシでスタートします
ENDIF
;-------------------------------------------------
;ランダムキャラを使用するかどうか選択
;-------------------------------------------------
@SELECT_RCHARA
IF FLAG:4 == -1
	FLAG:44 = 0
ELSEIF FLAG:4 == 1
	FLAG:44 = 1
ELSE
	DRAWLINE
	PRINTFORML このシナリオではランダムキャラを使用できます
	PRINTFORML ランダムキャラを使用しますか？
	CALL ASK_YN
	IF RESULT == 0
		FLAG:44 = 1
	ELSE
		FLAG:44 = 0
	ENDIF
ENDIF


;-------------------------------------------------
;野盗の強さを選択
;-------------------------------------------------
@SELECT_BANDIT
DRAWLINE
PRINTFORML シナリオに登場する野盗の強さを選んでください
IF !BANDIT_START
	PRINTL [0] 映す価値無し(野盗なんて居なかった、イイネ？)
ELSE
	SETCOLOR COLOR("選択不可")
	PRINTPLAINFORM [0] 野盗スタートを選択したので、野盗なし設定は選べません
	RESETCOLOR
	PRINTL
ENDIF
PRINTL [1] チンピラ級(標準)
PRINTL [2] 兵士崩れ級(チンピラの1.4倍程度)
PRINTL [3] 傭兵くずれ級(チンピラの2倍程度の強さ)
PRINTL [4] モヒカン級(チンピラの2.8倍程度の強さ)
PRINTL [5] 陵辱エロ同人級(チンピラの4倍程度の強さ)

$INPUT_LOOP_BANDIT
INPUT

IF RESULT == 0 && !BANDIT_START
	BANDIT_RANK = RESULT
	RETURN RESULT

ELSEIF INRANGE(RESULT, 1, 5)
	BANDIT_RANK = RESULT
	RETURN RESULT
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP_BANDIT

;-------------------------------------------------
;外来人の強さを選択
;-------------------------------------------------
@SELECT_GAIRAI
DRAWLINE
PRINTFORML シナリオに登場する外来人の強さを選んでください
PRINTFORML 彼らは野盗と違い、一定条件を満たして初めて出現します
PRINTL [0] 大結界を越えれない
PRINTL [1] 貧弱一般人
PRINTL [2] 地元じゃ負け知らず
PRINTL [3] ボクシングインターハイ出場
PRINTL [4] 妖怪とも渡り合えるぞ！
PRINTL [5] なんなら大妖怪も堕とすよ

$INPUT_LOOP_GAIRAI
INPUT

SIF INRANGE(RESULT, 0, 5)
	GAIRAI_RANK = RESULT
	RETURN
CLEARLINE 1
GOTO INPUT_LOOP_GAIRAI

;-------------------------------------------------
;ホフゴブリンの強さを選択
;-------------------------------------------------
@SELECT_GOBLIN
DRAWLINE
PRINTFORML シナリオに登場するホフゴブリンの強さを選んでください
PRINTFORML 彼らは野盗と違い、一定条件を満たして初めて出現します
PRINTL [0] 一生紅魔館で働いてる
PRINTL [1] 所詮は屋敷妖精
PRINTL [2] ちょっとこざかしくなった
PRINTL [3] あれ、こいつやるんじゃね？
PRINTL [4] おー、強い強い
PRINTL [5] あっちょっとまってこれやばい

$INPUT_LOOP_GAIRAI
INPUT

SIF INRANGE(RESULT, 0, 5)
	GOBLIN_RANK = RESULT
	RETURN
CLEARLINE 1
GOTO INPUT_LOOP_GAIRAI

;-------------------------------------------------
;設定セットを選択
;-------------------------------------------------
@SELECT_SLG_SETTING
DRAWLINE
PRINTFORML SLG用設定を選択してください（下に行くほどSLG側の難易度が上がります）
PRINTFORML 一部例外を除き、これらの設定はゲーム中でも変更可能です
PRINTL 設定| 難易度 | 好感度補正 | 補正方式 | 兵力補正 | 継戦補正 | 敵対補正 |
PRINTL [0] |  簡単  |    高い    |   通常   | がっつり |   なし   |   EASY   | 初めて・嫁の活躍を見たい方向け
PRINTL [1] |  簡単  |    普通    |   通常   | がっつり | 武将：低 |   EASY   | 上の設定で少し物足りなくなった人に
PRINTL [2] |  簡単  |    普通    |   通常   |   強め   | 武将：低 |  NORMAL  | それぞれの設定を一通り試したい人向け
PRINTL [3] |  普通  |    低い    |   通常   |   弱め   | 回数：低 |  NORMAL  | キャラの能力に重点を置きたい人向け
PRINTL [4] |  普通  |    無し    |   －－   | がっつり | 回数：低 |   HARD   | 愛の力のない純粋な戦略を楽しみたい方に
PRINT [5] |  困難  |    高い    |  倍率型  |   強め   | 武将：高 | LUNATIC  | 
SETCOLOR COLOR("注意")
PRINTL 周回前提、準備はできていますか？

RESETCOLOR
PRINT [6] |  
SETCOLOR COLOR("警告")
PRINT 虐め  
PRINTD |    普通    |  倍率型  |   弱め   | 武将：高 |  EXTRA   | 
PRINTL 周回前提、まぁがんばれ
RESETCOLOR

PRINT [7] |  
SETCOLOR COLOR("警告")
PRINT 虐め  
PRINTD |    低い    |  倍率型  |   無し   | 回数：高 |  
PRINT EXTRA+  
PRINTD | 
SETCOLOR COLOR("灰色")
PRINTL ようこそ、悪夢へ
RESETCOLOR

PRINTL [99] デフォルトのままであそぶ
$INPUT_LOOP_SLG
INPUT

SELECTCASE RESULT
CASE 0
	CONFIG:300 = 0
	CONFIG:309 = 1
	CONFIG:310 = 0
	CONFIG:312 = 3
	CONFIG:313 = 0
	CONFIG:314 = 0
CASE 1
	CONFIG:300 = 0
	CONFIG:309 = 0
	CONFIG:310 = 0
	CONFIG:312 = 3
	CONFIG:313 = 3
	CONFIG:314 = 0
CASE 2
	CONFIG:300 = 0
	CONFIG:309 = 0
	CONFIG:310 = 0
	CONFIG:312 = 2
	CONFIG:313 = 3
	CONFIG:314 = 1
CASE 3
	CONFIG:300 = 1
	CONFIG:309 = 3
	CONFIG:310 = 0
	CONFIG:312 = 1
	CONFIG:313 = 1
	CONFIG:314 = 1
CASE 4
	CONFIG:300 = 1
	CONFIG:309 = 2
	CONFIG:310 = 0
	CONFIG:312 = 3
	CONFIG:313 = 1
	CONFIG:314 = 2
CASE 5
	CONFIG:300 = 2
	CONFIG:309 = 1
	CONFIG:310 = 2
	CONFIG:312 = 2
	CONFIG:313 = 4
	CONFIG:314 = 3
CASE 6
	CONFIG:300 = 3
	CONFIG:309 = 0
	CONFIG:310 = 2
	CONFIG:312 = 1
	CONFIG:313 = 4
	CONFIG:314 = 4
CASE 7
	CONFIG:300 = 3
	CONFIG:309 = 3
	CONFIG:310 = 2
	CONFIG:312 = 0
	CONFIG:313 = 2
	CONFIG:314 = 5
CASE 99
CASEELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_SLG
ENDSELECT

IF INRANGE(RESULT, 0, 7)
	PRINTL 設定が変更されました。
	SIF RESULT == 6
		PRINTL なお、難易度はゲーム中変更できません。
	SIF RESULT == 7
		PRINTL なお、難易度及び敵対補正がロックされました。
ELSE
	PRINTW 設定を変更せずに開始します。
ENDIF
DRAWLINE
WAIT

RETURN

;-------------------------------------------------
;セーブ時の処理
;-------------------------------------------------
@SAVEINFO
IF TIME == 0
	LOCALS:0 = 拠
ELSE
	LOCALS:0 = 戦
ENDIF
LOCAL:0 = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
IF LOCAL:0 >= 0
	LOCALS:1 = %NAME:(LOCAL:0)%
ELSE
	LOCALS:1 = ？
ENDIF
TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS:2 '= RESULTS
PUTFORM  %LOCALS:2, 26, LEFT% {MIN(99999, DAY), 5}期<%LOCALS:0%> %LOCALS:1, 6%勢力　%CALLNAME:MASTER%
