@PASS_CHARA_UI()
#DIM FIRST_LINE
#DIM 素質ページ
#DIM 経験ページ
REDRAW 1
FIRST_LINE = LINECOUNT
PRINTFORML 引き継ぎたい要素を選択してください
DRAWLINE
PRINTL □体力□
FOR LOCAL, 0, 5
	SIF BASENAME:LOCAL != ""
		CALL PRINT_TOGGLE_BUTTON(@"%"[" + BASENAME:LOCAL + "]", 16, LEFT%", 100 + LOCAL, BASE引き継ぎ:LOCAL)
NEXT
PRINTL
DRAWLINE
PRINTL □好感度□
CALL PRINT_TOGGLE_BUTTON(@"%"[好感度]", 16, LEFT%", 200 + GETNUM(CFLAG, "好感度"), CFLAG引き継ぎ:GETNUM(CFLAG, "好感度"))
CALL PRINT_TOGGLE_BUTTON(@"%"[従属度]", 16, LEFT%", 200 + GETNUM(CFLAG, "従属度"), CFLAG引き継ぎ:GETNUM(CFLAG, "従属度"))
CALL PRINT_TOGGLE_BUTTON(@"%"[依存度]", 16, LEFT%", 200 + GETNUM(CFLAG, "依存度"), CFLAG引き継ぎ:GETNUM(CFLAG, "依存度"))
PRINTL
DRAWLINE
PRINTL □能力□
PRINTBUTTON @"%"[すべてオフ]", 16, LEFT%", 2000
PRINTBUTTON @"%"[すべてオン]", 16, LEFT%", 2001
PRINTL
FOR LOCAL, 0, VARSIZE("ABL")
	SIF ABLNAME:LOCAL != ""
		CALL PRINT_TOGGLE_BUTTON(@"%"[" + ABLNAME:LOCAL +"]", 16, LEFT%", 300 + LOCAL, ABL引き継ぎ:LOCAL)
NEXT
PRINTL
DRAWLINE
PRINTL □素質□
PRINTBUTTON @"%"[すべてオフ]", 16, LEFT%", 2100
PRINTBUTTON @"%"[すべてオン]", 16, LEFT%", 2101
PRINTL
FOR LOCAL, 0, 300
	SIF TALENTNAME:LOCAL != "" && PASS_CHARA_CHECK_SHOW_TALENT(LOCAL)
		CALL PRINT_TOGGLE_BUTTON(@"%"[" + TALENTNAME:LOCAL + "]", 16, LEFT%", 500 + LOCAL, TALENT引き継ぎ:LOCAL)
NEXT
PRINTL
DRAWLINE
PRINTL □経験□
PRINTBUTTON @"%"[すべてオフ]", 16, LEFT%", 2200
PRINTBUTTON @"%"[すべてオン]", 16, LEFT%", 2201
PRINTL
FOR LOCAL, 40, 70
	SIF EXPNAME:LOCAL != ""
		CALL PRINT_TOGGLE_BUTTON(@"%"[" + EXPNAME:LOCAL + "]", 16, LEFT%", 800 + LOCAL, EXP引き継ぎ:LOCAL)
NEXT
PRINTL
DRAWLINE
PRINTL □その他□
CALL PRINT_TOGGLE_BUTTON(@"%"[陥落素質]", 16, LEFT%", 1000, 陥落素質引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[淫乱素質]", 16, LEFT%", 1001, 淫乱素質引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[ＳＬＧ素質]", 16, LEFT%", 1002, ＳＬＧ素質引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[特殊勢力陥落]", 16, LEFT%", 1003, 特殊勢力陥落引き継ぎ)
PRINTL
CALL PRINT_TOGGLE_BUTTON(@"%"[締まり]", 16, LEFT%", 1010, 締まり引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[陰毛]", 16, LEFT%", 1011, 陰毛引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[ピアス]", 16, LEFT%", 1012, ピアス引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[タトゥー]", 16, LEFT%", 1013, タトゥー引き継ぎ)
PRINTL
CALL PRINT_TOGGLE_BUTTON(@"%"[キス初体験]", 16, LEFT%", 1020, キス引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[処女喪失]", 16, LEFT%", 1021, 処女引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[Ａ処女喪失]", 16, LEFT%", 1022, アナル処女引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[童貞喪失]", 16, LEFT%", 1023, 童貞引き継ぎ)
PRINTL
CALL PRINT_TOGGLE_BUTTON(@"%"[バストサイズ]", 16, LEFT%", 1030, バストサイズ引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[ペニスサイズ]", 16, LEFT%", 1031, ペニスサイズ引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[性的嗜好]", 16, LEFT%", 1032, 性的嗜好引き継ぎ)
CALL PRINT_TOGGLE_BUTTON(@"%"[妊娠記録]", 16, LEFT%", 1033, 妊娠記録引き継ぎ)
PRINTL
DRAWLINE
PRINTBUTTON "[これでよい]", 9999

INPUT
SELECTCASE RESULT
	CASE IS < 200
		RESULT = RESULT - 100
		SIF BASENAME:RESULT != ""
			BASE引き継ぎ:RESULT = !BASE引き継ぎ:RESULT
	CASE IS < 300
		RESULT = RESULT - 200
		SIF GROUPMATCH(RESULT, GETNUM(CFLAG, "好感度"), GETNUM(CFLAG, "依存度"), GETNUM(CFLAG, "従属度"))
			CFLAG引き継ぎ:RESULT = !CFLAG引き継ぎ:RESULT
	CASE IS < 500
		RESULT = RESULT - 300
		SIF ABLNAME:RESULT != ""
			ABL引き継ぎ:RESULT = !ABL引き継ぎ:RESULT
	CASE IS < 800
		RESULT = RESULT - 500
		SIF TALENTNAME:RESULT != "" && PASS_CHARA_CHECK_SHOW_TALENT(RESULT)
			TALENT引き継ぎ:RESULT = !TALENT引き継ぎ:RESULT
	CASE 840 TO 870
		RESULT = RESULT - 800
		SIF EXPNAME:RESULT != ""
			EXP引き継ぎ:RESULT = !EXP引き継ぎ:RESULT
	CASE 1000
		陥落素質引き継ぎ = !陥落素質引き継ぎ
	CASE 1001
		淫乱素質引き継ぎ = !淫乱素質引き継ぎ
	CASE 1002
		ＳＬＧ素質引き継ぎ = !ＳＬＧ素質引き継ぎ
	CASE 1003
		特殊勢力陥落引き継ぎ = !特殊勢力陥落引き継ぎ
	CASE 1010
		締まり引き継ぎ = !締まり引き継ぎ
	CASE 1011
		陰毛引き継ぎ = !陰毛引き継ぎ
	CASE 1012
		ピアス引き継ぎ = !ピアス引き継ぎ
	CASE 1013
		タトゥー引き継ぎ = !タトゥー引き継ぎ
	CASE 1020
		キス引き継ぎ = !キス引き継ぎ
	CASE 1021
		処女引き継ぎ = !処女引き継ぎ
	CASE 1022
		アナル処女引き継ぎ = !アナル処女引き継ぎ
	CASE 1023
		童貞引き継ぎ = !童貞引き継ぎ
	CASE 1030
		バストサイズ引き継ぎ = !バストサイズ引き継ぎ
	CASE 1031
		ペニスサイズ引き継ぎ = !ペニスサイズ引き継ぎ
	CASE 1032
		性的嗜好引き継ぎ = !性的嗜好引き継ぎ
	CASE 1033
		妊娠記録引き継ぎ = !妊娠記録引き継ぎ
	CASE 2000, 2001
		RESULT = RESULT - 2000
		FOR LOCAL, 0, VARSIZE("ABL")
			SIF ABLNAME:LOCAL != ""
				ABL引き継ぎ:LOCAL = RESULT
		NEXT
	CASE 2100, 2101
		RESULT = RESULT - 2100
		FOR LOCAL, 0, 300
			SIF TALENTNAME:LOCAL != "" && PASS_CHARA_CHECK_SHOW_TALENT(LOCAL)
				TALENT引き継ぎ:LOCAL = RESULT
		NEXT
	CASE 2200, 2201
		RESULT = RESULT - 2200
		FOR LOCAL, 40, 70
			SIF EXPNAME:LOCAL != ""
				EXP引き継ぎ:LOCAL = RESULT
		NEXT
	CASE 9999
		RETURN
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
RESTART


@PASS_CHARA(対象)
#DIM 対象
#DIM ABL保存, VARSIZE("ABL引き継ぎ")
#DIM TALENT保存, VARSIZE("TALENT引き継ぎ")
#DIM EXP保存, VARSIZE("EXP引き継ぎ")
#DIM CFLAG保存, VARSIZE("CFLAG引き継ぎ")
#DIM BASE保存, VARSIZE("BASE引き継ぎ")
;#DIM 陥落素質保存
#DIM 淫乱素質保存
#DIM ＳＬＧ素質保存
#DIM Ｖ締まり保存
#DIM Ａ締まり保存
#DIM 陰毛目標値保存
#DIM 陰毛現在値保存
#DIM ピアス保存
#DIM ピアス穴保存
#DIM 特殊勢力陥落保存
#DIM 調教カウント保存, VARSIZE("SP_TRAIN_COUNT")
#DIM 性的嗜好保存
#DIM 性的嗜好ロック保存
#DIM COM_EXP保存, VARSIZE("COM_EXP")
#DIMS タトゥー保存, VARSIZE("TATOO")
#DIMS キス保存
#DIMS 童貞保存
#DIMS 処女保存
#DIMS アナル処女保存
#DIM バストサイズ保存
#DIM ペニスサイズ保存
#DIM PREGNANT_RECORD_TIMES保存, PREGNANT_RECORD_MAX
#DIM PREGNAMT_RECORD_FATHER保存, PREGNANT_RECORD_MAX
VARSET ABL保存
VARSET TALENT保存
VARSET EXP保存
VARSET CFLAG保存
VARSET BASE保存
;VARSET 陥落素質保存
VARSET 淫乱素質保存
VARSET ＳＬＧ素質保存
VARSET Ｖ締まり保存
VARSET Ａ締まり保存
VARSET 陰毛目標値保存
VARSET 陰毛現在値保存
VARSET ピアス保存
VARSET ピアス穴保存
VARSET 特殊勢力陥落保存
VARSET 調教カウント保存
VARSET 性的嗜好保存
VARSET 性的嗜好ロック保存
VARSET COM_EXP保存
VARSET タトゥー保存
VARSET キス保存
VARSET 童貞保存
VARSET 処女保存
VARSET アナル処女保存
VARSET バストサイズ保存
VARSET ペニスサイズ保存
VARSET PREGNANT_RECORD_TIMES保存
VARSET PREGNAMT_RECORD_FATHER保存

;デイリー系はPASS_CHARAで扱わない
TALENT保存:GETNUM(TALENT, "デイリー系") = TALENT:対象:デイリー系

;ABLを保存
FOR LOCAL, 0, VARSIZE("ABL引き継ぎ")
	SIF ABL引き継ぎ:LOCAL
		ABL保存:LOCAL = ABL:対象:LOCAL
NEXT

;TALENTを保存
FOR LOCAL, 0, VARSIZE("TALENT引き継ぎ")
	SIF TALENT引き継ぎ:LOCAL
		TALENT保存:LOCAL = TALENT:対象:LOCAL
NEXT

;EXPを保存
FOR LOCAL, 40, 70
	SIF EXP引き継ぎ:LOCAL
		EXP保存:LOCAL = EXP:対象:LOCAL
NEXT

;CFLAGを保存
SIF CFLAG引き継ぎ:GETNUM(CFLAG, "好感度")
	CFLAG保存:GETNUM(CFLAG, "好感度") = CFLAG:対象:好感度
SIF CFLAG引き継ぎ:GETNUM(CFLAG, "従属度")
	CFLAG保存:GETNUM(CFLAG, "従属度") = CFLAG:対象:従属度
SIF CFLAG引き継ぎ:GETNUM(CFLAG, "依存度")
	CFLAG保存:GETNUM(CFLAG, "依存度") = CFLAG:対象:依存度

;BASEを保存
FOR LOCAL, 0, VARSIZE("BASE引き継ぎ")
	SIF BASE引き継ぎ:LOCAL
		BASE保存:LOCAL = MAXBASE:対象:LOCAL
NEXT

;陥落素質を保存
IF 陥落素質引き継ぎ
	FOR LOCAL, 150, 157
		TALENT保存:LOCAL = TALENT:対象:LOCAL
	NEXT
ENDIF

SIF 淫乱素質引き継ぎ
	淫乱素質保存 = TALENT:対象:淫乱系

SIF ＳＬＧ素質引き継ぎ
	ＳＬＧ素質保存 = TALENT:対象:ＳＬＧ素質

IF 締まり引き継ぎ
	Ｖ締まり保存 = TALENT:対象:Ｖ締まり
	Ａ締まり保存 = TALENT:対象:Ａ締まり
ENDIF

IF 陰毛引き継ぎ
	陰毛現在値保存 = TALENT:対象:陰毛現在値
	陰毛目標値保存 = TALENT:対象:陰毛目標値
ENDIF

IF ピアス引き継ぎ
	ピアス保存 = TALENT:対象:ピアス
	ピアス穴保存 = TALENT:対象:ピアス穴
ENDIF

IF 特殊勢力陥落引き継ぎ
	特殊勢力陥落保存 = TALENT:対象:特殊勢力陥落系
	FOR LOCAL, 0, VARSIZE("SP_TRAIN_COUNT")
		調教カウント保存:LOCAL = SP_TRAIN_COUNT:対象:LOCAL
	NEXT
ENDIF

IF 性的嗜好引き継ぎ
	性的嗜好保存 = TALENT:対象:性的嗜好
	性的嗜好ロック保存 = TALENT:対象:性的嗜好ロック
	FOR LOCAL, 0, VARSIZE("COM_EXP")
		COM_EXP保存:LOCAL = COM_EXP:対象:LOCAL
	NEXT
ENDIF

IF タトゥー引き継ぎ
	FOR LOCAL, 0, VARSIZE("TATOO")
		タトゥー保存:LOCAL = %TATOO:対象:LOCAL%
	NEXT
ENDIF

IF キス引き継ぎ
	キス保存 = %SEXUAL_EXPERIENCE:対象:初体験_ファーストキス%
	TALENT保存:GETNUM(TALENT, "キス未経験") = TALENT:対象:キス未経験
ENDIF
IF 童貞引き継ぎ
	童貞保存 = %SEXUAL_EXPERIENCE:対象:初体験_童貞%
	TALENT保存:GETNUM(TALENT, "童貞") = TALENT:対象:童貞
ENDIF
IF 処女引き継ぎ
	処女保存 = %SEXUAL_EXPERIENCE:対象:初体験_処女%
	TALENT保存:GETNUM(TALENT, "処女") = TALENT:対象:処女
ENDIF
IF アナル処女引き継ぎ
	アナル処女保存 = %SEXUAL_EXPERIENCE:対象:初体験_アナル処女%
	TALENT保存:GETNUM(TALENT, "アナル処女") = TALENT:対象:アナル処女
ENDIF

SIF バストサイズ引き継ぎ
	バストサイズ保存 = GET_BUSTSIZE_NUM(対象)

SIF ペニスサイズ引き継ぎ
	ペニスサイズ保存 = GET_PENISSIZE_NUM(対象)

IF 妊娠記録引き継ぎ
	FOR LOCAL, 0, PREGNANT_RECORD_MAX
		PREGNAMT_RECORD_FATHER保存:LOCAL = PREGNANT_RECORD_FATHER:対象:LOCAL
		PREGNANT_RECORD_TIMES保存:LOCAL = PREGNANT_RECORD_TIMES:対象:LOCAL
	NEXT
ENDIF

;初期化
CALL INITIALIZE_CHARA(対象)

;-----------ここから戻す作業--------------
TALENT:対象:デイリー系 = TALENT保存:GETNUM(TALENT, "デイリー系")


;ABLを戻す
FOR LOCAL, 0, VARSIZE("ABL引き継ぎ")
	SIF ABL引き継ぎ:LOCAL
		ABL:対象:LOCAL = ABL保存:LOCAL
NEXT

;TALENTを戻す
FOR LOCAL, 0, VARSIZE("TALENT引き継ぎ")
	SIF TALENT引き継ぎ:LOCAL
		TALENT:対象:LOCAL = TALENT保存:LOCAL
NEXT

;EXPを戻す
FOR LOCAL, 40, 70
	SIF EXP引き継ぎ:LOCAL
		EXP:対象:LOCAL = EXP保存:LOCAL
NEXT

;CFLAGを戻す
SIF CFLAG引き継ぎ:GETNUM(CFLAG, "好感度")
	CFLAG:対象:好感度 = CFLAG保存:GETNUM(CFLAG, "好感度")
SIF CFLAG引き継ぎ:GETNUM(CFLAG, "従属度")
	CFLAG:対象:従属度 = CFLAG保存:GETNUM(CFLAG, "従属度")
SIF CFLAG引き継ぎ:GETNUM(CFLAG, "依存度")
	CFLAG:対象:依存度 = CFLAG保存:GETNUM(CFLAG, "依存度")

;BASEを戻す
FOR LOCAL, 0, VARSIZE("BASE引き継ぎ")
	SIF BASE引き継ぎ:LOCAL
		MAXBASE:対象:LOCAL = BASE保存:LOCAL
NEXT

;陥落素質を戻す
IF 陥落素質引き継ぎ
	FOR LOCAL, 150, 157
		TALENT:対象:LOCAL = TALENT保存:LOCAL
	NEXT
ENDIF

SIF 淫乱素質引き継ぎ
	TALENT:対象:淫乱系 = 淫乱素質保存

SIF ＳＬＧ素質引き継ぎ
	TALENT:対象:ＳＬＧ素質 = ＳＬＧ素質保存

IF 締まり引き継ぎ
	TALENT:対象:Ｖ締まり = Ｖ締まり保存
	TALENT:対象:Ａ締まり = Ａ締まり保存
ENDIF

IF 陰毛引き継ぎ
	TALENT:対象:陰毛現在値 = 陰毛現在値保存
	TALENT:対象:陰毛目標値 = 陰毛目標値保存
ENDIF

IF ピアス引き継ぎ
	TALENT:対象:ピアス = ピアス保存
	TALENT:対象:ピアス穴 =ピアス穴保存
ENDIF

IF 特殊勢力陥落引き継ぎ
	TALENT:対象:特殊勢力陥落系 = 特殊勢力陥落保存
	FOR LOCAL, 0, VARSIZE("SP_TRAIN_COUNT")
		SP_TRAIN_COUNT:対象:LOCAL = 調教カウント保存:LOCAL
	NEXT
ENDIF

IF 性的嗜好引き継ぎ
	TALENT:対象:性的嗜好 = 性的嗜好保存
	TALENT:対象:性的嗜好ロック = 性的嗜好ロック保存
	FOR LOCAL, 0, VARSIZE("COM_EXP")
		COM_EXP:対象:LOCAL = COM_EXP保存:LOCAL
	NEXT
ENDIF

IF タトゥー引き継ぎ
	FOR LOCAL, 0, VARSIZE("TATOO")
		TATOO:対象:LOCAL = %タトゥー保存:LOCAL%
	NEXT
ENDIF

IF キス引き継ぎ
	SEXUAL_EXPERIENCE:対象:初体験_ファーストキス = %キス保存%
	TALENT:対象:キス未経験 = TALENT保存:GETNUM(TALENT, "キス未経験")
ENDIF
IF 童貞引き継ぎ
	SEXUAL_EXPERIENCE:対象:初体験_童貞 = %童貞保存%
	TALENT:対象:童貞 = TALENT保存:GETNUM(TALENT, "童貞")
ENDIF
IF 処女引き継ぎ
	SEXUAL_EXPERIENCE:対象:初体験_処女 = %処女保存%
	TALENT:対象:処女 = TALENT保存:GETNUM(TALENT, "処女")
ENDIF
IF アナル処女引き継ぎ
	SEXUAL_EXPERIENCE:対象:初体験_アナル処女 = %アナル処女保存%
	TALENT:対象:アナル処女 = TALENT保存:GETNUM(TALENT, "アナル処女")
ENDIF

SIF バストサイズ引き継ぎ
	CALL SET_BUSTSIZE(対象, バストサイズ保存)

SIF ペニスサイズ引き継ぎ
	CALL SET_PENISSIZE(対象, ペニスサイズ保存)

IF 妊娠記録引き継ぎ
	FOR LOCAL, 0, PREGNANT_RECORD_MAX
		PREGNANT_RECORD_FATHER:対象:LOCAL = PREGNAMT_RECORD_FATHER保存:LOCAL
		PREGNANT_RECORD_TIMES:対象:LOCAL = PREGNANT_RECORD_TIMES保存:LOCAL
	NEXT
ENDIF

RETURN 


@PASS_CHARA_CHECK_SHOW_TALENT(ARG:0)
#FUNCTION
{
RETURNF !GROUPMATCH(ARG:0, GETNUM(TALENT, "性別"), GETNUM(TALENT, "童貞"), GETNUM(TALENT, "処女"), GETNUM(TALENT, "キス未経験"), GETNUM(TALENT, "アナル処女"), 
GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "親友"),
GETNUM(TALENT, "崩壊"), GETNUM(TALENT, "虚ろ"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "正妻"), GETNUM(TALENT, "妊娠"), GETNUM(TALENT, "育児中"), 
GETNUM(TALENT, "触手妊娠"), GETNUM(TALENT, "Ａ触手妊娠"), GETNUM(TALENT, "子供人質"), 
GETNUM(TALENT, "爆乳"), GETNUM(TALENT, "巨乳"), GETNUM(TALENT, "貧乳"), GETNUM(TALENT, "絶壁"),
GETNUM(TALENT, "超短小"), GETNUM(TALENT, "短小"), GETNUM(TALENT, "巨根"), GETNUM(TALENT, "超巨根"),
GETNUM(TALENT, "ＮＴＲ"))
}
