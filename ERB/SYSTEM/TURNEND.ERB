;-------------------------------------------------
;時間経過イベント
;-------------------------------------------------
@EVENTTURNEND
#PRI

FLAG:200 = -1
FLAG:201 = 0

;拠点フェイズ
IF TIME == 0
	IF SHOP_TIME >= CALC_SHOP_TIME() || CFLAG:MASTER:捕虜先
		CALL TURNEND_LIFE
	ELSE
		CALL COOK_HEAL
		BEGIN SHOP
	ENDIF
;戦略フェイズ
ELSE
	CALL TURNEND_POLITICS
ENDIF

;逆調教要求日数が残っている場合
IF DIPLOMACY_TRAINED_DAY > 0 && !FLAG:31 && !FLAG:調教要求フラグ
	DIPLOMACY_TRAINED_DAY --
	;全員の調教参加フラグを解除
	CVARSET CFLAG, 6, 0
	CFLAG:DIPLOMACY_TRAINING_CHARA:調教参加フラグ = 1
	;捕虜逆調教モード
	FLAG:12 = 5
	;ウフフモード
	FLAG:50 = 1
	PRINTFORML %ANAME(MASTER)%は外交交渉の見返りとして、%ANAME(DIPLOMACY_TRAINING_CHARA)%の寝室へ向かった……
	CALL START_TRAIN_COMMON
	RETURN
ENDIF

;君主逆調教要求日数が残っている場合
IF DIPLOMACY_TRAINED_DAY < 0 && !FLAG:調教要求フラグ
	DIPLOMACY_TRAINED_DAY ++
	VARSET TMP_好感度
	VARSET TMP_従属度
	VARSET TMP_依存度
	VARSET TMP_参加者
	;捕虜逆調教モード
	FLAG:12 = 5
	;ウフフモード
	FLAG:50 = 1
	FLAG:あてがうフラグ = 1
	FLAG:調教要求フラグ = 1
	;アイテムを一通り追加
	ITEM:A_ローター = 1
	ITEM:A_バイブ = 1
	ITEM:A_アナルバイブ = 1
	ITEM:A_ペニスバンド = 1
	ITEM:A_オナホール = 1
	ITEM:A_クリキャップ = 1
	ITEM:A_ニプルキャップ = 1
	ITEM:A_媚薬 = 99
	ITEM:A_排卵誘発剤 = 99
	;行動開始時の共通処理
	;好感度等を保存
	TMP_好感度:0 = CFLAG:DIPLOMACY_TRAINING_CHARA:好感度
	TMP_従属度:0 = CFLAG:DIPLOMACY_TRAINING_CHARA:従属度
	TMP_依存度:0 = CFLAG:DIPLOMACY_TRAINING_CHARA:依存度
	;調教中は好感度を1000に
	CFLAG:DIPLOMACY_TRAINING_CHARA:好感度 = 1000
	CFLAG:DIPLOMACY_TRAINING_CHARA:従属度 = 1000
	CFLAG:DIPLOMACY_TRAINING_CHARA:依存度 = 1000
	;参加済みに
	TMP_参加者:0 = DIPLOMACY_TRAINING_CHARA
	;全員の調教参加フラグを解除
	CVARSET CFLAG, 6, 0
	CFLAG:DIPLOMACY_TRAINING_CHARA:調教参加フラグ = 1
	CFLAG:DIPLOMACY_TRAINING_CHARA:91 = 1

	PRINTFORMW %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:1))%は外交交渉の見返りとして、%ANAME(DIPLOMACY_TRAINING_CHARA)%の寝室へ向かった……
	TMP_MASTER = GET_COUNTRY_BOSS(CFLAG:MASTER:1)
	SWAPCHARA MASTER, TMP_MASTER
	CALL KOJO_NTR_ATEGAU(MASTER, DIPLOMACY_TRAINING_CHARA)
	
	;時間の処理をしないと、「拠点フェイズの終了時にここに到達、調教→調教終了でSLGフェイズのターンエンドが呼ばれ、次期の拠点フェイズに飛ぶ」みたいな挙動になる
	;IF TIME == 0
	;	DAY --
	;	SIF !FLAG:28
	;		TIME = 1
	;ELSEIF TIME == 1
	;	TIME = 0
	;	SHOP_TIME = CALC_SHOP_TIME()
	;ENDIF
	
	;行動開始時の共通設定
	CALL START_TRAIN_COMMON
	REDRAW 1
	RETURN
ENDIF

$SKIPPED

;強制調教フラグが立っているなら即時調教モードに入る
IF FLAG:31 == 1
	FLAG:31 = 0
	FLAG:調教モード = 1
	FLAG:50 = 1
	;時間の処理をしないと、「拠点フェイズの終了時にここに到達、調教→調教終了でSLGフェイズのターンエンドが呼ばれ、次期の拠点フェイズに飛ぶ」みたいな挙動になる
	IF TIME == 0
		IF !FLAG:28
			TIME = 1
			DAY --
		ENDIF
	ELSEIF TIME == 1
		TIME = 0
		SHOP_TIME = CALC_SHOP_TIME()
	ENDIF
	FLAG:イベント発生禁止 = 1
	CALL START_TRAIN_COMMON
ELSEIF FLAG:31 == 2
	FLAG:31 = 0
	FLAG:調教モード = 5
	FLAG:50 = 1
	IF TIME == 0
		IF !FLAG:28
			TIME = 1
			DAY --
		ENDIF
	ELSEIF TIME == 1
		TIME = 0
		SHOP_TIME = CALC_SHOP_TIME()
	ENDIF
	FLAG:イベント発生禁止 = 1
	CALL START_TRAIN_COMMON
ELSE
	SIF FLAG:調教要求フラグ
		FLAG:調教要求フラグ = 0
	FLAG:イベント発生禁止 = 0
	;一応謎勢力にあなたが存在することが無いようにおまじない
	SIF CFLAG:MASTER:1 == 0 && CFLAG:MASTER:捕虜先 == 0
		TIME = 1
	BEGIN SHOP
ENDIF

;-------------------------------------------------
;拠点フェイズ終了時の処理
;-------------------------------------------------
@TURNEND_LIFE
SHOP_TIME = 0
SHOP_WORK_COUNT = 0

SIF FLAG:イベント発生禁止
	GOTO SKIPPED

;夜に起こるイベント(夜這いなど)
CALL EVENT_NIGHT

IF CONFIG:7 == 0
	;捕虜の扱いを設定
	CALL SETSTATE_PRISONER
ELSE
	;捕虜の人数をカウント
	LOCAL:5 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):捕虜先 != 0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:1 && CFLAG:(LOCAL:0):29 == 0
			LOCAL:5 ++
		ENDIF
	NEXT
	;捕虜が一人でもいればラインを引く
	IF LOCAL:5 >= 1
		DRAWLINE
	ENDIF
ENDIF

;説得イベント
CALL EVENT_SETTOKU

;捕虜の扱いに対応したイベント
CALL EVENT_PRISONER

;捕虜でなければ、FLAG:14(捕虜調教初回済みフラグ）とFLAG:47（捕虜調教回数）を消す
IF !CFLAG:MASTER:捕虜先
	FLAG:14 = 0
	FLAG:47 = 0
ENDIF

;口上呼び出し
FOR LOCAL:0, 0, CHARANUM
	;主人公と同一勢力の仕官で、捕虜でない
	IF CFLAG:(LOCAL:0):1 != 0 && LOCAL:0 != MASTER && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && CFLAG:(LOCAL:0):捕虜先 == 0
		CALL KOJO_EVENT(LOCAL:0, 30)
	ENDIF
NEXT

;デイリーイベント
CALL EVENT_DAILY

;奴隷市場の商品リストを更新
CALL SLAVEMARKET_PREPARE_GOODS
;スカウトの対象リストを更新
CALL SCOUT_PREPARE_CANDIDATES

$SKIPPED

;統一後の場合
IF FLAG:28
	;一期終了時の処理
	CALL TURNEND_COMMON
	;クールタイムのリセット
	CVARSET COOLTIME, 0

	;ターンを進める
	DAY ++

;捕虜の場合
ELSEIF CFLAG:MASTER:捕虜先 != 0
	;強制的に自動行動
	LOCAL:0 = CONFIG:302
	CONFIG:302 = 1

	DRAWLINE
	DRAWLINE

	;戦略フェイズの処理
	CALL SLG_TURNEND

	;自動行動の設定を元に戻す
	CONFIG:302 = LOCAL:0

	;一期終了時の処理
	CALL TURNEND_COMMON

	;ターンを進める
	DAY ++

ELSE
	;戦略フェイズへ進む
	TIME = 1
ENDIF

;-------------------------------------------------
;戦略フェイズ終了時の処理
;-------------------------------------------------
@TURNEND_POLITICS

SIF FLAG:イベント発生禁止
	GOTO SKIPPED

; 防衛兵と部隊兵を保存
ARRAYCOPY "CITY_SOLDIER", "CITY_SOLDIER_PREV"
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, MAX_UNIT
		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER:(LOCAL:0):(LOCAL:1)
	NEXT
NEXT

;戦闘関係の処理
CALL SLG_TURNEND

;損害を計算
FOR LOCAL:0, 0, MAX_CITY
	CITY_SOLDIER_PREV:(LOCAL:0) = CITY_SOLDIER_PREV:(LOCAL:0) - CITY_SOLDIER:(LOCAL:0)
NEXT
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, MAX_UNIT
		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) - UNIT_SOLDIER:(LOCAL:0):(LOCAL:1)
	NEXT
NEXT

;デッドエンディングなら戻る
IF FLAG:27 == 9999
	RETURN
ENDIF

;デイリーイベント
CALL SLG_DAILY

;一期終了時の処理
CALL TURNEND_COMMON

$SKIPPED

;時間を進める
DAY ++
IF (CFLAG:MASTER:1 == 0 && CFLAG:MASTER:捕虜先 == 0) || FLAG:1
	TIME = 1
ELSE
	TIME = 0
ENDIF

;-------------------------------------------------
;一期終了時の処理
;統一前なら戦略フェイズ終了後、統一後なら拠点フェイズ終了後に呼ばれる
;-------------------------------------------------
@TURNEND_COMMON
VARSET LOCAL, 0

;主人公用の捕虜状態からの脱出イベント呼び出し
CALL GREAT_ESCAPE

;口上呼び出し
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1 && LOCAL:0 != MASTER
		CALL KOJO_EVENT(LOCAL:0, 31)
	ENDIF
NEXT

VARSET PRISONER_TARGET, -1


IF CFLAG:MASTER:1 <= 0
	WAIT
;給料の支給(観戦中・捕虜のときはスキップ)◆
ELSEIF CFLAG:MASTER:捕虜先 == 0
	DRAWLINE
	LOCAL:0 = GET_SUM_ECONOMY(CFLAG:MASTER:1) * (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + 200) / 30000 - MIN(GET_OWN_CITY(CFLAG:MASTER:所属) - 5, 1) * 100
	MONEY += LOCAL:0
	PRINTFORML %CALLNAME:MASTER%に今期分の俸給が支払われました
	PRINTFORML <都市の経済規模×(%CALLNAME:MASTER%の能力合計＋200)÷300－(支配都市数－5)×100>
	PRINTFORMW 所持金＋{LOCAL:0}
ENDIF

;各勢力都市の経済規模変動及び税収処理
CALL VARY_ECONOMY
CALL GAIN_COUNTRY_TAX

;経済成長・兵数増強の補正値をクリア
FLAG:42 = 0
FLAG:43 = 0

PLAYER_HIRED_COUNTER = MAX(0, PLAYER_HIRED_COUNTER - 1)
COUNTRY_PLAYER_BELONGED_COUNTER++
IF COUNTRY_PLAYER_BELONGED_COUNTER == 2 
	ARRAYSHIFT COUNTRY_PLAYER_BELONGED, 1, 0
	COUNTRY_PLAYER_BELONGED_COUNTER = 0
ENDIF

;野盗出現しない期間を1へらす
BANDIT_POP_SUSPENDED = MAX(BANDIT_POP_SUSPENDED - 1, 0)


FOR LOCAL:0, 0, CHARANUM
	;全員の体力・気力を最大まで回復
	BASE:(LOCAL:0):0 = MAXBASE:(LOCAL:0):0
	BASE:(LOCAL:0):1 = MAXBASE:(LOCAL:0):1

	SIF CFLAG:(LOCAL:0):脱走不可能 && !CFLAG:(LOCAL:0):捕虜先
		CFLAG:(LOCAL:0):脱走不可能 = 0
	
	;陰毛が成長
	CALL INMOU_GROW(LOCAL:0)
	
	;締まりの改善
	CALL TIGHTNESS_GROW(LOCAL:0)
	
	
	IF TALENT:LOCAL:蓬莱人 && CFLAG:LOCAL:特殊状態フラグ == 2
		CALL COLORPRINTFORM("リザレクション！", COLOR("注意"), "W")
		CALL COLORPRINTFORM(@"%NAME_FORMAL(LOCAL)%が蘇りました", COLOR("注意"), "W")
		CFLAG:LOCAL:特殊状態フラグ = 1
		CFLAG:LOCAL:所属 = 0
		CFLAG:LOCAL:捕虜先 = 0
	ENDIF
	
	;NTR周り
	IF TALENT:LOCAL:ＮＴＲ
		LOCAL:1 = ID_TO_CHARA(CFLAG:LOCAL:ＮＴＲキャラ)
		IF LOCAL:1 == -1
			CFLAG:LOCAL:ＮＴＲキャラ = 0
			CFLAG:LOCAL:ＮＴＲ値 = 0
			TALENT:LOCAL:ＮＴＲ = 0
		ELSE
			IF GET_COUNTRY_BOSS(CFLAG:(LOCAL):1) != LOCAL
				IF !CFLAG:(LOCAL:1):捕虜先 && CFLAG:(LOCAL:1):特殊状態フラグ == 2 && !CFLAG:LOCAL:特殊状態フラグ
					CALL COLORPRINTFORM(@"%ANAME(LOCAL:1)%が死亡したことを儚み、%ANAME(LOCAL)%も後を追いました", COLOR("警告"), "W")
					CFLAG:LOCAL:特殊状態フラグ = 2
					CFLAG:LOCAL:所属 = 0
				ELSEIF !CFLAG:(LOCAL:1):捕虜先 && CFLAG:(LOCAL:1):特殊状態フラグ == 1 && !CFLAG:LOCAL:特殊状態フラグ
					CALL COLORPRINTFORM(@"放浪している%ANAME(LOCAL:1)%を追い、%ANAME(LOCAL)%も放浪しました", COLOR("警告"), "W")
					CFLAG:LOCAL:特殊状態フラグ = 1
					CFLAG:LOCAL:所属 = 0
				ELSEIF !CFLAG:LOCAL:捕虜先 && CFLAG:(LOCAL:1):所属 != CFLAG:LOCAL:所属 && CFLAG:LOCAL:特殊状態フラグ != 2
					CALL COLORPRINTFORM(@"別勢力にいる%ANAME(LOCAL:1)%を追い、%ANAME(LOCAL)%が\@ GET_COUNTRY_BOSS(CFLAG:(LOCAL:1):1) != -1 ? %ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:1):1))%に # \@移籍しました", COLOR("警告"), "W")
					CFLAG:LOCAL:所属 = CFLAG:(LOCAL:1):所属
					CFLAG:LOCAL:特殊状態フラグ = 0
				ELSEIF CFLAG:(LOCAL:1):捕虜先 && IS_COUNTRY(CFLAG:LOCAL:所属)
					CALL COLORPRINTFORM(@"%ANAME(LOCAL:1)%が牢につながれたままであることに、%ANAME(LOCAL)%は不満を募らせています……", COLOR("警告"), "W")
					CFLAG:LOCAL:好感度 = 0
					CFLAG:LOCAL:従属度 = 0
					CFLAG:LOCAL:依存度 = 0
					CALL CHANGE_RELATION_O_TO_O(LOCAL, GET_COUNTRY_BOSS(CFLAG:LOCAL:所属), -30, 30)
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;このターン～系フラグを折る
	CFLAG:(LOCAL:0):91 = 0
	CFLAG:(LOCAL:0):93 = 0

	;捕虜でなく、所属が特殊勢力でないなら、調教カウンタを減らしていく
	IF !IS_SP_COUNTRY(CFLAG:LOCAL:捕虜先) && !IS_SP_COUNTRY(CFLAG:LOCAL:所属)
		FOR LOCAL:1, 0, VARSIZE("SP_TRAIN_COUNT")
			SP_TRAIN_COUNT:LOCAL:(LOCAL:1) = MAX(SP_TRAIN_COUNT:LOCAL:(LOCAL:1), 0)
		NEXT
	ENDIF

	;同じ勢力で共に過ごした期数を加算
	IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):1 != 0 && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:1
		CFLAG:(LOCAL:0):77 ++
	ENDIF

	;本の効果の残りターンを減らす
	CFLAG:(LOCAL:0):47 = MAX(CFLAG:(LOCAL:0):47 - 1, 0)
	IF CFLAG:(LOCAL:0):47 == 0
		CFLAG:(LOCAL:0):46 = 0
	ENDIF

	;薬物依存値を減らす 薬物中毒あると300以下にならない
	IF GETBIT(TALENT:(LOCAL:0):淫乱系, 素質_淫乱_薬物中毒)
		CFLAG:(LOCAL:0):薬物依存 = MAX(CFLAG:(LOCAL:0):薬物依存 * 9 / 10, 300)
	ELSE
		CFLAG:(LOCAL:0):薬物依存 = MAX(CFLAG:(LOCAL:0):薬物依存 - RAND:(MAX(CFLAG:(LOCAL:0):薬物依存 * 1 / 10, 1)), 0)
		
	ENDIF

	;特定勢力に所属していて捕虜でない
	IF CFLAG:(LOCAL:0):1 != 0 && CFLAG:(LOCAL:0):捕虜先 == 0
		FOR LOCAL:1, 0, CHARANUM
			;同一勢力に所属するキャラの間で好感度を上昇させ、敵対度を減少させる
			IF LOCAL:1 != LOCAL:0 && CFLAG:(LOCAL:1):1 == CFLAG:(LOCAL:0):1 && CFLAG:(LOCAL:1):捕虜先 == 0
				LOCAL:3 = 5
				IF TALENT:(LOCAL:1):謎の魅力
					LOCAL:3 = 10
				ENDIF
				;自然に上がる好感度は1000まで
				IF REL_LIKE:(LOCAL:0):(LOCAL:1) < 1000
					REL_LIKE:(LOCAL:0):(LOCAL:1) = MIN(REL_LIKE:(LOCAL:0):(LOCAL:1) + LOCAL:3, 1000)
				ENDIF
				REL_HATE:(LOCAL:0):(LOCAL:1) = MAX(REL_HATE:(LOCAL:0):(LOCAL:1) - LOCAL:3, 0)
			ENDIF
		NEXT
	ENDIF

	;捕虜の場合
	IF CFLAG:(LOCAL:0):捕虜先 != 0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:1
		;捕虜の機嫌変化
		CALL PRISONER_CHANGE_EMOTION(LOCAL:0)

	;被育児状態の子供の場合
	ELSEIF CFLAG:(LOCAL:0):行動不能状態 == 4
		;機嫌は変動しない

	;捕虜でなく、被育児状態の子供でもない場合
	ELSEIF CFLAG:(LOCAL:0):行動不能状態 != 4
		;現在の機嫌を減衰させる
		TIMES PALAM:(LOCAL:0):怒外, 0.60
		TIMES PALAM:(LOCAL:0):哀外, 0.60
		TIMES PALAM:(LOCAL:0):怖外, 0.60
		TIMES PALAM:(LOCAL:0):怒主, 0.05
		TIMES PALAM:(LOCAL:0):哀主, 0.05
		TIMES PALAM:(LOCAL:0):怖主, 0.05

		;●外的要因による機嫌にランダム値を加える
		;<抑圧>持ちは機嫌が変動しない
		IF !TALENT:(LOCAL:0):抑圧
			;怪我をしている場合
			IF CFLAG:(LOCAL:0):行動不能状態 == 3
				IF RAND:2 == 0
					PALAM:(LOCAL:0):怒外 += RAND:1001
				ELSE
					PALAM:(LOCAL:0):怒外 += RAND:301
				ENDIF
				IF RAND:2 == 0
					PALAM:(LOCAL:0):哀外 += RAND:1001
				ELSE
					PALAM:(LOCAL:0):哀外 += RAND:301
				ENDIF

			ELSE
				IF RAND:15 == 0
					PALAM:(LOCAL:0):怒外 += RAND:2001 - 1000
				ELSE
					PALAM:(LOCAL:0):怒外 += RAND:601 - 300
				ENDIF
				IF RAND:15 == 0
					PALAM:(LOCAL:0):哀外 += RAND:2001 - 1000
				ELSE
					PALAM:(LOCAL:0):哀外 += RAND:601 - 300
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;捕虜の収監によるRELATIONの変化
	SIF CFLAG:(LOCAL:0):捕虜先 != 0
		CALL PRISONER_CHANGE_RELATION(LOCAL:0)

	;精神力の回復
	BASE:(LOCAL:0):精神力 = MIN(BASE:(LOCAL:0):精神力 + MAXBASE:(LOCAL:0):精神力 / 3, MAXBASE:(LOCAL:0):精神力)

	;崩壊値の減少
	IF !TALENT:(LOCAL:0):崩壊
		CFLAG:(LOCAL:0):崩壊 -= 300 + (MAXBASE:(LOCAL:0):精神力 / 10)
	ENDIF
	CFLAG:(LOCAL:0):崩壊 = LIMIT(CFLAG:(LOCAL:0):崩壊, 0, 10000)

	;超成長力があればランダムに能力経験値獲得(主人公は除外)
	IF (GETBIT(TALENT:(LOCAL:0):ＳＬＧ素質, 素質_ＳＬＧ_超成長力) || GETBIT(TALENT:(LOCAL:0):ＳＬＧ素質, 素質_ＳＬＧ_努力家)) && LOCAL:0 != MASTER
		FOR LOCAL:1, 0, 4
			IF RAND:4 == 0
				LOCAL:2 = RAND:2 + 60
			ELSE
				LOCAL:2 = RAND:3 + 50
			ENDIF
			IF ABL:(LOCAL:0):(LOCAL:2) < 80
				EXP:(LOCAL:0):(LOCAL:2 + 20) ++
			ENDIF
		NEXT
		CALL TRAIN_AUTO_ABLUP(LOCAL:0, 1)
	ENDIF

	;恋慕などの素質変化の処理(一回目)
	CALL TALENT_CHECK(LOCAL:0)

	;子供の成長度を加算
	CFLAG:(LOCAL:0):28 = MIN(CFLAG:(LOCAL:0):28 + 1, 999999)

	;浮気禁止フラグの解除判定
	IF CFLAG:(LOCAL:0):63 && !(TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):服従)
		CFLAG:(LOCAL:0):63 = 0
	ENDIF

	;説得者の状況判定
	IF CFLAG:(LOCAL:0):74
		LOCAL:10 = ID_TO_CHARA(CFLAG:(LOCAL:0):74)
		CALL CHECK_SELECTABLE_SETTOKU(LOCAL:10)
		SIF !(CFLAG:(LOCAL:0):捕虜先 == CFLAG:(LOCAL:10):1 && RESULT)
			CFLAG:(LOCAL:0):74 = 0
	ENDIF


NEXT

;触手部屋の管理者に経験値を加算
IF ITEM:触手部屋 && FLAG:35 >= 0 && IS_SYOKUSYU_MANAGE(FLAG:35)
	EXP:(FLAG:35):触手経験値 += 2
	EXP:(FLAG:35):妖術経験値 += 2
	CALL TRAIN_AUTO_ABLUP(FLAG:35, 1)
ENDIF

;外交による調教要求フラグの解除判定
CALL CHECK_DIPLOMACY_TRAIN_FLAG

;朝のイベント
CALL EVENT_MORNING

;アイテム開発イベント
CALL ITEM_DEVELOPMENT

;妊娠出産関連の処理(触手)
CALL TURNEND_PREGNANT_CHECK_SYOKUSYU

;妊娠出産関連の処理(一般)
CALL TURNEND_PREGNANT_CHECK

;特殊な朝イベント
CALL EVENT_MORNING_SP

FOR LOCAL:0, 0, CHARANUM
	;恋慕などの素質変化の処理(二回目)
	CALL TALENT_CHECK(LOCAL:0)
NEXT

;子育てモードのコマンド実行回数をクリア
CVARSET CFLAG, 27, 0

;捕虜用変数のクリア
CVARSET CFLAG, 30, 0
CVARSET CFLAG, 31, 0
CVARSET CFLAG, 32, 0

;各子育てコマンドの選択回数をクリア
FOR LOCAL:0, 110, 130
	CVARSET CFLAG, LOCAL:0, 0
NEXT

;登用コマンドの実行済みフラグをクリア
FLAG:17 = 0

;-------------------------------------------------
;料理長による回復
;-------------------------------------------------
@COOK_HEAL(対象 = 0)
#DIM 対象
IF GET_COOK() != -1
	LOCAL = GET_COOK()
	IF LOCAL != 0 && !CFLAG:(LOCAL):捕虜先 && CFLAG:(LOCAL):1 == CFLAG:MASTER:1
		BASE:対象:体力 = MIN(BASE:対象:体力 + (ABL:LOCAL:料理 / 10 * 100 + RAND:100), MAXBASE:対象:体力)
		BASE:対象:気力 = MIN(BASE:対象:気力 + (ABL:LOCAL:料理 / 10 * 100 + RAND:100), MAXBASE:対象:気力)
		PRINTFORML %ANAME(LOCAL)%の料理で、%ANAME(対象)%の体力と気力が回復しました
	ELSE
		SETCOLOR COLOR("注意")
		PRINTFORML 料理長が国内にいない（捕虜になっている、国外に追放された）ため、その恩恵をうけられません
		PRINTFORMW !料理長を解除しました!
		RESETCOLOR
		CALL SET_COOK(-1)
	ENDIF
ENDIF

;-------------------------------------------------
;脱出イベント
;-------------------------------------------------
@GREAT_ESCAPE

SIF !(CFLAG:MASTER:捕虜先 && RAND:100 < 15)
	RETURN -1
DRAWLINE
PRINTFORMW 囚われていた%ANAME(MASTER)%だが、看守の隙を見つけた
PRINTFORMW 今ならば脱出できるかもしれない……
CALL ASK_MULTI(@"脱出を試みる", "危険な真似はよそう")
IF RESULT == 1
	PRINTFORMW 失敗したらどんな酷い目に合わされるかもわからない
	PRINTFORMW 今はまだ大人しくしていたほうが良さそうだ……
ELSE
	PRINTFORMW この機を逃す手はない！
	PRINTFORMW %ANAME(MASTER)%は脱出を試みた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORMW ・
		SELECTCASE RAND:5
		CASE IS <= 3
			IF !CFLAG:MASTER:1
				PRINTFORMW 脱出に成功した！
				PRINTFORMW %ANAME(MASTER)%は放浪する事にしました
			ELSE
				PRINTFORMW 脱出に成功した！
				PRINTFORMW %ANAME(MASTER)%は勢力に復帰しました
			ENDIF
			CFLAG:MASTER:捕虜先 = 0
		CASEELSE
			IF IS_FEMALE(MASTER)
				PRINTFORMW 脱出に失敗してしまった……
				PRINTFORMW 兵士達に見つかった%ANAME(MASTER)%は、仕置きと称して連れて行かれる
				PRINTFORML 
					SELECTCASE RAND:5
						CASE 0
							PRINTFORMW %ANAME(MASTER)%は兵舎の隅にある飼育場に連れてこられた
							PRINTFORML 手足を縛られ、素っ裸の状態で豚の群れの中に放り込まれると
							PRINTFORMW たちまち盛りのついた豚達に群がられ、いきなり濡れていない膣にペニスを挿入された
							PRINTFORML 痛みと嫌悪感で悲鳴を上げる%ANAME(MASTER)%だったが、盛りのついた畜生に気遣う心などあるはずもなく
							PRINTFORMW 目の前の雌を確実に孕ませようとする豚の激しいピストンで、膣を蹂躙されていく
							PRINTFORML 必死で逃れようとする%ANAME(MASTER)%の努力も空しく、がっちりと奥深くまで挿入されており、それもかなわない
							PRINTFORMW やがて豚が一際大きな鳴き声を上げると、ぶびゅるぶびゅるとねっとりとした精液が%ANAME(MASTER)%の中へと放たれた
							PRINTFORMW そのおぞましい感触に%ANAME(MASTER)%は涙ながらに許しを請うが、兵士達はその姿を見てゲラゲラと笑うばかりだった
							PRINTFORMW その後も%ANAME(MASTER)%は数えきれないほどの豚に種付けされ、身体の中も外もドロドロにされてしまった
						CASE 1
							PRINTFORMW %ANAME(MASTER)%は男囚用の牢獄に連れてこられた
							PRINTFORML  牢の中には大量の囚人がいる。彼らは%ANAME(MASTER)%を見ると　女だ！犯させろ！　と狂気に近い叫びをあげる
							PRINTFORMW 何をされるか悟った%ANAME(MASTER)%は顔をひきつらせ許しを請うが、兵士達は無視して牢に%ANAME(MASTER)%を放り込んだ
							PRINTFORML 牢獄の中は薄暗く、怯える%ANAME(MASTER)%は、周囲の暗がりから注がれる無数の飢えた視線を感じた
							PRINTFORMW 危機を感じ逃れようとする%ANAME(MASTER)%に、無数の手が伸びてきて床に押さえつけると、その服を乱暴に剥ぎ取っていく
							PRINTFORML 絶叫を上げて抵抗する%ANAME(MASTER)%だが、屈強な男たちに囲まれてはなすすべもなく、全身を好き放題に弄られる
							PRINTFORMW やがて一際体躯の良い男が押さえつけられた%ANAME(MASTER)%の前に現れ、その凶悪なペニスを露わにした
							PRINTFORMW これ以上は止める様に震える声で説得する%ANAME(MASTER)%だったが、それがむしろ彼らの加虐心に火をつけてしまった
							PRINTFORMW 男達は泣き喚く%ANAME(MASTER)%を容赦なく犯しつくし、兵士に回収される頃には%ANAME(MASTER)%はボロボロにされてしまっていた
						CASE 2
							PRINTFORMW %ANAME(MASTER)%は牢屋ではなく拷問部屋に連れてこられた
							PRINTFORMW %ANAME(MASTER)%が拷問器具の数々に釘付けになっていると、隣の部屋から耳を覆いたくなる様な悲鳴が聞こえてきた
							PRINTFORML 使ってみたかった道具があるんだと兵士達は笑い、いかにも女性用といった器具を%ANAME(MASTER)%の前に並べる
							PRINTFORMW 一つ一つ丁寧に使い方を説明され、何をされるのか想像してしまった%ANAME(MASTER)%はガクガクと身体を震わせる
							PRINTFORML その姿に兵士達はニヤニヤと笑い、俺たちに奉仕するならやめてやってもいいぞと優しく囁いた
							PRINTFORMW 再び隣の部屋から聞こえてきた悲鳴に、%ANAME(MASTER)%の抵抗心は折れてしまい、奉仕させてくださいとすがりついた
							PRINTFORML ニヤケる兵士達がペニスを露出し%ANAME(MASTER)%に押し付けると、%ANAME(MASTER)%はひざまづいて口と手を使いそれを扱き出した
							PRINTFORMW %ANAME(MASTER)%は必死で兵士達に奉仕し、彼らが射精すると、両手と口で受け止め下品な音を立てながら飲み干していく
							PRINTFORMW 精液でドロドロになった%ANAME(MASTER)%だが、それで彼らが収まるはずもなく、その後は雌穴と尻穴も使い兵士達に奉仕し続けた
						CASE 3
							PRINTFORMW %ANAME(MASTER)%は裸に剥かれると、首輪とリードを付けた格好で連れ回された
							PRINTFORMW  他の兵士に出会うたびに好奇と欲情の混じった視線を投げられるが、身体を隠す事も許されず羞恥で顔を真っ赤にする
							PRINTFORML 更に、首輪をつけたのだからそれらしく四つん這いで歩け　と命令される
							PRINTFORML 睨みつける%ANAME(MASTER)%に対し、兵士達は剣を突き付け、脱走の罪で処刑されたくなければ大人しく従うように脅す
							PRINTFORMW ここは大人しく従うしかないと自分に言い聞かせ、%ANAME(MASTER)%は震える身体で四つん這いになる
							PRINTFORML その恰好のまま、犬の散歩の様に兵舎内を練り歩かされ、屈辱と羞恥で%ANAME(MASTER)%の頭は沸騰しそうになる
							PRINTFORMW 気付かぬうちに雌穴から愛液が垂れだしており　そのことを兵に指摘されると、ブルブルと身体を震わせ弁明の言葉をぼそぼそと呟く
							PRINTFORML しかし兵士達は%ANAME(MASTER)%の言葉を無視して、お待ちかねの物だぞと、%ANAME(MASTER)%の雌穴にペニスをぶち込んだ
							PRINTFORMW 想像以上に昂ぶっていた%ANAME(MASTER)%の身体はビクンと跳ね上がり、あられもない喘ぎ声をあげてしまった
							PRINTFORMW その後も繰り返し、まるで動物の交尾の様な濃厚な種付けをされてしまった
						CASE 4
							PRINTFORMW %ANAME(MASTER)%は見せしめとして、晒し台に拘束された
							PRINTFORML 必死で抜け出そうともがくが、頭を両手をがっちりと拘束されており抜け出せそうもない
							PRINTFORMW その姿に兵士達は、まるで自ら尻を振って誘っているようだと、嘲りの声を上げる
							PRINTFORMW やがて一人の兵士が%ANAME(MASTER)%の腰を掴み、汗で濡れだしている秘所にペニスを擦り付けてくる
							PRINTFORML 必死でやめる様に説得する%ANAME(MASTER)%に、兵士はもうこんなことはしないと誓うなら許してやると告げる
							PRINTFORMW %ANAME(MASTER)%が、ここで犯されるよりはと誓いの言葉を言おうとした瞬間、ずん！と強烈な衝撃が下半身に走った
							PRINTFORML 電撃が走ったような激しい衝撃に%ANAME(MASTER)%が悲鳴を上げる。目がチカチカして一瞬何が起こったのかわからなかったが
							PRINTFORMW 膣から伝わりだした熱ですぐに挿入されたのだとわかった。兵士は%ANAME(MASTER)%との約束など守るつもりはなかったのだ
							PRINTFORMW 屈辱で涙目になる%ANAME(MASTER)%を嘲笑いながら、兵士達は散々%ANAME(MASTER)%を犯しつくした
					ENDSELECT
				PRINTFORML 
				PRINTFORMW %ANAME(MASTER)%は散々凌辱された後、牢屋に放り込まれた
				CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("誰とも知らぬ兵士"), @"誰とも知らぬ兵士の\@ RAND:2 ? ペニス # 唇\@", "誰とも知らぬ兵士たち")
				CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("誰とも知らぬ兵士"), @"誰とも知らぬ兵士の\@ RAND:2 ? ペニス # 唇\@", "誰とも知らぬ兵士たち")
			ELSE
				PRINTFORMW 脱出に失敗してしまった……
				PRINTFORMW 見つかった%ANAME(MASTER)%はキツイ拷問を受けた後、牢屋に戻された
			ENDIF
		ENDSELECT
	ENDIF
DRAWLINE
RETURN 0