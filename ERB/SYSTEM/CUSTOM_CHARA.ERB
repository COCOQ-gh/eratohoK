;---------------------------------------
;カスタムキャラ用メニュー
;---------------------------------------
@CUSTOM_CHARA_MENU
#DIM 人数
#DIM 対象
#DIM FIRST_LINE
LOADGLOBAL
;ランダムキャラの名前候補リストを作成
CALL CREATE_NAME_LIST
;ランク閾値の生成
CALL INITIALIZE_RANK

$END_INIT
FIRST_LINE = LINECOUNT

DRAWLINE
PRINTFORML カスタムキャラ管理画面
DRAWLINE

人数 = 0
FOR LOCAL, 0, MAX_CUSTOM_CHARA
	IF CUSTOM_EXISTS(LOCAL)
		PRINTBUTTON @"[%CUSTOM_NAME:LOCAL:0%]", LOCAL
	ELSE
		PRINTBUTTON "[新規作成]", LOCAL
	ENDIF
	PRINTL
NEXT
DRAWLINE
PRINTBUTTON "[終了]", 99

INPUT

IF RESULT == 99
	DELALLCHARA
	RETURN
ELSEIF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA)
	IF CUSTOM_EXISTS(RESULT)
		CALL MODIFY_CUSTOM_DATA(RESULT)
	ELSE
		CALL CREATE_CUSTOM(RESULT)
	ENDIF
ENDIF
CLEARLINE FIRST_LINE - LINECOUNT
GOTO END_INIT

;---------------------------------------
;カスタムキャラの変更・削除
;---------------------------------------
@MODIFY_CUSTOM_DATA(番号)
#DIM 番号
#DIM 対象

ADDVOIDCHARA
対象 = CHARANUM - 1
CALL APPLY_CUSTOM_DATA(対象, 番号)

DRAWLINE
CALL SHOW_INFO(対象)
DRAWLINE

PRINTBUTTON "[修正]", 0
PRINT    
PRINTBUTTON "[削除]", 1
PRINTL 
PRINTBUTTON "[戻る]", 9
PRINTL
$INPUT_LOOP 
INPUT
SELECTCASE RESULT
	CASE 0
		CALL CREATE_CUSTOM(番号, 1)
		RETURN
	CASE 1
		PRINTFORML マジで？
		CALL ASK_YN()
		IF RESULT == 0
			CALL DELETE_CUSTOM(番号)
			SAVEGLOBAL
		ENDIF
		RETURN
	CASE 9
		DELALLCHARA
		RETURN
ENDSELECT
GOTO INPUT_LOOP

;---------------------------------------
;カスタムキャラの存在判定
;---------------------------------------
@CUSTOM_EXISTS(ARG:0)
#FUNCTION

RETURNF CUSTOM_MAXBASE:ARG:0 > 0

;---------------------------------------
;カスタムキャラの出現設定
;---------------------------------------
@CUSTOM_SETTING
#DIM 勢力, MAX_CUSTOM_CHARA
#DIM FIRST_LINE
#DIM 対象
VARSET 勢力, -1

FIRST_LINE = LINECOUNT

$SHOW_LOOP
DRAWLINE
PRINTFORML カスタムキャラの設定
DRAWLINE

FOR LOCAL, 0, MAX_CUSTOM_CHARA
	SIF !CUSTOM_EXISTS(LOCAL)
		CONTINUE
	PRINTBUTTON @"[%CUSTOM_NAME:LOCAL:0, 20, LEFT%]", LOCAL
	PRINT  - 
	IF 勢力:LOCAL == -1
		PRINTFORML 出現しない
	ELSEIF 勢力:LOCAL == 0
		PRINTFORML 放浪
	ELSE
		PRINTFORML %ANAME(GET_COUNTRY_BOSS(勢力:LOCAL))%
	ENDIF
NEXT
PRINTBUTTON "[終了]", 99

$INPUT_LOOP

INPUT

IF RESULT == 99
	FOR LOCAL, 0, MAX_CUSTOM_CHARA
		SIF 勢力:LOCAL == -1 || !CUSTOM_EXISTS(LOCAL)
			CONTINUE
		ADDVOIDCHARA
		対象 = CHARANUM - 1
		;口上が設定されてしまうのでNEWCHARA_INITはNO設定の前に行う
		CALL NEWCHARA_INIT(対象)
		CALL APPLY_CUSTOM_DATA(対象, LOCAL)
		;NOの設定
		NO:対象 = FLAG:汎用武将カウント + MIN_NO_RANDOM_CHARA
		FLAG:汎用武将カウント ++
		CALL CHANGE_COUNTRY(対象, 勢力:LOCAL, 1)
	NEXT
	RETURN
ELSEIF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA) && CUSTOM_EXISTS(RESULT)
	対象 = RESULT
	CALL CUSTOM_SETTING_SET_COUNTRY()
	勢力:対象 = RESULT
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;---------------------------------------
;カスタムキャラの出現設定内で使う関数
;---------------------------------------
@CUSTOM_SETTING_SET_COUNTRY
#DIM 勢力
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL)
		CONTINUE
	PRINTBUTTON @"[%ANAME(GET_COUNTRY_BOSS(LOCAL))%]", LOCAL
	PRINTL
NEXT
PRINTBUTTON "[放浪]", 0
PRINTL
PRINTBUTTON "[出現しない]", -1

$INPUT_LOOP
INPUT

勢力 = RESULT

SIF GROUPMATCH(勢力, 0, -1) || IS_COUNTRY(勢力)
	RETURN 勢力

GOTO INPUT_LOOP
