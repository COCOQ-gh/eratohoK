;野盗蜂起イベント
@BANDIT_RISE
#DIM 野盗
#DIM 勢力, MAX_COUNTRY
#DIM 判定値, MAX_COUNTRY
#DIM 都市, MAX_CITY
#DIM 結果
#DIM 候補数
野盗 = GET_COUNTRY_FROM_ID(COUNTRY_BANDIT)

;野盗勢力が存在しない、野盗勢力が都市を持っている、まだ侵攻ができない、確率判定に失敗した場合は発生しない
SIF 野盗 == -1
	RETURN -1

SIF GET_OWN_CITY(野盗) > 0
	RETURN -1

SIF DAY < SLG_PP:1
	RETURN -1
	
IF RAND:1000 >= DVAR:野盗蜂起カウンタ * 5
	DVAR:野盗蜂起カウンタ ++
	RETURN -1
ENDIF

VARSET 勢力
VARSET 判定値
VARSET 候補数

FOR LOCAL, 0, MAX_COUNTRY
	IF GET_OWN_CITY(LOCAL) > 1 || (LOCAL == 0 && GET_OWN_CITY(LOCAL) > 0)
		勢力:候補数 = LOCAL
		判定値:候補数 = RAND:GET_OWN_CITY(LOCAL)
		候補数 ++
	ENDIF
NEXT

SIF 候補数 == 0
	RETURN -1

結果 = 勢力:FINDELEMENT(判定値, MAXARRAY(判定値, 0, 候補数), 0, 候補数)

VARSET 候補数

FOR LOCAL, 1, CITY_NUM + 1
	IF CITY_OWNER:LOCAL == 結果
		都市:候補数 = LOCAL
		候補数 ++
	ENDIF
NEXT

SIF 候補数 == 0
	RETURN -1

CALL FISHER_YATES_SHAFFLE(候補数)

結果 = 都市:(SHAFFLE_ARRAY:0)

DRAWLINE
PRINTFORML
CALL COLORPRINTFORM(@"野盗どもが%CITY_NAME:結果%を奪い、蜂起しました！", COLOR("警告"), "W")

LOCAL:1 = CITY_OWNER:結果
IF IS_COUNTRY(LOCAL:1)
	FOR LOCAL, 0, CHARANUM
		IF CFLAG:LOCAL:所属 == LOCAL:1 && !CFLAG:LOCAL:捕虜先 && GET_COUNTRY_BOSS(LOCAL:1) != LOCAL && !RAND:5
			CALL COLORPRINTFORM(@"野盗は%ANAME(LOCAL)%を性奴隷として拉致しました", COLOR("警告"), "W")
			CFLAG:LOCAL:捕虜先 = 野盗
		ENDIF
	NEXT
ENDIF

CITY_OWNER:結果 = 野盗
COUNTRY_SOLDIER:野盗 += MIN(DAY * 100, 10000)

DVAR:野盗蜂起カウンタ = 0