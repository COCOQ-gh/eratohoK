@SET_BODYSIZE(対象, セット位置 = 0)
#DIM 対象
#DIM セット位置

SIF GETBIT(セット位置, サイズ_身長) || セット位置 == 0
    CALL SET_BODYSIZE_HEIGHT(対象)

SIF GETBIT(セット位置, サイズ_バスト) || セット位置 == 0
    CALL SET_BODYSIZE_BUST(対象)

SIF GETBIT(セット位置, サイズ_ウェスト) || セット位置 == 0
    CALL SET_BODYSIZE_WEIST(対象)

SIF GETBIT(セット位置, サイズ_ヒップ) || セット位置 == 0
    CALL SET_BODYSIZE_HIP(対象)

SIF GETBIT(セット位置, サイズ_体重) || セット位置 == 0
    CALL SET_BODYSIZE_WEIGHT(対象)

SIF GETBIT(セット位置, サイズ_ペニス) || セット位置 == 0
    CALL SET_BODYSIZE_PENIS(対象)


;-------------------------
;身長の設定
;-------------------------
@SET_BODYSIZE_HEIGHT(対象)
#DIM 対象
#DIM 身長基礎値

IF CFLAG:対象:行動不能状態 == 行動不能_子供
	身長基礎値 = MIN(500 + CFLAG:対象:子の成長度 * 20, 1300)
ELSEIF TALENT:対象:幼児
	身長基礎値 = MIN(800 + (CFLAG:対象:子の成長度 - 6) * 50, 1300)
ELSE
    身長基礎値 = 1 + RAND:70 + RAND:70 + RAND:70 + RAND:70 + RAND:70 + RAND:70 + RAND:50 + GET_BUSTSIZE(対象) *20
    身長基礎値 += IS_MALE(対象) ? 1500 # 1300
    SELECTCASE TALENT:対象:体格
        CASE 体格_子供
            身長基礎値 -= 300
        CASE 体格_小柄
            身長基礎値 -= 170
        CASE 体格_長身
            身長基礎値 += 100
        CASE 体格_大柄
            身長基礎値 += 175
        CASE 体格_巨大
            身長基礎値 += 300
    ENDSELECT
ENDIF

BASE:対象:身長 = 身長基礎値
MAXBASE:対象:身長 = 身長基礎値
;-------------------------
;ウェストの基礎値算出
;-------------------------
@GET_WEISTSIZE_BASE(対象)
#FUNCTION
#DIM 対象
RETURNF (BASE:対象:身長 * (37 + TALENT:対象:バストサイズ)) / 101


;-------------------------
;バストサイズの設定
;-------------------------
@SET_BODYSIZE_BUST(対象)
#DIM 対象
#DIM ウェスト基礎値
#DIM アンダーバスト基礎値
#DIM 乳サイズ基礎値

ウェスト基礎値 = GET_WEISTSIZE_BASE(対象)
アンダーバスト基礎値 = GET_UNDERBUST_BASE(対象, ウェスト基礎値)
乳サイズ基礎値 = GET_BUSTSIZE_BASE(対象, アンダーバスト基礎値)

;バスト決定
BASE:対象:バスト = アンダーバスト基礎値 + 乳サイズ基礎値
MAXBASE:対象:バスト = BASE:対象:バスト

;-------------------------
;アンダーバストの算出
;-------------------------
@GET_UNDERBUST_BASE(対象, ウェスト)
#FUNCTION
#DIM 対象
#DIM ウェスト
#DIM アンダーバスト
;アンダーバスト算出
SELECTCASE TALENT:対象:体格
    CASE 体格_子供
    	アンダーバスト = ウェスト + BASE:対象:身長 / 27
    CASE 体格_小柄
    	アンダーバスト = ウェスト + BASE:対象:身長 / 24
    CASE 体格_普通
    	アンダーバスト = ウェスト + BASE:対象:身長 / 21
    CASE 体格_長身
        アンダーバスト = ウェスト + BASE:対象:身長 / 20
    CASE 体格_大柄
    	アンダーバスト = ウェスト + BASE:対象:身長 / 18
    CASE 体格_巨大
    	アンダーバスト = ウェスト + BASE:対象:身長 / 16
ENDSELECT

SELECTCASE TALENT:対象:バストサイズ
    CASE -2
    	アンダーバスト += ウェスト / 10
    CASE -1
    	アンダーバスト += ウェスト / 8
    CASE 0
    	アンダーバスト += ウェスト / 7
    CASE 1
    	アンダーバスト += ウェスト / 5
    CASE 2
    	アンダーバスト += ウェスト / 4
ENDSELECT

SELECTCASE TALENT:対象:体型
    CASE 体型_グラマー
        アンダーバスト += ウェスト / 12
    CASE 体型_豊満
        アンダーバスト += ウェスト / 10
    CASE 体型_肥満
        アンダーバスト += ウェスト / 8
ENDSELECT

RETURNF アンダーバスト

;-------------------------
;乳そのもののサイズの算出
;-------------------------
@GET_BUSTSIZE_BASE(対象, アンダーバスト)
#FUNCTION
#DIM 対象
#DIM アンダーバスト
#DIM 乳サイズ
;バスト補正
SELECTCASE GET_BUSTSIZE(対象)
	CASE -2
		乳サイズ = アンダーバスト / 20
	CASE -1
		乳サイズ = アンダーバスト / 18
	CASE 0
		乳サイズ = アンダーバスト / 15
	CASE 1
		乳サイズ = アンダーバスト / 13
	CASE 2
		乳サイズ = アンダーバスト / 11
ENDSELECT

SELECTCASE TALENT:対象:体格
    CASE 体格_子供, 体格_小柄
        TIMES 乳サイズ, 0.9
    CASE 体格_長身
        TIMES 乳サイズ, 1.05
    CASE 体格_大柄
        TIMES 乳サイズ, 1.1
    CASE 体格_巨大
        TIMES 乳サイズ, 1.3
ENDSELECT

SELECTCASE TALENT:対象:体型
    CASE 体型_痩せすぎ
        乳サイズ -= 乳サイズ / 10
    CASE 体型_細身
        乳サイズ -= 乳サイズ / 15
    CASE 体型_スレンダー
        乳サイズ -= 乳サイズ / 20
    CASE 体型_グラマー
        乳サイズ += 乳サイズ / 15
    CASE 体型_豊満
        乳サイズ += 乳サイズ / 10
    CASE 体型_肥満
        乳サイズ += 乳サイズ / 5
ENDSELECT

RETURNF 乳サイズ

;-------------------------
;ウェストサイズの設定
;-------------------------
@SET_BODYSIZE_WEIST(対象)
#DIM 対象
#DIM ウェスト基礎値
#DIM アンダーバスト
#DIM 乳サイズ

乳サイズ = GET_BUSTSIZE_BASE(対象, GET_UNDERBUST_BASE(対象, GET_WEISTSIZE_BASE(対象)))

IF TALENT:対象:バストサイズ >= 0
    乳サイズ = 乳サイズ / 11
ELSE
    乳サイズ = 乳サイズ / 12
ENDIF

ウェスト基礎値 = GET_WEISTSIZE_BASE(対象) + 乳サイズ

SELECTCASE TALENT:対象:体格
    CASE 体格_子供
        ウェスト基礎値 += ウェスト基礎値 / 6
    CASE 体格_小柄
        ウェスト基礎値 += ウェスト基礎値 / 12
ENDSELECT

SELECTCASE TALENT:対象:体型
    CASE 体型_痩せすぎ
        ウェスト基礎値 -= ウェスト基礎値 / 10
    CASE 体型_細身
        ウェスト基礎値 -= ウェスト基礎値 / 15
    CASE 体型_スレンダー
        ウェスト基礎値 -= ウェスト基礎値 / 20
    CASE 体型_豊満
        ウェスト基礎値 += ウェスト基礎値 / 15
    CASE 体型_肥満
        ウェスト基礎値 += ウェスト基礎値 / 10
ENDSELECT

BASE:対象:ウェスト = ウェスト基礎値
MAXBASE:対象:ウェスト = BASE:対象:ウェスト

;-------------------------
;ヒップサイズの設定
;-------------------------
@SET_BODYSIZE_HIP(対象)
#DIM 対象
#DIM ヒップ基礎値
#DIM 乳サイズ基礎値

ヒップ基礎値 = BASE:対象:身長 * (53 + TALENT:対象:バストサイズ) / 100

SELECTCASE TALENT:対象:体格
    CASE 体格_子供, 体格_小柄
        TIMES ヒップ基礎値, 1.05
    CASE 体格_巨大
        TIMES ヒップ基礎値, 1.1
ENDSELECT

SELECTCASE GET_HIPSIZE(対象)
    CASE -2
        TIMES ヒップ基礎値, 0.85
    CASE -1
        TIMES ヒップ基礎値, 0.94
    CASE 0
    CASE 1
        TIMES ヒップ基礎値, 1.06
    CASE 2
        TIMES ヒップ基礎値, 1.15
ENDSELECT

SELECTCASE TALENT:対象:体型
    CASE 体型_痩せすぎ
        TIMES ヒップ基礎値, 0.93
    CASE 体型_細身, 体型_スレンダー
        TIMES ヒップ基礎値, 0.97
    CASE 体型_グラマー
        TIMES ヒップ基礎値, 1.03
    CASE 体型_豊満
        TIMES ヒップ基礎値, 1.07
    CASE 体型_肥満
        TIMES ヒップ基礎値, 1.13
ENDSELECT

SIF TALENT:対象:美尻
    TIMES ヒップ基礎値, 1.05

乳サイズ基礎値 = GET_BUSTSIZE_BASE(対象, GET_UNDERBUST_BASE(対象, GET_WEISTSIZE_BASE(対象)))
SELECTCASE GET_BUSTSIZE(対象)
	CASE -2, -1
		乳サイズ基礎値 /= 7
	CASE 0
		乳サイズ基礎値 /= 6
	CASE 1, 2
		乳サイズ基礎値 /= 5
ENDSELECT

BASE:対象:ヒップ = ヒップ基礎値 + 乳サイズ基礎値
MAXBASE:対象:ヒップ = BASE:対象:ヒップ

;-------------------------
;体重の設定
;-------------------------
@SET_BODYSIZE_WEIGHT(対象)
#DIM 対象
#DIM 体重基礎値
#DIM アンダーバスト
#DIM アンダーバスト重量
#DIM 乳
#DIM 乳重量
IF IS_MALE(対象) && CFLAG:対象:行動不能状態 != 行動不能_子供
	体重基礎値 = BASE:対象:身長 * BASE:対象:身長 * 19 /101500
ELSE
	体重基礎値 = BASE:対象:身長 * BASE:対象:身長 * 21 /100000
ENDIF

アンダーバスト =  GET_UNDERBUST_BASE(対象, GET_WEISTSIZE_BASE(対象))
アンダーバスト重量 = 4 * POWER(アンダーバスト / 100, 3) / 11
乳 = GET_BUSTSIZE_BASE(対象, アンダーバスト)
乳重量 =  4 * POWER(乳 / 100, 3) / 11

体重基礎値 += アンダーバスト重量 + 乳重量

SELECTCASE TALENT:対象:体格
    CASE 体格_子供
        TIMES 体重基礎値, 0.85
    CASE 体格_小柄
        TIMES 体重基礎値, 0.95
    CASE 体格_長身
        TIMES 体重基礎値, 1.05
    CASE 体格_大柄
        TIMES 体重基礎値, 1.1
    CASE 体格_巨大
        TIMES 体重基礎値, 1.5

ENDSELECT

SELECTCASE TALENT:対象:体型
    CASE 体型_痩せすぎ
        TIMES 体重基礎値, 0.85
    CASE 体型_細身
        TIMES 体重基礎値, 0.90
    CASE 体型_スレンダー
        TIMES 体重基礎値, 0.95
    CASE 体型_豊満
        TIMES 体重基礎値, 1.05
    CASE 体型_肥満
        TIMES 体重基礎値, 1.20
ENDSELECT

BASE:対象:体重 = 体重基礎値
MAXBASE:対象:体重 = BASE:対象:体重

;-------------------------
;チンポ
;-------------------------
@SET_BODYSIZE_PENIS(対象)
#DIM 対象
#DIM サイズ基礎値
SELECTCASE GET_PENISSIZE(対象)
    CASE -2
        サイズ基礎値 = RAND:3 + 2
    CASE -1
        サイズ基礎値 = RAND:4 + 4
    CASE 0
        サイズ基礎値 = RAND:4 + 8
    CASE 1
        サイズ基礎値 = RAND:4 + 13
    CASE 2
        サイズ基礎値 = RAND:7 + 17
ENDSELECT

サイズ基礎値 = MIN(BASE:対象:身長 / 12 * サイズ基礎値 / 10, 300)

BASE:対象:ペニス = サイズ基礎値
MAXBASE:対象:ペニス = BASE:対象:ペニス

;-------------------------
;子供の重量の取得（妊娠時の体重に加算）
;-------------------------
@GET_BABY_WEIGHT(対象)
#FUNCTION
#DIM 対象
RETURNF TALENT:対象:妊娠 ? BASE:対象:体重 / ( BASE:対象:体重 <= 200 ? 24 # 40 ) * CFLAG:対象:妊娠期間 / 7 # 0

;-------------------------
;子供のサイズの取得（妊娠時のウェストに加算）
;-------------------------
@GET_BABY_SIZE(対象)
#FUNCTION
#DIM 対象
RETURNF TALENT:対象:妊娠 ? BASE:対象:ウェスト / 20 * CFLAG:対象:妊娠期間 / 7 # 0

;-------------------------
;スリーサイズその他の表示
;-------------------------
@SHOW_BODYSIZE(対象)
#DIM 対象
#DIM 子供重量
#DIM 子供サイズ

子供重量 = GET_BABY_WEIGHT(対象)
子供サイズ = GET_BABY_SIZE(対象)

IF HAS_TAG(対象, タグ_小人) || GROUPMATCH(対象, NAME_TO_CHARA("上海人形"), NAME_TO_CHARA("蓬莱人形"), NAME_TO_CHARA("大江戸人形"), NAME_TO_CHARA("針妙丸"))
	PRINTFORM 身長:%@"{BASE:対象:身長 / 100}.{BASE:対象:身長 % 10}", 6, RIGHT%cm/ 
	PRINTFORM 体重:{BASE:対象:体重 / 6 + 子供重量, 5, RIGHT}g
	SIF TALENT:対象:妊娠
		PRINTFORM (妊娠前{BASE:対象:体重 / 5}g)
    PRINTFORM /
	PRINTFORM  B:%@"{BASE:対象:バスト / 100}.{BASE:対象:バスト % 10}", 6, RIGHT%cm 
	CALL PRINT_CUPSIZE(対象)
	PRINTFORM / W:%@"{BASE:対象:ウェスト / 100 + 子供サイズ}.{BASE:対象:ウェスト}", 6, RIGHT%cm
    SIF TALENT:対象:妊娠
        PRINTFORM (妊娠前%@"{BASE:対象:ウェスト / 100}.{BASE:対象:ウェスト % 10}", 6, RIGHT%cm)
	PRINTFORM / H:%@"{BASE:対象:ヒップ / 100}.{BASE:対象:ヒップ % 10}", 6, RIGHT%cm 
	CALL PRINT_HIPSIZE(対象)
    SIF HAS_PENIS(対象)
        PRINTFORM / P:%@"{BASE:対象:ペニス / 100}.{BASE:対象:ペニス % 10}", 6, RIGHT%CM
ELSE
	PRINTFORM 身長:%@"{BASE:対象:身長 / 10}.{BASE:対象:身長 % 10}", 6, RIGHT%cm/ 
	PRINTFORM 体重:%@"{BASE:対象:体重 / 10 + 子供重量}.{BASE:対象:体重 % 10}", 6, RIGHT%kg
	SIF TALENT:対象:妊娠
		PRINTFORM (妊娠前{BASE:対象:体重 / 10}.{BASE:対象:体重 % 10}kg)
    PRINTFORM /
	PRINTFORM  B:%@"{BASE:対象:バスト / 10}.{BASE:対象:バスト % 10}", 6, RIGHT%cm 
	CALL PRINT_CUPSIZE(対象)
	PRINTFORM / W:%@"{BASE:対象:ウェスト / 10 + 子供サイズ}.{BASE:対象:ウェスト % 10}" , 6, RIGHT%cm
    SIF TALENT:対象:妊娠
        PRINTFORM (妊娠前%@"{BASE:対象:ウェスト / 10}.{BASE:対象:ウェスト % 10}", 6, RIGHT%cm)
	PRINTFORM / H:%@"{BASE:対象:ヒップ / 10}.{BASE:対象:ヒップ % 10}", 6, RIGHT%cm 
	CALL PRINT_HIPSIZE(対象)
    SIF HAS_PENIS(対象)
        PRINTFORM / P:%@"{BASE:対象:ペニス / 10}.{BASE:対象:ペニス % 10}", 6, RIGHT%cm 
ENDIF

PRINTL

;バストのカップサイズ生成及び表示処理
@PRINT_CUPSIZE(対象)
#DIM 対象
VARSET LOCALS,""
SPLIT "(-),(AAA),(AA),(A),(B),(C),(D),(E),(F),(G),(H),(I),(J),(K),(L),(M),(N),(O),(P),(Q),(R),(S),(T),(U),(V),(W),(X),(Y),(Z),(Z+) ",",",LOCALS
IF TALENT:対象:体格 == 体格_子供
	LOCAL = (BASE:対象:バスト - (BASE:対象:ウェスト + BASE:対象:身長 / 27)) / 25
ELSEIF TALENT:対象:体格 == 体格_小柄
	LOCAL = (BASE:対象:バスト - (BASE:対象:ウェスト + BASE:対象:身長 / 24)) / 25
ELSEIF HAS_TAG(対象, タグ_小人) || GROUPMATCH(対象, NAME_TO_CHARA("上海人形"), NAME_TO_CHARA("蓬莱人形"), NAME_TO_CHARA("大江戸人形"), NAME_TO_CHARA("針妙丸"))
	LOCAL = (BASE:対象:バスト - (BASE:対象:ウェスト + BASE:対象:身長 / 21)) / 25
ELSE
	LOCAL = (BASE:対象:バスト - (BASE:対象:ウェスト + BASE:対象:身長 / 22)) / 25
ENDIF

;文字列変数が若干不自由なので無理やり作成
LOCAL = MAX(LOCAL , 1)
LOCALS:0 = %LOCALS:(LOCAL)%
;[オトコ]でないプレイヤーは非表示ではなく不明
IF !HAS_VAGINA(対象)
	PRINTFORM (--)
ELSE
	PRINTFORM %LOCALS:0%
ENDIF

@PRINT_HIPSIZE(対象)
#DIM 対象
SELECTCASE BASE:対象:ヒップ / 10
    CASE IS <= 71
        LOCALS = XS
    CASE 72 TO 76
        LOCALS = 3S
    CASE 77 TO 81
        LOCALS = SS
    CASE 82 TO 86
        LOCALS = S 
    CASE 87 TO 91
        LOCALS = M 
    CASE 92 TO 96
        LOCALS = L 
    CASE 97 TO 101
        LOCALS = LL
    CASE 102 TO 106
        LOCALS = 3L
    CASE 107 TO 111
        LOCALS = 4L
    CASE 112 TO 116
        LOCALS = 5L
    CASEELSE
        LOCALS = XL
ENDSELECT
SIF IS_MALE(対象)
    LOCALS = --
PRINTFORM (%LOCALS:0%) 
