;-------------------------------------------------
;
;eratohoK用ゲーム開始時のオプション設定
;
;					開発開始2015/08/23
;					最終更新2015/09/10
;-------------------------------------------------
@NEWGAME_SETTING
;
;現在　勢力決定後の再設定を無視するため
;ランダムキャラ登場判定にFLAG:4を使用しています(@SELECT_RCHARA参照)
;登場判定に使われているFLAG:44の方が良いかもしれません
;
#DIM 放浪FLG
#DIM ランダムFLG
#DIM 反乱FLG
放浪FLG = 1
反乱FLG = -1
ランダムFLG = -1
FOR LOCAL:0, 1, CHARANUM
	SIF CFLAG:(LOCAL:0):1 == 0 && CFLAG:(LOCAL:0):12 == 0
		放浪FLG = 0
NEXT
;ランダムFLG = FLAG:44
IF ABS(FLAG:4) > 0
	ランダムFLG = (FLAG:4 < 0)
ENDIF
TOINT SCENARIOID
IF RESULT == 1 || RESULT == 5
	IF (SCVAR:1 > 0)
		反乱FLG = 0
	ELSE
		反乱FLG = 1
	ENDIF
ENDIF


WHILE 1
;
;	//オプション設定画面の呼び出し
;
DRAWLINE
PRINTL ★★オプション設定★★
DRAWLINE
PRINTFORML [1]国配置入れ替え
PRINTL

PRINT 放浪旧作キャラの追加
CALL MENU_YN(2,放浪FLG)
IF 放浪FLG
	PRINTFORML [91]個別選択オプション
ENDIF
PRINT ランダムキャラの追加
CALL MENU_YN(4,ランダムFLG)
IF 反乱FLG >= 0
	PRINT 封獣ぬえ・鬼人正邪離反イベント(シナリオ1,5限定)
	CALL MENU_YN(6,反乱FLG)
	PRINTL
ENDIF

PRINTL
PRINTFORML [90]キャラクター所属設定
;PRINTL
;PRINTFORML [99]シナリオオプション設定
DRAWLINE
PRINTBUTTON "100[設定終了]", 100

$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 1
		CALL SET_RAND_COUNTRIES
	CASE 2 TO 3
		放浪FLG = 3 - RESULT
		CALL SET_OLDCHARAALL(放浪FLG)
		IF 放浪FLG
			PRINTFORMW 無所属の旧作キャラ等が放浪するようになりました
		ELSE
			PRINTFORMW 無所属の旧作キャラは登場しません
		ENDIF
	CASE 4 TO 5
		FLAG:4 = 2 * (5 - RESULT) - 1
;		FLAG:44 = 5 - RESULT
		ランダムFLG = 5 - RESULT
		IF ランダムFLG == 1
			PRINTFORMW ランダムキャラが登場するようになりました
		ELSEIF ランダムFLG == 0
			PRINTFORMW ランダムキャラは登場しません
		ENDIF
	CASE 6 TO 7
		IF 反乱FLG < 0
			CONTINUE
		ENDIF
		反乱FLG = 7 - RESULT
		CALL SET_REBELALL(反乱FLG)
		IF 反乱FLG
			PRINTFORMW 離反を目論む野心の種が生まれたようです
		ELSE
			PRINTFORMW 幻想郷に離反の影はないようです
		ENDIF
		
	CASE 90
		CALL SET_COMMIT_ALLCHARA
	CASE 91
		CALL SET_OLDCHARASOLO
	CASE 99
		TRYCCALLFORM SCENARIO_SETTING_%SCENARIOID%
		CATCH
			PRINTFORMW 設定できるオプションが発見できませんでした
		ENDCATCH
	CASE 100
		RETURN
ENDSELECT
WEND
;-------------------------------------------------
;YNメニュー用命令
;-------------------------------------------------
@MENU_YN(ARG, ARG:1, ARG:2 = 0x00FFF7)

;	ARG/対応する入力数字,ARGとARG+1を使用する
;	ARG:1/選択表示用フラグ、1ではい、0でいいえ
;	ARG:2/選択表示色、初期色は水色

#DIM NCOLOR, 0x00FFF7
PRINTPLAIN     
IF ARG:1 == 1
	SETCOLOR ARG:2
ENDIF
PRINTBUTTON "[はい]", ARG
PRINTPLAIN   
IF ARG:1 == 0
	SETCOLOR ARG:2
ELSE
	RESETCOLOR
ENDIF
PRINTBUTTON "[いいえ]", ARG + 1
RESETCOLOR
PRINTL
RETURN

@CHK_OLDCHARA(ARG)
#FUNCTION
#DIM OLDCHRLIST = 70, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110, 111, 112, 113, 114, 115, 116, 117, 118, 119, 120, 121, 122, 123, 124, 125, 126, 127, 128, 129, 130, 131

FINDELEMENT OLDCHRLIST, ARG
IF RESULT >= 0
	RETURNF 1
ENDIF
RETURNF 0
;-------------------------------------------------
;YNオプション系
;-------------------------------------------------
@SET_OLDCHARAALL(ARG)
IF ARG == 1
	FOR LOCAL:0, 1, CHARANUM
		
		SIF CFLAG:(LOCAL:0):1 == 0 && CHK_OLDCHARA(LOCAL:0)
			CFLAG:(LOCAL:0):12 = 1
	NEXT
ELSE
	FOR LOCAL:0, 1, CHARANUM
		SIF CFLAG:(LOCAL:0):1 == 0 && CHK_OLDCHARA(LOCAL:0)
			CFLAG:(LOCAL:0):12 = 0
	NEXT
ENDIF
RETURN

@SET_OLDCHARASOLO
#DIM PSIZE = 15
WHILE 1
PRINTL ★旧作キャラ放浪設定★
DRAWLINE
FOR LOCAL:0, 1, CHARANUM
	IF CHK_OLDCHARA(LOCAL:0) && CFLAG:(LOCAL:0):1 == 0
		PRINTFORM %CSTR:ID_TO_CHARA(GET_ID(LOCAL:0)):99%
		STRLENS CSTR:ID_TO_CHARA(GET_ID(LOCAL:0)):99
		FOR LOCAL:1, 0, MAX(PSIZE-RESULT, 0)
			PRINTPLAIN  
		NEXT
		CALL MENU_YN(LOCAL:0 * 2, CFLAG:(LOCAL:0):12)
	ENDIF
NEXT
DRAWLINE
PRINTBUTTON "1000[設定終了]", 1000
INPUT
SELECTCASE RESULT
	CASE 0 TO CHARANUM*2
		CFLAG:(RESULT / 2):12 = !(RESULT % 2)
	CASE 1000
		BREAK
ENDSELECT
WEND
RETURN

@SET_REBELALL(ARG)
;
;	ARG = 謀反フラグ(1:ON、2:OFF)
;
TOINT SCENARIOID
IF RESULT == 1 || RESULT == 5
	FOR LOCAL:0, 0, 63
		IF ARG == 1
			CLEARBIT SCVAR:1, LOCAL:0
		ELSE
			SETBIT SCVAR:1, LOCAL:0
		ENDIF
	NEXT
ENDIF
RETURN
;-------------------------------------------------
;キャラ出現設定
;-------------------------------------------------
@SET_COMMIT_ALLCHARA
#DIM FILL_COUNTRY = 0
#DIM LISTHEAD = 0
#DIM PSIZE = 16
#DIM LSIZE = 30
#DIM BSIZE = 30 * MAX_COUNTRY
#DIM SEL_CHARA
#DIM SEL_IDNUM

WHILE 1
PRINTL ★★キャラクター所属設定★★
DRAWLINE
FOR LOCAL:0, 0, PSIZE
	PRINTPLAIN  
NEXT
FOR LOCAL:0, 1, MAX_COUNTRY
	IF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	ENDIF

	SETCOLOR COUNTRY_COLOR:(LOCAL:0)
	SUBSTRING NAME:ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0)), 0, 2
	PRINTFORM [%RESULTS:0%]
	RESETCOLOR
NEXT
PRINTL
;
;	// キャラ所属勢力表示
;
FOR LOCAL:0, LISTHEAD, MIN(CHARANUM, LISTHEAD + LSIZE)
	SEL_CHARA = GET_ID(LOCAL:0)
	IF CFLAG:(LOCAL:0):1 == 0 && CFLAG:(LOCAL:0):12 == 0
		SETCOLOR COLOR_UNSELECTABLE
	ELSEIF GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):1) == LOCAL:0
		SETCOLOR COUNTRY_COLOR:(CFLAG:(LOCAL:0):1)
	ENDIF 
	;
	;	// ここからキャラ名表示と表の罫線合わせ処理 //
	;
	PRINTFORM %NAME:ID_TO_CHARA(SEL_CHARA)%
	RESETCOLOR
	STRLENS NAME:ID_TO_CHARA(SEL_CHARA)
	FOR LOCAL:1, 0, MAX(PSIZE-RESULT, 0)
		PRINTPLAIN  
	NEXT
	IF CFLAG:(LOCAL:0):1 == 0 && CFLAG:(LOCAL:0):12 == 0
		PRINTL
		CONTINUE
	ENDIF
	;
	;	// ここから所属表 //
	;
	FOR LOCAL:1, 1, MAX_COUNTRY
		IF !IS_COUNTRY(LOCAL:1)
			CONTINUE
		ENDIF
		IF CFLAG:(LOCAL:0):1 == LOCAL:1
			SETCOLOR COUNTRY_COLOR:(LOCAL:1)
			PRINTBUTTON "[○]", ((LOCAL:0) + 1 - LISTHEAD) * MAX_COUNTRY + LOCAL:1
		ELSE
			SETCOLOR 0x000000
			PRINTBUTTON "[×]", ((LOCAL:0) + 1 - LISTHEAD) * MAX_COUNTRY + LOCAL:1
		ENDIF
		RESETCOLOR
	NEXT
	;
	;	// ここから追加要素 //
	;
	PRINTBUTTON "[改名]", ((LOCAL:0) + 1 - LISTHEAD) * MAX_COUNTRY
	PRINTL
NEXT
DRAWLINE
PRINTFORM [999] 前のページ
PRINTFORM [1000] 設定終了
PRINTFORM [1001] 次のページ
PRINTL
INPUT
SELECTCASE RESULT
	CASE 999
		IF LISTHEAD > 0
			LISTHEAD = MAX(0, LISTHEAD - LSIZE)
		ENDIF
	CASE 1000
		RETURN
	CASE 1001
		IF LISTHEAD < (CHARANUM - LSIZE)
			LISTHEAD = MIN(CHARANUM-1, LISTHEAD + LSIZE)
		ENDIF
	CASE 1 TO MAX_COUNTRY
		;SORT
	CASE MAX_COUNTRY TO (LSIZE + 1) * MAX_COUNTRY
		SEL_IDNUM = RESULT / MAX_COUNTRY - 1 + LISTHEAD
		IF RESULT % MAX_COUNTRY == 0
			CALL NAME_SETTING(SEL_IDNUM)
			IF RESULT == 1
				PRINTFORML 改名しました
			ELSE
				PRINTFORML 改名しませんでした
			ENDIF
		ELSEIF GET_COUNTRY_BOSS(CFLAG:(SEL_IDNUM):1) == SEL_IDNUM
			PRINTFORML 当主は勢力変更できません
		ELSEIF (RESULT % MAX_COUNTRY) == CFLAG:(SEL_IDNUM):1
			PRINTFORML %NAME:(SEL_IDNUM)%は放浪状態になりました
			CFLAG:(SEL_IDNUM):1 = 0
			CFLAG:(SEL_IDNUM):12 = 1
		ELSE
			PRINTFORML %NAME:(SEL_IDNUM)%は勢力変更しました
			CFLAG:(SEL_IDNUM):1 = RESULT % MAX_COUNTRY
			CFLAG:(SEL_IDNUM):12 = 0
		ENDIF
		 
ENDSELECT
WEND
RETURN

;-------------------------------------------------
;名前呼び名の差し替え
;-------------------------------------------------
@NAME_SETTING(SEL_IDNUM)
#DIM SEL_IDNUM
#DIMS NEWNAME
#DIMS NEWCALLNAME
NEWNAME = %NAME:(SEL_IDNUM)%
NEWCALLNAME = %CALLNAME:(SEL_IDNUM)%
PRINTFORML %NAME:(SEL_IDNUM)%の新しい名前を入力してください
INPUTS
STRLENS RESULTS
SIF RESULT >= 1
	NEWNAME = %RESULTS%
PRINTFORML %CALLNAME:(SEL_IDNUM)%の新しい呼び名を入力してください
INPUTS
STRLENS RESULTS
SIF RESULT >= 1
	NEWCALLNAME = %RESULTS%
PRINTFORML %NAME:(SEL_IDNUM)%から%NEWNAME%に
PRINTFORML %CALLNAME:(SEL_IDNUM)%から%NEWCALLNAME%に変更します。
PRINTFORML よろしいですか？
CALL ASK_YN()
IF RESULT == 0
	NAME:(SEL_IDNUM) = %NEWNAME%
	CALLNAME:(SEL_IDNUM) = %NEWCALLNAME%
	RETURN 1
ENDIF
RETURN 0
;-------------------------------------------------
;国配置の差し替え
;-------------------------------------------------
@SET_RAND_COUNTRIES
#DIM SWAPED, MAX_COUNTRY
#DIM DEFAULT, MAX_CITY
#DIM FILL_COUNTRY
#DIM SWAPA = -1
#DIM SWAPB = -1
#DIM FDEF
#DIMS CITYNAME

VARSET FILL_COUNTRY

IF FDEF == 0 
;
;	// デフォルト配置の保存処理 //
;
	FDEF = FDEF + 1
	FOR LOCAL:0, 0, MAX_CITY
			DEFAULT:(LOCAL:0) = CITY_OWNER:(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0)
			SWAPED:(LOCAL:0) = LOCAL:0
			FILL_COUNTRY ++
		ENDIF
	NEXT
ENDIF
;
;	// 表示処理 //
;
WHILE 1
DRAWLINE
PRINTFORML ★★勢力の配置をデフォルトから入れ替えることが出来ます
DRAWLINE
IF SWAPA >= 0
	SETCOLOR COUNTRY_COLOR:(SWAPED:(SWAPA))
	PRINTFORM [{SWAPA+3}]%NAME:ID_TO_CHARA(COUNTRY_BOSS:(SWAPED:(SWAPA)))%
	RESETCOLOR
	PRINTFORML の入れ替え先を選択してください
ENDIF
PRINTBUTTON " [0][デフォルト配置]", 0
PRINTL 
PRINTBUTTON " [1][ランダム配置]", 1
PRINTL 
;
;	// 個別入れ替え画面 //
;
FOR LOCAL:0, 0, MAX_COUNTRY

	IF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	ENDIF

	SETCOLOR COUNTRY_COLOR:(LOCAL:0)
	PRINTFORM %NAME:ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))%領
	STRLENS NAME:ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))
	FOR LOCAL:1, 0, MAX(17-RESULT, 0)
		PRINTPLAIN  
	NEXT
	RESETCOLOR

	IF SWAPA != LOCAL:0
		SETCOLOR COUNTRY_COLOR:(SWAPED:(LOCAL:0))
	ELSE
		SETCOLOR 0xEEEEEE
	ENDIF
	PRINTFORM [{(LOCAL:0) + 3}]%NAME:ID_TO_CHARA(COUNTRY_BOSS:(SWAPED:(LOCAL:0)))%
	STRLENS NAME:ID_TO_CHARA(COUNTRY_BOSS:(SWAPED:(LOCAL:0)))
	FOR LOCAL:1, 0, MAX(17-RESULT, 0)
		PRINTPLAIN  
	NEXT
	IF (LOCAL:0 + 3) < 10
		PRINTPLAIN  
	ENDIF
	RESETCOLOR

	PRINTFORM 領土：
	FOR LOCAL:1, 0, MAX_CITY
		IF DEFAULT:(LOCAL:1) == LOCAL:0
			PRINTFORM %GET_CITYNAME_BUTTON(LOCAL:1)%　
		ENDIF
	NEXT
	PRINTL
NEXT
;	// 個別入れ替え画面ここまで //
DRAWLINE
PRINTBUTTON " [2][設定終了]", 2
PRINTL 
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
	FOR LOCAL:0, 0, MAX_CITY
		CITY_OWNER:(LOCAL:0) = DEFAULT:(LOCAL:0)
	NEXT
	
	FOR LOCAL:0, 0, MAX_COUNTRY
		SWAPED:(LOCAL:0) = LOCAL:0
	NEXT
	PRINTFORML デフォルト配置にしました
;
;	// デフォルト配置はここでCONTINUEしています //
;
	CONTINUE

	CASE 1
	PRINTFORML ランダム配置にしました
	FILL_COUNTRY = 0
	FOR LOCAL:0, 0, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0)
			SWAPED:(LOCAL:0) = LOCAL:0
			FILL_COUNTRY ++
		ENDIF
	NEXT
;
;	// ランダム配置の処理 //
;
	CALL FISHER_YATES_SHAFFLE(FILL_COUNTRY)

	FOR LOCAL:0, 0, FILL_COUNTRY
		SWAPED:(LOCAL:0 + 1) = SHAFFLE_ARRAY:(LOCAL:0) + 1
	NEXT

	CASE 2
	RETURN

	CASE 3 TO FILL_COUNTRY + 3
	IF SWAPA < 0
		SWAPA = (RESULT-3)
	ELSE
		SWAPB = (RESULT-3)
		IF IS_COUNTRY(SWAPA) && IS_COUNTRY(SWAPB)
			SWAP SWAPED:(SWAPA), SWAPED:(SWAPB)
		ENDIF
		SWAPA = -1
		SWAPB = -1	
	ENDIF
;	SWAPED:(RESULT-3) = (SWAPED:(RESULT - 3) ) % FILL_COUNTRY + 1
ENDSELECT
;
;// 配置の差し替え //
;
	FOR LOCAL:0, 0, MAX_CITY
		CITY_OWNER:(LOCAL:0) = SWAPED:(DEFAULT:(LOCAL:0))
	NEXT

WEND
