;-------------------------------------------------
;ゴブリンによる調教
;-------------------------------------------------
@TRAIN_BY_GOBLIN
#DIM ゴブリン
#DIM 対象
;IDからゴブリン勢力の番号を取得(ゴブリン勢力のIDは300)
ゴブリン = GET_COUNTRY_FROM_ID(COUNTRY_GOBLIN)

SIF ゴブリン == -1 || CONFIG:200
	RETURN -1

LOCAL:9 = 1
FOR 対象, 0, CHARANUM
	;ゴブリン勢力所属で捕虜でないか、ゴブリン勢力の捕虜。SPキャラでない。
	IF (CFLAG:対象:所属 == ゴブリン && !CFLAG:対象:捕虜先 || CFLAG:対象:9 == ゴブリン) && NO:対象 < 2000

		;調教カウンタを増やす
		CFLAG:対象:調教カウンタ++
		
		;調教の様子を表示
		;SPキャラでなく、テキスト表示設定で、「堕ちているか、堕ちていて1/3の確率」
		IF NO:対象 < 2000 && TRAIN_IS_SHOW_TEXT(対象) && (!GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン) || (GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン) && CFLAG:対象:所属 != ゴブリン) || (GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン) && !RAND:3))
			IF LOCAL:9
				LOCAL:9 = 0
				DRAWLINE
				PRINTW ホフゴブリンに捕らえられた士官が陵辱されているようです…
			ENDIF
			PRINTL 
			CALL GOBLINS_TRAIN_MSG(対象)
			CALL FORCE_FUCK(対象, SP_NAME_TO_ID("ホフゴブリン"), @"ホフゴブリンの\@ RAND:2 ? ペニス # 唇\@", @"ホフゴブリン", 2, SP_TRAIN_IS_DECREASE_CFLAG(対象, COUNTRY_GOBLIN))
					;堕ちそうなら特殊メッセ
			IF CFLAG:対象:調教カウンタ >= TRAIN_FALLEN_TERM:2 && CFLAG:対象:調教カウンタ <= TRAIN_FALLEN_TERM:3 && !GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン)
				SETCOLOR COLOR("警告")
				PRINTFORML 度重なる陵辱で、%ANAME(対象)%はほとんど性奴隷に堕ちてしまっている……
				PRINTFORML このまま助けがなければ、その心は本当に折れてしまうだろう
				RESETCOLOR
			ENDIF

			;「解放カウンタが一定以上で、堕ちていない」か「ゴブリン所属でなく、堕ちている」で寝返る
			IF (CFLAG:対象:調教カウンタ >= TRAIN_FALLEN_TERM:3 && !GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン)) || (CFLAG:対象:所属 != ゴブリン && GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン))
				;調教の様子を表示
				IF TRAIN_IS_SHOW_TEXT(対象)
					PRINTFORML ・
					PRINTFORML ・
					PRINTFORML ・
					IF GETBIT(TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン)
						PRINTFORMW ホフゴブリンたちの容赦ない陵辱で、%ANAME(対象)%は奴隷としての己の分際を思い出した
						PRINTFORMW 奴隷としての「仕事」をこなすため、%ANAME(対象)%は再び彼らの下につくことにした……
					ELSE
						IF CFLAG:MASTER:所属 != ゴブリン
							PRINTFORMW ホフゴブリンたちの容赦ない陵辱で%ANAME(対象)%の心は折れ、浅ましく快楽を求める奴隷へとなりはててしまった
							PRINTFORMW もっと彼らに犯して「いただく」ため、一生を性奴隷として過ごすことに同意してしまった……
							IF TALENT:対象:烙印
								PRINTFORML %ANAME(対象)%が%ANAME(MASTER)%専用であることを示す烙印の上から、
								PRINTFORMW 新たに%ANAME(対象)%がホフゴブリンの性奴隷となったことを示す焼き印が施された
								TALENT:対象:烙印 = 0
							ELSE
								PRINTFORMW %ANAME(対象)%の体に、ホフゴブリンの性奴隷となったことを示す焼き印が施された
							ENDIF
							SIF IS_LOVER(対象)
								PRINTFORMW %ANAME(対象)%の頭の中から、かつて愛していた%ANAME(MASTER)%のことが、綺麗さっぱり消え失せた……
						ELSE
							PRINTFORMW ホフゴブリンの陵辱と%ANAME(MASTER)%の努力で、%ANAME(対象)%は浅ましく快楽を求める奴隷へと成長した
							PRINTFORMW もっと彼らに犯して「いただく」ため、一生を性奴隷として過ごすことに同意した……
						ENDIF
					ENDIF
				ENDIF
				CALL KOJO_SPECIAL_FALLEN_EVENT(対象, COUNTRY_GOBLIN)
				SETCOLOR COLOR("注意")
				PRINTFORMW %ANAME(対象)%はホフゴブリンに寝返ってしまった
				RESETCOLOR
				CFLAG:対象:1 = ゴブリン
				CFLAG:対象:9 = 0
				SETBIT TALENT:対象:特殊勢力陥落系, 陥落_ホフゴブリン
				$HUKUJU_LOOP
				SELECTCASE RAND:10
					CASE IS < 5
						CALL SET_PIERCE_RANDOM(対象, 0)
						SIF RESULT != -1
							CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_PIERCE_NAME(RESULT)%に、服従の証のピアスが取り付けられた……", COLOR("桃"), "W")
					CASE IS < 6
						SIF IS_MALE(対象)
							GOTO HUKUJU_LOOP
						CALL SET_TATOO_RANDOM(対象, STR_FOR_TATOO(-1), 0)
						SIF RESULT != -1
							CALL COLORPRINTFORM(@"%ANAME(対象)%の%GET_TATOO_NAME(RESULT)%に、「%TATOO:対象:RESULT%」とタトゥーが刻まれた……", COLOR("桃"), "W")
					CASEELSE
						IF GET_INMOU(対象) >= 陰毛_標準
						CALL COLORPRINTFORM(@"服従の証として、%ANAME(対象)%の陰毛はハート型に整えられた……", COLOR("桃"), "W")
						TALENT:対象:陰毛目標値 = 陰毛_ハート型
						TALENT:対象:陰毛現在値 = 陰毛_ハート型
					ELSE
						CALL COLORPRINTFORM(@"服従の証として、%ANAME(対象)%の陰毛は永久脱毛された……", COLOR("桃"), "W")
						TALENT:対象:陰毛目標値 = 陰毛_パイパン
						TALENT:対象:陰毛現在値 = 陰毛_パイパン
					ENDIF
				ENDSELECT
				SIF CFLAG:MASTER:所属 != ゴブリン
					CALL LOSE_RELATION_TALENT(対象)
			ENDIF
			WAIT
		ENDIF
	ENDIF
NEXT