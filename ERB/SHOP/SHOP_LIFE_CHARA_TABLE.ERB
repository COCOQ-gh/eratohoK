;-------------------------------------------------
;能力の一覧表示
;LOCAL:1=表示ページ 2=ページ最大 3=表示チャンネル 4=表示行数 0,10,11=カウンタ
;-------------------------------------------------
@SHOP_LIFE_CHARA_TABLE
#DIM CHARA_TABLE ,1000
#DIM SORT_VALUE,1000
#DIM HEAD_NUM,50 = 50,51,52,0,53,60,61,2,3,4,0,1,2,3,4,12,13,14,0,1,2,20,21,22,23,24,25,26,27,28,29,30,31
#DIM FIRST_LINE
LOCAL:1 = 1
LOCAL:3 = 1
LOCAL:4 = 23
LOCAL:10 = 0
FOR LOCAL,0,CHARANUM
	CHARA_TABLE:LOCAL = SHOP_CHARA_LIST:LOCAL
NEXT
FOR LOCAL:0, 0, CHARANUM
	TRYCALLFORM CHECK_SELECTABLE{FLAG:7}(CHARA_TABLE:(LOCAL:0))
	SIF RESULT
		LOCAL:10 ++
NEXT
LOCAL:2 = LOCAL:10 / LOCAL:4 + 1
FIRST_LINE = LINECOUNT
$INPUT_LOOP

;ヘッダ
DRAWLINE
PRINTFORML   ★★能力を表示するキャラを選択して下さい★★項目名クリックでソートできます★★
		PRINTFORM %TOSTR_REPEAT("-",22)%
SELECTCASE LOCAL:3
	CASE 1
		PRINTFORM |
		PRINTBUTTON "  ★", 59
		FOR LOCAL,0,10
			PRINTFORM |
			SELECTCASE LOCAL
				CASE 0 TO 2,4 TO 6
					PRINTBUTTON " " + ABLNAME:(HEAD_NUM:LOCAL),60+LOCAL
				CASE 3
					PRINTBUTTON "3合計",60+LOCAL
				CASE 7
					PRINTBUTTON "  好感度",60+LOCAL
				CASE 8
					PRINTBUTTON "  依存度",60+LOCAL
				CASE 9
					PRINTBUTTON "  従属度",60+LOCAL
			ENDSELECT
		NEXT
		PRINTFORML |%TOSTR_REPEAT("-",2)%
	CASE 2
		FOR LOCAL,10,21
			PRINTFORM |
			SELECTCASE LOCAL
				CASE 10 TO 17
					PRINTBUTTON " " + SUBSTRING(ABLNAME:(HEAD_NUM:LOCAL),0,4),60+LOCAL
				CASE 18 TO 20
					PRINTBUTTON "  " + SUBSTRING(BASENAME:(HEAD_NUM:LOCAL),0,4),60+LOCAL
			ENDSELECT
		NEXT
		PRINTFORML |%TOSTR_REPEAT("-",6)%
	CASE 3
		FOR LOCAL,21,33
			PRINTFORM |
			PRINTBUTTON " " + ABLNAME:(HEAD_NUM:LOCAL),60+LOCAL
		NEXT
		PRINTFORML |%TOSTR_REPEAT("-",3)%
	CASEELSE
		PRINTFORML %TOSTR_REPEAT("-",76)%
ENDSELECT

;メイン
LOCAL:10 = 0
FOR LOCAL:0, 0, CHARANUM
	LOCAL:5 = CHARA_TABLE:(LOCAL:0)
	TRYCALLFORM CHECK_SELECTABLE{FLAG:7}(LOCAL:5)
;主人公の表示
	SIF LOCAL:5 == 0
		RESULT = 1
	IF RESULT
		LOCAL:10 ++
		IF (LOCAL:1-1)*LOCAL:4 <= LOCAL:10 -1 && LOCAL:1*LOCAL:4 > LOCAL:10 -1
			CALL PRINT_PARTNER_DATA_TABLE(LOCAL:5,LOCAL:3)
			PRINTL
		ENDIF
	ENDIF
NEXT


;フッタ
PRINTFORML %TOSTR_REPEAT("-",98)%
PRINTFORM %TOSTR_SPACE(4)%
PRINTBUTTON "55[表示切替 "+TOSTR(LOCAL:3)+"/ 4]", 55
PRINTFORM %TOSTR_SPACE(4)%
IF LOCAL:2 >= 2
	PRINTBUTTON "54[前のページ   ]", 54
	PRINTFORM %TOSTR_SPACE(3)%page{LOCAL:1,2}/{LOCAL:2,2}%TOSTR_SPACE(3)%
	PRINTBUTTON "56[次のページ   ]", 56
	PRINTFORM %TOSTR_SPACE(4)%
ELSE
	PRINTFORM %TOSTR_SPACE(58)%
ENDIF
PRINTBUTTON "0[戻る         ]", 0
PRINTL

INPUT
CLEARLINE 1

SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 54
		LOCAL:1 --
			SIF LOCAL:1 < 1
				LOCAL:1 = LOCAL:2
	CASE 56
		LOCAL:1 ++
			SIF LOCAL:1 > LOCAL:2
				LOCAL:1 = 1
	CASE 55
		LOCAL:3 ++
		SIF LOCAL:3 > 4
			LOCAL:3 = 1
;ソート
	CASE 59 TO 92
		SELECTCASE RESULT
			CASE 59
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:(LOCAL:0) = TMP_CHARA_STARS:LOCAL
				NEXT
			CASE 60 TO 62,64 TO 66,70 TO 77,81 TO 92
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):(HEAD_NUM:(RESULT-60))
				NEXT
			CASE 63
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):50+ABL:(LOCAL:0):51+ABL:(LOCAL:0):52
				NEXT
			CASE 67 TO 69
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = CFLAG:(LOCAL:0):(HEAD_NUM:(RESULT-60))
				NEXT
			CASE 78 TO 80
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = MAXBASE:(LOCAL:0):(HEAD_NUM:(RESULT-60))
				NEXT
		ENDSELECT
			FOR LOCAL,1,CHARANUM
				FOR LOCAL:11,LOCAL,0,-1
					LOCAL:12 = CHARA_TABLE:(LOCAL:11-1)
					LOCAL:13 = CHARA_TABLE:(LOCAL:11)
					IF SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)
						SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
					ELSE
						BREAK
					ENDIF
				NEXT
			NEXT
	CASE 100 TO 999
		CALL USERSHOP_LIFE_CHARA1(NO_TO_CHARA(RESULT - 100))
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP
;-------------------------------------------------
;ARG:0番のキャラの情報を表示する関数
;ARG:1は表示チャンネル
;ボタンの選択番号は NO + ARG:2 になる
;-------------------------------------------------
@PRINT_PARTNER_DATA_TABLE(ARG:0, ARG:1 = 0, ARG:2 = 100)
IF ARG:1 != 4
	PRINTFORM [{NO:(ARG:0) + ARG:2, 4}] %ANAME(ARG:0), 16, LEFT%
ENDIF
IF ARG:1 == 1
	CALL TMP_PRINT_CHARA_STARS_NUM(ARG:0)
	PRINT |
	CALL PRINT_ALPHABET_RANK(0, ABL:(ARG:0):武闘)
	PRINTFORM {ABL:(ARG:0):武闘, 4}|
	CALL PRINT_ALPHABET_RANK(0, ABL:(ARG:0):知略)
	PRINTFORM {ABL:(ARG:0):知略, 4}|
	CALL PRINT_ALPHABET_RANK(0, ABL:(ARG:0):政治)
	PRINTFORM {ABL:(ARG:0):政治, 4}|
	PRINTFORM {ABL:(ARG:0):武闘 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治,5}|
	CALL PRINT_ALPHABET_RANK(1, ABL:(ARG:0):妖術)
	PRINTFORM {ABL:(ARG:0):妖術, 4}|
	CALL PRINT_ALPHABET_RANK(2, ABL:(ARG:0):歌唱)
	PRINTFORM {ABL:(ARG:0):歌唱, 4}|
	CALL PRINT_ALPHABET_RANK(2, ABL:(ARG:0):料理)
	PRINTFORM {ABL:(ARG:0):料理, 4}|
	PRINTFORM {CFLAG:(ARG:0):好感度, 8}|
	PRINTFORM {CFLAG:(ARG:0):依存度, 8}|
	PRINTFORM {CFLAG:(ARG:0):従属度, 8}|
ELSEIF ARG:1 == 2
	FOR LOCAL, 0, 5
		CALL PRINT_ALPHABET_RANK(3, ABL:(ARG:0):LOCAL)
		PRINTFORM %TOSTR_R(ABL:(ARG:0):LOCAL, 4)%|
	NEXT
	FOR LOCAL,12,15
		CALL PRINT_ALPHABET_RANK(3, ABL:(ARG:0):LOCAL)
		PRINTFORM %TOSTR_R(ABL:(ARG:0):LOCAL, 4)%|
	NEXT
	FOR LOCAL,0,3
		PRINTFORM %TOSTR_R(MAXBASE:(ARG:0):LOCAL,6)%|
	NEXT
ELSEIF ARG:1 == 3
	FOR LOCAL,20,32
		CALL PRINT_ALPHABET_RANK(3, ABL:(ARG:0):LOCAL)
		PRINTFORM %TOSTR_R(ABL:(ARG:0):LOCAL, 4)%|
	NEXT
ENDIF
IF ARG:1 == 4
	CALL PRINT_PARTNER_DATA(ARG:0)
ENDIF
RETURN
;-------------------------------------------------
; ARG:0を文字列に変換 ARG:1の幅で右寄せ
;-------------------------------------------------
@TOSTR_R(ARG:0,ARG:1)
#FUNCTIONS
VARSET LOCALS
LOCALS '= TOSTR(ARG:0)
LOCAL = STRLENS(LOCALS)
IF LOCAL < ARG:1
	FOR LOCAL:1,0,ARG:1-LOCAL
		LOCALS:1 += " "
	NEXT
ENDIF
LOCALS:1 += LOCALS
RETURNF LOCALS:1
