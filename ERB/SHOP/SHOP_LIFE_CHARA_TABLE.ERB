;-------------------------------------------------
;能力の一覧表示
;LOCAL:1=表示ページ 2=ページ最大 3=表示チャンネル 4=表示行数 0,10,11=カウンタ
;-------------------------------------------------
@SHOP_LIFE_CHARA_TABLE
#DIM CHARA_TABLE,3000
#DIM SORT_VALUE,3000
#DIM HEAD_NUM,50 = 50,51,52,53,0,54,60,61,2,3,4,0,1,2,3,4,12,13,14,0,1,2,20,21,22,23,24,25,26,27,28,29,30,31, 2, 3, 4
#DIM FIRST_LINE
#DIM DESC
#DIM PREV_SORT
VARSET CHARA_TABLE, -1
LOCAL:1 = 1
LOCAL:3 = 1
LOCAL:4 = 23
LOCAL:10 = 0
FOR LOCAL, 0, CHARANUM
	SIF SHOP_CHARA_LIST:LOCAL == -1
		CONTINUE
	CHARA_TABLE:LOCAL = SHOP_CHARA_LIST:LOCAL
NEXT
FOR LOCAL:0, 0, CHARANUM
	SIF CHARA_TABLE:(LOCAL:0) == -1
		BREAK
	TRYCALLFORM CHECK_SELECTABLE1040(CHARA_TABLE:(LOCAL:0))
	SIF RESULT
		LOCAL:10 ++
NEXT

LOCAL:2 = LOCAL:10 / LOCAL:4 + 1
FIRST_LINE = LINECOUNT
$INPUT_LOOP

;ヘッダ
DRAWLINE
PRINTFORM ★能力を表示するキャラを選択して下さい★項目名クリックでソートできます★   
PRINTBUTTON @"[%CHARALIST_MODE()%]", 57
PRINTL
PRINTBUTTON "|  NO", 58
PRINTFORM |
PRINTFORM %TOSTR_REPEAT("-",MAX_CHARANAME_LENGTH/2)%
SELECTCASE LOCAL:3
	CASE 1
		PRINTFORM |
		PRINTBUTTON "  ★", 59
		FOR LOCAL,0,9
			PRINTFORM |
			SELECTCASE LOCAL
				CASE 0 TO 3,5 TO 7
					PRINTBUTTON " " + ABLNAME:(HEAD_NUM:LOCAL),60+LOCAL
				CASE 4
					PRINTBUTTON "4合計",60+LOCAL
				CASE 8
					PRINTBUTTON "7合計", 60+LOCAL
			ENDSELECT
		NEXT
		PRINTFORML |%TOSTR_REPEAT("-",6)%
	CASE 2
		FOR LOCAL,11,22
			PRINTFORM |
			SELECTCASE LOCAL
				CASE 11 TO 18
					PRINTBUTTON " " + SUBSTRING(ABLNAME:(HEAD_NUM:LOCAL),0,4),60+LOCAL
				CASE 19 TO 21
					PRINTBUTTON "  " + SUBSTRING(BASENAME:(HEAD_NUM:LOCAL),0,4),60+LOCAL
			ENDSELECT
		NEXT
		PRINTFORML |%TOSTR_REPEAT("-",6)%
	CASE 3
		FOR LOCAL,22,34
			PRINTFORM |
			PRINTBUTTON " " + ABLNAME:(HEAD_NUM:LOCAL),60+LOCAL
		NEXT
		PRINTFORML |%TOSTR_REPEAT("-",3)%
	CASE 4
		PRINTFORM |
		PRINTBUTTON "  好感度",93
		PRINTFORM |
		PRINTBUTTON "  依存度",94
		PRINTFORM |
		PRINTBUTTON "  従属度",95
		PRINTFORM |
		PRINTFORML %TOSTR_REPEAT("-",49)%
	CASEELSE
		PRINTFORML %TOSTR_REPEAT("-",76)%
ENDSELECT

;メイン
LOCAL:10 = 0
FOR LOCAL:0, 0, CHARANUM
	LOCAL:5 = CHARA_TABLE:(LOCAL:0)
	SIF LOCAL:5 == -1
		BREAK
	TRYCALLFORM CHECK_SELECTABLE1040(LOCAL:5)
	IF RESULT
		LOCAL:10 ++
		IF (LOCAL:1-1)*LOCAL:4 <= LOCAL:10 -1 && LOCAL:1*LOCAL:4 > LOCAL:10 -1
			CALL PRINT_PARTNER_DATA_TABLE(LOCAL:5,LOCAL:3)
			PRINTL
		ENDIF
	ENDIF
NEXT


;フッタ
PRINTFORML %TOSTR_REPEAT("-",98)%
PRINTFORM %TOSTR_SPACE(4)%
PRINTBUTTON "55[表示切替 "+TOSTR(LOCAL:3)+"/ 4]", 55
PRINTFORM %TOSTR_SPACE(4)%
IF LOCAL:2 >= 2
	PRINTBUTTON "54[前のページ   ]", 54
	PRINTFORM %TOSTR_SPACE(3)%page{LOCAL:1,2}/{LOCAL:2,2}%TOSTR_SPACE(3)%
	PRINTBUTTON "56[次のページ   ]", 56
	PRINTFORM %TOSTR_SPACE(4)%
ELSE
	PRINTFORM %TOSTR_SPACE(58)%
ENDIF
PRINTBUTTON "0[戻る         ]", 0
PRINTL

INPUT
CLEARLINE 1

SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 54
		LOCAL:1 --
			SIF LOCAL:1 < 1
				LOCAL:1 = LOCAL:2
	CASE 56
		LOCAL:1 ++
			SIF LOCAL:1 > LOCAL:2
				LOCAL:1 = 1
	CASE 55
		LOCAL:3 ++
		SIF LOCAL:3 > 4
			LOCAL:3 = 1
	CASE 57
		;キャラ数の再計算が必要になるのでリスタート
		FLAG:能力表示フィルタ = ROUND_INCREMENT(FLAG:能力表示フィルタ, 0, 3)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
;ソート
	CASE 58 TO 95
		;直前に押したやつなら昇順降順の切り替え。
		IF RESULT == PREV_SORT
			DESC = !DESC
		ELSE
			DESC = 0
		ENDIF
		PREV_SORT = RESULT
		SELECTCASE RESULT
			CASE 58
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = NO:LOCAL
				NEXT
			CASE 59
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:(LOCAL:0) = TMP_CHARA_STARS:LOCAL
				NEXT
			CASE 60 TO 63,65 TO 67,70 TO 77,81 TO 93
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):(HEAD_NUM:(RESULT-60))
				NEXT
			CASE 64
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):50+ABL:(LOCAL:0):51+ABL:(LOCAL:0):52+ABL:(LOCAL:0):53
				NEXT
			CASE 68
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):50+ABL:(LOCAL:0):51+ABL:(LOCAL:0):52+ABL:(LOCAL:0):53+ABL:(LOCAL:0):54+ABL:(LOCAL:0):60+ABL:(LOCAL:0):61
				NEXT
			CASE 93 TO 95
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = CFLAG:(LOCAL:0):(HEAD_NUM:(RESULT-60))
				NEXT
			CASE 78 TO 80
				FOR LOCAL,0,CHARANUM
					SORT_VALUE:(LOCAL:0) = MAXBASE:(LOCAL:0):(HEAD_NUM:(RESULT-60))
				NEXT
		ENDSELECT
		FOR LOCAL,1,CHARANUM
			SIF CHARA_TABLE:LOCAL == -1
				BREAK
			FOR LOCAL:11,LOCAL,0,-1
				LOCAL:12 = CHARA_TABLE:(LOCAL:11-1)
				LOCAL:13 = CHARA_TABLE:(LOCAL:11)
				IF (DESC && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!DESC && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
					SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
				ELSE
					BREAK
				ENDIF
			NEXT
		NEXT
		;ソート順に従ってSHOP_CHARA_LISTを更新
		VARSET SHOP_CHARA_LIST, -1
		FOR LOCAL, 0, VARSIZE("SHOP_CHARA_LIST")
			SHOP_CHARA_LIST:LOCAL = CHARA_TABLE:LOCAL
		NEXT
	CASEELSE
		SIF NO_TO_CHARA(RESULT - 100) != -1
			CALL SHOW_INFO_WITH_UI(NO_TO_CHARA(RESULT - 100), 1)
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP
;-------------------------------------------------
;ARG:0番のキャラの情報を表示する関数
;ARG:1は表示チャンネル
;ボタンの選択番号は NO + ARG:2 になる
;-------------------------------------------------
@PRINT_PARTNER_DATA_TABLE(ARG:0, ARG:1 = 0, ARG:2 = 100)
PRINTFORM [{NO:(ARG:0) + ARG:2, 4}] %SNAME(ARG:0), MAX_CHARANAME_LENGTH/2, LEFT%
IF ARG:1 == 1
	CALL TMP_PRINT_CHARA_STARS_NUM(ARG:0)
	PRINT |
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):武闘)
	PRINTFORM {ABL:(ARG:0):武闘, 4}|
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):防衛)
	PRINTFORM {ABL:(ARG:0):防衛, 4}|
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):知略)
	PRINTFORM {ABL:(ARG:0):知略, 4}|
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):政治)
	PRINTFORM {ABL:(ARG:0):政治, 4}|
	PRINTFORM {ABL:(ARG:0):武闘 + ABL:(ARG:0):防衛 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治,5}|
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):妖術)
	PRINTFORM {ABL:(ARG:0):妖術, 4}|
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):歌唱)
	PRINTFORM {ABL:(ARG:0):歌唱, 4}|
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):料理)
	PRINTFORM {ABL:(ARG:0):料理, 4}|
	PRINTFORM {ABL:(ARG:0):武闘 + ABL:(ARG:0):防衛 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治 + ABL:(ARG:0):妖術 + ABL:(ARG:0):歌唱+ABL:(ARG:0):料理,5}|
ELSEIF ARG:1 == 2
	FOR LOCAL, 0, 5
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):LOCAL)
		PRINTFORM %TOSTR_R(ABL:(ARG:0):LOCAL, 4)%|
	NEXT
	FOR LOCAL,12,15
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):LOCAL)
		PRINTFORM %TOSTR_R(ABL:(ARG:0):LOCAL, 4)%|
	NEXT
	FOR LOCAL,0,3
		PRINTFORM %TOSTR_R(MAXBASE:(ARG:0):LOCAL,6)%|
	NEXT
ELSEIF ARG:1 == 3
	FOR LOCAL,20,32
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):LOCAL)
		PRINTFORM %TOSTR_R(ABL:(ARG:0):LOCAL, 4)%|
	NEXT
ELSEIF ARG:1 == 4
	PRINTFORM {CFLAG:(ARG:0):好感度, 8}|
	PRINTFORM {CFLAG:(ARG:0):依存度, 8}|
	PRINTFORM {CFLAG:(ARG:0):従属度, 8}|
ENDIF
RETURN
;-------------------------------------------------
; ARG:0を文字列に変換 ARG:1の幅で右寄せ
;-------------------------------------------------
@TOSTR_R(ARG:0,ARG:1)
#FUNCTIONS
VARSET LOCALS
LOCALS '= TOSTR(ARG:0)
LOCAL = STRLENS(LOCALS)
IF LOCAL < ARG:1
	FOR LOCAL:1,0,ARG:1-LOCAL
		LOCALS:1 += " "
	NEXT
ENDIF
LOCALS:1 += LOCALS
RETURNF LOCALS:1
