;----------------------------
;スキル習得画面の親
;----------------------------
@SHOP_LIFE_SKILL(対象)
#DIM 対象
#DIM FIRST_LINE
#DIM ジャンル

SIF COOLTIME:対象:0
	RETURN

CALL SHOP_LIFE_SKILL_CREATE_LIST(対象)

REDRAW 0
FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL ICPRINT(@"金を消費し、<%ANAME(対象)%>にスキルを習得・忘却させます%TOSTR_SPACE(10)%現在の金:<{MONEY}>", "", COLOR("注意"))
PRINTFORM %TOSTR_SPACE(5)%
PRINTBUTTON "[戻る]", 99999
PRINTL
PRINTFORML ※%ANAME(MASTER)%が親しく、かつ手の空いている相手でないと、スキルを教えてもらえません
PRINTFORML ※時間が経過し、教える/教わるキャラにクールタイムがつきます
DRAWLINE
CALL SHOP_LIFE_SKILL_PRINT_TABS(ジャンル)
DRAWLINE
PRINTFORML %ジャンル == VARSIZE("SKILL_GENRE_ENG") ? "-スキルの忘却-" # "-スキルの習得-"%
DRAWLINE

IF ジャンル == VARSIZE("SKILL_GENRE_ENG")
	CALL SHOP_LIFE_SKILL_PRINT_FORGET_LIST(対象)
ELSE
	CALL SHOP_LIFE_SKILL_PRINT_LEARN_LIST(ジャンル,対象)
ENDIF

INPUT


SELECTCASE RESULT
	CASE 0 TO 3000
		IF ジャンル != VARSIZE("SKILL_GENRE_ENG")
			CALL SHOP_LEARN_SKILL(対象, ジャンル, RESULT)
		ELSE
			CALL SHOP_FORGET_SKILL(対象, RESULT)
		ENDIF
		IF RESULT == 1
			SHOP_TIME ++
			BEGIN TURNEND
			RETURN 1
		ENDIF
CASE 4000 TO (4000 + VARSIZE("SKILL_GENRE_ENG"))
		ジャンル = RESULT - 4000
	;タブ切り替え処理
	CASE 5000
		SHOP_SKILL_PAGE:ジャンル --
	CASE 5001
		SHOP_SKILL_PAGE:ジャンル ++
	;ページ切り替え処理
	CASE 99999
		REDRAW 1
		RETURN
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;----------------------------
;スキル習得をこころみる
;----------------------------
@SHOP_LEARN_SKILL(対象, ジャンル, 番号)
#DIM 対象
#DIM 教師
#DIM 番号
#DIM ジャンル
#DIM スキル
#DIM スキル位置
#DIM レベル
#DIMS スキル名

;空きスロットを探す
FOR スキル位置, 0, MAX_SKILLS
	PRINTFORML {SKILL_NO_SLOT:対象:ジャンル:スキル位置} {SKILL_ID_SLOT:対象:ジャンル:スキル位置}
	IF SKILL_NO_SLOT:対象:ジャンル:スキル位置 < 0 && SKILL_ID_SLOT:対象:ジャンル:スキル位置 < 0
		スキル位置 --
		BREAK
	ENDIF
NEXT

;いっぱいならだめ
IF スキル位置 == MAX_SKILLS
	PRINTFORML (このジャンルのスキルは既に覚えきっています)
	PRINTFORMW (新たなものを覚えるには、何かを忘れる必要があります)
	RETURN 
ENDIF

CALLFORM SKILL_{SHOP_SKILL_NO_LIST:ジャンル:番号}_%SKILL_GENRE_ENG:ジャンル%_{SHOP_SKILL_ID_LIST:ジャンル:番号}_LEVEL
レベル = RESULT

;資金不足ならだめ
IF SKILL_LEARN_PRICE:レベル > MONEY
	PRINTFORMW (資金不足です)
	RETURN
ENDIF

CALLFORM SKILL_{SHOP_SKILL_NO_LIST:ジャンル:番号}_%SKILL_GENRE_ENG:ジャンル%_{SHOP_SKILL_ID_LIST:ジャンル:番号}_NAME
スキル名 = %RESULTS%

教師 = NO_TO_CHARA(SHOP_SKILL_NO_LIST:ジャンル:番号)

IF 教師 == -1 || 教師 == 対象
	CALL COLORPRINTFORM(@"%ANAME(対象)%は『%スキル名%』を習得した！", COLOR("注意"), "W")
ELSE
	CALL COLORPRINTFORM(@"%ANAME(対象)%は%ANAME(教師)%から『%スキル名%』を習得した！", COLOR("注意"), "W")
ENDIF

SKILL_NO_SLOT:対象:ジャンル:スキル位置 = SHOP_SKILL_NO_LIST:ジャンル:番号
SKILL_ID_SLOT:対象:ジャンル:スキル位置 = SHOP_SKILL_ID_LIST:ジャンル:番号


MONEY -= SKILL_LEARN_PRICE:レベル

CFLAG:対象:行動済み = 1
CALL ADD_COOLTIME(対象, 1)

IF 教師 != -1
	CFLAG:教師:行動済み = 1
	CALL ADD_COOLTIME(教師, 1)
ENDIF

RETURN 1

;----------------------------
;スキル忘却をこころみる
;----------------------------
@SHOP_FORGET_SKILL(対象, 番号)
#DIM 対象
#DIM 番号
#DIM ジャンル
#DIM スキル
#DIM スキル位置
#DIM レベル
#DIMS スキル名

ジャンル = 番号 / MAX_SKILLS
スキル = 番号 % MAX_SKILLS

;資金不足ならだめ
IF SKILL_FORGET_PRICE > MONEY
	PRINTFORMW (資金不足です)
	RETURN
ENDIF

;該当スキルの習得判定
IF SKILL_NO_SLOT:対象:ジャンル:スキル < 0 || SKILL_ID_SLOT:対象:ジャンル:スキル < 0
	PRINTFORML (そのスキルは覚えていません)
	RETURN
ENDIF

CALLFORM SKILL_{SKILL_NO_SLOT:対象:ジャンル:スキル}_%SKILL_GENRE_ENG:ジャンル%_{SKILL_ID_SLOT:対象:ジャンル:スキル}_NAME
スキル名 = %RESULTS%


CALL COLORPRINTFORM(@"%ANAME(対象)%は『%スキル名%』を忘れた！", COLOR("注意"), "W")

SKILL_NO_SLOT:対象:ジャンル:スキル = -1
SKILL_ID_SLOT:対象:ジャンル:スキル = -1


MONEY -= SKILL_FORGET_PRICE

CFLAG:対象:行動済み = 1
CALL ADD_COOLTIME(対象, 1)

SHOP_TIME ++
BEGIN TURNEND
RETURN 1




;----------------------------
;スキルタブ描画
;----------------------------
@SHOP_LIFE_SKILL_PRINT_TABS(ジャンル)
#DIM ジャンル
FOR LOCAL, 0, VARSIZE("SKILL_GENRE_ENG")
	IF ジャンル == LOCAL
		SETCOLOR COLOR("選択中")
	ENDIF
	PRINTBUTTON @"[%SKILL_GENRE_ENG:LOCAL%]", 4000 + LOCAL
	PRINT  
	RESETCOLOR
NEXT
IF ジャンル == LOCAL
	SETCOLOR COLOR("選択中")
ENDIF
PRINTBUTTON　@"[FORGET]", 4000 + LOCAL
RESETCOLOR
PRINTL

;----------------------------
;習得可能スキル描画
;----------------------------
@SHOP_LIFE_SKILL_PRINT_LEARN_LIST(ジャンル, 対象)
#DIM 対象
#DIM ジャンル
#DIM スキル
#DIM 最大ページ
#DIM CONST 項目数 = 30
#DIM スキル数
#DIMS アクセサ
#DIMS キャラ名
#DIMS スキル名
#DIM レベル
#DIMS 説明
#DIM 描画行

最大ページ = SHOP_SKILL_NUM:ジャンル / 項目数 + 1

IF SHOP_SKILL_PAGE:ジャンル <= 0
	SHOP_SKILL_PAGE:ジャンル = 最大ページ
ELSEIF SHOP_SKILL_PAGE:ジャンル > 最大ページ
	SHOP_SKILL_PAGE:ジャンル = 1
ENDIF

スキル数 = 0
FOR スキル, 0, MAX_SKILLS
	TRYCCALLFORM SKILL_{SKILL_NO_SLOT:対象:ジャンル:スキル}_%SKILL_GENRE_ENG:ジャンル%_{SKILL_ID_SLOT:対象:ジャンル:スキル}
		スキル数 ++
	CATCH
	ENDCATCH
NEXT

描画行 = 0
FOR スキル, (SHOP_SKILL_PAGE:ジャンル - 1) * 項目数, MIN(SHOP_SKILL_PAGE:ジャンル * 項目数, SHOP_SKILL_NUM:ジャンル)

	;描画内容を用意
	アクセサ = SKILL_{SHOP_SKILL_NO_LIST:ジャンル:スキル}_%SKILL_GENRE_ENG:ジャンル%_{SHOP_SKILL_ID_LIST:ジャンル:スキル}_
	CALLFORM %アクセサ%NAME
	スキル名 = %(STRLENS(RESULTS:0) > 20 ? TOHALF(RESULTS:0) # RESULTS:0)%
	キャラ名 = %SNAME(NO_TO_CHARA(SHOP_SKILL_NO_LIST:ジャンル:スキル))%
	CALLFORM %アクセサ%LEVEL
	レベル = RESULT
	CALLFORM %アクセサ%EXPLANATION
	説明 = %RESULTS%

	;スキル名と説明を削って行をはみ出さないように
	スキル名 = %SUBSTRING(スキル名, 0, 20)%
	説明 = %SUBSTRING(説明, 0, 54)%

	;習得ボタンは一番左
	IF SKILL_LEARN_PRICE:レベル > MONEY || スキル数 == MAX_SKILLS
		SETCOLOR COLOR("選択不可")
		PRINTPLAIN [習得]
		RESETCOLOR
	ELSE
		PRINTBUTTON "[習得]", スキル
	ENDIF
	;項目描画
	PRINTFORM  %スキル名, 21, LEFT% LV{レベル} %キャラ名, MAX_CHARANAME_LENGTH/2, LEFT% %説明, 54, LEFT% 価格:{SKILL_LEARN_PRICE:レベル, 7, LEFT}

	PRINTL
	描画行 ++
NEXT

IF 項目数 - 描画行 > 0
	FOR LOCAL, 描画行, 項目数
		PRINTL
	NEXT
ENDIF

DRAWLINE
PRINTFORML {SHOP_SKILL_PAGE:ジャンル} / {最大ページ}
DRAWLINE
PRINTBUTTON "[前のページ]", 5000
PRINTBUTTON "[次のページ]", 5001
PRINTL


;----------------------------
;忘却スキル描画
;----------------------------
@SHOP_LIFE_SKILL_PRINT_FORGET_LIST(対象)
#DIM 対象
#DIM ジャンル
#DIM ジャンル数
#DIM スキル数
#DIM スキル
#DIM 最大ページ
#DIM CONST 項目数 = 30
#DIMS アクセサ
#DIMS キャラ名
#DIMS スキル名
#DIM レベル
#DIMS 説明
#DIM 描画行

ジャンル数 = VARSIZE("SKILL_GENRE_ENG")

;ページ数出す用
スキル数 = 0
FOR ジャンル, 0, ジャンル数
	FOR スキル, 0, MAX_SKILLS
		SIF SKILL_NO_SLOT:対象:ジャンル:スキル >= 0 && SKILL_ID_SLOT:対象:ジャンル:スキル >= 0
			スキル数 ++
	NEXT
NEXT

最大ページ = スキル数 / 項目数 + 1

IF SHOP_SKILL_PAGE:ジャンル数 <= 0
	SHOP_SKILL_PAGE:ジャンル数 = 最大ページ
ELSEIF SHOP_SKILL_PAGE:ジャンル数 > 最大ページ
	SHOP_SKILL_PAGE:ジャンル数 = 1
ENDIF

描画行 = 0
FOR ジャンル, 0, VARSIZE("SKILL_GENRE_ENG")
	FOR スキル, 0, MAX_SKILLS

		アクセサ = SKILL_{SKILL_NO_SLOT:対象:ジャンル:スキル}_%SKILL_GENRE_ENG:ジャンル%_{SKILL_ID_SLOT:対象:ジャンル:スキル}_
		;存在判定
		TRYCCALLFORM %アクセサ%EXIST
		CATCH
			CONTINUE
		ENDCATCH

		;描画内容を用意
		CALLFORM %アクセサ%NAME
		スキル名 = %(STRLENS(RESULTS:0) > 20 ? TOHALF(RESULTS:0) # RESULTS:0)%
		キャラ名 = %SNAME(NO_TO_CHARA(SKILL_NO_SLOT:対象:ジャンル:スキル))%
		CALLFORM %アクセサ%LEVEL
		レベル = RESULT
		CALLFORM %アクセサ%EXPLANATION
		説明 = %RESULTS%

		;スキル名と説明を削って行をはみ出さないように
		スキル名 = %SUBSTRING(スキル名, 0, 20)%
		説明 = %SUBSTRING(説明, 0, 54)%

		;習得ボタンは一番左
		IF SKILL_FORGET_PRICE > MONEY || スキル数 == MAX_SKILLS
			SETCOLOR COLOR("選択不可")
			PRINTPLAIN [忘却]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[忘却]", ジャンル * MAX_SKILLS + スキル
		ENDIF
		;項目描画
		PRINTFORM  %スキル名, 21, LEFT% LV{レベル} %キャラ名, MAX_CHARANAME_LENGTH/2, LEFT% %説明, 54, LEFT% 価格:{SKILL_FORGET_PRICE, 7, LEFT}

		PRINTL
		描画行 ++
	NEXT
NEXT

IF 項目数 - 描画行 > 0
	FOR LOCAL, 描画行, 項目数
		PRINTL
	NEXT
ENDIF

DRAWLINE
PRINTFORML {SHOP_SKILL_PAGE:ジャンル数} / {最大ページ}
DRAWLINE
PRINTBUTTON "[前のページ]", 5000
PRINTBUTTON "[次のページ]", 5001
PRINTL


;----------------------------
;あるキャラが習得できるスキルリスト生成
;----------------------------
@SHOP_LIFE_SKILL_CREATE_LIST(対象)
#DIM キャラ
#DIM ジャンル
#DIM スキルID
#DIM 対象
#DIM 対象スキル
VARSET SHOP_SKILL_PAGE
VARSET SHOP_SKILL_NO_LIST
VARSET SHOP_SKILL_ID_LIST
VARSET SHOP_SKILL_NUM

FOR キャラ, 0, CHARANUM
	SIF CFLAG:キャラ:所属 != CFLAG:MASTER:所属
		CONTINUE
	SIF CFLAG:キャラ:捕虜先
		CONTINUE
	SIF !IS_FIXED_CHARA(キャラ)
		CONTINUE
	SIF !IS_LOVER(キャラ) && !IS_SLAVE(キャラ)
		CONTINUE
	SIF TMP_IS_FREE:キャラ:0
		CONTINUE
	FOR ジャンル, 0, VARSIZE("SKILL_GENRE_ENG")
		FOR スキルID, 0, MAX_SKILLS
			TRYCCALLFORM SKILL_{NO:キャラ}_%SKILL_GENRE_ENG:ジャンル%_{スキルID}_EXIST
				;教えられない
				TRYCCALLFORM SKILL_{NO:キャラ}_%SKILL_GENRE_ENG:ジャンル%_{スキルID}_CANT_TELL
					SIF NO:キャラ != NO:対象
						GOTO CANCEL
				CATCH
				ENDCATCH
				;所持してたらダメ
				FOR 対象スキル, 0, MAX_SKILLS
					SIF SKILL_NO_SLOT:対象:ジャンル:対象スキル == NO:キャラ && SKILL_ID_SLOT:対象:ジャンル:対象スキル == スキルID
						GOTO CANCEL
				NEXT
				SHOP_SKILL_NO_LIST:ジャンル:(SHOP_SKILL_NUM:ジャンル) = NO:キャラ
				SHOP_SKILL_ID_LIST:ジャンル:(SHOP_SKILL_NUM:ジャンル) = スキルID
				SHOP_SKILL_NUM:ジャンル ++
			CATCH
				$CANCEL
			ENDCATCH
		NEXT
	NEXT
NEXT



DRAWLINE
