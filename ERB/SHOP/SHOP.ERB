;ショップ画面の処理(共通)

;-------------------------------------------------
;ショップ画面開始時の処理
;-------------------------------------------------
@EVENTSHOP
;オートセーブ判定
TFLAG:99 = 1

;情報表示対象の都市を初期化
SHOWN_CITY = -1

;ページ番号を初期化
FLAG:拠点フェイズページ = 1

;デッドエンディングなら戻る
;IF FLAG:強制エンドフラグ == 1
;	FLAG:強制エンドフラグ = 0
;	BEGIN TITLE
;ENDIF

;-------------------------------------------------
;ショップ画面の表示処理
;-------------------------------------------------
@SHOW_SHOP
;オートセーブ判定を解除
TFLAG:99 = 0

;コマンドの選択可否判定フラグをクリア
VARSET SHOP_AVAIL, 1

;所持金の上限設定
MONEY = LIMIT(MONEY, 0, 9999999999999)

;国家関係マップの作成
CALL TMP_CREATE_RELATION_MAP

;士官の部隊所属マップを作成
CALL TMP_CREATE_IS_FREE_MAP

;士官の星評価を用意
CALL TMP_PREPARE_CHARA_STARS
CALL TMP_PREPARE_COUNTRY_STARS

;無効な入力時に消去する時の行数
LINES_SHOP = LINECOUNT

DRAWLINE
IF CFLAG:MASTER:捕虜先 && FLAG:クリアフラグ
	PRINTFORM 第XX期  
ELSE
	PRINTFORM 第{DAY}期  
ENDIF
IF TIME == 0
	PRINTFORM 金:{MONEY, 13, LEFT} 
	IF COOLTIME:MASTER:0 >= 2
		CALL COLORPRINT("主人公がクールタイム中につき特定行動不可", COLOR("赤"))
	ELSEIF CFLAG:MASTER:行動不能状態 == 1
		CALL COLORPRINT("主人公が臨月につき特定行動不可", COLOR("赤"))
	ENDIF
	PRINTL
ELSE
	IF IS_COUNTRY(CFLAG:MASTER:所属)
		PRINTFORM 金:{MONEY, 12, LEFT} 
		PRINTFORM 国庫:{MONEY:(CFLAG:MASTER:所属), 10, LEFT} 
		PRINTFORM 総兵数:\@ STRLENS(TOSTR(GET_SUM_SOLDIER(CFLAG:MASTER:所属))) > 10 ? たくさん # {GET_SUM_SOLDIER(CFLAG:MASTER:所属), 10, LEFT} \@ 
		PRINTFORM 遊撃兵数:\@ STRLENS(TOSTR(COUNTRY_SOLDIER:(CFLAG:MASTER:所属))) > 10 ? たくさん # {COUNTRY_SOLDIER:(CFLAG:MASTER:所属), 10, LEFT} \@ 
	ELSE
		SIF !FLAG:観戦モード
			PRINTFORM 金:{MONEY, 13, LEFT} 
	ENDIF
	LOCALS:0 = 
	SIF DAY < SLG_PP:0
		LOCALS:0 = %LOCALS:0%外交/
	SIF DAY < SLG_PP:1
		LOCALS:0 = %LOCALS:0%軍事
	SIF LOCALS:0 != ""
		LOCALS:0 = %LOCALS:0%未解禁
	PRINTFORM %LOCALS:0%
	SIF TREATY_U_TARGET:0 != 0
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(TREATY_U_TARGET:0))%討伐連合結成中
	PRINTL
ENDIF
DRAWLINE

;拠点フェイズ
IF TIME == 0
	IF CFLAG:MASTER:捕虜先 == 0
		CALL SHOW_SHOP_LIFE_A
	ELSE
		CALL SHOW_SHOP_LIFE_B
	ENDIF
;戦略フェイズ
ELSE
	CALL SHOW_SHOP_SLG
ENDIF

REDRAW 0

;-------------------------------------------------
;ショップ画面の入力処理
;-------------------------------------------------
@USERSHOP
LOCAL:0 = RESULT

REDRAW 1

;拠点フェイズ
IF TIME == 0
	IF CFLAG:MASTER:捕虜先 == 0
		CALL USERSHOP_LIFE_A(LOCAL:0)
	ELSE
		CALL USERSHOP_LIFE_B(LOCAL:0)
	ENDIF
;戦略フェイズ
ELSE
	CALL USERSHOP_SLG(LOCAL:0)
ENDIF

;処理を行わなかった場合
IF !RESULT
	REDRAW 0
	CLEARLINE LINECOUNT - LINES_SHOP
ENDIF

;-------------------------------------------------
;ショップ画面で0～99が入力されたときのダミー処理
;-------------------------------------------------
@EVENTBUY
ITEM:BOUGHT = 0
LOCAL:0 = BOUGHT

REDRAW 1

;拠点フェイズ
IF TIME == 0
	IF CFLAG:MASTER:捕虜先 == 0
		CALL EVENTBUY_LIFE_A(LOCAL:0)
	ELSE
		CALL EVENTBUY_LIFE_B(LOCAL:0)
	ENDIF
;戦略フェイズ
ELSE
	CALL EVENTBUY_SLG(LOCAL:0)
ENDIF

;処理を行わなかった場合
IF !RESULT
	;コンフィグ
	IF LOCAL:0 == 70
		CALL SHOW_CONFIG
	;セーブ
	ELSEIF LOCAL:0 == 80
		DUMPRAND
		SAVEGAME
	;ロード
	ELSEIF LOCAL:0 == 90
		LOADGAME
	ELSE
		REDRAW 0
		CLEARLINE LINECOUNT - LINES_SHOP
	ENDIF
ENDIF

RETURN
