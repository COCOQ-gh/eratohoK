;拠点フェイズにおけるショップ画面の処理
;「婚姻の儀」「触手部屋」などの各メニューの処理を行う

;-------------------------------------------------
;キャラARG:0について、素質<正妻>が<夫>と表示される条件を満たしているかどうか判定
;-------------------------------------------------
@IS_HUSBAND(ARG:0)
#FUNCTION
IF GROUPMATCH(TALENT:MASTER:性別, 1, 2) && GROUPMATCH(TALENT:(ARG:0):性別, 0, 3, 4, 5)
	RETURNF 1
ELSEIF GROUPMATCH(TALENT:MASTER:性別, 4, 5) && GROUPMATCH(TALENT:(ARG:0):性別, 0, 3)
	RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;婚姻の儀
;-------------------------------------------------
@WEDDING(ARG:0)
IF MONEY < 300000
	PRINTFORMW 所持金が不足しています
	RETURN
ENDIF

IF IS_HUSBAND(ARG:0)
	LOCALS:0 = 夫
ELSE
	LOCALS:0 = 正妻
ENDIF

PRINTFORML %ANAME(ARG:0)%を%LOCALS:0%として迎えます
PRINTL よろしいですか？
CALL ASK_YN
IF RESULT == 1
	RETURN
ENDIF

TALENT:(ARG:0):正妻 = 1
MONEY -= 300000

;口上の表示
CALL KOJO_EVENT(ARG:0, 90)

PRINTL 
CUSTOMDRAWLINE *-
PRINTL 婚姻の儀
CUSTOMDRAWLINE *-
WAIT
PRINTFORMW 式の当日、多くの観衆に祝福されながら、%ANAME(MASTER)%は%ANAME(ARG:0)%を%LOCALS:0%として迎えることを宣言した

;口上の表示
CALL KOJO_EVENT(ARG:0, 91)

PRINTL 
SETCOLOR COLOR("注意")
PRINTFORMW %ANAME(ARG:0)%が%ANAME(MASTER)%の%LOCALS:0%となりました
RESETCOLOR
PRINTL 
PRINTL 

;-------------------------------------------------
;触手部屋の処理
;-------------------------------------------------
@SHOP_SYOKUSYU
#DIM FIRSTLINE
;触手部屋の管理が可能なキャラの数を数える
LOCAL:9 = 0
FOR LOCAL:0, 0, CHARANUM
	IF IS_SYOKUSYU_MANAGE(LOCAL:0)
		LOCAL:9 ++
	ENDIF
NEXT

;ページ数を計算
LOCAL:12 = 30
LOCAL:10 = MAX((LOCAL:9 - 1) / LOCAL:12 + 1, 1)
LOCAL:11 = 1

$SHOW_LOOP1

DRAWLINE
PRINTL 触手部屋
DRAWLINE

PRINTL ●管理者の設定
PRINTL     触手部屋の管理を行うには  <妖術知識>・性知識Lv5以上・主人公以外なら触手Lv5以上  が必要です
PRINTL     触手部屋の管理を行うと、毎ターン若干の妖術経験値と触手経験値を得られます
IF ID_TO_CHARA(FLAG:触手部屋管理者) == -1
	PRINTFORML     現在の管理者はいません
ELSEIF IS_SYOKUSYU_MANAGE(ID_TO_CHARA(FLAG:触手部屋管理者))
	PRINTFORML     現在の管理者は %ANAME(ID_TO_CHARA(FLAG:触手部屋管理者))% です
ELSE
	PRINTFORML     現在の管理者は %ANAME(ID_TO_CHARA(FLAG:触手部屋管理者))% ですが、現在は管理不能です
ENDIF

DRAWLINE
FIRSTLINE = LINECOUNT
$SHOW_LOOP2

;キャラリストの表示
LOCAL:13 = 0
LOCAL:14 = 0
LOCAL:15 = (LOCAL:11 - 1) * LOCAL:12
LOCAL:16 = LOCAL:11 * LOCAL:12
LOCAL:20 = 3
FOR LOCAL:0, 0, CHARANUM
	IF IS_SYOKUSYU_MANAGE(LOCAL:0)
		IF LOCAL:13 >= LOCAL:15 && LOCAL:13 < LOCAL:16
			IF LOCAL:14 % 2 != 0
				PRINT     
			ELSEIF LOCAL:14 >= 1
				PRINTL 
				LOCAL:20 ++
			ENDIF
			PRINT     
			IF LOCAL:0 == ID_TO_CHARA(FLAG:触手部屋管理者)
				SETCOLOR COLOR("シアン")
			ENDIF
			PRINTBUTTON @"{NO:(LOCAL:0) + 100, 3}[%ANAME(LOCAL:0), 14, LEFT%]  妖Lv{ABL:(LOCAL:0):妖術, 3, RIGHT}", NO:(LOCAL:0) + 100
			RESETCOLOR
			LOCAL:14 ++
		ENDIF
		LOCAL:13 ++
	ENDIF
NEXT
IF LOCAL:14 >= 1
	PRINTL 
	LOCAL:20 ++
ENDIF

IF LOCAL:10 >= 2
	LOCAL:20 += 2
	DRAWLINE
	PRINT      
	IF LOCAL:11 >= 2
		PRINTBUTTON "54[前のページ    ]", 54
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM 54[前のページ    ]
		RESETCOLOR
	ENDIF
	PRINTPLAINFORM %TOSTR_SPACE(10)%
	LOCALS:0 = page{LOCAL:11}/{LOCAL:10}
	PRINTPLAINFORM %LOCALS:0, 14, LEFT%
	IF LOCAL:11 < LOCAL:10
		PRINTBUTTON "56[次のページ    ]", 56
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM 56[次のページ    ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

DRAWLINE
PRINT      
PRINTBUTTON " 0[戻る]", 0

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN -1
ELSEIF RESULT == 54 && LOCAL:11 >= 2
	LOCAL:11 --
	CLEARLINE LINECOUNT - FIRSTLINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56 && LOCAL:11 < LOCAL:10
	LOCAL:11 ++
	CLEARLINE LINECOUNT - FIRSTLINE
	GOTO SHOW_LOOP2
ELSE
	LOCAL:5 = NO_TO_CHARA(RESULT - 100)

	IF LOCAL:5 >= 0 && IS_SYOKUSYU_MANAGE(LOCAL:5)
		REDRAW 1
		IF LOCAL:5 == ID_TO_CHARA(FLAG:触手部屋管理者)
			PRINTFORMW %ANAME(LOCAL:5)%を触手部屋の管理者から外しました
			FLAG:触手部屋管理者 = 0
		ELSE
			PRINTFORMW %ANAME(LOCAL:5)%を触手部屋の管理者に設定しました
			FLAG:触手部屋管理者 = GET_ID(LOCAL:5)
		ENDIF
		GOTO SHOW_LOOP1
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;キャラARG:0が触手部屋を管理可能であるかどうかを判定する関数
;-------------------------------------------------
@IS_SYOKUSYU_MANAGE(ARG:0)
#FUNCTION
IF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0
	IF TALENT:(ARG:0):妖術知識 && ABL:(ARG:0):性知識 >= 5 && (ARG:0 == MASTER || ABL:(ARG:0):触手 >= 5)
		RETURNF 1
	ENDIF
ENDIF
RETURNF 0

;-------------------------------------------------
;「民の慰安」のモブ追加処理
;-------------------------------------------------
@CREATE_IAN_MOB(行き先, 人数)
#DIM 行き先
#DIM 人数
#DIM 種類
#DIM 男女
#DIMS 名前
#DIM 対象
種類 = CREATE_IAN_MOB_GET_CHARA(行き先)
男女 = CREATE_IAN_MOB_GET_SEX(種類)
名前 = %CREATE_IAN_MOB_GET_NAME(種類, 男女)%%TOFULL(TOSTR(人数))%

CALL CREATE_RANDOM_CHARA(0, -1, 0)
FLAG:汎用武将カウント --
対象 = RESULT
IF LOCAL == -1
	RETURN -1
ENDIF

NO:対象 = GET_EMPTY_NO()
NAME:対象 = %名前%
CALLNAME:対象 = %名前%
CSTR:対象:0 =
CSTR:対象:1 =
TALENT:対象:性別 = 男女
MAXBASE:対象:体力 = 99999999
MAXBASE:対象:気力 = 99999999
MAXBASE:対象:精神力 = 1500
BASE:対象:体力 = MAXBASE:対象:体力
BASE:対象:気力 = MAXBASE:対象:気力
BASE:対象:精神力 = MAXBASE:対象:精神力
ABL:対象:性知識 = 5
ABL:対象:Ｃ感 = RAND(0,3)
ABL:対象:Ｖ感 = RAND(0,3)
ABL:対象:Ａ感 = RAND(0,3)
ABL:対象:Ｂ感 = RAND(0,3)
ABL:対象:Ｍ感 = RAND(0,3)
ABL:対象:欲望 = RAND(0,3)
ABL:対象:性技 = RAND(0,3)
ABL:対象:性知識 = 5
ABL:対象:奉仕 = RAND(0,3)
ABL:対象:性交 = RAND(0,3)
ABL:対象:レズ = RAND:2
ABL:対象:精愛 = RAND:2
ABL:対象:露出 = RAND:2
ABL:対象:射精 = RAND:2
ABL:対象:排泄 = RAND:2
ABL:対象:主導度Ｕ = RAND(-300, 300)
ABL:対象:主導度Ｎ = RAND(-300, 300)
ABL:対象:倒錯度 = RAND(0, 300)
EXP:対象:絶頂経験 = 500
ABL:対象:武闘 = 30
ABL:対象:防衛 = 30
ABL:対象:知略 = 30
ABL:対象:政治 = 30
ABL:対象:料理 = 10
ABL:対象:肝臓 = 3
TALENT:対象:絶倫 = 1
TALENT:対象:陰毛現在値 = 陰毛_標準
TALENT:対象:陰毛目標値 = 陰毛_標準
TALENT:対象:Ｖ締まり = GET_DEFAULT_TIGHTNESS("普通")
TALENT:対象:Ａ締まり = GET_DEFAULT_TIGHTNESS("普通")
TALENT:(対象):処女 = 0
TALENT:(対象):キス未経験 = 0
TALENT:(対象):童貞 = 0
TALENT:(対象):アナル処女 = 1
SEXUAL_EXPERIENCE:(対象):初体験_処女 = 不明
SEXUAL_EXPERIENCE:(対象):初体験_ファーストキス = 不明
SEXUAL_EXPERIENCE:(対象):初体験_童貞 = 不明
SEXUAL_EXPERIENCE:(対象):初体験_アナル処女 = ----
CALL CREATE_IAN_MOB_SET_STATUS(種類, 対象)
CFLAG:対象:慰安モブ = 1
CFLAG:対象:調教参加フラグ  = 1
CFLAG:対象:強制友好化 = 1

;-------------------------------------------------
;「民の慰安」のモブ種類取得処理
;-------------------------------------------------
@CREATE_IAN_MOB_GET_CHARA(行き先)
#FUNCTION
#DIM 行き先
SELECTCASE 行き先
	CASE 慰安_行き先_スラム
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_貧民}, {慰安_キャラ_浮浪者}, {慰安_キャラ_ごろつき}", 1,
		 @"{慰安_キャラ_民}", RAND:100 < 20,
		 @"{慰安_キャラ_露出狂}", RAND:100 < 20)
}
	CASE 慰安_行き先_一般住宅街
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_民}", 1,
		 @"{慰安_キャラ_貧民}, {慰安_キャラ_浮浪者}, {慰安_キャラ_ごろつき}", RAND:100 < 20,
		 @"{慰安_キャラ_露出狂}", RAND:100 < 10,
		 @"{慰安_キャラ_貴族}, {慰安_キャラ_富豪}", RAND:100 < 10,
		 @"{慰安_キャラ_妖怪}", RAND:100 < 20,
		 @"{慰安_キャラ_子供}", RAND:100 < 30)
}
	CASE 慰安_行き先_貴族の居住地
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_貴族}, {慰安_キャラ_富豪}, {慰安_キャラ_資産家}", 1,
		 @"{慰安_キャラ_民}", RAND:100 < 15,
		 @"{慰安_キャラ_子供}", RAND:100 < 15)
}
	CASE 慰安_行き先_妖怪の山
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_天狗}, {慰安_キャラ_河童}", 1,
		 @"{慰安_キャラ_妖怪}", RAND:100 < 20)
}
	CASE 慰安_行き先_旧地獄
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_鬼}", 1,
		 @"{慰安_キャラ_妖怪}", RAND:100 < 20)
}
	CASE 慰安_行き先_竹林
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_兎}", 1,
		 @"{慰安_キャラ_妖怪}", RAND:100 < 20)
}
	CASE 慰安_行き先_野盗のアジト
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_野盗}", 1,
		 @"{慰安_キャラ_ごろつき}, {慰安_キャラ_浮浪者}", RAND:100 < 20)
}
	CASE 慰安_行き先_子供たちの秘密基地
		RETURNF 慰安_キャラ_子供
	CASE 慰安_行き先_兵舎
		RETURNF 慰安_キャラ_兵士
	CASE 慰安_行き先_犬舎
		RETURNF 慰安_キャラ_犬
	CASE 慰安_行き先_畜舎
		RETURNF 慰安_キャラ_豚
	CASE 慰安_行き先_魔界
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_悪魔}", 1,
		 @"{慰安_キャラ_妖怪}", RAND:100 < 15)
}
	CASE 慰安_行き先_魔法の森
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_妖精}", 1,
		 @"{慰安_キャラ_妖怪}", RAND:100 < 15)
}
	CASE 慰安_行き先_彼岸
{
		RETURNF IFRAND(
		 @"{慰安_キャラ_死神}", 1,
		 @"{慰安_キャラ_幽霊}", RAND:100 < 30)
}
	CASE 慰安_行き先_紅魔館
{
		RETURNF IFRAND(
		@"{慰安_キャラ_ホフゴブリン}", 1,
		@"{慰安_キャラ_妖精}", RAND:100 < 30
		)
}
	CASE 慰安_行き先_月の都
{
		RETURNF IFRAND(
		@"{慰安_キャラ_月人}, {慰安_キャラ_月兎}", 1
		)
}
	CASE 慰安_行き先_天界
		RETURNF 慰安_キャラ_天人
ENDSELECT
RETURNF 慰安_キャラ_民

;-------------------------------------------------
;「民の慰安」のモブ種類に応じた性別の取得処理
;-------------------------------------------------
@CREATE_IAN_MOB_GET_SEX(種類)
#FUNCTION
#DIM 種類
SELECTCASE 種類
	CASE 慰安_キャラ_貴族, 慰安_キャラ_天狗, 慰安_キャラ_死神, 慰安_キャラ_河童, 慰安_キャラ_民, 慰安_キャラ_露出狂, 慰安_キャラ_子供, 慰安_キャラ_月人, 慰安_キャラ_月兎, 慰安_キャラ_天人
		RETURNF IFRAND("0", 1, "1", RAND:100 < 50, "2", RAND:100 < 20)
	CASE 慰安_キャラ_妖精
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;「民の慰安」のモブ種類に応じた名前の取得処理
;-------------------------------------------------
@CREATE_IAN_MOB_GET_NAME(種類, 性別)
#FUNCTIONS
#DIM 種類
#DIM 性別
SELECTCASE 種類
	CASE 慰安_キャラ_民
		RETURNF "民"
	CASE 慰安_キャラ_貧民
		RETURNF "貧民"
	CASE 慰安_キャラ_浮浪者
		RETURNF "浮浪者"
	CASE 慰安_キャラ_ごろつき
		RETURNF "ごろつき"
	CASE 慰安_キャラ_富豪
		RETURNF "富豪"
	CASE 慰安_キャラ_貴族
		RETURNF 性別 == 0 ? "貴族" # "貴婦人"
	CASE 慰安_キャラ_資産家
		RETURNF "資産家"
	CASE 慰安_キャラ_天狗
		RETURNF "天狗"
	CASE 慰安_キャラ_河童
		RETURNF "河童"
	CASE 慰安_キャラ_鬼
		RETURNF "鬼"
	CASE 慰安_キャラ_妖怪
		RETURNF "妖怪"
	CASE 慰安_キャラ_露出狂
		RETURNF 性別 == 0 ? "露出狂" # "痴女"
	CASE 慰安_キャラ_兎
		RETURNF "兎"
	CASE 慰安_キャラ_野盗
		RETURNF "野盗"
	CASE 慰安_キャラ_子供
		RETURNF "子供"
	CASE 慰安_キャラ_兵士
		RETURNF "兵士"
	CASE 慰安_キャラ_犬
		RETURNF "犬"
	CASE 慰安_キャラ_豚
		RETURNF "豚"
	CASE 慰安_キャラ_悪魔
		RETURNF "悪魔"
	CASE 慰安_キャラ_妖精
		RETURNF "妖精"
	CASE 慰安_キャラ_死神
		RETURNF "死神"
	CASE 慰安_キャラ_幽霊
		RETURNF "幽霊"
	CASE 慰安_キャラ_ホフゴブリン
		RETURNF "ホフゴブリン"
	CASE 慰安_キャラ_月人
		RETURNF "月人"
	CASE 慰安_キャラ_月兎
		RETURNF "月兎"
	CASE 慰安_キャラ_天人
		RETURNF 性別 == 0 ? "天人" # "天女"
ENDSELECT

;-------------------------------------------------
;「民の慰安」のモブ種類に応じた能力セット処理
;-------------------------------------------------
@CREATE_IAN_MOB_SET_STATUS(種類, 対象)
#DIM 種類
#DIM 対象
SELECTCASE 種類
	CASE 慰安_キャラ_貧民, 慰安_キャラ_浮浪者
		ABL:対象:奉仕 = 0
		ABL:対象:性技 = RAND:2
		ABL:対象:欲望 = RAND(3, 7)
		ABL:対象:性交 = RAND(4, 7)
		ABL:対象:射精 = RAND(4, 7)
		ABL:対象:主導度Ｕ = RAND(0, 500)
		ABL:対象:倒錯度 = RAND(0, 300)
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
	CASE 慰安_キャラ_ごろつき, 慰安_キャラ_兵士
		ABL:対象:奉仕 = 1
		ABL:対象:性技 = RAND(0, 4)
		ABL:対象:欲望 = RAND(3, 7)
		ABL:対象:性交 = RAND(3, 5)
		ABL:対象:射精 = RAND(3, 5)
		ABL:対象:主導度Ｕ = RAND(0, 500)
		ABL:対象:倒錯度 = RAND(0, 300)
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
	CASE 慰安_キャラ_富豪, 慰安_キャラ_貴族, 慰安_キャラ_資産家
		ABL:対象:欲望 = RAND(2,6)
		ABL:対象:性技 = RAND(2,5)
		ABL:対象:奉仕 = RAND(0,3)
		ABL:対象:性交 = RAND(1,3)
		ABL:対象:レズ = RAND(0,2)
		ABL:対象:精愛 = RAND(1,3)
		ABL:対象:露出 = RAND:2
		ABL:対象:射精 = RAND(0,2)
		ABL:対象:排泄 = RAND(0,2)
		ABL:対象:主導度Ｕ = RAND(0, 500)
		ABL:対象:倒錯度 = RAND(0, 500)
		TALENT:対象:プライド高い = 1
		TALENT:対象:プライド低い = 0
	CASE 慰安_キャラ_天狗, 慰安_キャラ_河童, 慰安_キャラ_死神, 慰安_キャラ_幽霊
		ABL:対象:欲望 = RAND(4,7)
		ABL:対象:性技 = RAND(3,8)
		ABL:対象:奉仕 = RAND(1,4)
		ABL:対象:性交 = RAND(0,4)
		ABL:対象:精愛 = RAND(0,2)
		ABL:対象:射精 = RAND(0,3)
		ABL:対象:排泄 = RAND(0,2)
		ABL:対象:主導度Ｕ = RAND(300, 500)
		ABL:対象:倒錯度 = RAND(300, 500)
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:プライド高い = 1
		TALENT:対象:プライド低い = 0
		SIF 種類 == 慰安_キャラ_河童
			TALENT:対象:技師 = 1
	CASE 慰安_キャラ_鬼
		ABL:対象:Ｃ感 = RAND(0,2)
		ABL:対象:Ｖ感 = RAND(0,2)
		ABL:対象:Ａ感 = RAND(0,2)
		ABL:対象:Ｂ感 = RAND(0,2)
		ABL:対象:Ｍ感 = RAND(0,2)
		ABL:対象:欲望 = RAND(4,8)
		ABL:対象:性技 = RAND(1,4)
		ABL:対象:奉仕 = RAND:2
		ABL:対象:性交 = RAND(5,8)
		ABL:対象:射精 = RAND(0,5)
		ABL:対象:主導度Ｕ = RAND(300, 700)
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:プライド高い = 1
		TALENT:対象:プライド低い = 0
	CASE 慰安_キャラ_露出狂
		ABL:対象:欲望 = RAND(5,8)
		ABL:対象:露出 = RAND(5,10)
		ABL:対象:排泄 = RAND(4,8)
		ABL:対象:主導度Ｕ = RAND(600, 1000)
		ABL:対象:倒錯度 = RAND(600, 1000)
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:恥薄い = 1
		TALENT:対象:恥じらい = 0
		TALENT:対象:好奇心 = 1
		TALENT:対象:解放 = 1
	CASE 慰安_キャラ_兎, 慰安_キャラ_月兎
		ABL:対象:欲望 = RAND(5,10)
		ABL:対象:性技 = RAND:2
		ABL:対象:性交 = RAND(5,10)
		ABL:対象:射精 = RAND(5,9)
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
	CASE 慰安_キャラ_野盗
		ABL:対象:Ｃ感 = RAND(1, 5)
		ABL:対象:欲望 = 5
		ABL:対象:性技 = RAND(2, 5)
		ABL:対象:奉仕 = 3
		ABL:対象:性交 = 5
		ABL:対象:露出 = 2
		ABL:対象:射精 = 3
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 500
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		TALENT:対象:特殊勢力素質 = 特殊勢力_野盗
	CASE 慰安_キャラ_子供, 慰安_キャラ_妖精
		ABL:対象:性知識 = RAND(1, 3)
		ABL:対象:欲望 = RAND:3
		ABL:対象:性技 = RAND:3
		ABL:対象:性交 = RAND:2
		ABL:対象:射精 = RAND:2
		TALENT:(対象):処女 = 1
		SEXUAL_EXPERIENCE:(対象):初体験_処女 = ----
		TALENT:(対象):キス未経験 = 1
		SEXUAL_EXPERIENCE:(対象):初体験_ファーストキス = ----
		TALENT:(対象):童貞 = 1
		SEXUAL_EXPERIENCE:(対象):初体験_童貞 = ----
		TALENT:(対象):アナル処女 = 1
		SEXUAL_EXPERIENCE:(対象):初体験_アナル処女 = ----
	CASE 慰安_キャラ_犬, 慰安_キャラ_豚
		ABL:対象:Ｃ感 = RAND(3, 6)
		ABL:対象:欲望 = RAND(3, 8)
		ABL:対象:性技 = RAND:3
		ABL:対象:奉仕 = RAND(2, 5)
		ABL:対象:性交 = RAND(5, 8)
		ABL:対象:射精 = RAND(3, 8)
		TALENT:対象:動物耳 = 1
		TALENT:対象:しっぽ = 1
		TALENT:対象:(種類 == 慰安_キャラ_犬 ? GETNUM(TALENT, "犬") # GETNUM(TALENT, "豚")) = 1
	CASE 慰安_キャラ_悪魔
		ABL:対象:欲望 = RAND(4,7)
		ABL:対象:性技 = RAND(3,8)
		ABL:対象:奉仕 = RAND(1,4)
		ABL:対象:性交 = RAND(0,4)
		ABL:対象:精愛 = RAND(0,2)
		ABL:対象:射精 = RAND(0,3)
		ABL:対象:排泄 = RAND(0,2)
		ABL:対象:主導度Ｕ = RAND(300, 500)
		ABL:対象:倒錯度 = RAND(300, 500)
		TALENT:対象:Ｓ気質 = 1
		SETBIT TALENT:対象:淫乱系, 素質_淫乱_サド
		TALENT:対象:貞操無頓着 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:プライド高い = 1
		TALENT:対象:プライド低い = 0
	CASE 慰安_キャラ_ホフゴブリン
		ABL:対象:Ｃ感 = 4
		ABL:対象:欲望 = 6
		ABL:対象:性技 = 4
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 2
		ABL:対象:性交 = 4
		ABL:対象:露出 = 1
		ABL:対象:射精 = 4
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 700
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		EXP:対象:絶頂経験 = 500
		TALENT:対象:特殊勢力素質 = 特殊勢力_ホフゴブリン
	CASE 慰安_キャラ_天人, 慰安_キャラ_月人
		ABL:対象:欲望 = 5
		ABL:対象:性技 = 5
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 5
		ABL:対象:性交 = 0
		ABL:対象:射精 = 0
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 700
		ABL:対象:倒錯度 = 600
		TALENT:対象:技師 = 1
	CASEELSE
		;デフォ
ENDSELECT

;-------------------------------------------------
;「民の慰安」開始時のメッセージ表示処理
;-------------------------------------------------
@SHOW_IAN_MESSAGE(対象, 人数)
#DIM 対象
#DIM 人数
SELECTCASE IFRAND("0TO2", 1, "3", IS_FEMALE(対象))
	CASE 0
		PRINTFORML 民を喜ばせることが勢力を強くする
		PRINTFORML なので、お前の身体で民を喜ばせてこい
	CASE 1
		PRINTFORML 民衆にもガス抜きは必須だ
		PRINTFORML お前が責任もって「抜いて」やれ
	CASE 2
		PRINTFORML お前に民を慰安する任務を与える
		PRINTFORML しっかりと満足させてやれ
	CASE 3
		PRINTFORML 優秀な士官の子は優秀になるに決まっている
		PRINTFORML だから孕まされてこい
ENDSELECT
PRINTFORMW そう命じ、%ANAME(対象) + (人数 > 1 ? "たち" # "")%を送り出した……

SIF RAND:3
	RETURN

IF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_淫乱) && !RAND:3
	PRINTFORMW %ANAME(対象)%は淫らな行為を期待し、身体を疼かせているようだ……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_精液便女) && !RAND:3
	PRINTFORMW 便女として扱われる期待に、%ANAME(対象)%は興奮を隠せないでいる……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_娼婦) && !RAND:3
	PRINTFORMW 娼婦とは若干異なる仕事に%ANAME(対象)%は戸惑いを覚えているようだが、それでも前向きな姿勢をみせている……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_マゾ) && !RAND:3
	PRINTFORMW %ANAME(対象)%は誰かに虐げられることを期待し、身体を疼かせているようだ……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_孕みたがり) && !RAND:3
	PRINTFORMW 任務の結果子を宿すことを想像し、%ANAME(対象)%は興奮を覚えているようだ……
ELSEIF IS_LOVER(対象) && !RAND:3
	SELECTCASE ABL:対象:欲望
		CASE IS > ランク閾値:ランク_その他:ランク_B
			PRINTFORMW %ANAME(MASTER)%以外に抱かれるというのに、%ANAME(対象)%は興奮を覚えているようだ……
		CASE IS > ランク閾値:ランク_その他:ランク_D
			PRINTFORMW %ANAME(対象)%は%ANAME(MASTER)%以外に抱かれることを拒もうとしているが、身体は意志とかかわらず高ぶっている……
		CASEELSE
			PRINTFORMW %ANAME(対象)%は%ANAME(MASTER)%の命令の意図を理解できず悲しんでいるが、それでも受け入れた……
	ENDSELECT
ELSEIF IS_SLAVE(対象) && !RAND:3
	SELECTCASE ABL:対象:欲望
		CASE IS > ランク閾値:ランク_その他:ランク_B
			PRINTFORMW %ANAME(MASTER)%の命令以前に、誰かに抱かれるということに、%ANAME(対象)%は興奮を覚えているようだ……
		CASE IS > ランク閾値:ランク_その他:ランク_D
			PRINTFORMW %ANAME(対象)%は%ANAME(MASTER)%の命令であると言っているが、身体はそんなことと関係なしに高ぶっている……
		CASEELSE
			PRINTFORMW %ANAME(対象)%は%ANAME(MASTER)%の命令ならば仕方がないと受け入れた……
	ENDSELECT
ELSEIF !RAND:3
	PRINTFORMW %ANAME(対象)%は、任務ならば仕方がないと受け入れた……
ENDIF
