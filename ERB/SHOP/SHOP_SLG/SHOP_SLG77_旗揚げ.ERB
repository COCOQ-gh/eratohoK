;-------------------------------------------------
;「旗揚げする」の名称
;-------------------------------------------------
@SHOP_SLG_NAME77
RESULTS:0 '= "旗揚げする"

;-------------------------------------------------
;「旗揚げする」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK77
SIF CFLAG:MASTER:所属 != 0
	RETURN 0
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
;勢力に空きがなければ不可
SIF GET_NEW_COUNTRY() == -1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「旗揚げする」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY77
CALL SHOP_CREATE_COUNTRY
RETURN 1

;-------------------------------------------------
;「旗揚げする」本体
;-------------------------------------------------
@SHOP_CREATE_COUNTRY
CALL SINGLE_DRAWLINE
PRINTL 旗揚げする都市を選択して下さい。無所属の都市のみ選択可能です
CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

PRINTBUTTON " 0[戻る]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF !IS_COUNTRY(LOCAL:4) && CITY_TYPE:(LOCAL:3) == 0 && GET_RELAYNAME(LOCAL:3) != "無名"
		PRINTFORML %GET_RELAYNAME(LOCAL:3)%で旗揚げしますか？
		PRINTBUTTON " 1[旗揚げする]", 1
		PRINTPLAIN     
		PRINTBUTTON " 2[キャンセル]", 2
		PRINTL 
		PRINTBUTTON " 0[戻る]", 0

		REDRAW 0

		$INPUT_LOOP2
		INPUT

		IF RESULT == 0
			RETURN
		ELSEIF RESULT == 1
			LOCAL:5 = GET_NEW_COUNTRY()
			CITY_OWNER:(LOCAL:3) = LOCAL:5
			COUNTRY_BOSS:(LOCAL:5) = GET_ID(MASTER)
			COUNTRY_SOLDIER:(LOCAL:5) = CITY_ECONOMY:(LOCAL:3) / 20
			CITY_SOLDIER:(LOCAL:3) = 2000
			CALL CHANGE_COUNTRY(MASTER, LOCAL:5)

			CALL SINGLE_DRAWLINE
			PRINTL 新しい勢力の色を選んで下さい
			CALL SINGLE_DRAWLINE
			CALL CHANGE_COUNTRY_COLOR(LOCAL:5, 1)

			TIME = 0
			RETURN
		ELSEIF RESULT == 2
			RESTART
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

