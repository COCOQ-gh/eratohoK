;-------------------------------------------------
;CHECK_SELECTABLE{n}関数の呼び出し関数
;ARG:0 呼び出し番号
;ARG:1 キャラ番号
;
;返り値の意味は以下
;0:通常の選択不可（リストに載らない）
;1:選択可能
;2:灰色でリストに載るが選択不可能
;-------------------------------------------------
@CHECK_SELECTABLE(ARG:0, ARG:1)
RESULT = 0
TRYCALLFORM CHECK_SELECTABLE%TOSTR(ARG:0)%(ARG:1)
RETURN RESULT

;-------------------------------------------------
;[会いに行く]の選択可否判定
;※使用前に TMP_CREATE_RELATION_MAP 関数を呼び出しておくこと！
;-------------------------------------------------
@CHECK_SELECTABLE1000(ARG:0)
LOCAL:0 = TMP_COUNTRY_RELATION:(CFLAG:MASTER:1):(CFLAG:(ARG:0):1)
;捕虜として捕まっている場合
IF CFLAG:(ARG:0):9
	;非主人公、主人公勢力の捕虜かつ軟禁中
	RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && CFLAG:(ARG:0):9 == CFLAG:MASTER:1 && CFLAG:(ARG:0):29 == 1)
ENDIF
;捕虜でない、非主人公、同一勢力or共闘勢力
RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && LOCAL:0 >= 2 && CHECKDVAR(ARG:0))

;-------------------------------------------------
;[閨に呼ぶ]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1001(ARG:0)
	;捕虜として捕まっている場合
	IF CFLAG:(ARG:0):9
		;非主人公、通常状態または育児中、合意、主人公勢力の捕虜かつ軟禁中
		RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):16, 0, 2) && TALENT:(ARG:0):合意 && CFLAG:(ARG:0):9 == CFLAG:MASTER:1 && CFLAG:(ARG:0):29 == 1)
	ENDIF
	;捕虜でない、非主人公、通常状態または育児中、合意、同一勢力
	RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):16, 0, 2) && TALENT:(ARG:0):合意 && CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && CHECKDVAR(ARG:0))
RETURN 0

;-------------------------------------------------
;[捕虜の調教]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1002(ARG:0)

	;非主人公、主人公勢力の捕虜、牢獄で監禁中
	RETURN CHECK91(ARG:0, 2, (ARG:0 != MASTER && CFLAG:(ARG:0):9 == CFLAG:MASTER:1 && CFLAG:(ARG:0):29 == 0) || CFLAG:(ARG:0):59)

RETURN 0

;-------------------------------------------------
;[夜這い]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1015(ARG:0)
	;非主人公、捕虜ではない、別勢力、君主ではない、どこかの勢力に所属している（放浪、未登場、死亡済みではない）
	IF (ARG:0 != MASTER && !CFLAG:(ARG:0):9 && CFLAG:MASTER:1 != CFLAG:(ARG:0):1 && GET_ID(ARG:0) != COUNTRY_BOSS:(CFLAG:(ARG:0):1) && CFLAG:(ARG:0):1 )
		SIF YOBAI_ABLE(ARG:0)
			RETURN CHECK91(ARG:0, 2, 1)
		RETURN 2
	ENDIF
	RETURN 0
RETURN 0

;-------------------------------------------------
;[手引きさせる]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1016(ARG:0)
	;非主人公、捕虜ではない、別勢力、君主ではない、どこかの勢力に所属している（放浪、未登場、死亡済みではない）
	IF (ARG:0 != MASTER && !CFLAG:(ARG:0):9 && CFLAG:MASTER:1 != CFLAG:(ARG:0):1 && GET_ID(ARG:0) != COUNTRY_BOSS:(CFLAG:(ARG:0):1) && CFLAG:(ARG:0):1 )
		SIF TEBIKI_ABLE(ARG:0)
			RETURN CHECK91(ARG:0, 2, 1)
		RETURN 2
	ENDIF
	RETURN 0
RETURN 0

;-------------------------------------------------
;[手引きさせる]の内通者の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE_NAITSU(ARG:0)
	;捕虜でない、非主人公、通常状態または育児中、烙印、別勢力
	SIF TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:1):(CFLAG:(ARG:0):1)
		RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && !CFLAG:(ARG:0):9 && GROUPMATCH(CFLAG:(ARG:0):16, 0, 2) && TALENT:(ARG:0):烙印 && CHECKDVAR(ARG:0) ) && CFLAG:MASTER:1 != CFLAG:(ARG:0):1
RETURN 0

;-------------------------------------------------
;[捕虜の調教]の助手の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE_ASSI(ARG:0)
;[閨に呼ぶ]の選択条件を満たしている

	CALL CHECK_SELECTABLE1001(ARG:0)
	RETURN CHECK91(ARG:0, 2, RESULT)

RETURN 0

;
;
;ここから行動済みでも可能
;
;

;-------------------------------------------------
;[地方へ派遣]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1010(ARG:0)
;非主人公、同一勢力、汎用キャラ、捕虜でない、部隊に所属していない、好感度300必要
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && NO:(ARG:0) >= 201 && CFLAG:(ARG:0):9 == 0 && TMP_IS_FREE:(ARG:0):0 == 0 && CFLAG:(ARG:0):2 >= 300  && CHECKDVAR(ARG:0)

;-------------------------------------------------
;[婚姻の儀]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1011(ARG:0)
;非主人公、同一勢力、捕虜でない、好感度3000以上、[恋人]or[烙印]、特殊状態フラグが立っていない
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && CFLAG:(ARG:0):9 == 0 && CFLAG:(ARG:0):2 >= 3000 && (TALENT:(ARG:0):恋人 || TALENT:(ARG:0):烙印) && CFLAG:(ARG:0):12 == 0 && CHECKDVAR(ARG:0)

;-------------------------------------------------
;[能力表示]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1020(ARG:0)
;非主人公(主人公は別処理)、同一勢力or共闘勢力or自国の捕虜
RETURN ARG:0 != MASTER && (TMP_COUNTRY_RELATION:(CFLAG:MASTER:1):(CFLAG:(ARG:0):1) >= 2 || CFLAG:(ARG:0):9 == CFLAG:MASTER:1)

;-------------------------------------------------
;[服を替える]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1022(ARG:0)
;非主人公(主人公は別処理)、同一勢力or自国の捕虜
RETURN ARG:0 != MASTER && (CFLAG:(ARG:0):1 == CFLAG:MASTER:1 || CFLAG:(ARG:0):9 == CFLAG:MASTER:1)

;-------------------------------------------------
;[投獄する]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1030(ARG:0)
;主人公より政治が低い、君主でない、捕虜でない
RETURN CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && ABL:(ARG:0):政治 < ABL:(MASTER):政治 && CFLAG:ARG:9 == 0 && ARG:0 != GET_COUNTRY_BOSS(CFLAG:MASTER:1) && CHECKDVAR(ARG:0)

;-------------------------------------------------
;[推しメン]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1040(ARG:0)
;非主人公、捕虜でない
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && CFLAG:(ARG:0):9 == 0

;-------------------------------------------------
;[能力表示]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1021(ARG:0)
;非主人公(主人公は別処理)、外来勢力の士官or外来勢力の捕虜、イベントキャラでない
RETURN ARG:0 != MASTER && (CFLAG:(ARG:0):1 == CFLAG:MASTER:9 || CFLAG:(ARG:0):9 == CFLAG:MASTER:9) && NO:(ARG:0) < 2000

;-------------------------------------------------
;[料理長]の選択可否判定
;-------------------------------------------------
@CHECK_SELECTABLE1050(ARG:0)
;非主人公、捕虜でない、イベキャラでない
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && CFLAG:(ARG:0):9 == 0 && NO:(ARG:0) < 2000 && CHECKDVAR(ARG:0)

;-------------------------------------------------
;[説得させる]の選択可否判定
;-------------------------------------------------
;説得を受ける側
@CHECK_SELECTABLE1017(ARG:0)
;非主人公、主人公勢力に所属していない、主人公勢力の捕虜、牢獄で軟禁中
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):1 != CFLAG:MASTER:1 && CFLAG:(ARG:0):9 == CFLAG:MASTER:1 && CFLAG:(ARG:0):29 == 1

;説得する側
@CHECK_SELECTABLE_SETTOKU(ARG:0)
;捕虜でない、非主人公、通常状態または育児中、同一勢力
RETURN CFLAG:(ARG:0):9 == 0 && ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):16, 0, 2) && CFLAG:(ARG:0):1 == CFLAG:MASTER:1 && CHECKDVAR(ARG:0)


;-------------------------------------------------
;行動済みチェック
;-------------------------------------------------
@CHECK91(ARG:0, ARG:1, ARG:2)
#FUNCTION
SIF ARG:2 <= 0
	RETURNF ARG:2
IF !CFLAG:(ARG:0):91
	RETURNF ARG:2
ELSE
	IF CONFIG:13 == 1
		RETURNF 0
	ELSEIF ARG:2 == 1
		RETURNF ARG:1
	ELSE
		RETURNF ARG:2
	ENDIF
ENDIF

;-------------------------------------------------
;DVAR回りのチェック
;-------------------------------------------------
@CHECKDVAR(ARG:0)
#FUNCTION
IF DVAR:金貸し娘 == ARG:0
	RETURNF 0
ENDIF
RETURNF 1