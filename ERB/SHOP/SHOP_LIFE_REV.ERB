;逆調教時のショップ画面処理

;-------------------------------------------------
;拠点フェイズのメニューコマンドを表示する関数 ARG:0は行数
;-------------------------------------------------
@PRINT_LIFE_MENU_B(ARG:0)
SELECTCASE ARG:0
	CASE 0
		CALL PRINTBUTTON_CENTER(" 0[……………]", 0, 6)
	;CASE 2
	;	CALL PRINTBUTTON_CENTER(" 1{様子を見る}", 1, 6, 1, FLAG:7 == 1030)
	CASE 7
		CALL PRINTBUTTON_CENTER(" 5{  能力表示}", 5, 6, 1, FLAG:7 == 1040)
	CASE 20
		IF FLAG:28
			CALL PRINTBUTTON_CENTER("50[次の周回へ]", 50, 6)
		ELSE
			PRINT 　　　　　　　
		ENDIF
	CASE 23
		CALL PRINTBUTTON_CENTER("70[      設定]", 70, 7)
	CASE 24
		CALL PRINTBUTTON_CENTER("80[    セーブ]", 80, 7)
	CASE 25
		CALL PRINTBUTTON_CENTER("90[    ロード]", 90, 7)
	CASEELSE
		PRINT 　　　　　　　
ENDSELECT
RESETCOLOR
PRINT ┃

;-------------------------------------------------
;ショップ画面の表示処理(拠点フェイズ・投獄中)
;-------------------------------------------------
@SHOW_SHOP_LIFE_B
;無効な入力時に消去する時の行数
FLAG:24 = 30

VARSET LOCAL, 0

;キャラクタ表示順のリストを作成
CALL CREATE_SELECOTR_SORT_LIST

;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0

;各コマンドが実行不能ならメニューの選択状態を解除する
IF GROUPMATCH(FLAG:7, 1000, 1001, 1002, 1010, 1011, 1020)
	FLAG:7 = 0
ENDIF

;様子を見る
IF FLAG:7 == 1030
	CALL PRINT_LIFE_MENU_B(0)
	PRINTFORML ★★様子を見たいキャラを選択して下さい★★
	CALL PRINT_LIFE_MENU_B(1)
	PRINTFORML %TOSTR_REPEAT("-", 70)%
	LOCAL:20 = 2
;能力表示
ELSEIF FLAG:7 == 1040
	CALL PRINT_LIFE_MENU_B(0)
	PRINTFORML ★★能力を表示するキャラを選択して下さい★★
	CALL PRINT_LIFE_MENU_B(1)
	PRINTFORML %TOSTR_REPEAT("-", 70)%
	CALL PRINT_LIFE_MENU_B(2)
	CALL PRINT_PARTNER_DATA(MASTER)
	PRINTL 
	LOCAL:20 = 3
ELSE
	LOCAL:20 = 0
ENDIF

;ページ数を計算
LOCAL:12 = (24 - LOCAL:20) * 2
LOCAL:10 = 0
FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうかを判定
	RESULT = 0
	TRYCALLFORM CHECK_SELECTABLE{FLAG:7}(LOCAL:0)
	IF RESULT
		LOCAL:10 ++
	ENDIF
NEXT
LOCAL:10 = MAX((LOCAL:10 - 1) / LOCAL:12 + 1, 1)

;ページ数を範囲に収める
IF FLAG:6 <= 0
	FLAG:6 = LOCAL:10 - ((-FLAG:6) % LOCAL:10)
ELSEIF FLAG:6 >= LOCAL:10 + 1
	FLAG:6 = (FLAG:6 + 1) % LOCAL:10 + 1
ENDIF
LOCAL:11 = FLAG:6

;キャラリストの表示
LOCAL:13 = 0
LOCAL:14 = 0
LOCAL:15 = (LOCAL:11 - 1) * LOCAL:12
LOCAL:16 = LOCAL:11 * LOCAL:12
FOR LOCAL:0, 0, CHARANUM
	LOCAL:5 = SHOP_CHARA_LIST:(LOCAL:0)
	;選択可能な条件を満たしているかどうかを判定
	RESULT = 0
	TRYCALLFORM CHECK_SELECTABLE{FLAG:7}(LOCAL:5)
	IF RESULT
		IF LOCAL:13 >= LOCAL:15 && LOCAL:13 < LOCAL:16
			IF LOCAL:14 % 2 != 0
				PRINTPLAIN 　
			ELSE
				IF LOCAL:14 >= 1
					PRINTL 
				ENDIF
				CALL PRINT_LIFE_MENU_B(LOCAL:20)
				LOCAL:20 ++
			ENDIF
			CALL PRINT_PARTNER_DATA(LOCAL:5, LOCAL:21, 100, 1, 1, 1)
			LOCAL:14 ++
		ENDIF
		LOCAL:13 ++
	ENDIF
NEXT
IF LOCAL:14 >= 1
	PRINTL 
ENDIF

IF LOCAL:20 < 26
	FOR LOCAL:0, LOCAL:20, 26
		CALL PRINT_LIFE_MENU_B(LOCAL:0)
		IF LOCAL:0 == 24
			PRINTFORM %TOSTR_REPEAT("-", 70)%
		ELSEIF LOCAL:0 == 25
			PRINT   
			IF LOCAL:10 >= 2
				PRINTBUTTON "54[前のページ    ]", 54
			ELSE
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM 54[前のページ    ]
				RESETCOLOR
			ENDIF
			PRINTPLAINFORM %TOSTR_SPACE(10)%
			LOCALS:0 = page{LOCAL:11}/{LOCAL:10}
			PRINTPLAINFORM %LOCALS:0, 14, LEFT%
			IF LOCAL:10 >= 2
				PRINTBUTTON "56[次のページ    ]", 56
			ELSE
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM 56[次のページ    ]
				RESETCOLOR
			ENDIF
		ENDIF
		PRINTL 
	NEXT
ENDIF

;-------------------------------------------------
;ショップ画面の入力処理(拠点フェイズ・投獄中 0～99)
;ARG:0に入力した値が入る
;処理を行った場合は1を、行わなかった場合は0を返すこと
;-------------------------------------------------
@EVENTBUY_LIFE_B(ARG:0)
;会いに行く
IF ARG:0 == 0
	CALL TRAIN_IN_PRISON
	IF RESULT == 0
		RESULT = 1
		BEGIN TURNEND
	ENDIF
;様子を見る
;ELSEIF ARG:0 == 1
;	FLAG:7 = 1030
;	FLAG:6 = 1
;	RETURN 0
;能力表示
ELSEIF ARG:0 == 5
	FLAG:7 = 1040
	FLAG:6 = 1
	RETURN 0
;次の周回へ
ELSEIF ARG:0 == 50 && FLAG:28
	CALL GO_NEXT_LOOP
;前のページ
ELSEIF ARG:0 == 54
	FLAG:6 --
	RETURN 0
;次のページ
ELSEIF ARG:0 == 56
	FLAG:6 ++
	RETURN 0
ELSE
	RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;ショップ画面の入力処理(拠点フェイズ・投獄中 100～)
;ARG:0に入力した値が入る
;処理を行った場合は1を、行わなかった場合は0を返すこと
;-------------------------------------------------
@USERSHOP_LIFE_B(ARG:0)
CALL USERSHOP_LIFE_CHARA1(NO_TO_CHARA(ARG:0 - 100))
IF !RESULT
	RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;主人公が投獄中のときの逆調教イベント
;0を返すと調教開始せずにターンエンド
;-------------------------------------------------
@TRAIN_IN_PRISON
#DIM CHARA_LIST, 1000

TRYCCALLFORM REV_TRAIN_%SP_COUNTRY_NAME_ENG:SP_COUNTRY_TO_CONST(CFLAG:MASTER:9)%
	SIF !RESULT
	;行動開始時の共通処理
		CALL START_TRAIN_COMMON
	RETURN 1
CATCH
	;特殊勢力以外の捕虜の場合
	;全員の調教参加フラグを解除
	CVARSET CFLAG, 6, 0

	LOCAL:10 = -1
	LOCAL:11 = 0
	CALL SELECT_REV_TRAINER()
	LOCAL:10 = RESULT:0
	LOCAL:11 = RESULT:1

	;FLAG:46に記録されたIDからメイン調教者のキャラ番号を取得
	LOCAL:12 = ID_TO_CHARA(FLAG:46)
	IF LOCAL:12 >= 0 && CFLAG:(LOCAL:12):1 == CFLAG:MASTER:9 && CFLAG:(LOCAL:12):9 == 0 && GROUPMATCH(CFLAG:(LOCAL:12):16, 0, 2)
		;メイン調教者を調教に参加させる
		CFLAG:(LOCAL:12):6 = 1
		LOCAL:10 = LOCAL:12
		LOCAL:11 ++
	ENDIF

	IF CFLAG:MASTER:18 >= 7
		IF LOCAL:10 >= 0 && CFLAG:MASTER:18 == 7
			LOCALS:0 = 
			IF LOCAL:11 >= 2
				LOCALS:0 = たち
			ENDIF
			DRAWLINE
			PRINTFORML %ANAME(LOCAL:10)%%LOCALS:0%はすっかり大きくなった%ANAME(MASTER)%のお腹を見て、
			PRINTFORMW 子供が産まれるまで調教を見送ることに決めたようだ
			PRINTFORMW %ANAME(LOCAL:10)%%LOCALS:0%は%ANAME(MASTER)%に、元気な子を産むように言い聞かせて帰っていった…

		ELSE
			DRAWLINE
			PRINTFORMW %ANAME(MASTER)%は牢屋の中で日々を過ごした…
		ENDIF
		;解放カウンタをリセット
		CFLAG:MASTER:43 = 0

	;参加者が一人以上いれば逆調教を開始
	ELSEIF LOCAL:11 > 0
		IF FLAG:47 == 0
			LOCALS:0 = 
			IF LOCAL:11 >= 2
				LOCALS:0 = たち
			ENDIF
			DRAWLINE
			PRINTFORMW %ANAME(MASTER)%の牢屋の前に、%ANAME(LOCAL:10)%%LOCALS:0%がやって来た
			PRINTFORMW %ANAME(LOCAL:10)%%LOCALS:0%は%ANAME(MASTER)%に対し、%ANAME(MASTER)%が仕官する気になるまで調教すると宣言してきた…
			;説得してからの期間を0にする
			FLAG:32 = 0
		ENDIF

		;捕虜逆調教モード
		FLAG:12 = 5
		;ウフフモード
		FLAG:50 = 1
		;回数をカウント
		FLAG:47 ++
		;説得してからの期間を増加
		FLAG:32 ++
		;解放カウンタをリセット
		CFLAG:MASTER:43 = 0
		;アイテムを一通り追加
		ITEM:A_ローター = 1
		ITEM:A_バイブ = 1
		ITEM:A_アナルバイブ = 1
		ITEM:A_ペニスバンド = 1
		ITEM:A_オナホール = 1
		ITEM:A_クリキャップ = 1
		ITEM:A_ニプルキャップ = 1
		ITEM:A_鞭 = 1
		ITEM:A_ローション = 99
		;行動開始時の共通処理
		CALL START_TRAIN_COMMON
		RETURN 1

	ELSE
		IF RAND:3 == 0
			;恋慕または服従のキャラがいれば時々やってくる
			LOCAL:11 = 0
			FOR LOCAL:0, 0, CHARANUM
				IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):1 == CFLAG:MASTER:9 && CFLAG:(LOCAL:0):9 == 0 && GROUPMATCH(CFLAG:(LOCAL:0):16, 0, 2) && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):服従)
					CHARA_LIST:(LOCAL:11) = LOCAL:0
					LOCAL:11 ++
				ENDIF
			NEXT
			IF LOCAL:11 >= 1
				LOCAL:12 = CHARA_LIST:(RAND:(LOCAL:11))
				CFLAG:(LOCAL:12):6 = 1
				LOCAL:10 = 1

				DRAWLINE
				PRINTFORMW %ANAME(MASTER)%の牢屋の前に、%ANAME(LOCAL:12)%がやって来た
				PRINTFORMW %ANAME(LOCAL:12)%は%ANAME(MASTER)%の牢を空け中に入ると、少し話がしたいと言ってきた…

				;捕虜会話モード
				FLAG:12 = 6
				;非ウフフモード
				FLAG:50 = 0
				;行動開始時の共通処理
				CALL START_TRAIN_COMMON
				RETURN 1
			ENDIF
		ENDIF
		DRAWLINE
		PRINTFORMW %ANAME(MASTER)%は牢屋の中で日々を過ごした…
	ENDIF
	SHOP_TIME = CALC_SHOP_TIME()
	RETURN 0
ENDCATCH

;-------------------------------------------------
;様子を見る ARG:0=対象のキャラ番号
;-------------------------------------------------
@SHOP_WATCH_OTHERS(ARG:0)
;SHOW_INFOの初期画面を基本情報にする
FLAG:11 = 0

$SHOW_LOOP_INFO_A

;対象キャラの情報を表示
DRAWLINE
CALL SHOW_INFO(ARG:0)
DRAWLINE

PRINTFORML %ANAME(ARG:0)%の様子を見ますか？
CALL ASK_YN
IF RESULT == 0
	CALL GAIRAI_TRAIN_MSG(ARG:0)
	WAIT
	;IF CFLAG:(ARG:0):9 == 0
	;	SELECTCASE RAND:2
	;		CASE 0
	;			PRINTFORML %ANAME(ARG:0)%は外来人たちに前と後ろの穴を次から次へと犯され、
	;			PRINTFORMW 大量の子種を注ぎ込まれている…
	;			PRINTFORML %ANAME(ARG:0)%は蕩けた表情で快感に身を委ねながら、
	;			PRINTFORMW 待っている外来人のペニスを自ら手でシゴいて奉仕している…
	;	ENDSELECT
	;ELSEIF CFLAG:(ARG:0):3 <= -200
	;	SELECTCASE RAND:2
	;		CASE 0
	;			PRINTFORML %ANAME(ARG:0)%は外来人たちに前と後ろの穴を次から次へと犯され、
	;			PRINTFORMW 容赦なく子種を注ぎ込まれている…
	;			PRINTFORML %ANAME(ARG:0)%は必死に陵辱を耐えているが、
	;			PRINTFORMW その声には快感の色が混じっている…
	;	ENDSELECT
	;ELSEIF CFLAG:(ARG:0):3 < 1500 || TALENT:(ARG:0):2 < 1500
	;	SELECTCASE RAND:2
	;		CASE 0
	;			PRINTFORML %ANAME(ARG:0)%は外来人たちに前と後ろの穴を次から次へと犯され、
	;			PRINTFORMW 容赦なく子種を注ぎ込まれている…
	;			PRINTFORML %ANAME(ARG:0)%はひたすら屈辱に耐えながら
	;			PRINTFORMW 延々と陵辱を受け続けている…
	;	ENDSELECT
	;ELSE
	;	SELECTCASE RAND:2
	;		CASE 0
	;			PRINTFORML %ANAME(ARG:0)%は外来人たちに前と後ろの穴を次から次へと犯され、
	;			PRINTFORMW 容赦なく子種を注ぎ込まれている…
	;			PRINTFORML %ANAME(ARG:0)%は%ANAME(MASTER)%の名を呼びながら、
	;			PRINTFORMW 延々と続く陵辱に耐え忍んでいる…
	;		CASE 1
	;			PRINTFORML %ANAME(ARG:0)%は外来人たちに前と後ろの穴を次から次へと犯され、
	;			PRINTFORMW 容赦なく子種を注ぎ込まれている…
	;			PRINTFORML %ANAME(ARG:0)%は%ANAME(MASTER)%の名を呼びながら、
	;			PRINTFORMW 延々と続く陵辱に耐え忍んでいる…
	;	ENDSELECT
	;ENDIF

	;逆調教の開始
	CALL TRAIN_IN_PRISON
ENDIF

RETURN 1

@SELECT_REV_TRAINER()
#DIM LCOUNT, 3
#DIM 人数, 1
#DIM 好感度リスト, 1000
#DIM 候補者リスト, 10
#DIM 候補者人数
#DIM 調教人数, 1
#DIM メイン, 1
VARSET 人数, 0
VARSET 候補者人数, 0
VARSET 好感度リスト, __INT_MIN__
VARSET 候補者リスト, -1
VARSET 調教人数, 0
VARSET メイン, -1

FOR LCOUNT, 0, CHARANUM
	SIF GET_ID(LCOUNT) == FLAG:46
		CONTINUE
	SIF CFLAG:(LCOUNT):所属 != CFLAG:MASTER:捕虜先 || CFLAG:(LCOUNT):捕虜先 || CFLAG:(LCOUNT):特殊状態フラグ
		CONTINUE
	SIF !CFLAG:(LCOUNT):面識 && RAND:3
		CONTINUE
	SIF ABL:(LCOUNT):性知識 < 2
		CONTINUE
	SIF CONFIG:17 && !CFLAG:(LCOUNT):面識
		CONTINUE
	好感度リスト:LCOUNT = CFLAG:LCOUNT:好感度
	人数 ++
NEXT

FOR LCOUNT, 0, 10
	LOCAL = FINDELEMENT(好感度リスト, MAXARRAY(好感度リスト))
	SIF 好感度リスト:LOCAL == __INT_MIN__
		BREAK
	好感度リスト:LOCAL = __INT_MIN__
	候補者リスト:候補者人数 = LOCAL
	候補者人数 ++
NEXT

;候補者ゼロなら戻す
SIF 候補者人数 == 0
	RETURN -1, 0

CALL FISHER_YATES_SHAFFLE(候補者人数)

FOR LCOUNT, 0, 候補者人数
	LOCAL = 候補者リスト:(SHAFFLE_ARRAY:LCOUNT)
	IF LOCAL != -1
		CFLAG:LOCAL:調教参加フラグ = 1
		SIF 調教人数 == 0
			メイン = LOCAL
		調教人数 ++
	ENDIF
	SIF 調教人数 == 5
		BREAK
NEXT

RETURN メイン, 調教人数
