;-------------------------------------------------
;キャラクタ表示順のリストを作成
;-------------------------------------------------
@CREATE_SELECTOR_SORT_LIST
VARSET SHOP_CHARA_LIST, -1

LOCAL:5 = 0

;SHOP_CHARA_LIST:(LOCAL:5) = MASTER
;LOCAL:5 ++

;主人公と同一勢力の士官が第１優先
FOR LOCAL:0, 0, CHARANUM
	;主人公でない「あなた」は飛ばす
	SIF LOCAL == NAME_TO_CHARA("あなた") && LOCAL != MASTER
		CONTINUE
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		SHOP_CHARA_LIST:(LOCAL:5) = LOCAL:0
		LOCAL:5 ++
	ENDIF
NEXT

;他勢力・捕虜でない士官が第２優先
FOR LOCAL:0, 0, CHARANUM
	;主人公でない「あなた」は飛ばす
	SIF LOCAL == NAME_TO_CHARA("あなた") && LOCAL != MASTER
		CONTINUE
	IF CFLAG:(LOCAL:0):所属 != CFLAG:MASTER:所属 && !CFLAG:(LOCAL:0):捕虜先
		SIF LOCAL == NAME_TO_CHARA("あなた") && LOCAL != MASTER
			CONTINUE
		SHOP_CHARA_LIST:(LOCAL:5) = LOCAL:0
		LOCAL:5 ++
	ENDIF
NEXT

;他勢力・捕虜がラスト
FOR LOCAL:0, 0, CHARANUM
	;主人公でない「あなた」は飛ばす
	SIF LOCAL == NAME_TO_CHARA("あなた") && LOCAL != MASTER
		CONTINUE
	IF CFLAG:(LOCAL:0):所属 != CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先
		SHOP_CHARA_LIST:(LOCAL:5) = LOCAL:0
		LOCAL:5 ++
	ENDIF
NEXT

;-------------------------------------------------
;右カラムに何も表示しない場合
;-------------------------------------------------
@COLUMN_RIGHT_CLEAR
FOR LOCAL:0, LINECOUNT - FIRST_COLUMN_RIGHT_LINE, LINES_SHOP_DRAW
	CALL COLUMN_RIGHT_PRINTL
NEXT
RETURN 0

;-------------------------------------------------
;右カラムリスト用のキャラボタン
;CHARALIST_BUTTON_CHARA 			キャラ番号
;CHARALIST_BUTTON_ONCOLOR_FLAG 		0:通常カラー 1:選択中カラーで表示
;CHARALIST_BUTTON_BUTTON_NUMPLUS 	ボタンの選択番号（NO + CHARALIST_BUTTON_BUTTON_NUMPLUS）
;CHARALIST_BUTTON_CHECKCHARA 		@SHOP_LIFE_CHECKCHARA～系の返り値を代入
;CHARALIST_BUTTON_NOT_PREACTION		0:行動済みマークつき 1:行動済みマークなし
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST_BUTTON(CHARALIST_BUTTON_CHARA, CHARALIST_BUTTON_ONCOLOR_FLAG = 0, CHARALIST_BUTTON_BUTTON_NUMPLUS = 100, CHARALIST_BUTTON_CHECKCHARA = 1, CHARALIST_BUTTON_NOT_PREACTION)
#DIM CHARALIST_BUTTON_CHARA
#DIM CHARALIST_BUTTON_ONCOLOR_FLAG
#DIM CHARALIST_BUTTON_BUTTON_NUMPLUS
#DIM CHARALIST_BUTTON_CHECKCHARA
#DIM CHARALIST_BUTTON_NOT_PREACTION

;CHECKCHARAの戻り値が０なら表示しない
SIF CHARALIST_BUTTON_CHECKCHARA <= 0
	RETURN 0

PRINT 

;行動済みマーク表示
SIF !CHARALIST_BUTTON_NOT_PREACTION
	CALL PRINT_RESTMARK(CHARALIST_BUTTON_CHARA)

SIF CHARALIST_BUTTON_ONCOLOR_FLAG
	SETCOLOR COLOR("選択中")

;通常表示
IF CHARALIST_BUTTON_CHECKCHARA == 1
	CALL PRINTBUTTON_CENTER(@"[%ANAME(CHARALIST_BUTTON_CHARA), 16, LEFT%]", NO:(CHARALIST_BUTTON_CHARA) + CHARALIST_BUTTON_BUTTON_NUMPLUS, 0)
;グレー表示
ELSEIF CHARALIST_BUTTON_CHECKCHARA == 2
	CALL PRINTBUTTON_CENTER(@"[%ANAME(CHARALIST_BUTTON_CHARA), 16, LEFT%]", NO:(CHARALIST_BUTTON_CHARA) + CHARALIST_BUTTON_BUTTON_NUMPLUS, 0, 0)
ENDIF

SIF CHARALIST_BUTTON_ONCOLOR_FLAG
	RESETCOLOR

;ボタンに追加の情報を表示する指定があれば
RESULT = 0
TRYCALLFORM SHOP_LIFE_BUTTON_ADD{FLAG:拠点フェイズ選択コマンド}(CHARALIST_BUTTON_CHARA)
;戻り値が１なら他の情報は表示しない
SIF RESULT
	RETURN 0

CALL IS_KOJO(NO:(CHARALIST_BUTTON_CHARA))
IF RESULT
	PRINT *
ELSE
	PRINT  
ENDIF

CALL PRINT_SEX(CHARALIST_BUTTON_CHARA, 1, 0, 2)

IF CHARALIST_BUTTON_CHARA == MASTER
	PRINT   
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):所属 == CFLAG:MASTER:所属
	SETCOLOR 0x97f8e8
	PRINT 自
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):所属 != 0
	SETCOLOR 0x28cc28
	PRINT 他
ELSE
	SETCOLOR 0x808080
	PRINT 無
ENDIF
RESETCOLOR
PRINT  

CALL TMP_PRINT_CHARA_STARS_NUM(CHARALIST_BUTTON_CHARA)
PRINT  

IF CHARALIST_BUTTON_CHARA == MASTER
	PRINT   
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):捕虜先
	IF CFLAG:(CHARALIST_BUTTON_CHARA):捕虜先 == CFLAG:MASTER:所属
		IF CFLAG:(CHARALIST_BUTTON_CHARA):監禁状態 == 0
			SETCOLOR 0x4434E5
			PRINT 監
		ELSE
			SETCOLOR 0xA39ED7
			PRINT 軟
		ENDIF
	ELSE
		SETCOLOR 0xdf0000
		PRINT 囚
	ENDIF
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):外交調教経過ターン
	IF CFLAG:(CHARALIST_BUTTON_CHARA):外交要求成功フラグ == 2
		SETCOLOR COLOR("女")
		PRINT 虜
	ELSE
		SETCOLOR 0x489200
		PRINT 脅
	ENDIF
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):面識
	SETCOLOR 0xD0D0D0
	PRINT 会
ELSE
	SETCOLOR 0x404040
	PRINT 会
ENDIF
RESETCOLOR
PRINT  
IF TALENT:(CHARALIST_BUTTON_CHARA):崩壊
	SETCOLOR COLOR("警告")
	PRINT 崩
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):虚ろ
	SETCOLOR COLOR("警告")
	PRINT 虚
ELSEIF ID_TO_CHARA(FLAG:推しメン指定キャラ) == CHARALIST_BUTTON_CHARA
	SETCOLOR COLOR("注意")
	PRINT 推
ELSEIF GETBIT(TALENT:(CHARALIST_BUTTON_CHARA):特殊勢力陥落系, 特殊勢力_外来人)
	SETCOLOR 0x0000A0
	PRINT 外
ELSEIF GETBIT(TALENT:(CHARALIST_BUTTON_CHARA):特殊勢力陥落系, 特殊勢力_ホフゴブリン)
	SETCOLOR 0x2B5C1B
	PRINT ゴ
ELSEIF GETBIT(TALENT:(CHARALIST_BUTTON_CHARA):特殊勢力陥落系, 特殊勢力_野盗)
	SETCOLOR 0x800000
	PRINT 野
ELSEIF GETBIT(TALENT:(CHARALIST_BUTTON_CHARA):特殊勢力陥落系, 特殊勢力_触手)
	SETCOLOR 0xFF1493
	PRINT 寄
ELSEIF GETBIT(TALENT:(CHARALIST_BUTTON_CHARA):特殊勢力陥落系, 特殊勢力_自警団)
	SETCOLOR 0x33CC66
	PRINT 自
ELSEIF GETBIT(TALENT:(CHARALIST_BUTTON_CHARA):特殊勢力陥落系, 特殊勢力_サキュバス)
	SETCOLOR 0x8B008B
	PRINT 夢
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):親愛
	SETCOLOR 0xF5CE13
	PRINT 愛
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):恋慕
	SETCOLOR 0xFF4080
	PRINT 慕
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):親友
	SETCOLOR 0xFF8000
	PRINT 友
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):隷属
	SETCOLOR 0x00FFFF
	PRINT 隷
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):服従
	SETCOLOR 0x00D0D0
	PRINT 酔
ELSE
	PRINT   
ENDIF
RESETCOLOR

IF TALENT:(CHARALIST_BUTTON_CHARA):正妻
	SETCOLOR 0xF5CE13
	PRINT 妻
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):恋人
	SETCOLOR 0xFF80C0
	PRINT 恋
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):烙印
	SETCOLOR 0x00FF80
	PRINT 印
ELSE
	PRINT   
ENDIF
RESETCOLOR
PRINT  

IF CFLAG:(CHARALIST_BUTTON_CHARA):行動不能状態 == 4
	SETCOLOR COLOR("緑")
	PRINTPLAIN 幼
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):行動不能状態 == 1
	SETCOLOR COLOR("妊娠")
	PRINTPLAIN 臨
ELSEIF TALENT:(CHARALIST_BUTTON_CHARA):妊娠
	SETCOLOR COLOR("妊娠")
	PRINTPLAIN 妊
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):避妊薬残ターン
	SETCOLOR COLOR("妊娠")
	PRINTPLAIN 避
ELSEIF IS_OVULATION(CHARALIST_BUTTON_CHARA)
	SETCOLOR COLOR("ピンク")
	PRINTPLAIN 危
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):行動不能状態 == 2
	SETCOLOR 0xFFC0C0
	PRINTPLAIN 育
ELSEIF CFLAG:(CHARALIST_BUTTON_CHARA):行動不能状態 == 3
	SETCOLOR COLOR("警告")
	PRINTPLAIN 傷
ELSEIF COOLTIME:(CHARALIST_BUTTON_CHARA):0
	SETCOLOR COLOR("警告")
	PRINTFORM %TOFULL(TOSTR(COOLTIME:(CHARALIST_BUTTON_CHARA):0))%
ELSE
	PRINTPLAIN   
ENDIF
RESETCOLOR

;-------------------------------------------------
;右カラムの標準的なキャラリスト
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST(LINE_NUM_BUTTON = 2)
#DIM MAX_BUTTON				;１ページに表示可能なボタンの最大値
#DIM NOW_BUTTON				;現在何個目のボタンを表示したか
#DIM NOW_BUTTON_PAGE		;現在のページで何個目のボタンを表示したか
#DIM MENU_CHARANUM			;このメニューで選択可能なキャラの人数（グレー表示で選択できないキャラも含める）
#DIM NOW_CHECK_CHARA		;現在チェック中のキャラ（キャラリスト順にチェックするのでループカウンタをキャラ番号としてそのまま扱わない）
#DIM NOW_PAGE_BUTTON_START	;現在のページの最初に置くボタンが何番か
#DIM NOW_PAGE_BUTTON_END	;現在のページの最後に置くボタンが何番か
#DIM LINE_NUM_BUTTON		;一行にボタンを何個並べるか
#DIM PRINTOPTION			;このキャラは選択不可能か選択可能かグレー表示か判定の戻り値

;ひとつのページに表示可能なボタン数の最大値を計算 (拠点フェイズ画面最大行数 - 2 - 現在表示行数) * 2
MAX_BUTTON = (LINES_SHOP_DRAW - 2 - (LINECOUNT - FIRST_COLUMN_RIGHT_LINE)) * 2

;選択可能キャラ人数
MENU_CHARANUM = 0

FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているキャラを判定
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT
		;選択可能キャラ人数加算
		MENU_CHARANUM ++
	ENDIF
NEXT

;最大ページ数の計算（キャラ数－１÷最大ボタン数で計算して＋１し、最低でも１以上にする）
SHOP_LIFE_LIST1_PAGE_MAX = MAX((MENU_CHARANUM - 1) / MAX_BUTTON + 1, 1)

;現在のページ数を範囲に収める
FLAG:拠点フェイズページ = (SHOP_LIFE_LIST1_PAGE_MAX - 1 + FLAG:拠点フェイズページ % SHOP_LIFE_LIST1_PAGE_MAX) % SHOP_LIFE_LIST1_PAGE_MAX + 1

;リセット現在ボタン数
NOW_BUTTON = 0
;リセット現在ページの表示ボタン数
NOW_BUTTON_PAGE = 0
;リセット現在ページの開始値（ボタン数）
NOW_PAGE_BUTTON_START = (FLAG:拠点フェイズページ - 1) * MAX_BUTTON
;リセット現在ページの終了値（ボタン数）
NOW_PAGE_BUTTON_END = FLAG:拠点フェイズページ * MAX_BUTTON

;キャラリスト開始
FOR LOCAL:0, 0, CHARANUM
	;今回チェックするキャラ（キャラ番号順ではなく、リスト用に並び替えられた順でチェック）
	NOW_CHECK_CHARA = SHOP_CHARA_LIST:(LOCAL:0)
	;リストから除外されているキャラなら次のキャラへ
	SIF NOW_CHECK_CHARA == -1
		CONTINUE
	;キャラが選択可能な条件を満たしているかを判定（PRINTOPTION=2 条件は満たしているが行動済みの場合グレー表示）
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(NOW_CHECK_CHARA)
	PRINTOPTION = RESULT
	IF RESULT
		IF NOW_BUTTON >= NOW_PAGE_BUTTON_START && NOW_BUTTON < NOW_PAGE_BUTTON_END
			;ボタンを１行に並べる数
			IF NOW_BUTTON_PAGE % LINE_NUM_BUTTON != 0
				PRINTPLAIN 　
			ELSE
				IF NOW_BUTTON_PAGE >= 1
					CALL COLUMN_RIGHT_PRINTL
				ENDIF
			ENDIF
			;ボタンの独自指定があれば呼び、なければ通常のボタンを表示
			TRYCCALLFORM SHOP_LIFE_LIST1_BUTTON{FLAG:拠点フェイズ選択コマンド}(NOW_CHECK_CHARA, PRINTOPTION)
			CATCH
				CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA, 0, 100, PRINTOPTION)
			ENDCATCH
			;現在ページの表示ボタン数を追加
			NOW_BUTTON_PAGE ++
		ENDIF
		;現在ボタン数を追加
		NOW_BUTTON ++
	ENDIF
NEXT

;ページ移動表示
IF LINECOUNT - FIRST_COLUMN_RIGHT_LINE < LINES_SHOP_DRAW
	FOR LOCAL:0, LINECOUNT - FIRST_COLUMN_RIGHT_LINE, LINES_SHOP_DRAW - 1
		CALL COLUMN_RIGHT_PRINTL
		IF LOCAL:0 == LINES_SHOP_DRAW - 3
			CALL COLUMN_RIGHT_LINE
		ELSEIF LOCAL:0 == LINES_SHOP_DRAW - 2
			PRINT   
			IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
				PRINTBUTTON @"{SHOP_LIFE_LIST1_PAGE_BACK}[前のページ    ]", SHOP_LIFE_LIST1_PAGE_BACK
			ELSE
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM {SHOP_LIFE_LIST1_PAGE_BACK}[前のページ    ]
				RESETCOLOR
			ENDIF
			PRINTPLAINFORM %TOSTR_SPACE(10)%
			LOCALS:0 = page{FLAG:拠点フェイズページ}/{SHOP_LIFE_LIST1_PAGE_MAX}
			PRINTPLAINFORM %LOCALS:0, 15, LEFT%
			IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
				PRINTBUTTON @"{SHOP_LIFE_LIST1_PAGE_NEXT}[次のページ    ]", SHOP_LIFE_LIST1_PAGE_NEXT
			ELSE
				SETCOLOR COLOR("選択不可")
				PRINTPLAINFORM {SHOP_LIFE_LIST1_PAGE_NEXT}[次のページ    ]
				RESETCOLOR
			ENDIF
		ENDIF
	NEXT
ENDIF

PRINTL 

RETURN 0

;-------------------------------------------------
;右カラムの上下２段のキャラリスト
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST_TOP(LINE_NUM_BUTTON = 2)
#DIM MAX_BUTTON
#DIM NOW_BUTTON
#DIM NOW_BUTTON_PAGE
#DIM MENU_CHARANUM
#DIM NOW_CHECK_CHARA
#DIM NOW_PAGE_BUTTON_START
#DIM NOW_PAGE_BUTTON_END
#DIM LINE_NUM_BUTTON
#DIM PRINTOPTION

MAX_BUTTON = (LINES_SHOP_DRAW / 2 - 2 - (LINECOUNT - FIRST_COLUMN_RIGHT_LINE)) * 2

MENU_CHARANUM = 0

FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT
		MENU_CHARANUM ++
	ENDIF
NEXT

SHOP_LIFE_LIST1_PAGE_MAX = MAX((MENU_CHARANUM - 1) / MAX_BUTTON + 1, 1)

FLAG:拠点フェイズページ = (SHOP_LIFE_LIST1_PAGE_MAX - 1 + FLAG:拠点フェイズページ % SHOP_LIFE_LIST1_PAGE_MAX) % SHOP_LIFE_LIST1_PAGE_MAX + 1

NOW_BUTTON = 0
NOW_BUTTON_PAGE = 0
NOW_PAGE_BUTTON_START = (FLAG:拠点フェイズページ - 1) * MAX_BUTTON
NOW_PAGE_BUTTON_END = FLAG:拠点フェイズページ * MAX_BUTTON

FOR LOCAL:0, 0, CHARANUM
	NOW_CHECK_CHARA = SHOP_CHARA_LIST:(LOCAL:0)
	SIF NOW_CHECK_CHARA == -1
		CONTINUE
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(NOW_CHECK_CHARA)
	PRINTOPTION = RESULT
	IF RESULT
		IF NOW_BUTTON >= NOW_PAGE_BUTTON_START && NOW_BUTTON < NOW_PAGE_BUTTON_END
			IF NOW_BUTTON_PAGE % LINE_NUM_BUTTON != 0
				PRINTPLAIN 　
			ELSE
				IF NOW_BUTTON_PAGE >= 1
					CALL COLUMN_RIGHT_PRINTL
				ENDIF
			ENDIF
			TRYCCALLFORM SHOP_LIFE_LIST1_BUTTON{FLAG:拠点フェイズ選択コマンド}(NOW_CHECK_CHARA, PRINTOPTION)
			CATCH
				CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA, 0, 100, PRINTOPTION)
			ENDCATCH
			NOW_BUTTON_PAGE ++
		ENDIF
		NOW_BUTTON ++
	ENDIF
NEXT

FOR LOCAL:0, LINECOUNT - FIRST_COLUMN_RIGHT_LINE, LINES_SHOP_DRAW / 2 - 1
	CALL COLUMN_RIGHT_PRINTL
	IF LOCAL:0 == LINES_SHOP_DRAW / 2 - 3
		CALL COLUMN_RIGHT_LINE
	ELSEIF LOCAL:0 == LINES_SHOP_DRAW / 2 - 2
		PRINT   
		IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
			PRINTBUTTON @"{SHOP_LIFE_LIST1_PAGE_BACK}[前のページ    ]", SHOP_LIFE_LIST1_PAGE_BACK
		ELSE
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM {SHOP_LIFE_LIST1_PAGE_BACK}[前のページ    ]
			RESETCOLOR
		ENDIF
		PRINTPLAINFORM %TOSTR_SPACE(10)%
		LOCALS:0 = page{FLAG:拠点フェイズページ}/{SHOP_LIFE_LIST1_PAGE_MAX}
		PRINTPLAINFORM %LOCALS:0, 15, LEFT%
		IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
			PRINTBUTTON @"{SHOP_LIFE_LIST1_PAGE_NEXT}[次のページ    ]", SHOP_LIFE_LIST1_PAGE_NEXT
		ELSE
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM {SHOP_LIFE_LIST1_PAGE_NEXT}[次のページ    ]
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
RETURN 0

;-------------------------------------------------
;右カラムの上下２段のキャラリスト下
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST_BOTTOM(LINE_NUM_BUTTON_BOTTOM = 2)
#DIM MAX_BUTTON_BOTTOM
#DIM NOW_BUTTON_BOTTOM
#DIM NOW_BUTTON_PAGE_BOTTOM
#DIM MENU_CHARANUM_BOTTOM
#DIM NOW_CHECK_CHARA_BOTTOM
#DIM NOW_PAGE_BUTTON_START_BOTTOM
#DIM NOW_PAGE_BUTTON_END_BOTTOM
#DIM LINE_NUM_BUTTON_BOTTOM
#DIM PRINTOPTION_BOTTOM

MAX_BUTTON_BOTTOM = (LINES_SHOP_DRAW - 2 - (LINECOUNT - FIRST_COLUMN_RIGHT_LINE)) * 2
SHOP_LIFE_LIST2_PAGE_MAX = 0
FOR LOCAL:0, 0, CHARANUM
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT
		SHOP_LIFE_LIST2_PAGE_MAX ++
	ENDIF
NEXT
SHOP_LIFE_LIST2_PAGE_MAX = MAX((SHOP_LIFE_LIST2_PAGE_MAX - 1) / MAX_BUTTON_BOTTOM + 1, 1)

FLAG:拠点フェイズページ2 = (SHOP_LIFE_LIST2_PAGE_MAX - 1 + FLAG:拠点フェイズページ2 % SHOP_LIFE_LIST2_PAGE_MAX) % SHOP_LIFE_LIST2_PAGE_MAX + 1

;キャラリストの表示
NOW_BUTTON_BOTTOM = 0
NOW_BUTTON_PAGE_BOTTOM = 0
NOW_PAGE_BUTTON_START_BOTTOM = (FLAG:拠点フェイズページ2 - 1) * MAX_BUTTON_BOTTOM
NOW_PAGE_BUTTON_END_BOTTOM = FLAG:拠点フェイズページ2 * MAX_BUTTON_BOTTOM
FOR LOCAL:0, 0, CHARANUM
	NOW_CHECK_CHARA_BOTTOM = SHOP_CHARA_LIST:(LOCAL:0)
	SIF NOW_CHECK_CHARA_BOTTOM == -1
			CONTINUE
	;選択可能な条件を満たしているかどうかを判定
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:拠点フェイズ選択コマンド}(NOW_CHECK_CHARA_BOTTOM)
	PRINTOPTION_BOTTOM = RESULT
	IF RESULT
		IF NOW_BUTTON_BOTTOM >= NOW_PAGE_BUTTON_START_BOTTOM && NOW_BUTTON_BOTTOM < NOW_PAGE_BUTTON_END_BOTTOM
			IF NOW_BUTTON_PAGE_BOTTOM % LINE_NUM_BUTTON_BOTTOM != 0
				PRINTPLAIN 　
			ELSE
				IF NOW_BUTTON_PAGE_BOTTOM >= 1
					CALL COLUMN_RIGHT_PRINTL
				ENDIF
			ENDIF
			TRYCCALLFORM SHOP_LIFE_LIST2_BUTTON{FLAG:拠点フェイズ選択コマンド}(NOW_CHECK_CHARA_BOTTOM, PRINTOPTION_BOTTOM)
			CATCH
				CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA_BOTTOM, 0, 1100, PRINTOPTION_BOTTOM)
			ENDCATCH
			NOW_BUTTON_PAGE_BOTTOM ++
		ENDIF
		NOW_BUTTON_BOTTOM ++
	ENDIF
NEXT

FOR LOCAL:0, (LINECOUNT - FIRST_COLUMN_RIGHT_LINE), LINES_SHOP_DRAW - 1
	CALL COLUMN_RIGHT_PRINTL
	IF LOCAL:0 == LINES_SHOP_DRAW - 3
		CALL COLUMN_RIGHT_LINE
	ELSEIF LOCAL:0 == LINES_SHOP_DRAW - 2
		PRINT   
		IF SHOP_LIFE_LIST2_PAGE_MAX >= 2
			PRINTBUTTON @"{SHOP_LIFE_LIST2_PAGE_BACK}[前のページ    ]", SHOP_LIFE_LIST2_PAGE_BACK
		ELSE
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM {SHOP_LIFE_LIST2_PAGE_BACK}[前のページ    ]
			RESETCOLOR
		ENDIF
		PRINTPLAINFORM %TOSTR_SPACE(10)%
		LOCALS:0 = page{FLAG:拠点フェイズページ2}/{SHOP_LIFE_LIST2_PAGE_MAX}
		PRINTPLAINFORM %LOCALS:0, 15, LEFT%
		IF SHOP_LIFE_LIST2_PAGE_MAX >= 2
			PRINTBUTTON @"{SHOP_LIFE_LIST2_PAGE_NEXT}[次のページ    ]", SHOP_LIFE_LIST2_PAGE_NEXT
		ELSE
			SETCOLOR COLOR("選択不可")
			PRINTPLAINFORM {SHOP_LIFE_LIST2_PAGE_NEXT}[次のページ    ]
			RESETCOLOR
		ENDIF
	ENDIF
NEXT

PRINTL 

RETURN 0

