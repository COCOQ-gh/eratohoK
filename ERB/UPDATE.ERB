;-------------------------------------------------
;セーブデータのアップデート処理
;-------------------------------------------------
@UPDATE
IF LASTLOAD_VERSION <= 161
	CONFIG:319 = 0
	FOR LOCAL, 0, 200
		IF EXISTCSV(LOCAL) && NO_TO_CHARA(LOCAL) != -1
			CSVTALENT LOCAL, 334
			TALENT:LOCAL:性的嗜好 = RESULT
		ENDIF
	NEXT
ENDIF

;易者、門番追加
IF LASTLOAD_VERSION <= 163
	FOR LOCAL, 0, 200
			IF NO_TO_CHARA(LOCAL) == -1 && EXISTCSV(LOCAL)
				ADDCHARA LOCAL
				CALL NEWCHARA_INIT(CHARANUM -1)
				CFLAG:(CHARANUM -1):12 = 1
			ENDIF
	NEXT
ENDIF

IF LASTLOAD_VERSION <= 164
	PRINTFORML 登録キャラパッチの不具合修正処理中……
	FOR LOCAL, 0, CHARANUM
		;登録キャラの不具合を修正
		IF GET_ID(LOCAL) == 0
			SETCOLOR COLOR("注意")
			PRINTFORML %ANAME(LOCAL)%に該当する不具合あり
			CALL NEWCHARA_INIT(LOCAL)
		ENDIF
	NEXT
	RESETCOLOR
	PRINTFORML 修正しました
	PRINTFORML これで多分ちゃんと動くんじゃね？
	
	;蓬莱人
	FOR LOCAL, 0, 200
		IF EXISTCSV(LOCAL) && NO_TO_CHARA(LOCAL) != -1
			CSVTALENT LOCAL, GETNUM(TALENT, "蓬莱人")
			TALENT:LOCAL:蓬莱人 = RESULT
		ENDIF
	NEXT
	
	CALL COLORPRINTFORM("デイリーイベントがコンフィグ保存/読み込みに対応しました", COLOR("注意"), "W")
	CALL COLORPRINTFORM("引き換えに、旧VERのコンフィグを読み込むとデイリーのフィルタ状況が吹き飛ぶ（多分）ので気をつけて下さい", COLOR("注意"), "W")
	
	SP_COUNTRY_APPEARED:特殊勢力出現_ホフゴブリン = ゴブリン出現済み
	SP_COUNTRY_APPEARED:特殊勢力出現_外来人 = 外来勢力出現済み
ENDIF

IF LASTLOAD_VERSION <= 166
	PRINTFORML IDのないキャラがいないか確認中……

	FOR LOCAL, 0, CHARANUM
		;登録キャラの不具合を修正
		IF GET_ID(LOCAL) == 0
			SETCOLOR COLOR("注意")
			PRINTFORML %ANAME(LOCAL)%に該当する不具合あり
			CALL NEWCHARA_INIT(LOCAL)
			PRINTFORML 修正しました
		ENDIF
	NEXT
	RESETCOLOR
	PRINTFORMW IDの不具合を修正しました
	PRINTFORML

	PRINTFORMW 全員に性的嗜好を設定します
	FOR LOCAL, 0, 200
		IF EXISTCSV(LOCAL) && NO_TO_CHARA(LOCAL) != -1
			CSVTALENT LOCAL, GETNUM(TALENT, "性的嗜好")
			TALENT:LOCAL:性的嗜好 = RESULT
		ENDIF
	NEXT
	PRINTFORMW 全員に性的嗜好を設定しました	
	SHOP_TIME = 0
	PRINTFORML
	PRINTFORMW 犬と豚を行動回数の計算に含めないようにしました
	PRINTFORMW 拠点フェイズにてセーブしていた場合、残行動回数が全回復します
ENDIF

IF LASTLOAD_VERSION <= 167
	SETCOLOR COLOR("注意")
	PRINTFORMW ！ver167対応パッチで追加されたデイリーイベントについて、コンフィグの割り当てが変更されました！
	PRINTFORMW ！ver167にパッチを自分で導入していた人は、デイリーイベントのフィルタが外れているかもしれません！
	PRINTFORMW ！確認してみてね！
	RESETCOLOR
ENDIF


IF LASTLOAD_VERSION <= 168

	;種族名、肝臓
	PRINTFORML ver169にアップデートしました
	PRINTFORML 各キャラに種族名と能力「肝臓」が追加されました
	SETCOLOR COLOR("注意")
	PRINTFORMW 処女・ファーストキス・童貞について、保存形式が大幅に変更されました
	PRINTFORMW 各キャラの処女・ファーストキス・童貞の状況がリセットされました
	RESETCOLOR
	FOR LOCAL, 0, CHARANUM
		IF EXISTCSV(LOCAL) && NO_TO_CHARA(LOCAL) != -1

			;種族名
			CSVCSTR LOCAL, 8
			CSTR:(LOCAL:0):8 = %RESULTS%

			;肝臓
			CSVABL LOCAL, GETNUM(ABL, "肝臓")
			ABL:LOCAL:肝臓 = RESULT
			
			SIF LOCAL:0 == MASTER && ABL:LOCAL:肝臓 == 0
				ABL:LOCAL:肝臓 = 3

			;処女、ファーストキス、童貞
			CSVTALENT LOCAL, GETNUM(TALENT, "処女")
			TALENT:LOCAL:処女 = RESULT
			CSVTALENT LOCAL, GETNUM(TALENT, "キス未経験")
			TALENT:LOCAL:キス未経験 = RESULT
			CSVTALENT LOCAL, GETNUM(TALENT, "童貞")
			TALENT:LOCAL:童貞 = RESULT

			IF TALENT:LOCAL:処女
				VIRGIN_PARTNER:LOCAL:0 = ----
			ELSE
				VIRGIN_PARTNER:LOCAL:0 = 不明
			ENDIF

			IF TALENT:LOCAL:キス未経験
				FIRSTKISS_PARTNER:LOCAL:0 = ----
			ELSE
				FIRSTKISS_PARTNER:LOCAL:0 = 不明
			ENDIF

			IF TALENT:LOCAL:童貞
				DOUTEI_PARTNER:LOCAL:0 = ----
			ELSE
				DOUTEI_PARTNER:LOCAL:0 = 不明
			ENDIF
		ENDIF
	NEXT
ENDIF

IF LASTLOAD_VERSION <= 169
	FOR LOCAL, 0, CHARANUM
		IF EXISTCSV(LOCAL) && NO_TO_CHARA(LOCAL) != -1
			;種族名
			CSVCSTR LOCAL, 8
			CSTR:(LOCAL:0):8 = %RESULTS%
		ENDIF
	NEXT
	;捕虜
	VARSET PRISONER_TARGET, -1
	
	;調教中帰らないフラグ
	CVARSET CFLAG, 60, 0
	
	;死亡扱いにされていたら修復（富豪の遊興）
	IF CFLAG:MASTER:特殊状態フラグ == 2
		CFLAG:MASTER:特殊状態フラグ = 1
	ENDIF
	
	;行動回数の計算式変更
	SHOP_TIME = 0

	PRINTFORMW ver170にアップデートしました
	PRINTFORMW 拠点フェイズのデータをロードしていた場合、行動回数が最大まで回復しています
ENDIF



IF LASTLOAD_VERSION <= 170

	;カナの姓名が逆になっているのを修正
	LOCAL = NAME_TO_CHARA("カナ")
	IF LOCAL != -1
		TALENT:LOCAL:姓名逆転 = 1
	ENDIF
	
	;行動回数の計算式変更
	SHOP_TIME = 0
	PRINTFORMW VER171にアップデートしました
	PRINTFORMW 「アナベラル カナ」と表示されていたのが直ります
	PRINTFORMW 拠点フェイズのデータをロードしていた場合、行動回数が最大まで回復しています

ENDIF

IF LASTLOAD_VERSION <= 171
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:特殊状態フラグ == 3
			CFLAG:LOCAL:特殊状態フラグ = 0
	NEXT
	FOR LOCAL, 0, CHARANUM
		IF EXISTCSV(LOCAL) && NO_TO_CHARA(LOCAL) != -1
			CSVTALENT LOCAL, GETNUM(TALENT, "Ｖ締まり")
			TALENT:LOCAL:Ｖ締まり = RESULT
			CSVTALENT LOCAL, GETNUM(TALENT, "Ａ締まり")
			TALENT:LOCAL:Ａ締まり = RESULT
		ENDIF
		IF NO:LOCAL > 200
			TALENT:LOCAL:Ｖ締まり = GET_DEFAULT_TIGHTNESS("普通")
			TALENT:LOCAL:Ａ締まり = GET_DEFAULT_TIGHTNESS("普通")
		ENDIF
	NEXT
	IF CSTR:MASTER:99 == "あなた"
		TALENT:MASTER:Ｖ締まり = GET_DEFAULT_TIGHTNESS("普通")
		TALENT:MASTER:Ａ締まり = GET_DEFAULT_TIGHTNESS("普通")
	ENDIF
	
	PRINTFORMW VER173にアップデートしました
	PRINTFORMW モブ会話デイリーを進行させたことによる進行不可能バグが直ります
	PRINTFORMW 各キャラにＶ締まりとＡ締まりが追加されます
	PRINTFORMW ランダム・子供・イベントキャラの締まりは、とりあえず「普通」に設定されます
ENDIF

IF LASTLOAD_VERSION <= 173
	;霖之助、藍関係の素質削除
	FOR LOCAL, 0, CHARANUM
		TALENT:LOCAL:295 = 0
		TALENT:LOCAL:296 = 0
		TALENT:LOCAL:297 = 0
		TALENT:LOCAL:298 = 0
		CFLAG:LOCAL:35 = 0
		CFLAG:LOCAL:36 = 0
		CFLAG:LOCAL:37 = 0
		CFLAG:LOCAL:38 = 0
		CFLAG:LOCAL:39 = 0
		CFLAG:LOCAL:73 = 0
	NEXT
	FLAG:102 = 0
	CONFIG:70 = 0
	CONFIG:71 = 0
	CONFIG:88 = 0
ENDIF

;衣装システム削除
IF LASTLOAD_VERSION <= 174
	VARSET ITEM, 0, 240, 246
	VARSET ITEMSALES, 0, 240, 246
	VARSET ITEMTERM, 0, 240, 246
	ITEM:240 = 0
	ITEM:241 = 0
	ITEM:242 = 0
	ITEM:243 = 0
	ITEM:244 = 0
	ITEM:245 = 0
	CVARSET CFLAG, 92, 0
	CVARSET ABL, 15, 0
	CVARSET EXP, 32, 0
ENDIF