;クリキャップ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME63
LOCALS:0 = クリキャップ

RESULTS:0 = %LOCALS:0%で責める
RESULTS:1 = %LOCALS:0%で奉仕する
RESULTS:2 = %LOCALS:0%で奉仕させる
RESULTS:3 = %LOCALS:0%で責められる
RESULTS:4 = %LOCALS:0%で責めさせる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE63
;共通部分
CALL COM_ABLE_COMMON(63)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;クリキャップが必要
SIF !ITEM:クリキャップ && !ITEM:A_クリキャップ
	RETURN 0
;対象にヴァギナがないなら不可
SIF !VAGINA(MTAR:0)
	RETURN 0
;既に装着中なら不可
SIF IS_EQUIP_TARGET(MTAR:0, 63)
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;ターゲットからプレイヤーに挿入中で体位が後背位・背面座位なら不可
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4)
	RETURN 0
;プレイヤーがクンニ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 2, 103)
	RETURN 0
;プレイヤーがターゲット以外にフェラ中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 11) && SEARCH_EQUIP(11, MPLY:0, MTAR:0) < 0
	RETURN 0
;プレイヤーがターゲット以外にパイズリ中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 12) && SEARCH_EQUIP(12, MPLY:0, MTAR:0) < 0
	RETURN 0
;プレイヤーからターゲットに尻コキ中なら不可
SIF SEARCH_EQUIP(15, MPLY:0, MTAR:0) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM63
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 60

EXP:(MPLY:0):性技経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):接触 = 10
SOURCE:(MPLY:0):性行動 = 90

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 20)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 60

SOURCE:(MTAR:0):露出 = 50
SOURCE:(MTAR:0):逸脱 = 120
SOURCE:(MTAR:0):接触 = 10
SOURCE:(MTAR:0):性行動 = 180
IF TALENT:(MPLY:0):技師
	SOURCE:(MTAR:0):快Ｃ = SENSE_HOUSHI(MPLY:0, MTAR:0, 900)
ELSE
	SOURCE:(MTAR:0):快Ｃ = SENSE_HOUSHI(MPLY:0, MTAR:0, 600)
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 50, 20)

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP63
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP63(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

SOURCE:(LOCAL:2):性行動 += 30

IF TALENT:(LOCAL:2):技師
	SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600)
ELSE
	SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400)
ENDIF
SOURCE:(LOCAL:3):露出 += 20
SOURCE:(LOCAL:3):逸脱 += 50
SOURCE:(LOCAL:3):性行動 += 60

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE63(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%にクリキャップを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP63(ARG:0)
PRINTFORML 振動するキャップが%EQUIP_TARGET_ANAME(ARG:0)%のクリトリスを責め続けている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP63(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%のクリトリスを責め立てるキャップを外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER63(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE63
IF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = てらてらと濡れる
ELSE
	LOCALS:0 = 
ENDIF

IF TALENT:(MTAR:0):小柄体型
	LOCALS:0 = %LOCALS:0%包皮に包まれた淫豆
ELSE
	LOCALS:0 = %LOCALS:0%ぷくりと膨れた秘豆
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:0%に震えるキャップを取り付け%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%にキャップを握らせ、自身の%LOCALS:0%に取り付けるよう命じた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%のクリトリスにキャップを付けるように命令した
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:0%に震えるキャップを取り付けた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST63
