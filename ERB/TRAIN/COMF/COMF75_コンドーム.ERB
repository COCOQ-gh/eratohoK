;ローション

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME75
LOCALS:0 = コンドーム

RESULTS:0 = %LOCALS:0%をつける
RESULTS:1 = %LOCALS:0%をつけさせられる
RESULTS:2 = %LOCALS:0%をつけさせる
RESULTS:3 = %LOCALS:0%をつけられる
RESULTS:4 = %LOCALS:0%をつけさせる
RESULTS:5 = %LOCALS:0%装着見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE75
;共通部分
CALL COM_ABLE_COMMON(75)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットがいること
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	;ターゲットがＰ挿入中なら不可
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "Ｐ")
		RETURN 0
	;ターゲットにチンポが必要
	SIF !PENIS(MTAR:(LOCAL:0))
		RETURN 0
NEXT
;ローションが人数分必要
SIF ITEM:コンドーム <= MTAR_NUM && ITEM:A_コンドーム <= MTAR_NUM
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM75
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;ローションを１つ減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_コンドーム <= 0
	ITEM:コンドーム = MAX(ITEM:コンドーム - MTAR_NUM, 0)
ENDIF

;●プレイヤー側の処理
SOURCE:(MPLY:0):奉仕 = 50
SOURCE:(MPLY:0):性行動 = 60

FOR LOCAL, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)
	IF LOCAL:2 == MASTER
		CALL ADD_SOURCE_AIZYOU(MPLY:0, 50)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 40, 40)

	;●ターゲット側の処理
	SOURCE:(LOCAL:2):性行動 = 150

	IF MPLY:0 == MASTER
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 50)
	ELSEIF LOCAL:2 != MASTER
		SOURCE:(LOCAL:2):露出 += 50
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 40, 40)

	;射精箇所と対象をセット
	CALL STACK_SPERM(MPLY:0, LOCAL:2, 0)
NEXT

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

;経過時間を無理矢理1減らす
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER75(ARG:0)
;実行値の設定
;合意がない方がむしろ上がる（避妊具的な意味で）
TCVAR:(ARG:0):25 = 30

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):貞操観念
	CALL COM_ORDER_ELEMENT(ARG:0, "貞操観念", 10)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", 10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE75
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF


SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:1%は自身のペニスを%ANAME(MPLY:0)%に見せつけると、コンドームを付けるよう要求した…
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%LOCALS:1%のペニスにコンドームを取り付けた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%LOCALS:1%のペニスにコンドームを付けるよう要求した…
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%LOCALS:1%のペニスにコンドームを取り付けた…
ENDSELECT
;-------------------------------------------------
;地の文(実行後)
;-------------------------------------------------
@COM_TEXT_AFTER75
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF
IF MTAR:0 == MASTER
	IF TALENT:(MPLY:0):恋慕
		SIF TALENT:(MPLY:0):育児中
			PRINT 育児の最中の
		IF TALENT:(MPLY:0):妊娠
			PRINTFORML %ANAME(MPLY:0)%のお腹が大きくなったことを心配してくれることに幸福感を感じながら、%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORML %ANAME(MPLY:0)%は体を心配してくれることを喜びながら%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
		ENDIF
	;捕虜調教のメインターゲット
	ELSEIF FLAG:12 == 2 && FINDELEMENT(PRISONER_TARGET, MPLY:0) != -1
		IF TALENT:(MPLY:0):妊娠
			PRINTFORML %ANAME(MPLY:0)%は既に妊娠しているのに無意味だと思いながら、%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
		ELSE
			IF TALENT:(MPLY:0):男嫌い && ((TALENT:(MPLY:0):性別 == 0) || (TALENT:(MPLY:0):性別 >= 3))
				PRINT 男に犯されることに嫌悪感を抱きながらも
			ELSEIF TALENT:(MPLY:0):プライド高い
				PRINT 屈辱に渋面を作りながらも
			ELSEIF TALENT:(MPLY:0):貞操観念
				PRINT 自ら犯される準備をすることに躊躇しながらも
			ENDIF
			PRINTFORML %ANAME(MPLY:0)%は生でされるよりマシだと考え、%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
		ENDIF
	ELSE
		SIF TALENT:(MPLY:0):育児中
			PRINT まだ育児中の
		IF TALENT:(MPLY:0):妊娠
			PRINTFORML %ANAME(MPLY:0)%はお腹の子のことも考え、%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORML %ANAME(MPLY:0)%は妊娠を避けるため、%LOCALS:1%のペニスにコンドームを被せ%TARGET_STR("た", "てきた")%…
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP75
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP75(ARG:0)
;特になし

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE75(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%にコンドームを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP75(ARG:0)

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP75(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%のコンドームを外した
