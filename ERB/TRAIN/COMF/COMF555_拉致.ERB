;拉致

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME555
RESULTS:0 = 拉致

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE555
;共通部分
CALL COM_ABLE_COMMON(555)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;自国の仕官は不可
SIF CFLAG:(MTAR:0):1 == CFLAG:(MPLY:0):1
	RETURN 0
;夜這い中限定
SIF !FLAG:70
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;相手が君主なら不可
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF COUNTRY_BOSS:(LOCAL:0) == GET_ID(MTAR:0)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM555
;●実行値の計算
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(MTAR:0):25 = 120

CALL COM_ORDER_BASE(MTAR:0)

IF TALENT:(MTAR:0):反抗的
	CALL COM_ORDER_ELEMENT(MTAR:0, "反抗的", -5)
ENDIF
IF TALENT:(MTAR:0):気丈
	CALL COM_ORDER_ELEMENT(MTAR:0, "気丈", -5)
ENDIF
IF TALENT:(MTAR:0):素直
	CALL COM_ORDER_ELEMENT(MTAR:0, "素直", 5)
ENDIF
IF TALENT:(MTAR:0):プライド高い
	CALL COM_ORDER_ELEMENT(MTAR:0, "プライド高い", -15)
ENDIF
IF TALENT:(MTAR:0):プライド低い
	CALL COM_ORDER_ELEMENT(MTAR:0, "プライド低い", 5)
ENDIF
IF TALENT:(MTAR:0):好奇心
	CALL COM_ORDER_ELEMENT(MTAR:0, "好奇心", 5)
ENDIF
IF TALENT:(MTAR:0):保守的
	CALL COM_ORDER_ELEMENT(MTAR:0, "保守的", -8)
ENDIF
IF TALENT:(MTAR:0):孤高
	CALL COM_ORDER_ELEMENT(MTAR:0, "孤高", -(20 * KOKOU_RATE(MTAR:0) / 100))
ENDIF
IF TALENT:(MTAR:0):一線越えない
	CALL COM_ORDER_ELEMENT(MTAR:0, "一線越えない", -15)
ENDIF
IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱) && FLAG:50
	CALL COM_ORDER_ELEMENT(MTAR:0, "淫乱", 5)
ENDIF

IF TALENT:(MTAR:0):親愛
	CALL COM_ORDER_ELEMENT(MTAR:0, "親愛", 50)
ELSEIF TALENT:(MTAR:0):恋慕
	CALL COM_ORDER_ELEMENT(MTAR:0, "恋慕", 20)
ELSEIF TALENT:(MTAR:0):親友
	CALL COM_ORDER_ELEMENT(MTAR:0, "親友", 20)
ENDIF

IF TALENT:(MTAR:0):恋人
	CALL COM_ORDER_ELEMENT(MTAR:0, "恋人", 10)
ENDIF

IF TALENT:(MTAR:0):隷属
	CALL COM_ORDER_ELEMENT(MTAR:0, "隷属", 60)
ELSEIF TALENT:(MTAR:0):服従
	CALL COM_ORDER_ELEMENT(MTAR:0, "服従", 30)
ENDIF

IF TALENT:(MTAR:0):烙印
	CALL COM_ORDER_ELEMENT(MTAR:0, "烙印", 10)
ENDIF

LOCAL:0 = GET_PALAMLV(PALAM:(MTAR:0):欲情)
IF LOCAL:0 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 30))
ENDIF

IF TALENT:(MTAR:0):プライド高い
	LOCAL:0 = 4
ELSEIF TALENT:(MTAR:0):プライド低い
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF

IF MARK:(MTAR:0):既成事実 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"既成事実Lv{MARK:(MTAR:0):既成事実}", MARK:(MTAR:0):既成事実 * 3 * LOCAL:0)
ENDIF

IF FLAG:調教モード == 6
	CALL COM_ORDER_ELEMENT(MTAR:0, "捕虜会話", -180)
ELSEIF CFLAG:(MTAR:0):1 != 0
	;CALL COM_ORDER_ELEMENT(MTAR:0, "仕えている君主がいる", -45)
	;今の君主への好印象-悪印象を10で割ったもの
	LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):1)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):1))) / 10
	IF LOCAL:0 >= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"今の君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):1))%に好印象", -(LOCAL:0))
	ELSEIF LOCAL:0 <= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"今の君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):1))%に悪印象", -(LOCAL:0))
	ENDIF
ENDIF

;誘われた勢力の君主への好印象-悪印象を10で割ったもの
LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:1)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:1))) / 10
IF LOCAL:0 >= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:1))%に好印象", LOCAL:0)
ELSEIF LOCAL:0 <= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:1))%に悪印象", LOCAL:0)
ENDIF

IF CFLAG:(MTAR:0):2 < 300
	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -20)
ELSEIF CFLAG:(MTAR:0):2 < 500
	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -10)
ELSEIF CFLAG:(MTAR:0):2 < 800
	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -5)
ENDIF

;判定結果を表示
PRINTFORMW  = {TCVAR:(MTAR:0):24} %TOSTR_COMPARE(TCVAR:(MTAR:0):24, TCVAR:(MTAR:0):25)% 実行値{TCVAR:(MTAR:0):25}

;判定成功
IF TCVAR:(MTAR:0):24 >= TCVAR:(MTAR:0):25
	;口上表示
	CALL KOJO_EVENT(MTAR:0, 54)
	PRINTL 
	IF !IS_SLAVE(MTAR:0)
		PRINTFORML %ANAME(MASTER)%はもがく%ANAME(MTAR:0)%を袋に詰め込んで拉致した……
		PRINTFORMW ……%ANAME(MTAR:0)%が我が国の捕虜になりました
		;所属国家の変更
		CFLAG:(MTAR:0):捕虜先 = CFLAG:MASTER:1
	ELSE
		PRINTFORML %ANAME(MASTER)%は%ANAME(MTAR:0)%に、自分につくよう命令した
		PRINTFORMW ……%ANAME(MTAR:0)%は命令を受け入れました
		CFLAG:(MTAR:0):1 = CFLAG:MASTER:1
	ENDIF
	;部隊に所属していた場合解除
	CALL FORCE_FREE(MTAR:0)
	;強制終了フラグを立てる
	TFLAG:38 = 1
;判定失敗
ELSE
	PRINTFORML %ANAME(MASTER)%は%ANAME(MTAR:0)%を袋に詰め込んで拉致しようとした……
	PRINTFORMW ……抵抗されてしまった
	IF CFLAG:(MTAR:0):捕虜先 == CFLAG:MASTER:1
		SOURCE:(MTAR:0):反感 = 300
	ELSE
		SOURCE:(MTAR:0):反感 = 3000
	ENDIF
ENDIF

RETURN 1
