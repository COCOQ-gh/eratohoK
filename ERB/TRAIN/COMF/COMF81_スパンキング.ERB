;スパンキング

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME81
LOCALS:0 = スパンキング

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE81
;共通部分
CALL COM_ABLE_COMMON(81)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーがＶかＡに挿入されているなら不可
SIF IS_INSERT_SINGLE(MPLY:0, "ＶＡ")
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;ターゲットが顔面騎乗されているなら不可
	SIF IS_RIDDEN(MTAR:(LOCAL:0))
		RETURN 0
	;ターゲットが顔面騎乗中なら不可
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;プレイヤーからターゲットに尻コキ中なら不可
	SIF SEARCH_EQUIP(15, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;プレイヤーからターゲット／ターゲットからプレイヤーに足コキ中なら不可
	SIF SEARCH_EQUIP_IC(14, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;プレイヤーからターゲット／ターゲットからプレイヤーに電気按摩中なら不可
	SIF SEARCH_EQUIP_IC(103, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;プレイヤーからターゲット／ターゲットからプレイヤーに足舐め中なら不可
	SIF SEARCH_EQUIP_IC(104, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM81
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 100

SOURCE:(MPLY:0):嗜虐 = 150
SOURCE:(MPLY:0):逸脱 = 50
SOURCE:(MPLY:0):接触 = 30


;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 50)

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60
	
	SIF TALENT:(LOCAL:1):美尻 || TALENT:(LOCAL:1):豊尻
		SOURCE:(MPLY:0):嗜虐 += 30

	SOURCE:(LOCAL:1):苦痛 = 50 + MIN(ABL:(MPLY:0):武闘, 90) * 3
	SOURCE:(LOCAL:1):露出 = 100
	SOURCE:(LOCAL:1):逸脱 = 50
	SOURCE:(LOCAL:1):接触 = 30

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 0, 150)

	IF TALENT:(MPLY:0):サド
		TIMES SOURCE:(LOCAL:1):苦痛, 1.50
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER81(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF TALENT:(ARG:0):サド
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE81
;ターゲットが２人
IF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	LOCALS:1 = 交互に
	LOCALS:3 = 尻

;ターゲットが１人
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
	LOCALS:1 = 
	LOCALS:3  = %STR_BODY("尻：長", MTAR:0)%
ENDIF

;IF TALENT:(MTAR:0):美尻
;	LOCALS:2 = 美しい
;ELSEIF TALENT:(MTAR:0):豊尻
;	LOCALS:2 = 豊かな
;ELSE
;	LOCALS:2 = 
;ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%の%LOCALS:3%を%LOCALS:1%、赤く腫れ上がるほどに何度も叩い%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:0%は%ANAME(MPLY:0)%に自身の%LOCALS:3%を向け、それを手で叩くように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%LOCALS:0%の尻を%LOCALS:1%何度も叩いた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:0%の尻を叩くように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%LOCALS:0%の%LOCALS:3%を%LOCALS:1%何度も叩いた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST81
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF LOCAL:1 != MASTER
		LOCAL:2 = CUP:(LOCAL:1):怖主
		LOCAL:3 = CUP:(LOCAL:1):怒主

		IF TALENT:(LOCAL:1):マゾ && IS_PLAYABLE(LOCAL:1)
			PRINTFORML %ANAME(LOCAL:1)%は痛みを感じているにもかかわらず嬉しそうに甘い声を上げている
		ELSEIF ABL:(LOCAL:1):主導度Ｕ <= -100 && ABL:(LOCAL:1):倒錯度 >= 300
			PRINTFORML %ANAME(LOCAL:1)%は悲鳴を上げるが、その声には甘い色が混じっている
		ELSEIF LOCAL:2 >= 3000
			PRINTFORML %ANAME(LOCAL:1)%はあまりの痛みと恐怖に恥も外聞もなく泣き叫んでいる
		ELSEIF LOCAL:2 >= 1500
			PRINTFORML %ANAME(LOCAL:1)%は痛みに耐えきれず、何度も悲鳴を上げながら悶え狂っている
		ELSEIF LOCAL:2 >= 500
			PRINTFORML %ANAME(LOCAL:1)%は痛みのあまり口から呻きを漏らしている
		ELSE
			IF LOCAL:3 >= 200 && !IS_INITIATIVE(MTAR:(LOCAL:0))
				PRINTFORML %ANAME(LOCAL:1)%は痛みに耐えながら、%ANAME(MPLY:0)%を睨みつけた
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%は歯を食いしばって痛みに耐えている
			ENDIF
		ENDIF
	ENDIF
	SIF TALENT:(LOCAL:1):豊尻
		PRINTFORML 叩かれるたび、%ANAME(LOCAL:1)%の%STR_BODY("尻肉：短", MTAR:0)%はふるふると震えている
NEXT
