;触手挿入

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME201
LOCALS:0 = 触手挿入

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE201
;共通部分
CALL COM_ABLE_COMMON(201)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーが触手召喚中でないなら不可
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;Ｖが必要
	SIF !VAGINA(MTAR:(LOCAL:0))
		RETURN 0
	;ターゲットのＶが塞がっているなら不可(プレイヤーからターゲットへの触手挿入中は可)
	SIF IS_V_HOLD(MTAR:(LOCAL:0)) && SEARCH_EQUIP(201, MPLY:0, MTAR:(LOCAL:0)) == -1
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM201
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):妖術経験値 += 1
EXP:(MPLY:0):触手経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 150)
SOURCE:(MPLY:0):嗜虐 = 50
SOURCE:(MPLY:0):逸脱 = 100
SOURCE:(MPLY:0):触手 = 30
SOURCE:(MPLY:0):性行動 = 150

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 30)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):触手経験値 += 1

	SOURCE:(LOCAL:2):快Ｖ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 1000 + ABL:(MPLY:0):妖術 * 8)
	SOURCE:(LOCAL:2):逸脱 = 500
	SOURCE:(LOCAL:2):触手 = 600
	SOURCE:(LOCAL:2):性行動 = 300

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 80)

	;共通処理
	CALL COM_COMMON201(LOCAL:2)

	IF TALENT:(LOCAL:2):処女
		IF MPLY:0 == MASTER
			;処女喪失フラグ(主人に性交以外で奪われる)
			TCVAR:(LOCAL:2):13 = 2
		ELSE
			;処女喪失フラグ(主人以外に性交以外で奪われる)
			TCVAR:(LOCAL:2):13 = 4
		ENDIF
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP201
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP201(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):性行動 += 50

;全てのターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	;ソースを退避
	CALL PUTOUT_SOURCE(LOCAL:3)

	EXP:(LOCAL:3):触手経験値 += 1

	SOURCE:(LOCAL:3):快Ｖ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 800 + ABL:(LOCAL:2):妖術 * 6)
	SOURCE:(LOCAL:3):逸脱 += 200
	SOURCE:(LOCAL:3):触手 += 300
	SOURCE:(LOCAL:3):性行動 += 100

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 2

	;共通処理
	CALL COM_COMMON201(LOCAL:3)

	;退避したソースを加算
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE201(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%の膣内に%EQUIP_PLAYER_ANAME(ARG:0)%の触手を挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP201(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%の操る触手が%EQUIP_TARGET_ANAME(ARG:0)%の膣を欲望のままに突き上げている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP201(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の膣内から触手を引き抜いた…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER201(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 125

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):触手}", ABL:(ARG:0):触手 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 2)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -7)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 4)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -2)
ENDIF

IF TALENT:(ARG:0):サド || TALENT:(ARG:0):マゾ || ABL:(ARG:0):倒錯度 >= 800
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 32)
ELSEIF ABL:(ARG:0):倒錯度 >= 500
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 24)
ELSEIF ABL:(ARG:0):倒錯度 >= 300
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 16)
ELSEIF ABL:(ARG:0):倒錯度 >= 100
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 8)
ELSE
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
ENDIF

IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE201
LOCAL:1 = 1
FOR LOCAL:0, 0, MTAR_NUM
	IF SEARCH_EQUIP(201, MPLY:0, MTAR:(LOCAL:0)) == -1
		LOCAL:1 = 0
	ENDIF
NEXT

;全員が既に触手挿入中なら分岐
IF LOCAL:1
	;継続中の地の文をカット
	FOR LOCAL:0, 0, MTAR_NUM
		LOCAL:2 = SEARCH_EQUIP(201, MPLY:0, MTAR:(LOCAL:0))
		IF LOCAL:2 >= 0
			MEQUIP_CUT:(LOCAL:2) = 1
		ENDIF
	NEXT

	IF MTAR_NUM >= 3
		LOCALS:1 = %ANAME(MTAR:0)%たち
	ELSEIF MTAR_NUM == 2
		LOCALS:1 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	ELSE
		LOCALS:1 = %ANAME(MTAR:0)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:1%の膣に挿入された触手をさらに激しく突き上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %LOCALS:1%は挿入された%ANAME(MPLY:0)%の触手を膣内で締め付け、快楽を貪るように激しく腰を動かした…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:1%の膣に挿入された触手をもっと激しく動かすように命令した
			PRINTFORMW %ANAME(MPLY:0)%は言われた通り、%LOCALS:1%に挿入した触手で膣奥を力強く突き上げた…
	ENDSELECT

ELSE
	IF MTAR_NUM >= 3
		LOCALS:0 = 秘唇
		LOCALS:1 = %ANAME(MTAR:0)%たち
		LOCALS:2 = 次々と
		LOCALS:3 = 全員の
	ELSEIF MTAR_NUM == 2
		LOCALS:0 = 秘唇
		LOCALS:1 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
		LOCALS:2 = 
		LOCALS:3 = 二人の
	ELSE
		IF TALENT:(MTAR:0):処女 == 1
			LOCALS:0 = まだ乙女の
		ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
			LOCALS:0 = すっかり濡れて光る
		ELSE
			LOCALS:0 = 
		ENDIF
		IF TALENT:(MTAR:0):小柄体型
			LOCALS:0 = %LOCALS:0%小さなワレメ
		ELSE
			LOCALS:0 = %LOCALS:0%ワレメ
		ENDIF

		LOCALS:1 = %ANAME(MTAR:0)%
		LOCALS:2 = 
		LOCALS:3 = 自身の
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:1%の%LOCALS:0%に%LOCALS:2%触手を挿入し、激しく突き上げた%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:1%は%ANAME(MPLY:0)%の触手で%LOCALS:3%膣を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%は言われた通り%LOCALS:1%の%LOCALS:0%に触手を挿入し、膣内を突き上げた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、触手で%LOCALS:1%の膣を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%は言われた通り%LOCALS:1%の%LOCALS:0%に触手を挿入し、膣内を突き上げた…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST201

;--------------------------------------------------------
;選択時と継続状態での共通処理
;--------------------------------------------------------
@COM_COMMON201(ARG:0)
SELECTCASE ABL:(ARG:0):Ｖ感
	CASE 0
		SOURCE:(ARG:0):苦痛 += 450
	CASE 1
		SOURCE:(ARG:0):苦痛 += 150
	CASE 2
		SOURCE:(ARG:0):苦痛 += 45
ENDSELECT

IF PALAM:(ARG:0):潤滑 < PALAMLV:1
	TIMES SOURCE:(ARG:0):快Ｖ, 0.05
	SOURCE:(ARG:0):苦痛 = SOURCE:(ARG:0):苦痛 * 4 + 1000
ELSEIF PALAM:(ARG:0):潤滑 < PALAMLV:3
	TIMES SOURCE:(ARG:0):快Ｖ, 0.20
	SOURCE:(ARG:0):苦痛 = SOURCE:(ARG:0):苦痛 * 2 + 300
ELSEIF PALAM:(ARG:0):潤滑 < PALAMLV:5
	TIMES SOURCE:(ARG:0):快Ｖ, 0.60
	TIMES SOURCE:(ARG:0):苦痛, 1.00
ELSEIF PALAM:(ARG:0):潤滑 < PALAMLV:7
	TIMES SOURCE:(ARG:0):快Ｖ, 0.85
	TIMES SOURCE:(ARG:0):苦痛, 0.70
ELSE
	TIMES SOURCE:(ARG:0):快Ｖ, 1.00
	TIMES SOURCE:(ARG:0):苦痛, 0.50
ENDIF

IF TALENT:(ARG:0):小柄体型
	TIMES SOURCE:(ARG:0):苦痛, 1.50
ENDIF

IF TALENT:(ARG:0):処女 == 1
	SOURCE:(ARG:0):苦痛 = SOURCE:(ARG:0):苦痛 * 2 + 500
ENDIF

IF ARG:0 != MASTER
	IF !(TALENT:(ARG:0):恋慕 || TALENT:(ARG:0):親友)
		IF TALENT:(ARG:0):貞操観念
			IF TALENT:(ARG:0):処女
				SOURCE:(ARG:0):反感 = 5000
			ELSE
				SOURCE:(ARG:0):反感 = 1000
			ENDIF
		ELSEIF TALENT:(ARG:0):貞操無頓着
			IF TALENT:(ARG:0):処女
				SOURCE:(ARG:0):反感 = 300
			ENDIF
		ELSE
			IF TALENT:(ARG:0):処女
				SOURCE:(ARG:0):反感 = 2500
			ENDIF
		ENDIF
		IF TALENT:(ARG:0):恋人
			TIMES SOURCE:(ARG:0):反感, 0.10
		ENDIF
	ENDIF

	;好感度・従属度の影響
	LOCAL:0 = MAX(CFLAG:(ARG:0):2, CFLAG:(ARG:0):4)
	IF LOCAL:0 < 100
		TIMES SOURCE:(ARG:0):反感, 1.00
	ELSEIF LOCAL:0 < 300
		TIMES SOURCE:(ARG:0):反感, 0.90
	ELSEIF LOCAL:0 < 500
		TIMES SOURCE:(ARG:0):反感, 0.80
	ELSEIF LOCAL:0 < 800
		TIMES SOURCE:(ARG:0):反感, 0.40
	ELSEIF LOCAL:0 < 1500
		TIMES SOURCE:(ARG:0):反感, 0.15
	ELSE
		SOURCE:(ARG:0):反感 = SOURCE:(ARG:0):反感 / ((LOCAL:0 - 1500) / 50 + 10)
	ENDIF
ENDIF
