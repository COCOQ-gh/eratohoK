;放尿する

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME130
LOCALS:0 = 放尿

RESULTS:0 = %LOCALS:0%を見せつける
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%を見せつけられる
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE130
;共通部分
CALL COM_ABLE_COMMON(130)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人以上
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;多対多は不可
SIF MPLY_NUM >= 2 && MTAR_NUM >= 2
	RETURN 0
;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;尿残量が十分でないなら不可
	SIF TCVAR:(MPLY:(LOCAL:0)):54 < 1000
		RETURN 0
	;顔面騎乗中は不可
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
	;フェラ・亀頭フェラされているなら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 11, 107)
		RETURN 0
	;竿がなく、クンニされているなら不可
	SIF !PENIS(MPLY:(LOCAL:0)) && IS_EQUIP_TARGET(MPLY:(LOCAL:0), 1)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM130
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):排泄経験値 += 1

	LOCAL:3 = 800
	LOCAL:4 = 300

	SELECTCASE ABL:(LOCAL:2):排泄
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:2):排泄 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:2):露出 = 1200
	SOURCE:(LOCAL:2):逸脱 = 500
	SOURCE:(LOCAL:2):中毒充足 = LOCAL:3
	SOURCE:(LOCAL:2):性行動 = 150
	SOURCE:(LOCAL:2):反感 = LOCAL:4

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 150)

	;尿残量を0にする
	TCVAR:(LOCAL:2):54 = 0
NEXT

;主導権の所在を取得
LOCAL:11 = GET_COM_INITIATIVE()

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	SOURCE:(LOCAL:1):性行動 = 90

	;ターゲットに主導権
	IF LOCAL:11 == 1 || LOCAL:11 == 2
		SOURCE:(LOCAL:1):嗜虐 = 150
		SOURCE:(LOCAL:1):逸脱 = 250
	;ターゲットに主導権がない
	ELSE
		SOURCE:(LOCAL:1):逸脱 = 500
		SOURCE:(LOCAL:1):恐怖 = MAX(150 - 40 * ABL:(LOCAL:1):欲望, 0)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 4

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP130
RETURN 0

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER130(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"排泄Lv{ABL:(ARG:0):排泄}", ABL:(ARG:0):排泄 * 7)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -12)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 8)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -6)
ENDIF
IF TALENT:(ARG:0):目立ちたがり
	CALL COM_ORDER_ELEMENT(ARG:0, "目立ちたがり", 6)
ENDIF
IF TALENT:(ARG:0):倒錯的
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯的", 8)
ENDIF

IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE130
IF MPLY_NUM == 1
	LOCALS:1 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:1 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
ELSE
	LOCALS:1 = %ANAME(MPLY:0)%たち
ENDIF

IF MTAR_NUM == 1
	LOCALS:2 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:2 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
ELSE
	LOCALS:2 = %ANAME(MTAR:0)%たち
ENDIF

;プレイヤーとターゲットの挿入関係を取得
LOCAL:5 = IS_INSERT_MUTUAL(MPLY:0, MTAR:0)

;プレイヤーがターゲットのＶにペニスを挿入中
IF LOCAL:5 == 1 && PENIS(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %LOCALS:1%は%LOCALS:2%の奥深くまでペニスを突き込むと、そのまま膣内で放尿し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:2%は%LOCALS:1%に、自身の膣内にペニスを突き入れたまま放尿するように命令した
			PRINTFORMW %LOCALS:1%は竿を膣の奥まで入れて腰を止めると、そのまま%LOCALS:2%の中で放尿した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%の膣内にペニスを突き入れたまま放尿するように命令した
			PRINTFORMW %LOCALS:1%は竿を膣の奥まで入れて腰を止めると、そのまま%LOCALS:2%の中で放尿した…
	ENDSELECT

;プレイヤーがターゲットのＡにペニスを挿入中
ELSEIF LOCAL:5 == 2 && PENIS(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %LOCALS:1%は%LOCALS:2%のアナルの奥深くまでペニスを侵入させると、そのまま腸内で放尿し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:2%は%LOCALS:1%に、自身の尻穴にペニスを突き入れたまま放尿するように命令した
			PRINTFORMW %LOCALS:1%はアナルの奥深くまで竿を突き入れて腰を止めると、そのまま%LOCALS:2%の中で放尿した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%の尻穴にペニスを突き入れたまま放尿するように命令した
			PRINTFORMW %LOCALS:1%は%LOCALS:2%のペニスをアナルの奥深くまで侵入させると、そのまま%LOCALS:2%の中で放尿した…
	ENDSELECT

;ターゲットがプレイヤーのＶにペニスを挿入中
ELSEIF LOCAL:5 == 3 && PENIS(MTAR:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %LOCALS:1%は%LOCALS:2%と結合したまま放尿した…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %LOCALS:2%は、%LOCALS:1%に膣でペニスを咥え込ませたまま放尿させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:1%に対して放尿するように命令した
			PRINTFORMW %LOCALS:1%は%LOCALS:2%と結合したまま放尿した…
	ENDSELECT

;プレイヤーがＡ正常位・Ａ対面座位・Ａ騎乗位中
;ELSEIF IS_INSERT_SINGLE(MPLY:0, "Ａ") &&

;プレイヤーがフェラされている
;ELSEIF 

;それ以外
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %LOCALS:1%は%LOCALS:2%の目の前で放尿し、その様子を見せつけ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %LOCALS:2%は自分の目の前で%LOCALS:1%に放尿させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:1%に対して、%LOCALS:2%の目の前で放尿するように命令した
			PRINTFORMW %LOCALS:1%は%LOCALS:2%に見せつけるようにしながら、言われた通りに放尿した…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST130
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):排泄 >= 2
			PRINTFORMW %ANAME(LOCAL:1)%は見られながら放尿する解放感に恍惚とした表情を浮かべている
		ELSEIF ABL:(LOCAL:1):排泄 >= 1
			PRINTFORMW %ANAME(LOCAL:1)%は羞恥に顔を赤らめながらも、排尿の解放感にぶるりと震えた
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%は放尿しながら顔を赤らめて恥ずかしそうにしている
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):怒主 + CUP:(LOCAL:1):哀主

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):欲情 >= 500
			PRINTFORMW %ANAME(LOCAL:1)%は放尿の様子を興奮した様子で見つめている
		ELSEIF LOCAL:2 < 0
			PRINTFORMW %ANAME(LOCAL:1)%は顔を赤らめながら放尿の様子をじっと見つめている
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%は恥ずかしさと不快感のあまり目を背けた
		ENDIF
	ENDIF
NEXT
