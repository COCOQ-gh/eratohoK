;足コキ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME14
IF MTAR_NUM == 2
	LOCALS:0 = 同時足コキ
ELSEIF MPLY_NUM == 3
	LOCALS:0 = Ｔ足コキ
ELSEIF MPLY_NUM == 2
	LOCALS:0 = Ｗ足コキ
ELSE
	LOCALS:0 = 足コキ
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE14
;共通部分
CALL COM_ABLE_COMMON(14)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で3人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;複数-複数は不可
SIF MPLY_NUM >= 2 && MTAR_NUM >= 2
	RETURN 0
;全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;挿入中は不可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "全")
		RETURN 0
	;顔面騎乗中の場合
	IF IS_RIDE(MPLY:(LOCAL:0))
		;プレイヤーが複数 or ターゲットが複数 or 騎乗している相手がターゲット以外なら不可
		SIF MPLY_NUM >= 2 || MTAR_NUM >= 2 || !IS_RIDE(MPLY:0, MTAR:0)
			RETURN 0
		;お尻で顔面騎乗中なら不可
		LOCAL:2 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
		SIF LOCAL:2 >= 0 && MEQUIP_PLAYER_NUM:(LOCAL:2) >= 2
			RETURN 0
	ENDIF
	;顔面騎乗されているなら不可
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーが土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
	;全ターゲットについて判定
	FOR LOCAL:1, 0, MTAR_NUM
		;プレイヤーからターゲット／ターゲットからプレイヤーに足舐め中なら不可
		SIF SEARCH_EQUIP_IC(104, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
	NEXT
NEXT
;全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;竿が必要
	SIF !PENIS(MTAR:(LOCAL:0))
		RETURN 0
	;挿入中は不可
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "全")
		RETURN 0
	;顔面騎乗中なら不可
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 110)
		RETURN 0
	;ターゲットが触手オナホされているなら不可
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 205)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM14
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;対Ｐ奉仕系コマンドの継続フラグを解除
CALL RELEASE_SERVE_P

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):逸脱 = 30
	SOURCE:(LOCAL:2):性行動 = 210

	;プレイヤー主導の場合
	IF GET_COM_INITIATIVE() == 0
		SOURCE:(LOCAL:2):嗜虐 = 80
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 170, 90)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1100) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 6)
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):逸脱 = 30
	SOURCE:(LOCAL:1):苦痛 = 20
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 120)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP14
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP14(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):逸脱 += 15
	SOURCE:(LOCAL:2):性行動 += 70

	;プレイヤー主導の場合
	IF IS_INITIATIVE(LOCAL:2)
		SOURCE:(LOCAL:2):嗜虐 = 80
	ENDIF

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 370) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 6)
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 10

	SOURCE:(LOCAL:1):接触 += 25
	SOURCE:(LOCAL:1):逸脱 += 15
	SOURCE:(LOCAL:1):苦痛 += 10
	SOURCE:(LOCAL:1):性行動 += 60

	;倒錯度変化基準値
	TCVAR:(LOCAL:1):50 += 1
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE14(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に足コキ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP14(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のペニスを足でシゴき上げている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP14(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER14(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 80

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF
IF TALENT:(ARG:0):男嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE14
;ターゲットが２人
IF MTAR_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0)%は
			IF MPLY:0 != MASTER && (TALENT:(MPLY:0):サド || ABL:(MPLY:0):主導度Ｕ >= 300)
				PRINT サディスティックな笑みを浮かべながら、
			ELSE
				PRINT 戸惑うようにしながら、
			ENDIF
			PRINTFORMW %ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを左右の足で踏みつけ擦りあげ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%は足を開き、%ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを左右の足で擦りあげて奉仕した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを足でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%は足を開き、%ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを左右の足で擦りあげた…
	ENDSELECT

;プレイヤーが３人
ELSEIF MPLY_NUM == 3
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、一斉に%ANAME(MTAR:0)%のペニスを踏みつけ擦り上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、協力しながら%ANAME(MTAR:0)%のペニスを足で擦り上げて奉仕した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人に、%ANAME(MTAR:0)%のペニスを足でシゴくように命令した
			PRINTFORMW 三人は言われた通り、一斉に%ANAME(MTAR:0)%のペニスを足で擦り上げた…
	ENDSELECT

;プレイヤーが２人
ELSEIF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、二人で%ANAME(MTAR:0)%のペニスを踏みつけ擦り上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、二人で協力しながら%ANAME(MTAR:0)%のペニスを足で擦り上げて奉仕した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%ANAME(MTAR:0)%のペニスを足でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、二人で%ANAME(MTAR:0)%のペニスを足で擦り上げた…
	ENDSELECT

;プレイヤーが１人
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0)%は
			IF MPLY:0 != MASTER && (TALENT:(MPLY:0):サド || ABL:(MPLY:0):主導度Ｕ >= 300)
				PRINT サディスティックな笑みを浮かべながら、両足で激しく
			ELSE
				PRINT 遠慮がちに、両足で
			ENDIF
			PRINTFORMW %ANAME(MTAR:0)%のペニスをシゴきあげ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の前で足を開き、ペニスを両足で擦りあげて奉仕した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%のペニスを足でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の前で足を開き、ペニスを両足で擦り上げた…
	ENDSELECT
ENDIF
