;露出プレイ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME112
LOCALS:0 = 露出プレイ
RESULTS:0 = %LOCALS:0%に出かける
RESULTS:1 = %LOCALS:0%に出かけさせられる
RESULTS:2 = %LOCALS:0%に出かけさせる
RESULTS:3 = %LOCALS:0%に出かけられる
RESULTS:4 = %LOCALS:0%に出かけさせる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE112
;共通部分
CALL COM_ABLE_COMMON(112)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人, MASTER
SIF MPLY:0 != MASTER || MPLY_NUM != 1
	RETURN 0

;ターゲット1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

;宮殿以外or既に露出プレイ中なら不可
SIF TFLAG:54 > 0
	RETURN 0

SIF ITEM:首輪 == 0
	RETURN 0


;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;顔面騎乗されているなら不可
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;口枷装着中なら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 86)
		RETURN 0
	;キス中なら不可
	SIF IS_EQUIP(MPLY:(LOCAL:0), 20)
		RETURN 0
	;フェラ・パイズリ・亀頭フェラ中なら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 11, 12, 107)
		RETURN 0
	;イラマ・触手口辱されているなら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 90, 210)
		RETURN 0
	;拘束されているなら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
NEXT
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;顔面騎乗されているなら不可
	SIF IS_RIDDEN(MTAR:(LOCAL:0))
		RETURN 0
	;挿入中なら不可
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "全")
		RETURN 0
	;キス中なら不可
	SIF IS_EQUIP(MTAR:(LOCAL:0), 20)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM112
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 40

	SOURCE:(LOCAL:2):嗜虐 = 20
	SOURCE:(LOCAL:2):露出 = 60
	SOURCE:(LOCAL:2):逸脱 = 20
	SOURCE:(LOCAL:2):不安 = 30

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 160)
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 80

	SOURCE:(LOCAL:2):逸脱 = 80
	SOURCE:(LOCAL:2):露出 = 100
	SOURCE:(LOCAL:2):不安 = 30

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 160, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

TFLAG:54 = 8
TFLAG:103 = 0

RETURN 1


;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP112
;継続コマンドかつフィルタリング不可
RETURN 2
;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP112(ARG:0)
;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):嗜虐 = 10
	SOURCE:(LOCAL:2):露出 = 30
	SOURCE:(LOCAL:2):逸脱 = 10
	SOURCE:(LOCAL:2):不安 = 15

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 10, 50)

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 3
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 40

	SOURCE:(LOCAL:2):逸脱 = 10
	SOURCE:(LOCAL:2):露出 = 20
	SOURCE:(LOCAL:2):不安 = 10
	
	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 20)

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 3
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE112(ARG:0)
LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%
RESULTS = %LOCALS:0%が露出プレイ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP112(ARG:0)
IF MEQUIP_TARGET_NUM:(ARG:0) > 1
	LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%たち
ELSE
	LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%
ENDIF

SELECTCASE TFLAG:103
	CASE 2
		SELECTCASE RAND:4
			CASE 0
				PRINTFORML 集まった男達は、目をハートにして%LOCALS:0%を眺めている…
			CASE 1
				PRINTFORML 集まった男達は、%LOCALS:0%の痴態を肴にペニスを扱いている…
			CASE 2
				PRINTFORML 集まった男達は、%LOCALS:0%に卑猥なヤジを投げかけている…
			CASE 3
				PRINTFORML 集まった男達は、%LOCALS:0%の裸身を間近で眺めている…
		ENDSELECT
	CASE 1
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML 物陰から熱い視線を感じる…
			CASE 1
				PRINTFORML 物陰から、男達が視線を送っている…
			CASE 2
				PRINTFORML 物陰から人の気配を感じる…
		ENDSELECT
		IF RAND:100 < 20
			PRINTFORMW 通りすがりの男達が、%LOCALS:0%を見つけたようだ
			PRINTFORMW %ANAME(MASTER)%が男達に許可を出すと、男達はニヤニヤと笑いながら%LOCALS:0%の痴態を眺め始めた…
			TFLAG:103 = 2
		ENDIF
	CASE 0
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML あたりに人の気配はないようだ…
			CASE 1
				PRINTFORML 近くを誰かが通りかかったようだ…
			CASE 2
				PRINTFORML 屋外のさわやかな風が吹き抜けた…
		ENDSELECT
		IF RAND:100 < 20
			PRINTFORMW 近くの物陰から、人の気配を感じる…
			PRINTFORMW どうやらでばがめに覗かれているようだ…
			TFLAG:103 = 1
		ENDIF
ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP112(ARG:0)
IF MEQUIP_TARGET_NUM:(ARG:0) > 1
	LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%たち
ELSE
	LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%
ENDIF
TFLAG:54 = 0
PRINTFORMW %LOCALS:0%を連れて屋内に戻った

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER112(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 5)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE112

LOCALS:0 = %ANAME(MPLY:0)%
;ターゲットが２人
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
;ターゲットが１人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:0%は%LOCALS:1%に首輪を嵌め、屋外へと出た…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:1%は%LOCALS:0%に首輪を嵌めてもらい、屋外へと出た…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%に首輪を嵌めるよう命令した
		PRINTFORMW %LOCALS:0%は言われるまま%LOCALS:1%に首輪を嵌め、屋外へと出た…
ENDSELECT
