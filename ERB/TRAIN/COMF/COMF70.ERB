;ローション

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME70
LOCALS:0 = ローション

RESULTS:0 = %LOCALS:0%を塗る
RESULTS:1 = %LOCALS:0%を塗らされる
RESULTS:2 = %LOCALS:0%を塗らせる
RESULTS:3 = %LOCALS:0%を塗られる
RESULTS:4 = %LOCALS:0%を塗らせる
RESULTS:5 = %LOCALS:0%塗り見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE70
;共通部分
CALL COM_ABLE_COMMON(70)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;ターゲットがＶＡ挿入中なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "ＶＡ")
	RETURN 0
;ローションが必要
SIF ITEM:ローション <= 0 && ITEM:A_ローション <= 0
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーが足コキ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
;ターゲットが尻コキ中なら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 15)
	RETURN 0
;ターゲットからプレイヤーに挿入中で体位が後背位・背面座位なら不可
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM70
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;ローションを１つ減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_ローション <= 0
	ITEM:ローション = MAX(ITEM:ローション - 1, 0)
ENDIF

;●プレイヤー側の処理
SOURCE:(MPLY:0):奉仕 = 50
SOURCE:(MPLY:0):性行動 = 60

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 50)
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 40, 40)

;●ターゲット側の処理
SOURCE:(MTAR:0):露出 = 100
SOURCE:(MTAR:0):性行動 = 150
SOURCE:(MTAR:0):液体追加 = 10000

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 50)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 50
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 40, 40)

;射精箇所と対象をセット
CALL STACK_SPERM(MPLY:0, MTAR:0, 0)

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER70(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE70
IF !VAGINA(MTAR:0)
	LOCALS:0 = アナル
ELSE
	LOCALS:0 = 股間
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:0%にローションを塗りつけ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MPLY:0)%は自身の%LOCALS:0%を指で広げて%ANAME(MTAR:0)%に見せつけると、ローションを塗るように要求した…
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%の%LOCALS:0%にローションを塗った…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の%LOCALS:0%にローションを塗るように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%の%LOCALS:0%にローションを塗りつけた…
ENDSELECT
