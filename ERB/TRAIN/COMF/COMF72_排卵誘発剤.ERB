;排卵誘発剤

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME72
LOCALS:0 = 排卵誘発剤

RESULTS:0 = %LOCALS:0%を飲ませる
RESULTS:1 = %LOCALS:0%を飲まさせられる
RESULTS:2 = %LOCALS:0%を飲まさせる
RESULTS:3 = %LOCALS:0%を飲まされる
RESULTS:4 = %LOCALS:0%を飲まさせる
RESULTS:5 = %LOCALS:0%使用見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE72
;共通部分
CALL COM_ABLE_COMMON(72)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;排卵誘発剤が必要
SIF ITEM:排卵誘発剤 <= 0 && ITEM:A_排卵誘発剤 <= 0
	RETURN 0
;既にターゲットが排卵誘発剤を使われているなら不可
SIF TCVAR:(MTAR:0):65
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーが足コキ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
;ターゲットの口が塞がっているなら不可
SIF IS_M_HOLD(MTAR:0)
	RETURN 0
;ターゲットからプレイヤーに挿入中で体位が後背位・背面座位なら不可
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM72
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;排卵誘発剤を１つ減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_排卵誘発剤 <= 0
	ITEM:排卵誘発剤 = MAX(ITEM:排卵誘発剤 - 1, 0)
ENDIF

;●プレイヤー側の処理

;●ターゲット側の処理
;排卵誘発剤使用フラグを立てる
TCVAR:(MTAR:0):65 = 1

IF TALENT:(MTAR:0):恋慕
	;主人公がプレイヤーなら愛情のソース追加
	IF IS_MPLY(MASTER)
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 50)
	ENDIF
ELSE
	LOCAL:1 = MAX(CFLAG:(MTAR:0):2, CFLAG:(MTAR:0):4)

	IF LOCAL:1 < 500
		SOURCE:(MTAR:0):反感 = 500
		SOURCE:(MTAR:0):不安 = 800
	ELSEIF LOCAL:1 < 800
		SOURCE:(MTAR:0):反感 = 250
		SOURCE:(MTAR:0):不安 = 500
	ELSEIF LOCAL:1 < 1500
		SOURCE:(MTAR:0):反感 = 150
		SOURCE:(MTAR:0):不安 = 350
	ELSE
		SOURCE:(MTAR:0):不安 = 100
	ENDIF

	SELECTCASE ABL:(MTAR:0):出産
		CASE 0
		CASE 1
			TIMES SOURCE:(MTAR:0):反感, 0.80
			TIMES SOURCE:(MTAR:0):不安, 0.80
			SOURCE:(MTAR:0):中毒充足 = 50
		CASE 2
			TIMES SOURCE:(MTAR:0):反感, 0.60
			TIMES SOURCE:(MTAR:0):不安, 0.60
			SOURCE:(MTAR:0):中毒充足 = 100
		CASE 3
			TIMES SOURCE:(MTAR:0):反感, 0.40
			TIMES SOURCE:(MTAR:0):不安, 0.40
			SOURCE:(MTAR:0):中毒充足 = 200
		CASE 4
			TIMES SOURCE:(MTAR:0):反感, 0.20
			TIMES SOURCE:(MTAR:0):不安, 0.20
			SOURCE:(MTAR:0):中毒充足 = 400
		CASEELSE
			SOURCE:(MTAR:0):反感 = 0
			SOURCE:(MTAR:0):不安 = 0
			SOURCE:(MTAR:0):中毒充足 = 600 + (ABL:(MTAR:0):出産 - 5) * 3
	ENDSELECT
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 40, 50)

;射精箇所と対象をセット
CALL STACK_SPERM(MPLY:0, MTAR:0, 0)

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

;経過時間を無理矢理1減らす
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER72(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE72
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に排卵誘発剤を飲ませ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に排卵誘発剤を飲ませるように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%に排卵誘発剤を飲み込ませた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%に排卵誘発剤を飲ませるように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%に排卵誘発剤を飲み込ませた…
ENDSELECT

IF MTAR:0 != MASTER
	IF TALENT:(MTAR:0):恋慕
		PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の子を孕む期待感に頬を緩ませている
	ELSEIF ABL:(MTAR:0):出産 >= 3
		PRINTFORML %ANAME(MTAR:0)%は子を孕み出産することを想像し、興奮を隠せずにいる
	;捕虜調教のメインターゲット
	ELSEIF FLAG:12 == 2 && MTAR:0 == FLAG:19
		PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の子を孕むことを想像して恐怖に顔を引きつらせた
	ELSE
		PRINTFORML %ANAME(MTAR:0)%は不安そうな表情を浮かべている
	ENDIF
ENDIF
WAIT
