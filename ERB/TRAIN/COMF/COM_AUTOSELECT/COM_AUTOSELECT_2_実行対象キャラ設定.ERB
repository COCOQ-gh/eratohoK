;-------------------------------------------------
;# おまかせ時のプレイヤー選択
;-------------------------------------------------
@COM_AUTOSELECT_DECIDE_PLAYER
#DIM L_COUNTER
#DIM RETURN_CHARA_NUM
#DIM DECIDE_REFERENCE_VALUE	;重みの基準

DEBUGPRINTFORML 
DEBUGPRINTFORML #### DEBUG MAIN_PLAYER選択 

;-------------------------------------------------
;## リセット
;-------------------------------------------------
CALL COM_AUTO_RANDOMIZED_WEIGHTED_START

;-------------------------------------------------
;## 候補設定
;-------------------------------------------------
FOR L_COUNTER, 0, CHARANUM
	;参加者ではない
	SIF !CFLAG:L_COUNTER:調教参加フラグ
		CONTINUE
	;行動不可能
	SIF !IS_AUTOSELECT(L_COUNTER)
		CONTINUE
	;---------------------------------------------
	;### 重みの基準
	;	+1は0除算回避用 最大100 最小10
	;---------------------------------------------
	DECIDE_REFERENCE_VALUE = MAX((1000 + ABL:L_COUNTER:主導度Ｕ + 1) / 20, 10)
	DEBUGPRINTFORM      %ANAME(L_COUNTER), MAX_CHARANAME_LENGTH, LEFT%  %TOSTR_SEX(TALENT:L_COUNTER:性別), 6%  
	;慰安でないときは主人公に挿入している・されているキャラを優先
	IF FLAG:調教モード != 調教_慰安 && IS_INSERT_MUTUAL(MASTER, L_COUNTER) >= 1
		DECIDE_REFERENCE_VALUE = DECIDE_REFERENCE_VALUE * 100
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}  あなたと結合中
	;挿入中ではないキャラ
	ELSE
		DECIDE_REFERENCE_VALUE = DECIDE_REFERENCE_VALUE * 50
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}
	ENDIF
NEXT

;-------------------------------------------------
;## 結果取得
;-------------------------------------------------
RETURN_CHARA_NUM = COM_AUTO_RANDOMIZED_WEIGHTED_RESULT()

RETURN RETURN_CHARA_NUM

;-------------------------------------------------
;# おまかせ時のターゲット選択
;-------------------------------------------------
@COM_AUTOSELECT_DECIDE_TARGET(プレイヤー)
#DIM L_COUNTER
#DIM プレイヤー
#DIM RETURN_CHARA_NUM
#DIM DECIDE_REFERENCE_VALUE					;重みの基準
#DIM MULTI_SEX_DECIDE_REFERENCE_VALUE		;性別による重みの乗算補正
#DIM MULTI_VIRGIN_DECIDE_REFERENCE_VALUE	;処女性による重みの乗算補正

DEBUGPRINTFORML 
DEBUGPRINTFORML #### DEBUG MAIN_TARGET選択 

;-------------------------------------------------
;## リセット
;-------------------------------------------------
CALL COM_AUTO_RANDOMIZED_WEIGHTED_START

;-------------------------------------------------
;## 候補設定
;	※特殊勢力の捕虜調教で、MASTERにCFLAG:L_COUNTER:調教参加フラグが立ってないぽい
;-------------------------------------------------
FOR L_COUNTER, 0, CHARANUM
	;参加者ではなく逆調教中のMASTERでもない
	SIF !CFLAG:L_COUNTER:調教参加フラグ && !(GROUPMATCH(FLAG:調教モード, 調教_逆調教特殊, 調教_逆調教通常) && L_COUNTER == MASTER)
		CONTINUE
	;実行者
	SIF L_COUNTER == プレイヤー
		CONTINUE
	;---------------------------------------------
	;### 性別組み合わせの補正
	;---------------------------------------------
	;実行が両刀or実行ふたなりor対象ふたなり
	IF TALENT:プレイヤー:両刀 || (HAS_PENIS(プレイヤー) && HAS_VAGINA(プレイヤー)) || (HAS_PENIS(L_COUNTER) && HAS_VAGINA(L_COUNTER))
		MULTI_SEX_DECIDE_REFERENCE_VALUE = 100
	;異性
	ELSEIF (HAS_PENIS(プレイヤー) && HAS_VAGINA(L_COUNTER)) || (HAS_VAGINA(プレイヤー) && HAS_PENIS(L_COUNTER))
		MULTI_SEX_DECIDE_REFERENCE_VALUE = 100
	;同性
	ELSE
		MULTI_SEX_DECIDE_REFERENCE_VALUE = 1
	ENDIF
	;---------------------------------------------
	;### 処女系の補正
	;---------------------------------------------
	;マスターが対象で処女系素質を持つとき
	IF L_COUNTER == MASTER && (TALENT:MASTER:キス未経験 || (HAS_VAGINA(MASTER) && TALENT:MASTER:処女) || (HAS_PENIS(MASTER) && TALENT:MASTER:童貞))
		MULTI_VIRGIN_DECIDE_REFERENCE_VALUE = 100
	;プレイヤーが処女系素質を持つとき
	ELSEIF TALENT:プレイヤー:キス未経験 || (HAS_VAGINA(プレイヤー) && TALENT:プレイヤー:処女) || (HAS_PENIS(プレイヤー) && TALENT:プレイヤー:童貞)
		MULTI_VIRGIN_DECIDE_REFERENCE_VALUE = 100
	;どちらも処女系は持たないとき
	ELSE
		MULTI_VIRGIN_DECIDE_REFERENCE_VALUE = 1
	ENDIF
	;---------------------------------------------
	;### 重みの基準
	;	主導度Ｕのマイナス量を元に10～100に
	;---------------------------------------------
	DECIDE_REFERENCE_VALUE = MAX((2000 - (1000 + ABL:L_COUNTER:主導度Ｕ) + 1) / 20, 10)
	DEBUGPRINTFORM      %ANAME(L_COUNTER), MAX_CHARANAME_LENGTH, LEFT%  %TOSTR_SEX(TALENT:L_COUNTER:性別), 6%  
	;逆調教の主人公
	IF L_COUNTER == MASTER && GROUPMATCH(FLAG:調教モード, 調教_逆調教特殊, 調教_逆調教通常, 調教_慰安)
		DECIDE_REFERENCE_VALUE = DECIDE_REFERENCE_VALUE * MULTI_SEX_DECIDE_REFERENCE_VALUE * MULTI_VIRGIN_DECIDE_REFERENCE_VALUE * 100
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}  逆調教中のあなた
	;逆調教以外の主人公
	ELSEIF L_COUNTER == MASTER
		DECIDE_REFERENCE_VALUE = DECIDE_REFERENCE_VALUE * MULTI_SEX_DECIDE_REFERENCE_VALUE * MULTI_VIRGIN_DECIDE_REFERENCE_VALUE * 70
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}  あなた
	;捕虜調教のターゲット
	ELSEIF FLAG:調教モード == 調教_捕虜調教 && FINDELEMENT(PRISONER_TARGET, L_COUNTER)
		DECIDE_REFERENCE_VALUE = DECIDE_REFERENCE_VALUE * MULTI_SEX_DECIDE_REFERENCE_VALUE * 70
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}  捕虜調教の対象
	;慰安モードでプレイヤーが慰安モブや慰安参加者のとき慰安モブや慰安参加者は確率低下
	ELSEIF FLAG:調教モード == 調教_慰安 && (TALENT:プレイヤー:慰安モブ || CFLAG:プレイヤー:慰安参加者) && (TALENT:L_COUNTER:慰安モブ || CFLAG:L_COUNTER:慰安参加者)
		DECIDE_REFERENCE_VALUE = 3
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}  実行対象共にモブや参加者
	;それ以外
	ELSE
		DECIDE_REFERENCE_VALUE = DECIDE_REFERENCE_VALUE * MULTI_SEX_DECIDE_REFERENCE_VALUE * 30
		CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, DECIDE_REFERENCE_VALUE)
		DEBUGPRINTFORML 重み {DECIDE_REFERENCE_VALUE, 10}
	ENDIF
NEXT

;-------------------------------------------------
;## 結果取得
;-------------------------------------------------
RETURN_CHARA_NUM = COM_AUTO_RANDOMIZED_WEIGHTED_RESULT()

RETURN RETURN_CHARA_NUM

;-------------------------------------------------
;# 第三者の参加を決定する
;-------------------------------------------------
@AUTO_SET_THIRD_PERSON(主導者, 対象者, コマンド)
#DIM L_COUNTER
#DIM コマンド
#DIM 行動可能者
#DIM 主導者
#DIM 対象者
#DIM 今回候補
#DIM 対象
#DIM 成功時BREAK

FOR L_COUNTER, 0, 5
	SIF !RAND:5
		CONTINUE

	成功時BREAK = 0
	;行動可能なキャラの数を調べる
	行動可能者 = 0
	FOR 対象, 0, CHARANUM
		IF IS_AUTOSELECT(対象) && !IS_MPLY(対象) && !IS_MTAR(対象)
			行動可能者 ++
		ENDIF
	NEXT

	;行動可能なキャラが1人以上存在する場合
	SIF 行動可能者 < 1
		BREAK

	;プレイヤー/ターゲットに追加するキャラの選択
	LOCAL:3 = RAND:(行動可能者)
	LOCAL:4 = 0
	今回候補 = -1
	FOR 対象, 0, CHARANUM
		IF IS_AUTOSELECT(対象) && !IS_MPLY(対象) && !IS_MTAR(対象)
			IF LOCAL:3 == LOCAL:4
				今回候補 = 対象
				BREAK
			ENDIF
			LOCAL:4 ++
		ENDIF
	NEXT

	SIF 今回候補 < -1
		CONTINUE

	IF FLAG:調教モード == 調教_慰安
		;慰安する側なら、「もう一人の慰安する側に合わせる」か、「主導者の側に入り、主導者を削除（命じられるシチュになる）」になる
		IF !CFLAG:今回候補:慰安参加者
			IF !RAND:4
				IF IS_MTAR(主導者)
					CALL ADD_MTAR(今回候補)
					CALL DEL_MTAR(今回候補)
				ELSE
					CALL ADD_MPLY(今回候補)
					CALL DEL_MPLY(主導者)
				ENDIF
				成功時BREAK = 1
			ELSE
				IF IS_MTAR(対象者)
					CALL ADD_MTAR(今回候補)
				ELSE
					CALL ADD_MPLY(今回候補)
				ENDIF
			ENDIF
			GOTO DECIDED
		ENDIF
	ENDIF
	;純粋な性別による判定とペニスがあるかの判定の二つを織り交ぜ、ふたなりの挙動に揺らぎをもたせる
	IF RAND:2
		IF IS_MALE(主導者) && IS_MALE(対象者) && IS_MALE(今回候補)
			LOCAL:30 = 0
		ELSEIF IS_MALE(主導者) && IS_MALE(対象者) && IS_FEMALE(今回候補)
			LOCAL:30 = 1
		ELSEIF IS_MALE(主導者) && IS_FEMALE(対象者) && IS_MALE(今回候補)
			LOCAL:30 = 2
		ELSEIF IS_FEMALE(主導者) && IS_MALE(対象者) && IS_MALE(今回候補)
			LOCAL:30 = 3
		ELSEIF IS_MALE(主導者) && IS_FEMALE(対象者) && IS_FEMALE(今回候補)
			LOCAL:30 = 4
		ELSEIF IS_FEMALE(主導者) && IS_MALE(対象者) && IS_FEMALE(今回候補)
			LOCAL:30 = 5
		ELSEIF IS_FEMALE(主導者) && IS_FEMALE(対象者) && IS_MALE(今回候補)
			LOCAL:30 = 6
		ELSEIF IS_FEMALE(主導者) && IS_FEMALE(対象者) && IS_FEMALE(今回候補)
			LOCAL:30 = 7
		ENDIF
	ELSE
		IF HAS_PENIS(主導者) && HAS_PENIS(対象者) && HAS_PENIS(今回候補)
			LOCAL:30 = 0
		ELSEIF HAS_PENIS(主導者) && HAS_PENIS(対象者) && HAS_VAGINA(今回候補)
			LOCAL:30 = 1
		ELSEIF HAS_PENIS(主導者) && HAS_VAGINA(対象者) && HAS_PENIS(今回候補)
			LOCAL:30 = 2
		ELSEIF HAS_VAGINA(主導者) && HAS_PENIS(対象者) && HAS_PENIS(今回候補)
			LOCAL:30 = 3
		ELSEIF HAS_PENIS(主導者) && HAS_VAGINA(対象者) && HAS_VAGINA(今回候補)
			LOCAL:30 = 4
		ELSEIF HAS_VAGINA(主導者) && HAS_PENIS(対象者) && HAS_VAGINA(今回候補)
			LOCAL:30 = 5
		ELSEIF HAS_VAGINA(主導者) && HAS_VAGINA(対象者) && HAS_PENIS(今回候補)
			LOCAL:30 = 6
		ELSEIF HAS_VAGINA(主導者) && HAS_VAGINA(対象者) && HAS_VAGINA(今回候補)
			LOCAL:30 = 7
		ENDIF
	ENDIF
	;プレイヤー／ターゲットを追加してコマンドが実行できるかチェック
	IF コマンド < ADD_NUM_対象が実行
		;愛撫系(胸愛撫など)
		IF GET_TRAINNUM("愛撫") <= コマンド && コマンド <= 9
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 5
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 3
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;奉仕系
		ELSEIF GET_TRAINNUM("手コキ") <= コマンド && コマンド < 19
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 5
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 3
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;キス
		ELSEIF GROUPMATCH(コマンド, GET_TRAINNUM("キス"))
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 5
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 3
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;順番挿入
		ELSEIF GROUPMATCH(コマンド, GET_TRAINNUM("順番に挿入"), GET_TRAINNUM("順番にＡ挿入"))
			;女なら問答無用でされる側
			SIF IS_FEMALE(今回候補)
				CALL ADD_MTAR(今回候補)
		;代わる代わる挿入
		ELSEIF GROUPMATCH(コマンド, GET_TRAINNUM("代わる代わる挿入"), GET_TRAINNUM("代わる代わるＡ挿入"))
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 3, 6
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 1, 4, 5, 7
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;道具系
		ELSEIF GET_TRAINNUM("ローター") <= コマンド && コマンド < 79
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 6
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 3
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;ＳＭ
		ELSEIF GET_TRAINNUM("脅す") <= コマンド && コマンド <= 99
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 5
					CALL ADD_MPLY(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;羞恥
		ELSEIF GET_TRAINNUM("自慰") <= コマンド && コマンド <= 129
			SELECTCASE LOCAL:30
				;する側
				CASE 0, 2, 5, 7
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 3
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		ELSE
			CALL ADD_MPLY(今回候補)
		ENDIF
	ELSEIF コマンド < ADD_NUM_第三者に実行
		;愛撫系(胸愛撫など)
		IF ADD_NUM_対象が実行 + GET_TRAINNUM("愛撫") <= コマンド && コマンド <= ADD_NUM_対象が実行 + 9
			SELECTCASE LOCAL:30
				;する側
				CASE 1, 3
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 2, 5
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;奉仕系
		ELSEIF ADD_NUM_対象が実行 + GET_TRAINNUM("手コキ") <= コマンド && コマンド <= ADD_NUM_対象が実行 + 19
			SELECTCASE LOCAL:30
				;する側
				CASE 3
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 2, 5
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;キス
		ELSEIF GROUPMATCH(コマンド, ADD_NUM_対象が実行 + GET_TRAINNUM("キス"))
			SELECTCASE LOCAL:30
				;する側
				CASE 3
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 2, 5
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;順番挿入
		ELSEIF GROUPMATCH(コマンド, ADD_NUM_対象が実行 + GET_TRAINNUM("順番に挿入"), ADD_NUM_対象が実行 + GET_TRAINNUM("順番にＡ挿入"))
			;女なら問答無用でされる側
			SIF IS_FEMALE(今回候補)
				CALL ADD_MTAR(今回候補)
		;代わる代わる挿入
		ELSEIF GROUPMATCH(コマンド, ADD_NUM_対象が実行 + GET_TRAINNUM("代わる代わる挿入"), ADD_NUM_対象が実行 + GET_TRAINNUM("代わる代わるＡ挿入"))
			SELECTCASE LOCAL:30
				;する側
				CASE 3, 6
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 1, 2, 5
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;道具系
		ELSEIF ADD_NUM_対象が実行 + GET_TRAINNUM("ローター") <= コマンド && コマンド <= ADD_NUM_対象が実行 + 79
			SELECTCASE LOCAL:30
				;する側
				CASE 3, 6
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 2, 5
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;ＳＭ
		ELSEIF ADD_NUM_対象が実行 + GET_TRAINNUM("脅す") <= コマンド && コマンド <= ADD_NUM_対象が実行 + 99
			SELECTCASE LOCAL:30
				;する側
				CASE 3, 4, 6
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 2, 5 
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		;羞恥
		ELSEIF ADD_NUM_対象が実行 + GET_TRAINNUM("自慰") <= コマンド && コマンド <= ADD_NUM_対象が実行 + 129
			SELECTCASE LOCAL:30
				;する側
				CASE 3, 4
					CALL ADD_MPLY(今回候補)
				;される側
				CASE 0, 2, 5
					CALL ADD_MTAR(今回候補)
				;ランダム
				CASEELSE
					IF RAND:2
						CALL ADD_MPLY(今回候補)
					ELSE
						CALL ADD_MTAR(今回候補)
					ENDIF
			ENDSELECT
		ELSE
			CALL ADD_MPLY(今回候補)
		ENDIF
	ENDIF
	$DECIDED

	;コマンド判定フラグON
	COM_ENABLE = 1

	RESULT = 0
	TRYCALLFORM COM_ABLE{コマンド % 1000}
	IF !RESULT
		;ダメならプレイヤー／ターゲットを外す
		IF コマンド < ADD_NUM_対象が実行
			CALL DEL_MPLY(今回候補)
			CALL DEL_MTAR(今回候補)
		ELSEIF コマンド < ADD_NUM_第三者に実行
			CALL DEL_MTAR(今回候補)
			CALL DEL_MPLY(今回候補)
		ENDIF

		;コマンド判定フラグOFF
		COM_ENABLE = 0
		BREAK
	ELSE
		;コマンド判定フラグOFF
		COM_ENABLE = 0
		SIF 成功時BREAK
			BREAK
	ENDIF
NEXT

;-------------------------------------------------
;# 第三者命令
;desc  :自動調教時、主導者が第三者に命じる形になるようにする処理
;param :主導者:その回におけるメイン行動者
;PARAM :対象者:その回におけるメイン対象者
;-------------------------------------------------
@AUTO_SET_REMOVE_MAIN(主導者, 対象者, コマンド)
#DIM 主導者
#DIM 対象者
#DIM コマンド
#DIM MTARフラグ

IF IS_MTAR(主導者) && MTAR_NUM > 1 && RAND:100 < 25
	CALL DEL_MTAR(主導者)
	MTARフラグ = 1
ELSEIF IS_MPLY(主導者) && MPLY_NUM > 1 && RAND:100 < 25
	CALL DEL_MPLY(主導者)
	MTARフラグ = 0
ENDIF

;コマンド判定フラグON
COM_ENABLE = 1

RESULT = 0
TRYCALLFORM COM_ABLE{コマンド % 1000}
IF !RESULT
	;ダメならプレイヤー／ターゲットを戻す
	IF MTARフラグ
		CALL ADD_MTAR(主導者)
	ELSE
		CALL ADD_MPLY(主導者)
	ENDIF
ENDIF

;コマンド判定フラグOFF
COM_ENABLE = 0

