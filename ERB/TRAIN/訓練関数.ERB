;------------------------------------------------------------
;訓練者ARG:0が 訓練相手ARG:1と　ARG:2（種類：50なら武闘。ABL.csvに対応する番号）を訓練する
;効果的な訓練かどうか％倍率のイメージのRETURN（訓練倍率）を返す
;TALENT(「怠け者」「超成長」)、GROWTYPE(成長タイプ)は 必要EXPのほうに考慮されているのでここでは扱わない
;歌唱・料理についてはABLUPの設定をみるに 武知政と差別化するのがよい？  歌30=武80？ 歌50=武90のイメージ？
;------------------------------------------------------------
@EXP_TRAIN_SLG(ARG:0, ARG:1, ARG:2)
#FUNCTION
#DIM 訓練者 	;経験値を得る人
#DIM 訓練者能力	;訓練対象の能力値
#DIM 相手 		;訓練相手
#DIM 種類 　	;50=武闘　51=知略　52=政治　60=歌唱　61=料理
#DIM 訓練倍率	;リターン項目

訓練者		= ARG:0
相手		= ARG:1
種類		= ARG:2
訓練者能力	= ABL:(訓練者):(種類)
訓練倍率	= 100

;歌唱・料理は効果絶大のためあがりにくくする
IF (種類) == 60 || (種類) == 61
	訓練者能力 += 30		;成長抑制をこの数値分早くから掛ける
ENDIF


	;成長抑制　現在能力が高いと経験になりにくい
	;能力50（3200)を基準として成長しにくく　減少傾向要調整
	;根本的に必要経験値が低いので苦肉の策
	IF 訓練者能力 >= (60 + (GETBIT(TALENT:訓練者:ＳＬＧ素質, 素質_ＳＬＧ_努力家) || GETBIT(TALENT:相手:ＳＬＧ素質, 素質_ＳＬＧ_努力家)) * 20)
		訓練倍率 = 訓練倍率 * ( 100 - (訓練者能力 - 50 ) ) / 100
		訓練倍率 = MAX( 訓練倍率 , 50 )
	ENDIF

IF !GETBIT(TALENT:訓練者:ＳＬＧ素質, 素質_ＳＬＧ_超成長力)	;超成長力を有するとこれらの抑制を受けない
	SIF 訓練者 == MASTER		;MASTERは1期に何人とも訓練できるので抑制
		TIMES 訓練倍率 , 0.66
ENDIF

;訓練相手との力量差による効率 ;能力＋１０でパワー倍、の時期作成
SELECTCASE ABL:(訓練者):(種類) - ABL:(相手):(種類)
	CASE IS < -10			;離れすぎ
			訓練倍率 *= 115
	CASE -10 TO -4			;格上を相手
			訓練倍率 *= 130
	CASE -3 TO 3			;同格の相手と訓練
			訓練倍率 *= 100	;基準
	CASE 4 TO 10			;格下を指導
			訓練倍率 *= 80
	CASEELSE				;離れすぎ
			訓練倍率 *= 70
ENDSELECT


IF 相手 == MASTER			;MASTER相手の訓練ならソース追加
	SELECTCASE ABL:(訓練者):(種類) - ABL:(相手):(種類)
		CASE IS < -10			;離れすぎ
				SOURCE:(訓練者):屈従 = 60
		CASE -10 TO -4			;格上を相手
				SOURCE:(訓練者):屈従 = 20
		CASE -3 TO 3			;同格の相手と訓練
				SOURCE:(訓練者):歓楽 += 20
		CASE 4 TO 10			;格下を指導
				SOURCE:(訓練者):優越 = 20
		CASEELSE				;離れすぎ
				SOURCE:(訓練者):優越 = 60
	ENDSELECT
ENDIF

訓練倍率 /= 100

RETURNF 訓練倍率
