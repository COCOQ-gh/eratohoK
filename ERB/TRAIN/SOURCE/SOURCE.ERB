;-------------------------------------------------
;ソースの処理など
;-------------------------------------------------
@SOURCE_CHECK
;コマンド実行時の地の文
CALL COM_TEXT_BEFORE

;そのコマンドに対応する継続状態の処理を中断
FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		CALL RELEASE_EQUIP_EX(SELECTCOM, MPLY:(LOCAL:0), MTAR:(LOCAL:1))
	NEXT
NEXT

;衣装嗜好の経験値増加
;(衣装を着ている状態で、且つウフフモードでない場合、経験値が入る)
FOR LOCAL:0, 0, CHARANUM
	IF !FLAG:50 && CFLAG:(LOCAL:0):92 && CFLAG:(LOCAL:0):6
		EXP:(LOCAL:0):衣装嗜好経験値 += 2 + MAX(18-ABL:(LOCAL:0):衣装嗜好*1, 0)
	ENDIF
NEXT

;装着系アイテムや継続状態の処理
FOR LOCAL:0, 0, MAX_MEQUIP
	IF MEQUIP:(LOCAL:0) >= 0
		TRYCALLFORM COM_EQUIP{MEQUIP:(LOCAL:0)}(LOCAL:0)
	ENDIF
NEXT

;同性の場合のソース補正
CALL SOURCE_SEX_CHECK(MASTER)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6
		CALL SOURCE_SEX_CHECK(LOCAL:0)
	ENDIF
NEXT

;EQUIPに対応する地の文
CALL COM_TEXT_BEFORE_EQUIP

;コマンド実行時の口上
CALL KOJO_COM

;★コマンドによる体力の消費を0にする暫定措置
CVARSET DOWNBASE, 0, 0

;●キス未経験・処女・童貞のチェック
CALL FIRST_FLAG_CHECK(MASTER)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6
		CALL FIRST_FLAG_CHECK(LOCAL:0)
	ENDIF
NEXT

;●露出ソースの保存
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6
		SOURCE:(LOCAL:0):露出保存 = SOURCE:(LOCAL:0):露出
	ENDIF
NEXT

;●ペニバン装着時の快Ｐソースを快Ｃソースに変換
FOR LOCAL:0, 0, CHARANUM
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6) && !PENIS(LOCAL:0)
		SOURCE:(LOCAL:0):快Ｃ += SOURCE:(LOCAL:0):快Ｐ / 6
		SOURCE:(LOCAL:0):快Ｐ = 0
	ENDIF
NEXT

;●主人のソース処理
CALL SOURCE_CHECK_MASTER

;●ターゲットのソース処理
;快楽系ソースと絶頂の処理
FOR LOCAL:0, 0, MTAR_NUM
	IF MTAR:(LOCAL:0) != MASTER
		CALL SOURCE_CHECK_1(MTAR:(LOCAL:0))
	ENDIF
NEXT

;快楽系以外のソースの処理
FOR LOCAL:0, 0, MTAR_NUM
	IF MTAR:(LOCAL:0) != MASTER
		CALL SOURCE_CHECK_2(MTAR:(LOCAL:0))
	ENDIF
NEXT

;●ターゲット以外のソース処理
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6 && !IS_MTAR(LOCAL:0)
		;快楽系ソースと絶頂の処理
		CALL SOURCE_CHECK_1(LOCAL:0)
	ENDIF
NEXT

FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6 && !IS_MTAR(LOCAL:0)
		;快楽系以外のソースの処理
		CALL SOURCE_CHECK_2(LOCAL:0)
	ENDIF
NEXT

;絶頂表示を行った場合は改行
IF TFLAG:3
	PRINTL 
ENDIF

FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6
		;ソース以外の変動処理
		CALL SOURCE_CHANGE_OTHER_STATUS(LOCAL:0)
	ENDIF
NEXT

LOCAL:10 = 0
;メッセージ表示
CALL COM_TEXT_AFTER

LOCAL:10 |= RESULT

;パラメータ変動後の口上
CALL KOJO_COM_AFTER

LOCAL:10 |= RESULT

IF LOCAL:10
	PRINTL 
ENDIF

;各種経験の取得
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6 || LOCAL:0 == MASTER
		CALL EXP_GOT_CHECK(LOCAL:0)
	ENDIF
NEXT

;テキストの表示
CALL COM_TEXT_LAST

;好感度・依存度・従属度の変動処理とその表示
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6 && LOCAL:0 != MASTER
		CALL LOVE_UP_CHECK(LOCAL:0)
	ENDIF
NEXT
PRINTL 

;コマンド実行により得たEXPを表示
CALL PRINT_EXP_UP(MASTER)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6
		CALL PRINT_EXP_UP(LOCAL:0)
	ENDIF
NEXT

;能力の上昇判定
CALL TRAIN_AUTO_ABLUP(MASTER)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6
		CALL TRAIN_AUTO_ABLUP(LOCAL:0)
	ENDIF
NEXT
PRINTL 

;調教ソースの表示
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):6 || LOCAL:0 == MASTER
		CALL SHOW_SOURCE(LOCAL:0)
	ENDIF
NEXT

IF !TFLAG:1
	;MEQUIPのセット
	CALL SET_EQUIP(SELECTCOM)
ENDIF

;逆調教中なら機嫌を常にリセット
IF FLAG:12 == 4
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):6
			FOR LOCAL:1, 10, 16
				PALAM:(LOCAL:0):(LOCAL:1) = 0
			NEXT
		ENDIF
	NEXT
ENDIF

;気絶回復判定
FOR LOCAL:0, 0, CHARANUM
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6) && TCVAR:(LOCAL:0):52
		;毎ターンの自然回復
		TCVAR:(LOCAL:0):23 -= 100

		;絶頂による回復
		TCVAR:(LOCAL:0):23 -= SUM_NOWEX(LOCAL:0) * 50

		;苦痛のソースによる回復
		TCVAR:(LOCAL:0):23 -= SOURCE:(LOCAL:0):苦痛 / 2

		;気絶回復のソースによる回復
		TCVAR:(LOCAL:0):23 -= SOURCE:(LOCAL:0):気絶回復

		;気絶深度が0以下なら復帰
		IF TCVAR:(LOCAL:0):23 <= 0
			TCVAR:(LOCAL:0):52 = 0
			BASE:(LOCAL:0):気力 = MAXBASE:(LOCAL:0):気力 * MAX(RAND:8 + 18 - TCVAR:(LOCAL:0):22 * 2, 2) / 100
			SETCOLOR COLOR("注意")
			IF SUM_NOWEX(LOCAL:0) >= 1
				IF SOURCE:(LOCAL:0):苦痛 >= 50
					PRINTFORML %ANAME(LOCAL:0)%は痛みと快感で無理やり意識を引き戻された！
				ELSE
					PRINTFORML %ANAME(LOCAL:0)%は激しい快感で意識を引き戻された！
				ENDIF
			ELSEIF SOURCE:(LOCAL:0):苦痛 >= 50
				PRINTFORML %ANAME(LOCAL:0)%は苦痛のあまり意識を引き戻された！
			ELSEIF SOURCE:(LOCAL:0):気絶回復 >= 50
				PRINTFORML %ANAME(LOCAL:0)%は無理やり意識を引き戻された！
			ELSE
				PRINTFORML %ANAME(LOCAL:0)%は意識を取り戻した！
			ENDIF
			RESETCOLOR
		ENDIF
	ENDIF
NEXT

;体力切れによる行動不能判定
FOR LOCAL:0, 0, CHARANUM
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6) && !TCVAR:(LOCAL:0):51 && !TCVAR:(LOCAL:0):53 && BASE:(LOCAL:0):体力 <= 0
		WAIT
		PRINTL 
		;捕虜調教かつターゲット場合
		IF FLAG:12 == 2 && LOCAL:0 != MASTER
			CALL KOJO_EVENT(LOCAL:0, 17)
			PRINTFORML %ANAME(LOCAL:0)%は疲れ果ててぐったりしている…
			;体力0による行動不能
			TCVAR:(LOCAL:0):51 = 1
		;逆調教で主人公の場合
		ELSEIF FLAG:12 == 4 && LOCAL:0 == MASTER
			PRINTFORML %ANAME(LOCAL:0)%は既に体力の限界だ…
			;体力0による行動不能
			TCVAR:(LOCAL:0):51 = 1
		;捕虜調教でない、あるいは捕虜調教でターゲットでない場合
		ELSE
			IF FLAG:12 == 2
				PRINTFORML %ANAME(LOCAL:0)%は疲れてしまい、これ以上調教を続けられそうにない…
				;体力0による行動不能
				TCVAR:(LOCAL:0):51 = 1
			ELSE
				CALL KOJO_EVENT(LOCAL:0, 11)
				PRINTFORML %ANAME(LOCAL:0)%は疲れ果ててぐったりしている…
				;体力0による行動不能
				TCVAR:(LOCAL:0):51 = 1
			ENDIF
		ENDIF
		;行動不能になった時のEQUIP解除
		CALL DOWN_RELEASE_EQUIP(LOCAL:0)
	ENDIF
NEXT

;気力切れによる失神判定
FOR LOCAL:0, 0, CHARANUM
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6) && !TCVAR:(LOCAL:0):52 && !TCVAR:(LOCAL:0):53 && BASE:(LOCAL:0):気力 <= 0
		WAIT
		PRINTL 
		;捕虜調教かつターゲット場合
		IF FLAG:12 == 2 && LOCAL:0 != MASTER
			CALL KOJO_EVENT(LOCAL:0, 18)
			PRINTFORML %ANAME(LOCAL:0)%は快感のあまり気を失ってしまった…
			;気力0による失神
			TCVAR:(LOCAL:0):52 = 1
			;気絶深度
			TCVAR:(LOCAL:0):23 = RAND:301 + 150 + TCVAR:(LOCAL:0):22 * 100
			;気絶回数を加算
			TCVAR:(LOCAL:0):22 ++
		;逆調教で主人公の場合
		ELSEIF FLAG:12 == 4 && LOCAL:0 == MASTER
			PRINTFORML %ANAME(LOCAL:0)%は快感のあまり気を失ってしまった…
			;気力0による失神
			TCVAR:(LOCAL:0):52 = 1
			;気絶深度
			TCVAR:(LOCAL:0):23 = RAND:301 + 150 + TCVAR:(LOCAL:0):22 * 100
			;気絶回数を加算
			TCVAR:(LOCAL:0):22 ++
		;捕虜調教でない、あるいは捕虜調教でターゲットでない場合
		ELSE
			CALL KOJO_EVENT(LOCAL:0, 12)
			PRINTFORML %ANAME(LOCAL:0)%は快感のあまり気を失ってしまった…
			;気力0による失神
			TCVAR:(LOCAL:0):52 = 1
			;気絶深度
			TCVAR:(LOCAL:0):23 = RAND:301 + 150 + TCVAR:(LOCAL:0):22 * 100
			;気絶回数を加算
			TCVAR:(LOCAL:0):22 ++
			;調教参加キャラが１人の場合
			IF TFLAG:44 == 1
				;行動不能になったのがプレイヤーなら行動側反転
				IF IS_MPLY(LOCAL:0)
					LOCAL:5 = MPLY:0
					LOCAL:6 = MTAR:0
					CALL CLEAR_MPLY
					CALL CLEAR_MTAR
					CALL ADD_MPLY(LOCAL:6)
					CALL ADD_MTAR(LOCAL:5)
				ENDIF
			;調教参加キャラが複数の場合
			ELSE
				;行動不能になったキャラをプレイヤーから外す
				CALL DEL_MPLY(LOCAL:0)
			ENDIF
		ENDIF
		;行動不能になった時のEQUIP解除
		CALL DOWN_RELEASE_EQUIP(LOCAL:0)
	ENDIF
NEXT

;離脱判定
FOR LOCAL:0, 0, CHARANUM
	;疲労中、気絶中は判定しない
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):6) && !TCVAR:(LOCAL:0):51 && !TCVAR:(LOCAL:0):52 && !TCVAR:(LOCAL:0):53
		LOCALS:0 = %TOSTR_EMOTION(LOCAL:0)%

		;酔い限界
		IF PALAM:(LOCAL:0):酔い >= 10000
			WAIT
			PRINTL 
			CALL KOJO_EVENT(LOCAL:0, 32)
			TCVAR:(LOCAL:0):53 = 1

		;感情が「怒」「恨」で主人公に対して怒りを感じた場合(会いに行く・閨に呼ぶ・捕虜会話のとき、または捕虜調教でメインターゲットでない（助手である）とき)
		ELSEIF (LOCALS:0 == "怒" || LOCALS:0 == "恨") && CUP:(LOCAL:0):怒主 > 0 && (GROUPMATCH(FLAG:12, 0, 1, 6) || (FLAG:12 == 2 && FLAG:19 != LOCAL:0))
			WAIT
			PRINTL 
			PRINTFORMW %ANAME(LOCAL:0)%の怒りが限界に達したようだ
			IF FLAG:12 == 1 || TFLAG:54 > 0
				CALL KOJO_EVENT(LOCAL:0, 15)
				TCVAR:(LOCAL:0):53 = 5
			ELSE
				CALL KOJO_EVENT(LOCAL:0, 13)
				TCVAR:(LOCAL:0):53 = 3
			ENDIF
			CALL DEL_MPLY(LOCAL:0)
			CALL DEL_MTAR(LOCAL:0)
			;EQUIP解除
			CALL RELEASE_ALL_EQUIP(LOCAL:0)

		;感情が「悲」「鬱」で主人公に対して哀しみを感じた場合(会いに行く・閨に呼ぶ・捕虜会話のとき、または捕虜調教でメインターゲットでない（助手である）とき)
		ELSEIF (LOCALS:0 == "悲" || LOCALS:0 == "鬱") && CUP:(LOCAL:0):哀主 > 0 && (GROUPMATCH(FLAG:12, 0, 1, 6) || (FLAG:12 == 2 && FLAG:19 != LOCAL:0))
			WAIT
			PRINTL 
			PRINTFORMW %ANAME(LOCAL:0)%の哀しみが限界に達したようだ
			IF FLAG:12 == 1 || TFLAG:54 > 0
				CALL KOJO_EVENT(LOCAL:0, 16)
				TCVAR:(LOCAL:0):53 = 6
			ELSE
				CALL KOJO_EVENT(LOCAL:0, 14)
				TCVAR:(LOCAL:0):53 = 4
			ENDIF
			CALL DEL_MPLY(LOCAL:0)
			CALL DEL_MTAR(LOCAL:0)
			;EQUIP解除
			CALL RELEASE_ALL_EQUIP(LOCAL:0)
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;ARG:0番のキャラについて、キス未経験・処女・童貞の喪失判定を行う
;-------------------------------------------------
@FIRST_FLAG_CHECK(ARG:0)
;●キス未経験を判定
IF TALENT:(ARG:0):キス未経験 && EXP:(ARG:0):キス経験 >= 1
	CALL KOJO_EVENT(ARG:0, 1)

	;状況に応じて初キスの相手を記録
	IF IS_MPLY(ARG:0) && MTAR_NUM == 1
		CALL KISS_COMMON(ARG:0, MTAR:0, 0, MTAR:0 == MASTER && GROUPMATCH(FLAG:12, 0, 1, 3, 6), 1)
	ELSEIF IS_MTAR(ARG:0) && MPLY_NUM == 1
		CALL KISS_COMMON(ARG:0, MPLY:0, 0, MPLY:0 == MASTER && GROUPMATCH(FLAG:12, 0, 1, 3, 6), 1)
	ELSE
		CALL KISS_COMMON(ARG:0, -1, 0, 0, 1)
	ENDIF
ENDIF

;●挿入種別に応じた隠し経験値を加算
;おまかせなら加算しない
IF !TFLAG:0
	IF GROUPMATCH(SELECTCOM, 30, 31, 32, 33, 34, 35, 36)
		IF IS_MPLY(ARG:0)
			EXP:(ARG:0):♂♀性交回数 ++
		ELSEIF IS_MTAR(ARG:0)
			EXP:(ARG:0):♀♂性交回数 ++
		ENDIF
	ELSEIF GROUPMATCH(SELECTCOM, 40, 41, 42, 43, 44, 45, 46)
		IF IS_MPLY(ARG:0)
			EXP:(ARG:0):♂Ａ性交回数 ++
		ELSEIF IS_MTAR(ARG:0)
			EXP:(ARG:0):Ａ♂性交回数 ++
		ENDIF
	ENDIF
ENDIF

;●童貞・処女喪失フラグのチェック
IF TCVAR:(ARG:0):14
	IF TALENT:(ARG:0):童貞 == 1
		;状況に応じて童貞を捧げた相手を記録
		IF IS_MPLY(ARG:0) && MTAR_NUM == 1
			CALL DOUTEI_COMMON(ARG:0, MTAR:0)
		ELSEIF IS_MTAR(ARG:0) && MPLY_NUM == 1
			CALL DOUTEI_COMMON(ARG:0, MPLY:0)
		ELSE
			CALL DOUTEI_COMMON(ARG:0, -1)
		ENDIF
	ENDIF
ENDIF

IF TCVAR:(ARG:0):13
	IF TALENT:(ARG:0):処女 == 1
		;状況に応じて処女を捧げた相手を記録
		IF CFLAG:(ARG:0):41
			;既に情報があるので何もしない
		ELSEIF IS_MPLY(ARG:0) && MTAR_NUM == 1
			CALL VIRGIN_COMMON(ARG:0, MTAR:0)
		ELSEIF IS_MTAR(ARG:0) && MPLY_NUM == 1
			CALL VIRGIN_COMMON(ARG:0, MPLY:0)
		ELSE
			CALL VIRGIN_COMMON(ARG:0, -1)
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;主人公の快楽系ソースと射精・絶頂の処理を行う
;-------------------------------------------------
@SOURCE_CHECK_MASTER
;●ソースが上限値を超えた場合、上限値に修正
FOR LOCAL:0, 0, 100
	SOURCE:MASTER:(LOCAL:0) = LIMIT(SOURCE:MASTER:(LOCAL:0), 0, 100000000)
NEXT

;●ソースの計算(MASTER専用)
FOR LOCAL:0, 0, 100
	TRYCALLFORM CALC_SOURCE_MASTER{LOCAL:0}(MASTER)
NEXT

;●絶頂の処理
CALL SOURCE_ORGASM(MASTER)

;●PALAMにCUPの値を加える
;CUPCHECKだと表示面で不都合があるため、独自に処理
FOR LOCAL:0, 0, 100
	IF GROUPMATCH(LOCAL:0, 40)
		;潤滑はLv9まで
		LOCAL:1 = PALAMLV:9
	ENDIF

	;快感パラメータは独自処理(絶頂により低下)
	IF LOCAL:0 >= 0 && LOCAL:0 <= 9
		LOCAL:2 = PALAM:MASTER:(LOCAL:0)
		PALAM:MASTER:(LOCAL:0) = LIMIT(PALAM:MASTER:(LOCAL:0) + CUP:MASTER:(LOCAL:0) - 10000 * NOWEX:MASTER:(LOCAL:0), 0, 9999)

		IF LOCAL:0 <= 4
			;快感系の経験取得
			CALL ADD_EXP_SENSE(MASTER, LOCAL:0, LOCAL:2)
		ENDIF
	;パラメータ上昇量にキャップを掛ける
	ELSEIF PALAM:MASTER:(LOCAL:0) + CUP:MASTER:(LOCAL:0) > LOCAL:1
		PALAM:MASTER:(LOCAL:0) = LOCAL:1
	ELSE
		PALAM:MASTER:(LOCAL:0) += CUP:MASTER:(LOCAL:0)
	ENDIF
NEXT

IF NOWEX:MASTER:射精 >= 1
	;射精した場合潮吹きのPALAMを0に
	PALAM:MASTER:潮吹き = 0
ENDIF

;-------------------------------------------------
;ARG:0番のキャラについて、快楽系ソースと絶頂の処理を行う
;-------------------------------------------------
@SOURCE_CHECK_1(ARG:0)
;●ソースが上限値を超えた場合、上限値に修正
FOR LOCAL:0, 0, 100
	SOURCE:(ARG:0):(LOCAL:0) = LIMIT(SOURCE:(ARG:0):(LOCAL:0), 0, 100000000)
NEXT

;●快楽系ソースの計算
;快楽系(0～9)のみを計算
FOR LOCAL:0, 0, 10
	TRYCALLFORM CALC_SOURCE{LOCAL:0}(ARG:0)
NEXT

;●状況に応じたパラメータ上昇の補正
FOR LOCAL:0, 0, 5
	;異常経験による効果増加(異常経験1000で約1.5倍、10000で約6倍)
	IF EXP:(ARG:0):50 >= 1000000
		CUP:(ARG:0):(LOCAL:0) = CUP:(ARG:0):(LOCAL:0) * (12 + (EXP:(ARG:0):50 / 1000000 * 5 + 95) / 100)
	ELSEIF EXP:(ARG:0):50 >= 10000
		CUP:(ARG:0):(LOCAL:0) = CUP:(ARG:0):(LOCAL:0) * (6 + (EXP:(ARG:0):50 / 10000 * 5 + 95) / 100)
	ELSEIF EXP:(ARG:0):50 >= 100
		CUP:(ARG:0):(LOCAL:0) = CUP:(ARG:0):(LOCAL:0) * (EXP:(ARG:0):50 / 100 * 5 + 95) / 100
	ENDIF
NEXT

;●素質などによるパラメータ上昇の補正(快感系)
CALL UP_TALENT_CHECK1(ARG:0)

;●デート場所によるパラメータ上昇の補正(快感系)
CALL UP_DATE_CHECK1(ARG:0)

;●絶頂の処理
CALL SOURCE_ORGASM(ARG:0)

;●ターゲット以外のキャラに感応のソースを追加する
;プレイヤーまたはターゲットなら欲情上昇量を記録
IF IS_MPLY(ARG:0) || IS_MTAR(ARG:0)
	TFLAG:52 += CUP:(ARG:0):欲情
	TFLAG:58 = MAX(SOURCE:(ARG:0):逸脱, TFLAG:58)

	;ターゲットが絶頂していた場合
	FOR LOCAL:0, 0, 5
		IF NOWEX:(ARG:0):(LOCAL:0) > 0
			TFLAG:53 += NOWEX:(ARG:0):(LOCAL:0)
		ENDIF
	NEXT

;プレイヤー・ターゲットでないなら記録した欲情上昇量に応じて感応・逸脱のソースを追加する(行動不能なら除く)
ELSEIF !(TCVAR:(ARG:0):51 || TCVAR:(ARG:0):52 || TCVAR:(ARG:0):53)
	IF TFLAG:52 >= 30000
		SOURCE:(ARG:0):感応 += 3000
	ELSEIF TFLAG:52 >= 10000
		SOURCE:(ARG:0):感応 += 1500
	ELSEIF TFLAG:52 >= 3000
		SOURCE:(ARG:0):感応 += 500
	ELSEIF TFLAG:52 >= 500
		SOURCE:(ARG:0):感応 += 150
	ELSEIF TFLAG:52 >= 100
		SOURCE:(ARG:0):感応 += 50
	ENDIF

	SOURCE:(ARG:0):感応 += TFLAG:53 * 100
	SOURCE:(ARG:0):逸脱 += TFLAG:58 / 3
ENDIF

;麻薬つかってたら歓喜が入る
SIF TCVAR:(ARG:0):麻薬
	SOURCE:(ARG:0):歓喜 += 500


;-------------------------------------------------
;ARG:0番のキャラについて、快楽系以外のソースの処理を行う
;-------------------------------------------------
@SOURCE_CHECK_2(ARG:0)
;気絶・睡眠中なら感情に関わるソースを全てゼロにする
IF TCVAR:(ARG:0):52 || TCVAR:(ARG:0):53 == 1
	FOR LOCAL:0, 10, 40
		IF TALENTNAME:(LOCAL:0) != "苦痛"
			SOURCE:(ARG:0):(LOCAL:0) = 0
		ENDIF
	NEXT
	SOURCE:(ARG:0):欲情追加 = 0
ENDIF

;各ソースによるパラメータの変動量を計算(快楽系(0～9)は飛ばす)
FOR LOCAL:0, 10, 100
	TRYCALLFORM CALC_SOURCE{LOCAL:0}(ARG:0)
NEXT

;●性癖設定に応じたソースの追加
CALL ADD_SOURCE_TENDENCY(ARG:0)

;●素質などによるパラメータ上昇の補正(快感系以外)
CALL UP_TALENT_CHECK2(ARG:0)

;●デート場所によるパラメータ上昇の補正(快感系以外)
CALL UP_DATE_CHECK2(ARG:0)

;●欲望経験値の取得
;今回の欲情パラメータの増加量と絶頂回数が影響する
LOCAL:0 = MIN(CUP:(ARG:0):欲情 / ((ABL:(ARG:0):欲望 + 1) * (ABL:(ARG:0):欲望 + 1) * 100), 50)
EXP:(ARG:0):欲望経験値 += LOCAL:0 + SUM_NOWEX(ARG:0, 1, 1) * 5

;●潤滑増加の処理(男性・男の娘はコンフィグで設定されていないと濡れない)
IF !((TALENT:(ARG:0):性別 == 0 && CONFIG:11 <= 1) || (TALENT:(ARG:0):性別 == 4 && CONFIG:11 == 0))
	;快感による潤滑増加
	LOCAL:0 = (CUP:(ARG:0):0 + CUP:(ARG:0):1 + CUP:(ARG:0):2 + CUP:(ARG:0):3 + CUP:(ARG:0):4) / 5
	;快感パラメータの上昇が合計100以下なら潤滑は増加しない
	IF LOCAL:0 < 20
		LOCAL:0 = 0
	ENDIF

	;欲情上昇による潤滑の増加
	IF FLAG:50
		;ウフフ中
		LOCAL:0 += CUP:(ARG:0):欲情 / 2
	ELSE
		;非ウフフ
		LOCAL:0 += CUP:(ARG:0):欲情 / 10
	ENDIF

	IF TALENT:(ARG:0):濡れやすい
		TIMES LOCAL:0, 2.50
	ENDIF
	IF TALENT:(ARG:0):濡れにくい
		TIMES LOCAL:0, 0.40
	ENDIF
	;男性・男の娘は少し濡れづらい
	IF GROUPMATCH(TALENT:(ARG:0):性別, 0, 4)
		TIMES LOCAL:0, 0.60
	ENDIF

	CUP:(ARG:0):潤滑 += LOCAL:0
ENDIF

;●失神or睡眠状態なら機嫌の変動量をゼロにする
IF TCVAR:(ARG:0):52 || TCVAR:(ARG:0):53 == 1
	FOR LOCAL:0, 10, 16
		CUP:(ARG:0):(LOCAL:0) = 0
	NEXT
ENDIF

;●PALAMにCUPの値を加える
;CUPCHECKだと表示面で不都合があるため、独自に処理
FOR LOCAL:0, 0, 100
	IF GROUPMATCH(LOCAL:0, 40)
		;潤滑はLv9まで
		LOCAL:1 = PALAMLV:9
	ELSEIF GROUPMATCH(LOCAL:0, 10, 11, 12, 13, 14, 15)
		;機嫌は1000000まで
		LOCAL:1 = 1000000
	ELSE
		;それ以外はLv25まで
		LOCAL:1 = PALAMLV:25
	ENDIF

	;快感パラメータは独自処理(絶頂により低下)
	IF LOCAL:0 >= 0 && LOCAL:0 <= 9
		LOCAL:2 = PALAM:(ARG:0):(LOCAL:0)
		PALAM:(ARG:0):(LOCAL:0) = LIMIT(PALAM:(ARG:0):(LOCAL:0) + CUP:(ARG:0):(LOCAL:0) - 10000 * NOWEX:(ARG:0):(LOCAL:0), 0, 9999)

		IF LOCAL:0 <= 4
			;快感系の経験取得
			CALL ADD_EXP_SENSE(ARG:0, LOCAL:0, LOCAL:2)
		ENDIF
	;パラメータ上昇量にキャップを掛ける
	ELSEIF PALAM:(ARG:0):(LOCAL:0) + CUP:(ARG:0):(LOCAL:0) > LOCAL:1
		PALAM:(ARG:0):(LOCAL:0) = LOCAL:1
	ELSE
		PALAM:(ARG:0):(LOCAL:0) += CUP:(ARG:0):(LOCAL:0)
	ENDIF
NEXT

IF NOWEX:(ARG:0):射精 >= 1
	;射精した場合潮吹きのPALAMを0に
	PALAM:(ARG:0):潮吹き = 0
ENDIF

;-------------------------------------------------
;調教ソースの表示
;-------------------------------------------------
@SHOW_SOURCE(ARG:0)
LOCAL:2 = 0

;主人公の場合、無意味なソースを表示しない
IF ARG:0 == MASTER
	FOR LOCAL:0, 0, 100
		;SOURCEの並び設定を取得
		LOCAL:1 = GET_SORTED_SOURCE(LOCAL:0)
		IF LOCAL:1 >= 0 && SOURCE:(ARG:0):(LOCAL:1) > 0 && GROUPMATCH(LOCAL:1, 0, 1, 2, 3, 4, 9, 40, 50)
			IF !LOCAL:2
				LOCAL:2 = 1
				PRINTFORM %ANAME(ARG:0), 8%:
			ENDIF
			PRINTFORM %SOURCENAME:(LOCAL:1)%({SOURCE:(ARG:0):(LOCAL:1)})
		ENDIF
	NEXT
ELSE
	FOR LOCAL:0, 0, 100
		;SOURCEの並び設定を取得
		LOCAL:1 = GET_SORTED_SOURCE(LOCAL:0)
		IF LOCAL:1 >= 0 && SOURCE:(ARG:0):(LOCAL:1) > 0
			IF !LOCAL:2
				LOCAL:2 = 1
				PRINTFORM %ANAME(ARG:0), 8%:
			ENDIF
			PRINTFORM %SOURCENAME:(LOCAL:1)%({SOURCE:(ARG:0):(LOCAL:1)})
		ENDIF
	NEXT
ENDIF
IF LOCAL:2
	PRINTL 
ENDIF
