;-------------------------------------------------
;第三者調教の処理
;霖之助・タチ役関係のイベント一括処理
;-------------------------------------------------
@EVENT_OTHERSLOVE_COMMON
;霖之助・タチ役の恋人になってからの経過期間を加算(最初の7期は子を妊娠しない)
FOR LOCAL:0, 0, CHARANUM
	IF TALENT:(LOCAL:0):霖之助の恋人 || (TALENT:(LOCAL:0):霖之助に恋慕 && TALENT:(LOCAL:0):恋人 && CFLAG:(LOCAL:0):35 >= 2000)
		CFLAG:(LOCAL:0):37 ++
	ELSEIF TALENT:(LOCAL:0):藍の恋人 || (TALENT:(LOCAL:0):藍に恋慕 && TALENT:(LOCAL:0):恋人 && CFLAG:(LOCAL:0):38 >= 2000)
		CFLAG:(LOCAL:0):37 ++
	ELSE
		CFLAG:(LOCAL:0):37 = 0
	ENDIF
NEXT


;;;;第三者への恋慕関係
;親密度上昇・情事・妊娠・情事覗き見
SIF CONFIG:70 && !CONFIG:200	;霖之助関係のイベント
	CALL EVENT_OTHERSLOVE_霖之助
SIF CONFIG:71 && !CONFIG:200	;タチ役関係のイベント
	CALL EVENT_OTHERSLOVE_RAN

;;第三者による捕虜調教
;タチ役による捕虜調教
SIF CONFIG:81 && !CONFIG:200
	CALL EVENT_OTHERS_RAN_PRISONER


;-------------------------------------------------
;覗き見イベントが発生するかどうかを判定する
;ARG:0=主人公の恋人が抱かれたなら1を指定、ARG:1=霖之助なら0・タチ役なら1を指定
;-------------------------------------------------
@JUDGE_EVENT_OTHERS(ARG:0, ARG:1)
#FUNCTION
SELECTCASE CONFIG:77
	CASE 0
		IF RAND:7 == 0 || (ARG:0 && RAND:3 == 0)
			RETURNF 1
		ENDIF
	CASE 1
		IF RAND:4 == 0 || (ARG:0 && RAND:3 == 0)
			RETURNF 1
		ENDIF
	CASE 2
		IF RAND:2 == 0 || (ARG:0 && RAND:2 == 0)
			RETURNF 1
		ENDIF
	CASE 3
		IF DAY % 3 == ARG:1
			RETURNF 1
		ENDIF
	CASE 4
		IF DAY % 2 == ARG:1
			RETURNF 1
		ENDIF
	CASEELSE
		RETURNF 1
ENDSELECT
RETURNF 0


;-------------------------------------------------
;○○への親密度の減少
;ARG:0 = 対象キャラクタ
;TCVAR 70:変動した好感度 / 71:変動した依存度 / 72:変動した従属度
;-------------------------------------------------
@DECREASE_LOVE_FOR_OTHERSLOVE(ARG:0)

	LOCAL:0 = MAX(TCVAR:(ARG:0):71/2 , 0)	;Ver0.135もともとの仕様のまま 変動依存度/2

;●依存度の上昇量に応じて霖之助・タチ役への親密度を減少させる
SIF CONFIG:70	;霖之助
	CFLAG:(ARG:0):35 = MAX(CFLAG:(ARG:0):35 - LOCAL:0, 0)

SIF CONFIG:71	;タチ役
	CFLAG:(ARG:0):38 = MAX(CFLAG:(ARG:0):38 - LOCAL:0, 0)

;PRINTFORML %NAME:(ARG:0)% 依存度変更 {TCVAR:(ARG:0):71}  親密度　霖{CFLAG:(ARG:0):35,4} / タチ役{CFLAG:(ARG:0):38,4}

;-------------------------------------------------
;TALENT:○○への恋慕の喪失判定
;ARG:0 = 対象キャラクタ
;-------------------------------------------------
@TALENT_CHECK_OTHERSLOVE_LOST(ARG:0)
IF CONFIG:70 && TALENT:(ARG:0):霖之助に恋慕
	;[霖之助に恋慕]の消去
	IF CFLAG:(ARG:0):3 >= 1500 && SUM_EX(ARG:0) >= 15 && CFLAG:(ARG:0):35 < 500
		PRINTL 
		CALL KOJO_EVENT(ARG:0, 402)
		PRINTFORMW すっかり%ANAME(MASTER)%との行為に依存しきった%ANAME(ARG:0)%は霖之助への興味を失ってしまったようだ…
		SETCOLOR COLOR("注意")
		PRINTFORML %ANAME(ARG:0)%は<霖之助に恋慕>を失った
		RESETCOLOR
		TALENT:(ARG:0):霖之助に恋慕 = 0
		CFLAG:(ARG:0):90 = 0
	ENDIF
ENDIF

IF CONFIG:71 && TALENT:(ARG:0):藍に恋慕
	;[タチ役に恋慕]の消去
	IF CFLAG:(ARG:0):3 >= 1500 && SUM_EX(ARG:0) >= 15 && CFLAG:(ARG:0):38 < 500
		PRINTL 
		CALL KOJO_EVENT(ARG:0, 412)
		PRINTFORMW すっかり%ANAME(MASTER)%との行為に依存しきった%ANAME(ARG:0)%は%ANAME(CONFIG:88)%への興味を失ってしまったようだ…
		SETCOLOR COLOR("注意")
		PRINTFORML %ANAME(ARG:0)%は<%ANAME(CONFIG:88)%に恋慕>を失った
		RESETCOLOR
		TALENT:(ARG:0):藍に恋慕 = 0
		CFLAG:(ARG:0):90 = 0
	ENDIF
ENDIF


;-------------------------------------------------
;押し倒し判定(@JUDGE_PUSHDOWN)補助
;-------------------------------------------------
@JUDGE_PUSHDOWN_SUB
IF CONFIG:70	;霖之助
	SIF TALENT:(MTAR:0):霖之助に恋慕
		CALL COM_ORDER_ELEMENT(MTAR:0, "霖之助に恋慕", -35)

	IF TALENT:(MTAR:0):霖之助の恋人
		IF TALENT:(MTAR:0):貞操観念
			CALL COM_ORDER_ELEMENT(MTAR:0, "霖之助の恋人／貞操観念", -35)
		ELSEIF TALENT:(MTAR:0):貞操無頓着
			CALL COM_ORDER_ELEMENT(MTAR:0, "霖之助の恋人／貞操無頓着", -10)
		ELSE
			CALL COM_ORDER_ELEMENT(MTAR:0, "霖之助の恋人", -20)
		ENDIF
	ENDIF
ENDIF

IF CONFIG:71	;タチ役
	SIF TALENT:(MTAR:0):藍に恋慕
		CALL COM_ORDER_ELEMENT(MTAR:0, ANAME(CONFIG:88) + "に恋慕", -35)

	IF TALENT:(MTAR:0):藍の恋人
		IF TALENT:(MTAR:0):貞操観念
			CALL COM_ORDER_ELEMENT(MTAR:0, ANAME(CONFIG:88) + "の恋人／貞操観念", -35)
		ELSEIF TALENT:(MTAR:0):貞操無頓着
			CALL COM_ORDER_ELEMENT(MTAR:0, ANAME(CONFIG:88) + "の恋人／貞操無頓着", -10)
		ELSE
			CALL COM_ORDER_ELEMENT(MTAR:0, ANAME(CONFIG:88) + "の恋人", -20)
		ENDIF
	ENDIF
ENDIF
